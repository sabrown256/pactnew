#!/bin/csh -f
#
# SCS-REMOVE - remove from repository whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set scsi = ( `$ldir/which-scs` )
unset ldir

set args = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: scs-remove [-h] <file>*"
           echo "   h   this help message"
           echo ""
           exit(1)
      default:
           set args = ( $args $1 )
           breaksw
   endsw
   shift
end

switch ($scsi[1])
   case git:
        git rm $args
        breaksw

   case cvs:
        setenv CVS_RSH  ssh
        rm -f $args
        cvs remove $args
        breaksw

   case svn:
        svn remove $args
        breaksw
endsw

exit($status)
