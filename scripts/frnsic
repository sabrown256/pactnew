#!/bin/csh -f
#
# FRNSIC - forensic checks on files
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

@ err = 0

set files = ""
set cmt   = ""

while ($#argv)
   switch ("$1")
      case -h:
           echo ""
           echo "Usage: frnsic [-c <cmt>] [-e <n>] [-h] <file>*"
           echo "   c   comment, e.g. the command producing the files"
           echo "   e   increment the error count which starts at 0"
           echo "   h   this help message"
           echo ""
           exit(1)
      case -c:
           shift
           set cmt = ( $1 )
           breaksw
      case -e:
           shift
           @ err = $err + $1
           breaksw
      default:
           set files = ( $files $1 )
           breaksw
   endsw
   shift
end

set dts = `date +"%Y/%m/%d %H:%M:%S.%N"`

echo "Host: `uname -n`"
echo "Date: $dts"
echo "CWD: $cwd"

foreach f ($files)
   echo "--- $f ---"
   if (-f $f) then
      echo "   Directory: `ls -ld $f:h`"
      echo "   File:      `ls -l $f`"
      echo "   Checksum:  `md5sum $f`"
   else
      echo "   No such file"
      @ err = $err + 1
   endif
end

echo ""

exit($err)
