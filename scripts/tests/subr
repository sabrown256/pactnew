#!/bin/csh -f
#
# SUBR - test csh-subroutines
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SUBROUTINE Test ................"

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: subr [-h]"
           echo "  h  this help message"
           echo ""
           exit(1);
      default:
           breaksw
   endsw
   shift
end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SUBROUTINE - test csh-subroutines

NoteD $Log "                    $msg"

set List = (subr1 subr2 subr3 subr4)

flog $Log cp $SrcDir/tests/{subr*} .

foreach t ($List)
   Separator $Log

   set f = ( `fill-string -n 28 $t` )

   NoteD $Log -n "                        $f "
   Note $Log ""

   set Res = $t.res
   set Ref = $t.ref

   $t run >&! $Res
   $t ans >&! $Ref

   flog $Log diff $Res $Ref
   set xstatus = $status
   if ($xstatus == 0) then
      NoteD $Log "ok"
      set rpf = ( $rpf $Res $Ref )
   else
      NoteD $Log "ng"
      flog $Log @ nerr = $nerr + 1
   endif
end

set rpf = ( $rpf run.* s* )

$TEST_CHECK part -a subr -e $nerr -rpf "$rpf" "$msg"

exit($status)
