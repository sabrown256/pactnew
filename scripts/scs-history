#!/bin/csh -f
#
# SCS-HISTORY - repository history whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set scsi = ( `$ldir/which-scs` )

switch ($scsi[1])
   case git:
#        set opt = ( --reverse -20 )
        set opt = ( --reverse )
        while ($#argv > 0)
           switch ($1)
              case -def:
                   set opt = ( $opt )
                   breaksw
              case -l:
                   echo "Option not implemented - exiting"
                   exit(1)
                   breaksw
              default:
                   set opt = ( $opt $1 )
                   breaksw
           endsw
           shift
        end

        pushd $scsi[2] >& /dev/null

        git log --reverse $opt |& cat

        popd >& /dev/null
        breaksw

   case cvs:
        setenv CVS_RSH  ssh
        set repo = `scs-meta -r`

# common options
#  -a  Show  data  for  all  users (the default is to show data only for the
#      user executing history).
#  -T  Report on all tags

        set opt = ""
        while ($#argv > 0)
           switch ($1)
              case -def:
                   set opt = ( $opt -T -a )
                   breaksw
              case -l:
                   if (-d $repo) then
                      more $repo/CVSROOT/commitlog
                      exit(0)
                   else
                      echo "No such directory '$repo' - exiting"
                      exit(1)
                   endif
                   breaksw
              default:
                   set opt = ( $opt $1 )
                   breaksw
           endsw
           shift
        end

        cvs history $opt
        breaksw

   case svn:
        set opt = ""
        while ($#argv > 0)
           switch ($1)
              case -def:
                   set opt = ( $opt )
                   breaksw
              case -l:
                   echo "Option not implemented - exiting"
                   exit(1)
                   breaksw
              default:
                   set opt = ( $opt $1 )
                   breaksw
           endsw
           shift
        end

        svn log $opt |& head -n 20
        breaksw

endsw

exit($status)
