#!/bin/csh -f
#
# LS-JOBS-CYGWIN - helper to get and print the process command lines
#

unalias *
set path = ( /usr/bin /bin /usr/sbin /sbin )

set noglob

while (1)
   set line = ( $< )
   if ("$line" == "") then
      break
   endif

   set pid = $line[2]

   if ($pid == PID) then

      echo ""

   else if (-f /proc/$pid/cmdline) then
      set inf = ( `cat /proc/$pid/cmdline | tr "\0" " "` )

      if ("$line[5]" =~ *:*) then
         @ n = 6
         echo "$line[1-5]" | awk '{ printf(" %-10s %5d %5d %3s %9s", $1, $2, $3, $4, $5); }'
      else
         @ n = 7
         echo "$line[1-6]" | awk '{ printf(" %-10s %5d %5d %3s %6s %2s", $1, $2, $3, $4, $5, $6); }'
      endif

      if ("$inf" == "<defunct>") then
         echo " $line[$n]"
      else
         echo " $inf"
      endif
   endif
end

exit ( $status )
