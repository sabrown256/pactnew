#!/bin/csh -f
#
# SCS-DIFF - diff source versions whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set scsi = ( `$ldir/which-scs` )

set args = ""
while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: scs-diff [-h] <arg>*"
           echo "   h      this help message"
           echo "   <arg>  options and arguments to be passed on"
           echo ""
           exit(1)
      default:
           set args = ( $args $1 )
           breaksw
   endsw
   shift
end

switch ($scsi[1])
   case git:
        git diff -U0 $args
        breaksw

   case cvs:
        setenv CVS_RSH  ssh
        cvs diff $args
        breaksw

   case svn:
        svn diff --diff-cmd `which diff` -x -r $args
        breaksw
endsw

exit($status)
