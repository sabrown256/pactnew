#!/bin/csh -f
#
# PATEST - test PANACEA
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package panacea
setenv Test    ""
set vgdirs = ( score )

set ldir   = `dirname $0`
set ldir   = `cd $ldir && pwd`       # ensure absolute path
setenv PSY_Base `dirname $ldir`
source $PSY_Base/scripts/test-env
unset ldir

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: patest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: gen abc"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "PANACEA TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $SrcDir/panace.h) then

   NoteD $Log ""
   NoteD $Log "PANACEA TEST ..."

   if ("$Tests" == "") then
      set Tests = (gen abc)
   endif

   foreach i ($Tests)
      $SrcDir/tests/$i
      @ nerr = $nerr + $status
   end

   $TEST_CHECK clean -n PANACEA -e $nerr -raf "$Tmp"

   set xstatus = $status

endif

$TEST_CHECK fin -e $xstatus

exit($status)

