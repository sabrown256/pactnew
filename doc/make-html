#!/bin/csh -f
#
# MAKE-HTML - create HTML documents by wrapping content
#           - with standard HTML boilerplate
#           - handles a certain amount of consistency
#

unalias *

@ err = 0

set pact    = `which pact`
set bindir  = $pact:h
set rootdir = $bindir:h
set htmldir = $rootdir/html

source $rootdir/etc/env-pact.csh

set inf = ""
set sfx = local

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: make-html [-h] [-i] <file>*"
           echo "   h       this help message"
           echo "   i       use institutional attribution content"
           echo "   s       suffix"
           echo "       local, inst, lc"
           echo "   <file>  should be <name>.h"
           echo ""
           exit(1)
      case -i:
           set sfx = inst
           breaksw
      case -s:
           shift
           set sfx = $1
           breaksw
      default:
           set inf = ( $inf $1 )
           breaksw
   endsw
   shift
end

if ("$inf" == "") then
   set inf = ( `ls -1 *.h` )
endif

set hdr = html-header.$sfx
set trl = html-trailer.$sfx

foreach i ($inf)
   set base = $i:r
   set outf = $htmldir/$base.html

   set Title = ( `awk '$1 == "TXT:" { print }' $i | sed 's/TXT: //' | sed "s/'/\\\&rsquo;/g" | sed 's/"/\\\&quot;/g'` )
   set LastM = ( `awk '$1 == "MOD:" { print }' $i | sed 's/MOD: //'` )

   echo "$i $outf:t $LastM" | awk '{printf("   %-16s -> %-20s (%s)\n", $1, $2, $3);}'

   cat $hdr $i $trl                              | \
   awk '$1 != "TXT:" && $1 != "MOD:" { print }'  | \
   sed 's/$TXT/'"$Title"'/'                      | \
   sed 's+$MOD+'"$LastM"'+'                      >! $outf
end

# copy the any GIF that does not exist over to HTMLDIR
foreach g (*.gif unix-README)
   if (!(-e $htmldir/$g)) then
      cp $g $htmldir
   endif
end

exit($err)

