#!/bin/csh -f
#
# INTP-DF - SX PDBView mesh interpolation test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set make_ps = FALSE

set msg = "PDBView Mesh IntpDF Test ....."
set exe = $BinDir/sx
set io  = intp-df.res

@ err = 0

NoteD $Log ""
if ($?PSY_OMIT_INTERP == 1) then
   NoteD $Log "                    $msg omitted"
else

   NoteD $Log "                    $msg"
   Note $Log ""

   flog $Log pwd

   if ($err == 0) then
      NoteD $Log -n "                       Running test .............."
      Note $Log ""

      set exe = $BinDir/sx
      set opt = ( $pvopt -l intp-df.scm )
      set io  = intp-df.dat

      source ../../../scripts/test-run

      $TEST_CHECK part -a intp-df_run -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
      @ err = $err + $status

   endif

   if ($err == 0) then
      NoteD $Log -n "                       Converting tables ........."
      Note $Log ""

      set exe = $BinDir/ultra
      set opt = ( -q -e -s -l intp-df-proc.scm )
      set io  = flog

      source ../../../scripts/test-run

      $TEST_CHECK part -a intp-df_conv -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
      @ err = $err + $status

   endif

   if ($err == 0) then
      set ref = $RefDir/intp-df.u
      NoteD $Log -n "                       Comparing curves .........."
      Note $Log ""

# 0.75 is one horrid tolerance but the MQ seems to be
# pretty unstable against round off
      set exe = $BinDir/pdbdiff
      set opt = ( -q -e -v -w -t 0.75 intp-df.u $ref )
      set opt = ( $opt -M 1 2 7 8 9 10 15 16 17 18 23 24 25 26 31 )
      set io  = flog

      source ../../../scripts/test-run

      $TEST_CHECK part -a intp-df_diff -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
      @ err = $err + $status

   endif

   if (($err == 0) && ($make_ps == TRUE)) then
      NoteD $Log -n "                       Making plots .............."
      Note $Log ""

      set exe = $BinDir/ultra
      set opt = ( -q -e -l intp-df-plot.scm )
      set io  = flog

      source ../../../scripts/test-run

      $TEST_CHECK part -a intp-df_plot -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
      @ err = $err + $status

   endif

   $TEST_CHECK part -a intp-df -e $err -dt $dt -rpf "$rpf" $msg
   @ err = $status
endif

exit($err)
