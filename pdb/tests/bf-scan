#!/bin/csh -f
#
# BF-SCAN - check I/O timings in various situations
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

if ($#argv > 0) then
   set n = $1
else
   set n = 1000000
endif

set ldir = `dirname $0`

foreach iv (1 10)

   set bsl = ( -1 10 100 1000 10000 100000 1000000 )
   set bsl = ( $bsl 10000000 27000000 29000000 )
   set bsl = ( $bsl 100000000 270000000 290000000 )
   set bsl = ( $bsl 1000000000 )

   set dat = bf-$iv.dat
   rm -f $dat
   touch $dat

   echo "   $iv kernels"

   set bt = `timer -r`
   echo -n "      "
   foreach i ($bsl)
      echo $i | awk '{printf(" %7.1e", $1);}'
      do-run tpdd -q -b $i -n $n -nv $iv                                    |& \
      awk '$0 ~ /-/ {printf("      %-12s %10d %10.2e %8.2e %10.2e %10.2e\n",   \
                            $1, '$i', $3, $4, $5, $6);}'                    |  \
      sed 's/ -1 /  1 /' >>& $dat
      rm -f bf-asc.dat bf-bins.dat bf-hdf5.dat bf-pdb.dat
   end

   set et = `timer -b $bt`

   echo ""
   echo "       scan complete ($et)"

   set base = $dat:r

   sort $dat                                                      |& \
   sed '/^$/d'                                                    |  \
   awk 'BEGIN  {cmt = none;}                                         \
        $1 != cmt {cmt = $1;                                         \
                   printf("# %s %s\n", cmt, "'$base'");}             \
        $1 == cmt {printf(" %10.2e %10.2e %8.2f %10.2e %10.2e\n",    \
                          $2, $3, $4, $5, $6);                       \
                   cmt = $1;}' >&! $base.tab
   echo "       tables made"

end

# GOTCHA: using -s loses the annotations (should fix that)
do-run ultra -e -l $ldir/bf-scan.scm

exit($status)
