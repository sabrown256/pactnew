#!/bin/csh -f
#
# BUILD-SPOKES - configure and compile the TRANSLATION spokes
#

@ err = 0

unalias *

set tgt = spoke

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: build-spokes [-h] <tgt>"
           echo "   h     this help message"
           echo "   <tgt> 'spoke' | 'build' (default is spoke)"
           echo ""
           exit(1)
      default:
           set tgt = $1
           breaksw
   endsw
   shift
end

cd spokes
@ err = $err + $status

# make sure there is a spoke and build it
@ ns = 0
@ np = 0
foreach entry (*)
   if ((-d $entry) && ($entry != CVS)) then
      echo "          Building $entry $tgt"
      @ np = $np + 1
      pushd $entry >>& /dev/null
      pact $tgt
      if ($status == 0) then
         @ ns = $ns + 1
      else
         @ err = $err + 1
      endif
      popd >>& /dev/null
   endif
end

echo "   Built $ns out of $np spokes"

exit($err)

