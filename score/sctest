#!/bin/csh -f
#
# SCTEST - test SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package score
setenv Test    ""
set vgdirs = ( . )
source $PSY_Base/scripts/test-env

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: sctest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: type sharp ar mk mkinfo error mpi array"
           echo "                  lookup heap mor mprf resrc retrc"
           echo "                  smp mmap io buf"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "SCORE TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $SrcDir/scstd.h) then
   NoteD $Log ""
   NoteD $Log "SCORE TEST ..."

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ($Tests type sharp ar mk mkinfo error mpi)
      set Tests = ($Tests array lookup)
      set Tests = ($Tests heap mor mprf resrc retrc)
      set Tests = ($Tests smp mmap io buf)
   endif

   foreach i ($Tests)
      $SrcDir/tests/$i
      @ nerr = $nerr + $status
   end

   $TEST_CHECK clean -n SCORE -e $nerr -raf "$Tmp"

   set xstatus = $status

endif

$TEST_CHECK fin -e $xstatus

exit($status)
