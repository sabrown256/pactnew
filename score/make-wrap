#!/bin/csh -f
#
# MAKE-WRAP - find or make an mpi-io-wrap that will work in CROSS-COMPILE situation
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

@ err = 1

set cmd = ""
set tgt = ""
while ($#argv > 0)
   switch ($1)
      case -o:
           set tgt = $2
      default:
           set cmd = ( $cmd $1 )
           breaksw
   endsw
   shift
end

# remove the existing target unconditionally
rm -f $tgt

# try the compilation
$cmd >& /dev/null
@ err = $status

# if it fails see if a viable executable can be found
if ($err != 0) then

   set exe   = $tgt:t
   set ut    = ""
   set sys   = `system-id`

   pushd ../dev >& /dev/null
   set lst = ""
   foreach d ( * )
      if (-d $d) then
         set lst = ( $lst $cwd/$d/bin )
      endif
   end
   popd >& /dev/null

   if ($?PSY_ASRoot == 1) then
      set lst = ( $lst $PSY_ASRoot/pactnew/bin $PSY_ASRoot/pact/bin )
   endif
 
   set path = ( /usr/bin /bin . )
   rehash

   foreach d ( $lst )

# NOTE: this should be generalized - it is only good for mpi-io-wrap
# and will fail there if the help message changes
# hard to find too
# see manager/which-exe
      set nl = `$d/$exe -h |& wc -l`
      if ($nl > 8) then
         strings $d/$exe |& grep $sys >>& /dev/null
         if ($status == 0) then
            pushd $d >>& /dev/null
            set ut = $cwd/$exe
	    cp $ut $tgt
            popd >>& /dev/null
            echo "   Using $ut instead"
            @ err = 0
            break
         endif
      endif
   end

endif

exit($err)
