#!/bin/csh -f
#
# MPI - test MPI in SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SCORE MPI Test ................"
set exe = $BinDir/sctty

   NoteD $Log ""
   NoteD $Log "                    $msg"

   flog $Log pushd $SrcDir
   flog $Log $MAKE -i mpitty
   set xstatus = $status
   flog $Log popd

   if ($xstatus != 0) then
      if (!(-x $exe)) then
         NoteD $Log ""
         NoteD $Log "                         ERROR Building MPI Wrap Tests"
         NoteD $Log "                    ${msg}FAILED"
      endif

   else

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# STDIN

      Separator $Log
      NoteD $Log -n "                       stdin ...................... "
      Note $Log ""
      if ("$MPI" != "") then

         set dt = `$TIMER -r`
         flog $Log $DP_FE -dr -p 2 $exe -t
         if ($status >= 254) then
            @ NOut = 255
         else
            @ NOut = `echo "a" | $DP_FE -p 2 $exe -t |& grep "Proc" |& wc -l`
         endif
         set dt = `$TIMER -b $dt`

         if ($NOut == 255) then
            NoteD $Log "omitted ($dt)"
         else if ($NOut < 2) then
            NoteD $Log "one process only ($dt)"
         else
            NoteD $Log "all processes ($dt)"
         endif
      else
         NoteD $Log "not MPI (0)"
      endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# WRAP

      Separator $Log
      NoteD $Log -n "                       wrap ......................."
      Note $Log ""

      if ("$MPI" == "") then
         if (-x $BinDir/mpi-io-wrap) then
            Note $Log "Running non-MPI version of test"
            flog $Log ( (echo "a"; echo "b"; echo "end") | $BinDir/mpi-io-wrap $exe -q >& scmpiwrap.res )
            set xstatus = $status
         else
            Note $Log "Running non-MPI version of test (without mpi-io-wrap)"
            flog $Log ( (echo "a"; echo "b"; echo "end") | $DORUN -m $exe -q >& scmpiwrap.res )
            set xstatus = $status
         endif
      else
         Note $Log "Running MPI version of test"
         flog $Log  ( (echo "a"; echo "b"; echo "end") | $DP_FE -f -p 1 $exe -q >&! scmpiwrap.res )
         set xstatus = $status
         flog $Log grep 'ATTENTION:' scmpiwrap.res 
         if ($status == 0) then
            flog $Log set xstatus = 255
         endif
      endif

# equate no output with unavailable resources and go the omitted route
      flog $Log cat scmpiwrap.res
      @ nl = ( `cat scmpiwrap.res | wc -l` )
      Note $Log "Status  = $xstatus"
      Note $Log "# lines = $nl"
      if ($nl == 0) then
         flog $Log set xstatus = 255
      endif

      if ($xstatus == 0) then
         flog $Log ( diff scmpiwrap.res $RefDir/scmpiwrap >! scmpiwrap.res.diff )
         if ($status != 0) then
            flog $Log cat scmpiwrap.res.diff
            set xstatus = 252
         endif
      endif

      set rpf = ( $rpf scmpiwrap.res scmpiwrap.res.diff )

      $TEST_CHECK part -a mpi_wrap -e $xstatus -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   endif

$TEST_CHECK part -a mpi -e $nerr "$msg"

exit($status)

