#!/bin/csh -f
#
# MLTEST - test PML
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package pml
setenv Test    ""
set vgdirs = ( score )
source ../scripts/test-env

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: mltest [-h] [-vg] [<test>]*"
           echo "  h       this help message"
           echo "  vg      run tests under valgrind"
           echo "  <test>  one of: matrix eigen func"
           echo "                  interp perf fpe nan chk++"
           echo "                  solv svd csum cgsolv"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "PML TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $SrcDir/pml.h) then
   echo ""
   echo "PML TEST ..."

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ( $Tests matrix eigen func interp perf )
      set Tests = ( $Tests fpe nan chk++ solv svd csum cgsolv )
   endif

   foreach i ($Tests)
      $SrcDir/tests/$i
      @ nerr = $nerr + $status
   end

   $TEST_CHECK clean -n PML -e $nerr

   set xstatus = $status

endif

$TEST_CHECK fin -e $xstatus

exit($status)

