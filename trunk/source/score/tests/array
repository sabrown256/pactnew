#!/bin/csh -f
#
# ARRAY - test SCORE dynamic arrays
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

NoteD $LogF ""
NoteD $LogF "                    SCORE Smart Array Test ...... "

set UTime = `$TIMER -r`
flog $LogF pushd $SrcDir
flog $LogF $MAKE compre
set TStatus = $status
flog $LogF popd

if ($TStatus == 0) then

   if ("$Valgrind" == "") then
      ftee $LogF $CFE $BinDir/scarts
      set xstatus = $status
   else
      ftee $LogF $Valgrind scarts          |& \
      grep "ERROR SUMMARY:"                |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      flog $LogF $RM $Tmp
   endif

   if ($xstatus != 0) then
      set Err = 1
   endif

endif

set ETime   = `$TIMER -b $UTime`

if ($TStatus != 0) then
   NoteD $LogF "                    SCORE Smart Array Test ...... FAILED ($ETime)"
   set Err = 1
else
   NoteD $LogF "                    SCORE Smart Array Test ...... PASSED ($ETime)"
endif

exit($Err)

