#!/bin/csh -f
#
# HEAP - test heap management facilities in SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SCORE Heap Check Test ......... "
set exe = $BinDir/scmtrt
set rpf = none
set xstatus = 0

NoteD $LogF ""
NoteD $LogF -n "                    $msg"
Note $LogF ""

# NOTE: OSF1 is incapable of building this test properly when statically linked
if (("$HaveDevZero" == "HAVE_DEV_ZERO" ) && ("$OS" != "OSF1")) then
   flog $LogF pushd $SrcDir
   flog $LogF $MAKE memchk
   set TStatus = $status
   flog $LogF popd

   if ($TStatus != 0) then
      NoteD $LogF "                         ERROR Building Heap Check Test Program"
      NoteD $LogF "                    ${msg}FAILED"
      set Err = 1
   else

      if ("$CFE" != "") then
         setenv CFE   "$CFE -v"
      endif

      if ("$Valgrind" == "") then
         flog $LogF $CFE $exe
         set xstatus = $status
      else
         ftee $LogF $Valgrind $exe      |& \
         grep "ERROR SUMMARY:"          |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

   endif
endif

$TEST_CHECK part -a heap -e $xstatus -x $exe -rpf "$rpf" none

exit($status)


