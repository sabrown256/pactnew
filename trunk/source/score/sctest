#!/bin/csh -f
#
# SCTEST - test SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package score
source ../scripts/test-env

setenv LogF  $Log

set Test = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: sctest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: sharp ar mk mkinfo error mpi array"
           echo "                  lookup heap mor mprf resrc retrc"
           echo "                  smp mmap io buf"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Test = ( $Test $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "SCORE TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $RootDir/score/scstd.h) then
   NoteD $Log ""
   NoteD $Log "SCORE TEST ..."

   Note $Log "BinDir           = $BinDir"
   Note $Log "path             = $path"
   Note $Log "MAKE             = $MAKE"
   Note $Log "RootDir          = $RootDir"
   Note $Log "SrcDir           = $SrcDir"
   Note $Log "DORUN            = $DORUN"
   if ($?CFE == 1) then
      Note $Log "CFE              = $CFE"
   endif
   if ($?RUN_SIGNATURE_DB == 1) then
      Note $Log "RUN_SIGNATURE_DB = $RUN_SIGNATURE_DB"
   endif

   setenv Tmp $cwd/.tmp.$$
   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress"
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress-test"
   endif

   if ("$Test" == "") then
      set Test = ""
      set Test = ($Test sharp ar mk mkinfo error mpi)
      set Test = ($Test array lookup)
      set Test = ($Test heap mor mprf resrc retrc)
      set Test = ($Test smp mmap io buf)
   endif

   foreach i ($Test)
      $SrcDir/tests/$i
      if ($status == 1) then
         set ERROR = TRUE
      endif
   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP - we are all done now

   $TEST_CHECK -n SCORE -e $ERROR -raf "$Tmp" clean

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK -e $ERROR fin

exit($status)
