#!/bin/csh -f
#
# CCTEST - test SCC
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package scc
setenv Test    ""
set vgdirs = ( . )
source ../scripts/test-env

setenv TEST_ADVISORY ${TEST_ADVISORY}:scc

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: cctest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: t1 t2"
           echo "          ttype tstruct tenum tunion ttypdef"
           echo "          tfunc tinc tdepa tdepb tdepc tdepd"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

flog $Log pact scc

flog $Log cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "SCC TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $SrcDir/scc_int.h) then

   NoteD $Log ""
   NoteD $Log "SCC TEST ....."

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ( $Tests t1 t2 )
      set Tests = ( $Tests ttype tenum tstruct tunion ttypdef )
      set Tests = ( $Tests tfunc tinc tdepa tdepb tdepc tdepd )
   endif

   foreach i ($Tests)
      $SrcDir/tests/driver $i
      @ nerr = $nerr + $status
   end

   $TEST_CHECK clean -n SCC -e $nerr

   set xstatus = $status

endif

$TEST_CHECK fin -e $xstatus

exit($status)

