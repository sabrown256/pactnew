#!/bin/csh -f
#
# ABC - test simple PANACEA code
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

if (-d $RootDir/panacea/abc) then
   NoteD $LogF ""
   NoteD $LogF -n "                    Building PANACEA ABC Test ....."
   Note $LogF ""

   set UTime = `$TIMER -r`

   if ( $?autotool ) then
     flog $LogF pushd $BuildDir/panacea/abc/
     flog $LogF $MAKE -s clean
     flog $LogF $MAKE -s all
     set xstatus = $status
     flog $LogF popd
   else
     setenv SESSION_CONFIG `cfgman use`
     flog $LogF pushd $SrcDir/abc/builder
     flog $LogF dsys remove
     flog $LogF dsys config
     flog $LogF dsys build
     set xstatus = $status
     flog $LogF popd
   endif

   if ($xstatus != 0) then
      NoteD $LogF ""
      NoteD $LogF "                         Can't build PANACEA ABC Test"
   else
      NoteD $LogF " DONE"

      NoteD $LogF ""
      NoteD $LogF "                    PANACEA ABC Test ....."

      if ( $?autotool ) then
        flog $LogF cp $RootDir/panacea/abc-tester .
        abc-tester
        set xstatus = $status
      else
        flog $LogF pushd $SrcDir/abc/builder
        if ("$Valgrind" == "") then
           ftee $LogF dsys test
           set xstatus = $status
        else
           ftee $LogF dsys test -vg
           set xstatus = $status
        endif
        flog $LogF popd
      endif

      set ETime = `$TIMER -b $UTime`

      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $LogF "                    PANACEA ABC Test .............. OMITTED ($xstatus/$ETime)"
      else if ($xstatus != 0) then
         NoteD $LogF "                    PANACEA ABC Test .............. FAILED ($xstatus/$ETime)"
         set Err = 1
      else
         NoteD $LogF "                    PANACEA ABC Test .............. PASSED ($ETime)"
      endif

      if ($xstatus == 0) then
         if ( $?autotool ) then
           flog $LogF pushd $BuildDir/panacea/abc/
           flog $LogF $MAKE -s clean
         else
           flog $LogF pushd $SrcDir/abc/builder
           flog $LogF dsys remove
         endif
         flog $LogF popd
      endif
   endif
endif

exit($Err)
