#!/bin/csh -f
#
# SCS-REVERT - revert changed sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set save  = ""
set files = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: scs-revert [-h] [-s <sfx>] <file>*"
           echo "   h   this help message"
           echo "   s   copy <file> to <file>.<sfx> before reverting"
           echo ""
           exit(1)
      case -s:
           shift
           set save = $1
           breaksw
      default:
           set files = ( $files $1 )
           breaksw
   end
   shift
end

# save the files to be reverted
if ("$save" != "") then
   foreach f ($files)
      if (-f "$f") then
         cp $f $f.$save
      endif
   end
endif

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )

if (-d $gitd[1]) then

# to revert changes made to your working copy:
#    git checkout .
# to revert changes made to the index (i.e., that you have added):
#    git reset
# to revert a change that you have committed:
#    git revert ...

   git checkout $files

else if (-d CVS) then

   setenv CVS_RSH  ssh

   foreach f ($files)
      if (-f $f) then
         rm -f $f
      endif
   end
   cvs update

else if (-d .svn) then

   svn revert $files

endif

exit($status)
