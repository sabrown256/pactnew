#!/bin/csh -f
#
# TEST-CHECK - post test checks and reporting
#            - handle all PACT tests consistently
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source $TestScrDir/env-csh

@ err = 0

set tgt  = none
set name = ""
set terr = 0
set raf  = ""
set rpf  = ""
set adv  = $Package

if ($?BTime == 1) then
   set t0 = $BTime
else
   set t0 = 0
endif

if ($?Log == 1) then
   set log = $Log
else
   set log  = /dev/null
endif

while ($#argv > 0)
   switch ($1)
      case -a:
           shift
           set adv = ( $adv ${Package}_$1 )
           breaksw
      case -e:
           shift
           if (("$1" == "FALSE") || ("$1" == "0")) then
              set terr = 0
           else
              set terr = 1
           endif
           breaksw
      case -l:
           shift
           set log = $1
           breaksw
      case -n:
           shift
           set name = $1
           breaksw
      case -raf:
           shift
           set raf = ( $1 )
           breaksw
      case -rpf:
           shift
           set rpf = ( $1 )
           breaksw
      case -t0:
           shift
           set t0 = $1
           breaksw
      default:
           set tgt = $1
           breaksw
   endsw
   shift
end

# make sure it is upper case
set name = ( `echo $name | tr "[a-z]" "[A-Z]"` )

# check the test advisory
set advise = FALSE
set alst = ( `printenv TEST_ADVISORY | sed 's/:/ /g'` )
foreach i ($alst)
   foreach j ($adv)
      if ($i == $j) then
         set advise = TRUE
         break
      endif
   end
   if ($advise == TRUE) then
      break
   endif
end

#echo "--------"
#echo "tgt = |$tgt|"
#echo "adv = |$adv|"
#echo "TEST_ADVISORY = |$TEST_ADVISORY|"
#echo "advise = $advise"

switch ($tgt)

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP
   case clean:

        if ("$raf" != "") then
           flog $log $RM $raf
        endif

        set dt = `$TIMER -b $t0`

        NoteD $log ""

        if ($terr == 1) then
           NoteD $log ""
           if ($advise == TRUE) then
              NoteD $log "$name TEST ... ACCEPTED AS PASSED ($dt)"
           else
              NoteD $log "$name TEST ... FAILED ($dt)"
           endif
           NoteD $log ""
           NoteD $log "See $log for details"
        else
           if ("$rpf" != "") then
              flog $log $RM $rpf
           endif
           NoteD $log "$name TEST ... PASSED ($dt)"
        endif
        breaksw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# FINISH
   case fin:

        NoteD $Log ""
        NoteD $Log " --------------------------------------------------------"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# PART
   case part:

        set err = 0
        if ($terr == 1) then
           if ($advise == TRUE) then
              Note $Log "PASSED by advisory match ( $adv ) in ( $TEST_ADVISORY )"
              flog $Log set err = 0
           else
              flog $Log set err = 1
           endif
        endif
        breaksw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endsw

exit($err)
