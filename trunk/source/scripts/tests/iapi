#!/bin/csh -f
#
# IAPI - test import-api
#

unalias *

source ../../tests/common

set msg = "IMPORT-API Test ................"

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: iapi [-h]"
           echo "  h  this help message"
           echo ""
           exit(1);
      default:
           breaksw
   endsw
   shift
end

@ nerr = 0

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

NoteD $Log "                    $msg"
Note $Log ""

set defl = ""
set defl = ( $defl $BaseDir/pdb/spokes/hdf5/hdf5.def none )
set defl = ( $defl $BaseDir/pdb/spokes/sql/mysql.def -I/usr/include/mysql )
set defl = ( $defl $BaseDir/pdb/spokes/sql/sqlite3.def none )
set defl = ( $defl $BaseDir/score/bfd.def none )

unsetenv xstatus
unset xstatus

while ($#defl > 0)
   Separator $Log
   set def = $defl[1]
   set inc = $defl[2]
   shift defl
   shift defl

   set j = $def:t

   set opt = ""
   if ("$inc" != none) then
      set opt = ( $opt $inc )
   endif

   set lmsg = `fill-string -n 28 $j:r `
   NoteD $Log -n "                        $lmsg "
   Note $Log ""

   set base = $j:r
   set a = $base.api
   flog $Log cp $def $j
   flog $Log import-api $opt -f $j -o $a
   set xstatus = $status

   flog $Log ls -l $j

   if ($xstatus == 0) then
      NoteD $Log "ok"
      flog $Log ls -l $a
      set rpf = ( $rpf $j $a log.$base )
   else if ($xstatus == 1) then
      NoteD $Log "omitted"
      set rpf = ( $rpf $j $a log.$base )
   else
      NoteD $Log "ng ($xstatus)"
      flog $Log @ nerr = $nerr + 1
   endif

end

$TEST_CHECK part -a iapi -e $nerr -rpf "$rpf" "$msg"

exit($status)
