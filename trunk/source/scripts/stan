#!/bin/csh -f
#
# STAN - run STatic ANalysis tool over source files
#
# include "cpyright.h"
#

unalias *

@ err = 0

set lst = ""
#set opt = ( -warnposix -retvalint +boolint +matchanyintegral )
set opt = ""
set opt = ( $opt -warnposix -retval +boolint +matchanyintegral -nullret )
set opt = ( $opt -paramuse )
set opt = ( $opt +charint +charintliteral )
set opt = ( $opt -nullpass )
set opt = ( $opt -exportlocal )

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: stan [-h] [-u] <C-files>*"
           echo "   h   this help message"
           echo "   u   turn off unrecognized identifier warning (splint only)"
           echo ""
           exit(0)
      case -u:
           set opt = ( $opt -unrecog )
           breaksw
      default:
           set lst = ( $lst $1 )
           breaksw
   endsw
   shift
end

if ("$lst" == "") then
   echo ""
   echo "No C source files specified - exiting"
   echo ""
   exit(1)
endif

set path   = ( /usr/gapps/pact/scripts $path )
set psy_id = `../scripts/cfgman use`

source ../dev/$psy_id/etc/functions-pact.csh
source ../dev/$psy_id/etc/env-pact.csh

InitLog log $cwd/log.$0:t

foreach c ($lst)
   if (($c =~ foo*) || ($c =~ bar*) || ($c =~ fu*) || ($c =~ baz*)) continue

   NoteD $log ""
   NoteD $log "------------------------------------------------------------"
   NoteD $log ""
   NoteD $log "File: $c"
   NoteD $log ""

   if ($PACT_CC_FAMILY == CLANG) then
      gtee $log clang --analyze -I../dev/$psy_id/include -I. $c
      @ err = $err + $gstatus[1]
   else
#      ftee $log splint $opt -I../dev/$psy_id/include -I. $c
      gtee $log splint $opt -I../dev/$psy_id/include -I. $c
      @ err = $err + $gstatus[1]
   endif
end

exit($err)
