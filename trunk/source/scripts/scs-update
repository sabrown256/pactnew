#!/bin/csh -f
#
# SCS-UPDATE - update sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )

set unmngd = FALSE
set cvsopt = ""
set svnopt = ""
set args   = ""
while ($#argv > 0)
   switch ($1)
      case -A:
           set cvsopt = ( $cvsopt -A )
           set svnopt = ( $svnopt --depth files )
           breaksw
      case -l:
           set cvsopt = ( $cvsopt -l )
           set svnopt = ( $svnopt --depth files )
           breaksw
      case -u:
           set unmngd = TRUE
           breaksw
      default:
           set args = ( $args $1 )
           breaksw
   endsw
   shift
end

if (-d $gitd[1]) then

   pushd $gitd[1] >& /dev/null

   if ($unmngd == TRUE) then
      git status -s |& egrep '^\?' | sed 's/^??/? /' | grep "$gitd[2]/"
   else
      set repo = ( `$ldir/scs-meta -r` )

# update local copy with changes from master repo
      git pull $repo master >&! .log.sup
      set xstatus = $status

# report changed files
      grep '|' .log.sup | sed 's/|.*$//' | awk '{printf("   P %s\n", $1);}'
      if ($xstatus == 0) then
         rm .log.sup
      else
         cat .log.sup
      endif

# report status of local changes
      if ("$gitd[2]" == ".") then
         git status -s |& egrep -v '^\?'
      else
         git status -s |& egrep -v '^\?' | grep "$gitd[2]/"
      endif
   endif

   popd >& /dev/null

else if (-d CVS) then

   if ($unmngd == TRUE) then
      cvs update $cvsopt $args |& egrep '^\?'
   else
      cvs update $cvsopt $args |& egrep -v '^\?' |& grep -v 'cvs update'
   endif

else if (-d .svn) then

   svn update $svnopt $args
   if ($unmngd == TRUE) then
      svn status $svnopt |& egrep '^\?'
   else
      svn status $svnopt |& egrep -v '^\?'
   endif

endif

exit($status)
