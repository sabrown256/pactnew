#!/bin/csh -f
#
# SCS-ADD - add to repository whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )
unset ldir

if (-d $gitd[1]) then

   git add $argv

else if (-d CVS) then

   setenv CVS_RSH  ssh
   set repo = `scs-meta -r`

# separate arguments that are directories
   set dir = ""
   set opt = ""
   while ($#argv > 0)
      if (-d $1) then
         set dir = ( $dir $1 )
      else
         set opt = ( $opt $1 )
      endif
      shift
   end

# if there are no directories just add files
   if ("$dir" == "") then
      cvs add $opt

# if there are directories cvs import them
   else
      foreach d ($dir)
         cvs import -m "Add directory $d" $opt $d V1 R1
      end
   endif

else if (-d .svn) then

   svn add $argv

endif

exit($status)
