#!/bin/csh -f
#
# SCS-META - report directories containing repository metadata
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )

set tgt = ""

while ($#argv > 0)
    switch ($1)
       case -d:
            set tgt = directories
            breaksw
       case -h:
            echo ""
            echo "Usage: scs-meta [-d] [-h] [-k]"
            echo "   d  list directories containing metadata"
            echo "   h  this help message"
            echo "   k  report system (one of git, svn, or cvs)"
            echo "   r  report location of master repository"
            echo ""
            exit(1)
       case -k:
            set tgt = kind
            breaksw
       case -r:
            set tgt = repo
            breaksw
       default:
            breaksw
    endsw
    shift
end

set res = ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# GIT

if (-d $gitd[1]) then

   pushd $gitd[1] >& /dev/null

   switch ($tgt)
      case directories:
           set res = ( $res `find $gitd[1] -name .git -type d | grep -v $gitd[1]/.git` )
           breaksw
      case repo:
           set res = ( `grep url $gitd[1]/.git/config | sed 's|^[ \t]*url =||'` )
           breaksw
      case kind:
           set res = git
           breaksw
   endsw

   popd >& /dev/null

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CVS

else if (-d CVS) then

   switch ($tgt)
      case directories:
           set res = ( $res `find . -name CVS -type d` )
           breaksw
      case repo:
           set res = ( `cat CVS/Root` )
           breaksw
      case kind:
           set res = cvs
           breaksw
   endsw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SVN

else if (-d .svn) then

   switch ($tgt)
      case directories:
           set res = ( $res `find . -name .svn -type d` )
           breaksw
      case repo:
           set base = ( `head -n 6 .svn/entries | tail -n 1` )
           set dir  = ( `head -n 5 .svn/entries | tail -n 1` )
           set res = $dir:h
           breaksw
      case kind:
           set res = svn
           breaksw
   endsw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

# print results
echo "$res"

exit($status)
