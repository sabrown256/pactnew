#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set repo = ( `$ldir/scs-meta -r` )
set knd  = ( `$ldir/scs-meta -k` )

set opt = ""
set pkg = ""

set srcdate = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           breaksw
      case -k:
           shift
           set knd = $1
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      case -r:
           set opt = ( $opt $1 $2 )
           shift
           breaksw
      case -*:
           set opt = ( $opt $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

#echo "repo = $repo"
#echo "kind = $knd"

if ($knd == cvs) then

   if ($#pkg > 1) then
      set base = $pkg[1]
      shift pkg
   else if ("$pkg" != "") then
      set base = $pkg
   else
      set base = work-copy
   endif

   setenv CVS_RSH  ssh
   set opt = ( $opt -d $repo )
   if ("$srcdate" != "") then
      cvs -d $repo co -D "$srcdate" $base $pkg
   else
      cvs -d $repo co $base $pkg
   endif

else if ($knd == svn) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   if ("$srcdate" != "") then
      set dt = "$srcdate[1]"
      svn co $repo -r "{${dt}}" $base
   else
      svn co $repo $base
   endif

else if ($knd == git) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   if ("$srcdate" != "") then

      git clone $repo $base

      cd $base

      set inf = ( `git log | grep -B 2 "$srcdate"` )
      if ($#inf > 1) then
         set tag = $inf[2]
         git reset --hard $tag
      endif
   else
      git clone $repo $base
   endif

endif

exit($status)
