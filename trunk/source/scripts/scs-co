#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set repo = ( `$ldir/scs-meta -r` )
set knd  = ( `$ldir/scs-meta -k` )

set opt = ""
set pkg = ""

set srcdate = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      case -h:
           echo ""
           echo "Usage: scs-co [-d <repo>] [-D <date>] [-h] [-k <knd>] <pkg> [<arg>]*"
           echo "   d      location of source repository"
           echo "   D      checkout sources as of <date> stamp"
           echo "   h      this help message"
           echo "   k      source control system: cvs, git, or svn"
           echo "   <pkg>  package to be checked out"
           echo "   <arg>  options to be passed on"
           echo ""
           exit(1)
      case -k:
           shift
           set knd = $1
           breaksw
      case -r:
           set opt = ( $opt $1 $2 )
           shift
           breaksw
      case -*:
           set opt = ( $opt $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

#echo "repo = $repo"
#echo "kind = $knd"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# fix source date some legal formats
#    2006-02-17
#    15:30
#    15:30:00.200000
#    "2006-02-17 15:30"
#    "2006-02-17 15:30 +0230"
#    2006-02-17T15:30
#    2006-02-17T15:30Z
#    2006-02-17T15:30-04:00
#    20060217T1530
#    20060217T1530Z
#    20060217T1530-0500


if ("$srcdate" != "") then
   set srcdate = `date --rfc-3339=date -d "$srcdate"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CVS

if ($knd == cvs) then

   if ($#pkg > 1) then
      set base = $pkg[1]
      shift pkg
   else if ("$pkg" != "") then
      set base = $pkg
   else
      set base = work-copy
   endif

   setenv CVS_RSH  ssh
   set opt = ( $opt -d $repo )
   if ("$srcdate" != "") then
      cvs -d $repo co -D "$srcdate" $base $pkg
   else
      cvs -d $repo co $base $pkg
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SVN

else if ($knd == svn) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   if ("$srcdate" != "") then
      set dt = "$srcdate[1]"
      svn co $repo -r "{${dt}}" $base
   else
      svn co $repo $base
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# GIT

else if ($knd == git) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   git clone $repo $base

   if ("$srcdate" != "") then

      set dt = ( `date -d $srcdate '+%b %e'` )
      cd $base

      set inf = ( `git log | grep -B 2 "$dt"` )
      if ($#inf > 1) then
         set tag = $inf[2]
         echo "---------------------------"
         echo "Resetting to $srcdate ($dt)"
         git reset --hard $tag
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

exit($status)
