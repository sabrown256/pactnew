#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

if ($?refdir == 0) then
   set exedir = `dirname $0`
   if ("$exedir" == ".") then
      set refdir = $cwd
   else
      set refdir = $exedir
   endif
endif

set opt = ""
set hst = ""
set dir = ""
set pkg = ""

set srcdate = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           set hst  = `echo $repo | sed 's|:/.*$||'`
           set dir  = `echo $repo | sed 's|^.*:||'`
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

if (-d $refdir/../.git) then

   pushd $refdir/.. >& /dev/null

   set opt  = ( $opt $dir )
   set inf  = ( `cat .git/config | grep url` )
   set root = $inf[3]:h

   popd >& /dev/null

   foreach p ( $pkg )
      if ("$srcdate" != "") then
         set dt = "$srcdate[1]"
         git clone $root/$p
      else
         git clone $root/$p
      endif
   end

else if (-d $refdir/CVS) then

   set opt = ( $opt -d ${hst}:$dir )
   if ("$srcdate" != "") then
      cvs $opt co -D "$srcdate" $pkg
   else
      cvs $opt co $pkg
   endif

else if (-d $refdir/.svn) then

   set opt  = ( $opt http://${hst}$dir )
   set root = `grep manager $refdir/.svn/entries | sed 's|/manager$||'`

   foreach p ( $pkg )
      if ("$srcdate" != "") then
         set dt = "$srcdate[1]"
         svn co $opt -r "{${dt}}" ${root}/$p
      else
         svn co $opt ${root}/$p
      endif
   end

endif

unset refdir

exit($status)
