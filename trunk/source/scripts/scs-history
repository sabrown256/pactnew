#!/bin/csh -f
#
# SCS-HISTORY - repository history whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )

if (-d $gitd[1]) then

   pushd $gitd[1] >& /dev/null

#   set opt = ( --reverse -20 )
   set opt = ( --reverse )
   if ($#argv > 0) then
      set opt = ( $argv )
   endif

   git log --reverse $opt |& cat

   popd >& /dev/null

else if (-d CVS) then

   setenv CVS_RSH  ssh
   set repo = `scs-meta -r`

   set opt = ""
   while ($#argv > 0)
      switch ($1)
         case -l:
              if (-d $repo) then
                 more $repo/CVSROOT/commitlog
                 exit(0)
              else
                 echo "No such directory '$repo' - exiting"
                 exit(1)
              endif
              breaksw
         default:
              set opt = ( $opt $1 )
              breaksw
      endsw
      shift
   end

   cvs history $opt

else if (-d .svn) then

   svn log $argv

endif

exit($status)
