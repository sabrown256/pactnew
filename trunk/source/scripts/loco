#!/bin/csh -f
#
# LOCO - log command before running it
#
# include "cpyright.h"
#

unalias *

@ err = 0

set log = $HOME/.log.command
touch $log

set cmd  = $0
set args = ( $argv )

set rcmd = ( `where $cmd:t` )
foreach c ($rcmd)
   if ($c != $cmd) then
      set cmd = $c
      break
   endif
end

set ppid   = ( `ps -ef | awk '$2 == '$$' { print $3 }'` )
set parent = ( `ps -ef | awk '$2 == '$ppid' { print }'` )
set date   = ( "["`date '+%Y/%m/%d %H:%M:%S.%N'`"]" )

echo "------------------------------------------------------------------" >>& $log
# info = user pid ppid time command
echo "$date info  = $parent[1] $$ $parent[2] $parent[7-]" >>& $log
echo "$date where = `uname -n`:$cwd" >>& $log
echo "$date $cmd $args" >>& $log

$cmd $args
set err = $status

echo "$date status = $err" >>& $log

exit($err)
