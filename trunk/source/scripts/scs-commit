#!/bin/csh -f
#
# SCS-COMMIT - commit sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set gitd = ( `$ldir/where-git` )
set repo = ( `$ldir/scs-meta -r` )

set args = ""
set msg  = ""

while ($#argv > 0)
   switch ($1)
      case -m:
           set msg = ( $argv )
           break
           breaksw
      default:
           set args = ( $args $1 )
           breaksw
   endsw
   shift
end

if (-d $gitd[1]) then

   set log = $cwd/log.commit
   rm -f $log
   touch $log

   set part = $gitd[2]
   pushd $gitd[1] >>& $log

# do a pull first
   echo -n "Sync ... "
   echo "git pull $repo master" >>& $log
   git pull $repo master >>& $log
   set xstatus = $status
   if ($xstatus == 0) then
      echo "ok"
   else
      echo "ng ($xstatus)"
   endif

# find changed files
   if ("$part" == ".") then
      set inf = ( `git status -s |& egrep -v '^\?'` )
   else
      set inf = ( `git status -s |& egrep -v '^\?' | grep $part` )
   endif
   echo "Committing:"
   set files = ""
   while ($#inf > 0)
      set tag  = $inf[1]
      set file = $inf[2]
      shift inf
      shift inf
      set files = ( $files $file )
      echo " $tag  $file"
   end

# do the local commit
   echo -n "Local commit ... "
   echo "git commit $args -q -m '$msg' $files" >&! $log
   git commit $args -q -m "$msg" $files >>& $log
   set xstatus = $status
   if ($xstatus == 0) then
      echo "ok"
   else
      echo "ng ($xstatus)"
   endif

   echo "---------------------" >>& $log

# push it up to the master repository
   if ($xstatus == 0) then
      echo -n "Pushing changes ... "
      echo "git push $repo" >>& $log
      git push $repo >>& $log
      if ($xstatus == 0) then
         echo "ok"
      else
         echo "ng ($xstatus)"
         echo "Need 'git config --bool core.bare true' on master"
      endif
   endif

   popd >& /dev/null

else if (-d CVS) then

   setenv CVS_RSH  ssh
   cvs commit $args -m "$msg"

else if (-d .svn) then

   svn commit $args -m "$msg"

endif

exit($status)
