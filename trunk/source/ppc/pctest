#!/bin/csh -f
#
# PCTEST - test PPC
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = ppc
source ../scripts/test-env

if ($HaveIPC == NO) then
   exit(0)
endif

setenv LogF  $Log

set Test = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: pctest [-h] [-vg] [<test>]*"
           echo "  h       this help message"
           echo "  vg      run tests under valgrind"
           echo "  <test>  one of: pipe socket pty"
           echo "                  bg bin multi"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Test = ( $Test $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

# omit tests - usually in difficult CROSS_COMPILE situation
if (($RUN_TESTS == FALSE) || ($CROSS_COMPILE != FALSE)) then
   NoteD $Log ""
   NoteD $Log "PPC TESTS ... omitted"

# run the tests
else if (-e $BinDir/pcexec) then
   set BeginTime = `$TIMER -r`

   NoteD $Log ""
   NoteD $Log "PPC TEST ..."

   Note $Log "BinDir = $BinDir"
   Note $Log "path   = $path"  
   Note $Log "MAKE   = $MAKE"
   Note $Log "PCEXEC = $PCEXEC"

   setenv Tmp $cwd/.tmp.$$

   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$RootDir/score/tests/vg.suppress"
   endif

   if ( !($?autotool) ) then
      Note $Log "PACT     = $PACT"
      Note $Log "Parallel = |$Parallel|"
      flog $Log ../../../manager/pwhich pact
      flog $Log ../../../manager/pwhich cc
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Build PPC tests

   NoteD $Log ""
   NoteD $Log -n "                    Building PPC Test Programs ..... "
   Note $Log ""

   flog $Log pushd $BinDir
   flog $Log rm pcmtst pcbsts pcctst
   flog $Log popd

   flog $Log pushd $SrcDir
   flog $Log $MAKE Cstd
   set TStatus = $status
   flog $Log popd

   if ($TStatus != 0) then
      NoteD $Log ""
      NoteD $Log "                    Can't build Standard PPC Test Programs"
      set ERROR = "TRUE"
   else
      NoteD $Log "DONE"

      set TEST = "cat $RootDir/ppc/DISCLAIMER"

      if ("$Test" == "") then
         set Test = ""
         set Test = ( $Test pipe socket pty bg bin multi )
      endif

      foreach i ($Test)
         $SrcDir/tests/$i
         if ($status == 1) then
            set ERROR = TRUE
         endif
      end
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Clean up

   flog $Log $RM $Tmp

   set ETime = `$TIMER -b $BeginTime`

   NoteD $Log ""

   if ($ERROR == "TRUE") then
      NoteD $Log ""
      NoteD $Log "PPC TEST ... FAILED ($ETime)"
      NoteD $Log ""
      NoteD $Log "See $Log for details"
   else
      NoteD $Log "PPC TEST ... PASSED ($ETime)"
   endif

endif

NoteD $Log ""
NoteD $Log " --------------------------------------------------------"

set xstatus = 0
if ($ERROR == "TRUE") then
   set xstatus = 1
endif

exit($xstatus)

