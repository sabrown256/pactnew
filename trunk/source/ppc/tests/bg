#!/bin/csh -f
#
# BG - PPC foreground/background tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PPC Background Test ............"

set PCFGBG = $SrcDir/tests/pcfgbg

set LogFB = log.fgbg.$LogSfx

NoteD $Log ""
NoteD $Log -n "                    $msg"
Note $Log ""

# PPC Background test
      set ok = TRUE
      if ($?DP_Path == 1) then
         if ("$DP_Path" != "") then
            set ok = FALSE
         endif
      endif

      set TCSH = `which tcsh`
      if (($ok == TRUE) && ($?CROSS_COMPILE == 0) && (-x "$TCSH")) then

# do foreground part
         NoteD $Log "                       foreground .........."
         Note $Log "( $CFE tcsh -f $PCFGBG -fg ) >&! res.fg"
         $CFE tcsh -f $PCFGBG -fg >&! res.fg
         set xstatus = $status

         cat res.fg
         flog $Log cat $LogFB

         $TEST_CHECK part -a bg_foreground -e $xstatus -l -rpf "$rpf" none

         @ nerr = $nerr + $status

# do background part
         NoteD $Log "                       background .........."
         Note $Log "( $CFE tcsh -f $PCFGBG -bg & ) >&! res.bg"
         ( $CFE tcsh -f $PCFGBG -bg & ) >&! res.bg
         set xstatus = $status

         @ count = 0
         while ($count < 20)
            sleep 5
            flog $Log grep "Exit status = " $LogFB
            if ($status == 0) break
            @ count = $count + 1
         end
         flog $Log sign pcfgbg

         cat res.bg
         flog $Log cat $LogFB

         $TEST_CHECK part -a bg_background -e $xstatus -l -rpf "$rpf" none

         @ nerr = $nerr + $status

         set rpf = ( $rpf res.fg res.bg )

      endif

$TEST_CHECK part -a bg -e $nerr -rpf "$rpf" none

exit($status)


