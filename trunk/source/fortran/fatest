#!/bin/csh -f
#
# FATEST - test FORTRAN APIs
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = fortran
source ../scripts/test-env
source $IncDir/env-pact.csh

setenv LogF  $Log

set Test = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: fatest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: score pdb pgs panacea"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Test = ( $Test $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

unsetenv DO_RUN_SUBMIT_ENV

# PGI compilers lack some key C interoperability features
# notably: C_NULL_PTR
# SUN compilers have trouble with command line argument routines
if (("$PACT_CC_FAMILY" == PGI) || ("$PACT_CC_FAMILY" == SUN)) then
   set RUN_TESTS = FALSE
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "FORTRAN TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if ($FC_Exe != none) then

   NoteD $Log ""
   NoteD $Log "FORTRAN TEST ..."

   Note $Log "BinDir           = $BinDir"
   Note $Log "path             = $path"
   Note $Log "MAKE             = $MAKE"
   Note $Log "RootDir          = $RootDir"
   Note $Log "SrcDir           = $SrcDir"
   Note $Log "DORUN            = $DORUN"
   if ($?CFE == 1) then
      Note $Log "CFE              = $CFE"
   endif
   if ($?RUN_SIGNATURE_DB == 1) then
      Note $Log "RUN_SIGNATURE_DB = $RUN_SIGNATURE_DB"
   endif

   setenv Tmp $cwd/.tmp.$$
   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress"
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress-test"
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   if ("$Test" == "") then
      set Test = ""
      set Test = ($Test score pdb pgs panacea)
   endif

   foreach i ($Test)
      $SrcDir/tests/$i
      if ($status == 1) then
         set ERROR = TRUE
      endif
   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP - we are all done now

   $TEST_CHECK -n FORTRAN -e $ERROR -raf "$Tmp" clean

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK -e $ERROR fin

exit($status)

