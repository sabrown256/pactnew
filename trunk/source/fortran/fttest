#!/bin/csh -f
#
# FTTEST - test FORTRAN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = fortran
source ../scripts/test-env

setenv LogF  $Log

set Test = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: fttest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: lex pdb history"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Test = ( $Test $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

# omit tests - usually in difficult CROSS_COMPILE situation
if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "FORTRAN TESTS ... omitted"

# run the tests
else if ($FC_Exe != none) then
   set BeginTime = `$TIMER -r`

   NoteD $Log ""
   NoteD $Log "FORTRAN TEST ..."

   Note $Log "BinDir           = $BinDir"
   Note $Log "path             = $path"
   Note $Log "MAKE             = $MAKE"
   Note $Log "RootDir          = $RootDir"
   Note $Log "SrcDir           = $SrcDir"
   Note $Log "DORUN            = $DORUN"
   if ($?CFE == 1) then
      Note $Log "CFE              = $CFE"
   endif
   if ($?RUN_SIGNATURE_DB == 1) then
      Note $Log "RUN_SIGNATURE_DB = $RUN_SIGNATURE_DB"
   endif

   setenv Tmp $cwd/.tmp.$$
   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress"
      setenv Valgrind  "$Valgrind --suppressions=$SrcDir/tests/vg.suppress-test"
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   if ("$Test" == "") then
      set Test = ""
      set Test = ($Test lex pdb history)
   endif

   foreach i ($Test)
      $SrcDir/tests/$i
      if ($status == 1) then
         set ERROR = TRUE
      endif
   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP - we are all done now

   flog $Log $RM $Tmp

   set ETime = `$TIMER -b $BeginTime`

   NoteD $Log ""

   if ($ERROR == "TRUE") then
      NoteD $Log ""
      NoteD $Log "FORTRAN TEST ... FAILED ($ETime)"
      NoteD $Log ""
      NoteD $Log "See $Log for details"
   else
      NoteD $Log "FORTRAN TEST ... PASSED ($ETime)"
   endif

endif

NoteD $Log ""
NoteD $Log " --------------------------------------------------------"

set xstatus = 0
if ($ERROR == "TRUE") then
#   set xstatus = 1
endif

Note $Log "Exiting with status $xstatus"

exit($xstatus)

