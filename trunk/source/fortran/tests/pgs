#!/bin/csh -f
#
# PGS - Fortran test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PGS API Test ....................... "

set tst = $1

# check for Fortran compiler before doing Fortran test
set FC = `awk '$1 == "FCompiler" { print $3 }' $IncDir/make-def`
if ("$FC" != "") then
   Note $LogF "Using fortran compiler $FC"
   flog $LogF swhich -1 $FC
   flog $LogF set xstatus = $status
else
   fog $LogF set xstatus = 1
endif

#if (($xstatus == 0) && ("$MPI" == "")) then
if ($xstatus == 0) then
   NoteD $LogF ""
   NoteD $LogF "                    $msg"

   flog $LogF pushd $SrcDir
   flog $LogF $MAKE +na 2 pgs
   set xstatus = $status
   flog $LogF popd

   if ($xstatus != 0) then
      NoteD $LogF ""
      NoteD $LogF "                         Cannot build PGS FORTRAN Test Program"
      flog $LogF rm -f $BinDir/pdftst
      flog $LogF rm -f $BinDir/pdfgts
      flog $LogF rm -f $BinDir/pdfhyts
   else

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# test hand written wrappers
      NoteD $LogF -n "                        manual ......................... "
      Note $LogF ""

      set exe = $BinDir/gsftst
      set rpf = none

      if ("$Valgrind" == "") then
         flog $LogF $CFE $exe -s
         set xstatus = $status
      else
         Note $LogF ""
         ftee $LogF $Valgrind $exe           |& \
         grep "ERROR SUMMARY:"               |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a pgs_manual -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# test generated wrappers
      NoteD $LogF -n "                        auto ........................... "
      Note $LogF ""

      set exe = $BinDir/gsfgts
      set rpf = none

      if ("$Valgrind" == "") then
         flog $LogF $CFE $exe -s
         set xstatus = $status
      else
         Note $LogF ""
         ftee $LogF $Valgrind $exe           |& \
         grep "ERROR SUMMARY:"               |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a pgs_auto -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# contour test
      NoteD $LogF -n "                        contour ........................ "
      Note $LogF ""

      set exe = $BinDir/gsfcts
      set rpf = none

      if ("$Valgrind" == "") then
         flog $LogF $CFE $exe -s
         set xstatus = $status
      else
         Note $LogF ""
         ftee $LogF $Valgrind $exe -s           |& \
         grep "ERROR SUMMARY:"                  |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a pgs_contour -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK part -a pgs -e $nerr "$msg"

exit($status)
