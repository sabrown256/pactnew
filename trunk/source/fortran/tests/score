#!/bin/csh -f
#
# SCORE - SCORE Fortran API tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SCORE API Test ....................."

@ err = 0

NoteD $LogF ""
NoteD $LogF "                    $msg"
Note $LogF ""

set UTime = `$TIMER -r`

flog $LogF pushd $SrcDir
flog $LogF $MAKE scan
set TStatus = $status
flog $LogF popd

set ETime   = `$TIMER -b $UTime`

if ($TStatus != 0) then
   NoteD $LogF ""
   NoteD $LogF "                         ERROR Building SCORE API Tests"
else

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   NoteD $LogF -n "                        lexical scan ................... "
   Note $LogF ""

   set exe = $BinDir/sclsts
   set rpf = ""

   if ("$Valgrind" == "") then
      $CFE $exe $TDir/sclsts.src > sclsts.res
      set xstatus = $status
      diff sclsts.res $RefDir/sclsts > sclsts.res.diff
   else
      ftee $LogF $Valgrind $exe $TDir/sclsts.src                    |& \
      grep "ERROR SUMMARY:"                                         |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

   set rpf = ( $rpf sclsts.res sclsts.res.diff )

   $TEST_CHECK part -a score_lex -e $xstatus -x $exe -l -rpf "$rpf" none

   @ err = $err + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   flog $LogF pushd $SrcDir
   flog $LogF $MAKE -na 2 fha
   set TStatus = $status
   flog $LogF popd

   NoteD $LogF -n "                        hash ........................... "
   Note $LogF ""

   set exe = $BinDir/schtst
   set rpf = ""

   if ("$Valgrind" == "") then
      flog $LogF $CFE $exe
      set xstatus = $status
   else
      ftee $LogF $Valgrind $exe                               |& \
      grep "ERROR SUMMARY:"                                   |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf = ( $rpf $Tmp )
   endif

   if ("$rpf" == "") then
      set rpf = none
   endif

   $TEST_CHECK part -a score_hash -e $xstatus -x $exe -l -rpf "$rpf" none

   @ err = $err + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   if ($HAVE_OPENMP == TRUE) then
      flog $LogF pushd $SrcDir
      flog $LogF $MAKE -na 2 omp
      set TStatus = $status
      flog $LogF popd

      NoteD $LogF -n "                        omp ............................ "
      Note $LogF ""

      set exe = $BinDir/scompt
      set rpf = ""

      if ("$Valgrind" == "") then
         flog $LogF ( echo 2 | $CFE $exe )
         set xstatus = $status
      else
         ftee $LogF ( echo 2 | $Valgrind $exe                    |& \
         grep "ERROR SUMMARY:"                                   |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      if ("$rpf" == "") then
         set rpf = none
      endif

      $TEST_CHECK part -a score_omp -e $xstatus -x $exe -l -rpf "$rpf" none

      @ err = $err + $status

   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK part -a score -e $err "$msg"

exit($status)

