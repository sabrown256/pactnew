#!/bin/csh -f
#
# PANACEA - PANACEA API tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common


NoteD $LogF ""
NoteD $LogF "                    PANACEA API Test ..................."

set UTime = `$TIMER -r`

flog $LogF pushd $SrcDir
flog $LogF $MAKE panacea
set xstatus = $status
flog $LogF popd

if ($xstatus != 0) then
   NoteD $LogF ""
   NoteD $LogF "                         Can't build PANACEA API Tests ($xstatus)"
   set ERROR = "TRUE"
else
   set ERROR = "FALSE"
   NoteD $LogF ""
   NoteD $LogF "                        time history ..................."

   if ("$Valgrind" == "") then
      ftee $LogF $CFE ${BinDir}/pathts -c 2
      set xstatus = $status
   else
      ftee $LogF $Valgrind $BinDir/pathts -c 2   |&  \
      grep "ERROR SUMMARY:"                      |&  \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
	 flog $LogF $RM $Tmp
   endif

   set ETime = `$TIMER -b $UTime`

   if (($xstatus == 254) || ($xstatus == 255)) then
      NoteD $LogF "                        time history ................... omitted"
   else if ($xstatus != 0) then
      NoteD $LogF "                        time history ................... ng ($xstatus/$ETime)"
      set ERROR = "TRUE"
   else
      NoteD $LogF "                        time history ................... ok ($ETime)"
   endif
endif

if ($ERROR == TRUE) then
   NoteD $LogF "                    PANACEA API Test ................... FAILED ($xstatus/$ETime)"
   set Err = 1
else
   NoteD $LogF "                    PANACEA API Test ................... PASSED ($ETime)"
endif

$TEST_CHECK -a panacea -e $Err part

exit($status)
