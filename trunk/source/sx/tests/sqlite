#!/bin/csh -f
#
# SQLITE - SX SQLite DB test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SX PDB SQLite Test ........... "
set exe = $BinDir/sx
set rpf = none
@ nerr  = 0

if (($HAVE_SQLITE == yes) && ($?IBM_BGP == 0)) then

   NoteD $Log ""
   NoteD $Log -n "                    $msg"
   Note $Log ""

   set Ref = $RootDir/sx/tests/ref/sxsqlite
   flog $Log rm -f sxsqlite.log sxsqlite.res foo.sqlite

   set opt = ( -l $RootDir/sx/tests/sxsqlite.scm )
   if ("$Valgrind" == "") then
      flog $Log ($PDBView $opt >& sxsqlite.res)
      set xstatus = $status
      if ($xstatus == 0) then
         if (-f $Ref) then
            flog $Log diff sxsqlite.res $Ref
            set xstatus = $status
         else
            flog $Log cp sxsqlite.res $Ref
         endif
      endif
   else
      ftee $Log $Valgrind $exe -e -p -q $opt     |& \
      grep "ERROR SUMMARY:"                      |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

   flog $Log touch foo.sxsqlite sxsqlite.foo
   set rpf = ( $rpf sxsqlite.* *.sxsqlite )

else
   set xstatus = 0
endif

$TEST_CHECK part -a sqlite -e $xstatus -rpf "$rpf" none

exit($status)
