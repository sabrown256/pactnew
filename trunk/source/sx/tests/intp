#!/bin/csh -f
#
# INTP - SX PDBView mesh interpolation test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

@ err = 0

set msg = "PDBView Mesh Intp Test ......."
set exe = $BinDir/sx

   NoteD $Log ""
   NoteD $Log "                    $msg"

   set lst = (intp1 intp2 intp3)
   foreach p ($lst)

      NoteD $Log -n "                       ${p} ....................."
      Note $Log ""

      set opt = ( $pvopt -l $FilesDir/${p}.scm )
      set io  = ${p}.res
      flog $Log rm -f ${p}*.diff ${p}*.ps

      source ../../../scripts/test-run

      if ($xstatus == 0) then
         set i = ${p}.ps
         if (-f $FilesDir/ref/$i) then
            flog $Log ($CROSS_FE $PSDIFF $i $FilesDir/ref/$i >&! $i.diff)
            @ xstatus = $status
            Note $Log "Test $i:r status $xstatus"
         else
            flog $Log cp $i $FilesDir/ref/$i
         endif
      endif

      if ($xstatus == 0) then
         touch ${p}_foo_.diff
         set rpf = ( $rpf ${p}.res.diff ${p}.res ${p}.ps ${p}*.diff )
         rm ${p}_foo_.diff
      endif

      $TEST_CHECK part -a ${p} -e $xstatus -dt $dt -rpf "$rpf" none
      @ err = $err + $status
   end

$TEST_CHECK part -a intp -e $err -dt $dt -rpf "$rpf" $msg

exit($status)
