#!/bin/csh -f
#
# DIFF - SX PDBDiff test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PDBDiff Test ................. "
set exe = $BinDir/sx

   NoteD $Log ""
   NoteD $Log "                    $msg"
   Note $Log ""

   flog $Log rm -f pdbd*.dat pdbd.res pdbd.res.diff

   flog $Log touch pdbd.res

   flog $Log $Ultra -l $RootDir/sx/tests/pdbdtu.scm
   set xstatus = $status

   if ($xstatus != 0) then
      NoteD $Log " FAILED (-1)"
      NoteD $Log "                       Making data failed"
      @ nerr = $nerr + 1
   else
      set t0 = `$TIMER -r`

      Note $Log "PDBDiff = |$PDBDiff|"
      Note $Log "SCHEME  = |$SCHEME|"
      if ($?PACT == 1) then
         Note $Log "PACT  = |$PACT|"
      else
         Note $Log "PACT  = undefined"
      endif
      if ($?DO_RUN_SUBMIT_ENV == 1) then
         Note $Log "DO_RUN_SUBMIT_ENV = |$DO_RUN_SUBMIT_ENV|"
      endif

# test 1 - all mappings
      NoteD $Log -n "                       All mappings .............. "
      Note $Log ""
      flog $Log ($PDBDiff -v -w pdbda.dat pdbdb.dat >>& pdbd.res)
      set xstatus = $status

      set dt = `$TIMER -b $t0`

      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $Log "omitted"
      else if ($xstatus == 0) then
         NoteD $Log "FAILED ($dt)"
         NoteD $Log "                       Erroneous agreement"
         @ nerr = $nerr + 1
      else
         echo  "Checking for 1 different mapping" >>& pdbd.res
         grep "1 mapping out of 4 differs between pdbda.dat and pdbdb.dat:" pdbda.dat.chk >>& pdbd.res
         if ($status == 0) then
            NoteD $Log "ok"
         else
            NoteD $Log "ng"
            NoteD $Log ""
            ftee $Log cat pdbd.res
            NoteD $Log ""
            @ nerr = $nerr + 1
         endif
      endif

# test 2 - selected mappings
      NoteD $Log -n "                       Selected mappings ......... "
      Note $Log ""
      flog $Log ($PDBDiff -v -w pdbda.dat pdbdb.dat -M 2 4 >&! pdbd.res)
      set xstatus = $status
      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $Log "omitted"
      else if ($xstatus == 0) then
         NoteD $Log "ok"
      else
         NoteD $Log "ng"
         NoteD $Log ""
         ftee $Log cat pdbd.res
         NoteD $Log ""
         @ nerr = $nerr + 1
      endif

      set rpf = ( $rpf pdbd*.dat pdbd.res pdbd*.dat.chk )
   
   endif

$TEST_CHECK part -a diff -e $nerr -rpf "$rpf" "$msg"

exit($status)
