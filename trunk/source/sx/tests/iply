#!/bin/csh -f
#
# IPLY - intersecting polygon test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

#--------------------------------------------------------------------------

#                        INTERSECTING POLYGON TEST

#--------------------------------------------------------------------------

   NoteD $Log ""
   NoteD $Log -n "                    Intersecting Polygon Test .... "
   Note $Log ""

   flog $Log rm -f iply*.diff iply*.ps

   set UTime = `$TIMER -r`
   if ("$Valgrind" == "") then
      flog $Log ($PDBView -l $RootDir/sx/tests/iply.scm >& iply.res)
      set xstatus = $status
   else
      ftee $Log $Valgrind $BinDir/sx -e -p -q -l $RootDir/sx/tests/iply.scm |& \
      grep "ERROR SUMMARY:"                                                 |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      flog $Log $RM $Tmp
   endif

   set ETime = `$TIMER -b $UTime`

   if ("$Valgrind" != "") then
      if ($xstatus != 0) then
         NoteD $Log "FAILED ($xstatus/$ETime)"
         set Err = 1
      else
         NoteD $Log "PASSED ($ETime)"
      endif
   else if (($xstatus == 254) || ($xstatus == 255)) then
      NoteD $Log "OMITTED ($xstatus/$ETime)"
      set xstatus = 0
   else if ($xstatus != 0) then
      NoteD $Log "CRASHED ($xstatus/$ETime)"
      set Err = 1
   else
      flog $Log @ xstatus = 0
      foreach i (iply.ps)
         if (-f $RootDir/sx/tests/ref/$i) then
            flog $Log ($CFE $PSDIFF $i $RootDir/sx/tests/ref/$i >&! $i.diff)
            @ st = $status
            @ xstatus = $xstatus + $st
            Note $Log "Test $i:r status $st"
         endif
      end

      if ($xstatus != 0) then
         NoteD $Log "FAILED ($ETime)"
         set Err = 1
      else
         NoteD $Log "PASSED ($ETime)"
         flog $Log rm -f iply.res.diff iply.res iply.ps iply*.diff
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($Err)
