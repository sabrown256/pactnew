#!/bin/csh -f
#
# IPLY - intersecting polygon test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SX PML Polygon Test .......... "
set exe = $BinDir/sx
set rpf = 0
@ nerr  = 0

   NoteD $Log ""
   NoteD $Log "                    $msg"

   foreach t (iply1 iply2)

      NoteD $Log -n "                       $t ..................... "
      Note $Log ""

      flog $Log rm -f $t*.diff $t*.ps

      set opt = ( -l $RootDir/sx/tests/$t.scm )
      if ("$Valgrind" == "") then
         set bt = `$TIMER -r`
         flog $Log ($PDBView $opt >& $t.res)
         set xstatus = $status
         if ($xstatus == 0) then
            foreach i ($t.ps)
               if (-f $RootDir/sx/tests/ref/$i) then
                  flog $Log ($CFE $PSDIFF $i $RootDir/sx/tests/ref/$i >&! $i.diff)
                  @ st = $status
                  @ xstatus = $xstatus + $st
                  Note $Log "Test $i:r status $st"
               endif
            end
            set et = `$TIMER -b $bt`

            if ($xstatus != 0) then
               NoteD $Log "ng ($et)"
               @ nerr = $nerr + 1
            else
               NoteD $Log "ok ($et)"
            endif
         endif
      else
         ftee $Log $Valgrind $exe -e -p -q $opt    |& \
         grep "ERROR SUMMARY:"                     |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif
   end

$TEST_CHECK part -a iply -e $nerr -rpf "$rpf" "$msg"

exit($status)

