#!/bin/csh -f
#
# WHAT-PYTHON - analyze the platform PYTHON
#        - PY_Exe       the full path to the python executable
#        - PY_Vers      the version of the python executable
#        - PY_Path      addition to path in order to find python executable
#        -              this comes from config files only
#        - PY_DirSrc    pact/dev/$arch/lib/pythond.d         (install to PY_DirDst and PY_DirPy)
#        - PY_DirDst    pact    lib/pythond.d                (install into)
#        - PY_DirPy     python  lib/pythond.d/site-packages  (install into)
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set HostOS = ( `uname -s` )
set HAVE_PYTHON     = FALSE
set HAVE_PY_NUMERIC = FALSE
set HAVE_PY_NUMPY   = FALSE
set PY_Exe          = ""
set PY_Vers         = ""
set PY_CC           = ""
set PY_Cfg          = ""
set PY_Inc          = ""
set PY_Path         = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: what-python [-h] <python>"
           echo "   h   this help message"
           echo ""
           exit(1)
      default:
           set PY_Exe = $1
           breaksw
   endsw
   shift
end

   echo -n "   Python: "

   set found = FALSE
   set PY_Path = $PY_Exe:h

      if (-x "$PY_Exe") then
         $PY_Exe ../python/python-cc.py >& /dev/null
         set LStat1 = $status
         set lpyinc = ( `$PY_Exe ../python/python-inc-dir.py |& cat` )
         if (($LStat1 != 0) || !(-f "$lpyinc/Python.h")) then
            set PY_Exe =  ""
         else
            set pyv = `$PY_Exe -V |& awk '{ print $2 }'`
            if ($#pyv > 2) continue

            set HAVE_PYTHON = TRUE
            set PY_CC  = `$PY_Exe ../python/python-cc.py |& awk '{ print $1 }'`
            set PY_Inc = `$PY_Exe ../python/python-inc-dir.py`
            set PY_Cfg = `$PY_Exe ../python/python-cfg-dir.py`

            echo "$PY_Exe ($pyv)"
            echo "      compiled with $PY_CC"
            echo "      has include directory $PY_Inc"
            set lPyVers = `$PY_Exe ../python/python-version.py`

            set PY_Lib = `echo $PY_Inc | sed 's/include/lib/'`
            if (-d $PY_Inc/Numeric) then
               echo "      has numeric extensions"
               set HAVE_PY_NUMERIC = TRUE
            endif
            if ((-d $PY_Lib/site-packages/NumPy) || (-d $PY_Lib/site-packages/numpy) || \
                (-d $PY_Lib/dist-packages/NumPy) || (-d $PY_Lib/dist-packages/numpy)) then
               echo "      has numpy extensions"
               set HAVE_PY_NUMPY = TRUE
            endif
            if (($HAVE_PY_NUMERIC == FALSE) && ($HAVE_PY_NUMPY == FALSE)) then
               echo "      does not have numeric extensions"
            endif

            if ((($HostOS == "Linux") || ($HostOS == "Darwin") || ($HostOS == "FreeBSD")) && ($PY_CC == cc)) then
               set PY_CC =  gcc
            endif
            set found = TRUE
         endif
      endif

   if ($found == FALSE) then
      echo "none recognizable"
      set HAVE_PYTHON     = FALSE
      set HAVE_PY_NUMERIC = FALSE
      set HAVE_PY_NUMPY   = FALSE
      set PY_Path         = ""
      set PY_Exe          = ""
      set PY_CC           = ""
      set PY_Cfg          = ""
      set PY_Inc          = ""
      set PY_Vers         = ""
   endif

   set lbase = /usr/local/apps/pact

   if ($HAVE_PYTHON == TRUE) then
      if (`expr "$lPyVers" : '.*\..*\..*'` > 0) then
         set lPyVers = $lPyVers:r
      endif
      set PY_Vers = $lPyVers
      set PY_Root = $lbase
      set PY_Head = python${PY_Vers}/pact
      if ("$PY_Path" != "") then
         set PY_Root = $PY_Exe
         set PY_Root = $PY_Root:h
         set PY_Root = $PY_Root:h
         set PY_Head = python${PY_Vers}/site-packages/pact
      endif
   endif

# export the results
echo "HAVE_PYTHON     = $HAVE_PYTHON"
echo "HAVE_PY_NUMERIC = $HAVE_PY_NUMERIC"
echo "HAVE_PY_NUMPY   = $HAVE_PY_NUMPY"
echo "PY_Path         = $PY_Path"
echo "PY_Exe          = $PY_Exe"
echo "PY_CC           = $PY_CC"
echo "PY_Cfg          = $PY_Cfg"
echo "PY_Inc          = $PY_Inc"
echo "PY_Vers         = $PY_Vers"

exit(0)

