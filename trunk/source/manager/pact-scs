#
# PACT-SCS - define the Source Control System being used
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set SCSVersion = "3.0"
set SCSRelease = "LLNL-CODE-422942"

# be sure to define the SCSOpt if undefined by this time
if ($?SCSOpt == 0) then
   set SCSOpt = ""
endif

set repo     = `$ScrDir/scs-meta -r`
set repohost = ""
set SCSDir   = `scs-meta -n`
set SCSExe   = `scs-meta -x`
set repohost = `scs-meta -m`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# handle the local repository case
if ("$repohost" == "--local--") then
   set Lock = commit.lock
   if (-d $repo) then
      set Lock = $repo/commit.lock
   endif

# handle the remote repository case
else
   set Stage = "$MngDir/stage"
   set Lock  = "$Stage/commit.lock"
endif

# make the Message
set Host    = `uname -n`
set WhoIAm  = $USER

# for commits
if ($?TagOnly == 1) then
   if ($TagOnly == TRUE) then
      set Date = `/bin/date '+T%y_%m_%d_%H_%M_%S'`
   else
      set Date = `/bin/date '+D%y_%m_%d_%H_%M_%S'`
   endif

# for other repository interactions
else
   set Date = `/bin/date '+%y_%m_%d_%H_%M_%S'`
endif

if ($?TagAnn == 1) then
   set Message = (\'<$WhoIAm $TagAnn $Date|Remote $Host> $argv\')
else
   set Message = (\'<$WhoIAm|$Date|Remote $Host> $argv\')
endif

