#!/bin/csh -f
#
# VERIFY-CFG - verify that the compiled codes is consistent with
#            - the configuration specifications
#

unalias *

set sys_id  = `../scripts/cfgman use`
set sys_dir = ""
set Err     = 0

while ($#argv > 0)
   switch ($1)
      case -b:
           shift
           set sys_dir = $1
           breaksw
      case -h:
           echo ""
           echo "Usage: verify-cfg [-b <path>] [-h] [<sys_id>]"
           echo "   b         the full path to the configuration to be verified"
           echo "   h         this help message"
           echo "   <sys_id>  the configuration to be verified"
           echo ""
           exit(1)
      default:
           set sys_id = $1
           breaksw
   endsw
   shift
end

if ("$sys_dir" == "") then
   set sys_dir = $cwd:h/dev/$sys_id
endif

set CfgLog = $sys_dir/log/config
set BinDir = $sys_dir/bin
set IncDir = $sys_dir/include
set EtcDir = $sys_dir/etc

source ../scripts/functions-pact.csh

InitLog Log $cwd/log.verify-cfg.$sys_id

set Cfg = `awk '$2 ~ /SCCONFIG.H/ { print $7 }' $IncDir/scconfig.h`

NoteD $Log ""
NoteD $Log "Verifying configuration $Cfg in $sys_dir"

# Debug/Optimize
Separator $Log
set CfgDO = `grep "Building absolutely" $CfgLog | awk '{ print $3 }'`
if ("$CfgDO" == debuggable) then
   set Req = `grep "Command: setenv CC_Optimize" $CfgLog | sed 's/Command: setenv CC_Optimize //'`
   set Got = `grep "CC_Optimize" $EtcDir/make-def | tail -n 1 | sed 's/CC_Optimize = //'`
   if ("$Req" == "$Got") then
      NoteD $Log "   debuggable ... yes"
   else
      NoteD $Log "   debuggable ... no"
      set Err = 1
   endif
else if ("$CfgDO" == optimized) then
   set Req = `grep "Command: setenv CC_Debug" $CfgLog | sed 's/Command: setenv CC_Debug //'`
   set Got = `grep "CC_Debug" $EtcDir/make-def | tail -n 1 | sed 's/CC_Debug = //'`
   if ("$Req" == "$Got") then
      NoteD $Log "   optimized .... yes"
   else
      NoteD $Log "   optimized .... no"
      set Err = 1
   endif
else
   Note $Log "   mixed debug/optimize"
endif

# PTHREAD
Separator $Log
flog $Log grep "include pthread" $CfgLog 
if ($status == 0) then
   NoteD $Log -n "   pthread ...... "
   Note $Log ""
   flog $Log ( grep '#define THREADING' $IncDir/scconfig.h | grep pthread )
   set LStat = $status
   if ($LStat == 0) then
      NoteD $Log "yes"
   else
      NoteD $Log "no"
      set Err = 2
   endif
endif

# OPENMP
Separator $Log
flog $Log (grep "include" $CfgLog | grep openmp)
if ($status == 0) then
   NoteD $Log -n "   openmp ....... "
   Note $Log ""
   flog $Log ( grep '#define THREADING' $IncDir/scconfig.h | grep openmp )
   set LStat = $status
   if ($LStat == 0) then
      NoteD $Log "yes"
   else
      NoteD $Log "no"
      set Err = 3
   endif
endif

# MPI
Separator $Log
flog $Log grep "include mpi" $CfgLog 
if ($status == 0) then
   NoteD $Log -n "   mpi .......... "
   Note $Log ""
   flog $Log grep '#define HAVE_MPI' $IncDir/scconfig.h
   set LStat = $status
   if ($LStat == 0) then
      NoteD $Log "yes"
   else
      NoteD $Log "no"
      set Err = 4
   endif
endif

# PYTHON
Separator $Log
flog $Log grep "include python" $CfgLog 
if ($status == 0) then
   NoteD $Log -n "   python ....... "
   Note $Log ""
   set PyDir = `grep "setenv PYTHONPATH" $CfgLog | awk '{ print $3 }'`
   Note $Log "PyDir = |$PyDir|"
   if (-d "$PyDir/pact") then
      flog $Log ls -l $PyDir/pact
      if ((-e "$PyDir/pact/libpypdb.so") && (-e "$PyDir/pact/libpypgs.so")) then
         NoteD $Log "yes"
      else
         NoteD $Log "no"
         set Err = 5
      endif
   else
      Note $Log "No directory |$PyDir|"
      NoteD $Log "no"
      set Err = 5
   endif
endif

# SHARED
Separator $Log
flog $Log grep "Building shared libraries" $CfgLog
set LStat = $status
if ($LStat == 0) then
   NoteD $Log -n "   shared ....... "
   Note $Log ""
   set build_shared = ( `pact +l -info MAKE_Shared_Libs` )
   if ("$build_shared[$#build_shared]" == TRUE) then
      NoteD $Log "yes"
   else
      NoteD $Log "no"
      set Err = 6
   endif
endif

Separator $Log

if ($Err != 0) then
   NoteD $Log "Code is NOT consistent with configuration"
else
   NoteD $Log "Code is consistent with configuration"
endif

NoteD $Log ""

exit($Err)

