#!/bin/csh -f
#
# OS - analyze the platform OS
#    - Computes: HSY_OS_Type, HSY_OS_Name, AF_VERSION_OSX
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

if ($?Log == 0) then
   setenv Log $ALog
endif

Note $ALog "----- analyze/os -----"
Note $ALog "Analyze: platform OS"
Note $ALog ""

set los = ( `uname -s` )

dbmget $ALog AF_VERSION_OSX  =\? ""^      \
             HSY_OS_Name     =\? "$los"^  \
             HSY_RootInc
             
unset los

if ($?PSY_Arch == 0) then
   setenv PSY_Arch `../scripts/system-id`
endif

# remove parens - they confuse shells later on
fexvar $ALog HSY_OS_Release uname -r @b sed 's/(/,/g' @o sed 's/)/,/g'

Note $ALog "HSY_RootInc   = $HSY_RootInc"
if ($CROSS_COMPILE == MINGW32) then
   setenv HSY_OS_Type     MSW
   setenv HSY_OS_Name     WIN32
   setenv HSY_OS_Release  mingw32

else if ($CROSS_COMPILE == MINGW64) then
   setenv HSY_OS_Type     MSW
   setenv HSY_OS_Name     WIN64
   setenv HSY_OS_Release  mingw64

else if (("$HSY_OS_Name" == "Windows_NT") || \
    (("$HSY_OS_Name" == "Linux") && (-e $HSY_RootInc/termio.h)) || \
    ($?IBM_BGP == 1) || ($?IBM_BGQ == 1) || \
    (-e $HSY_RootInc/sys/termio.h)) then
   setenv HSY_OS_Type "SYSV"

else
   setenv HSY_OS_Type "BSD"
endif
Note $ALog "HSY_OS_Name    = $HSY_OS_Name"
Note $ALog "HSY_OS_Release = $HSY_OS_Release"
Note $ALog "HSY_OS_Type    = $HSY_OS_Type"

if ($HSY_OS_Name == Darwin) then
   fexvar $ALog AF_VERSION_OSX echo $PSY_Arch @o sed 's/osx-10\.//' @o sed 's/-.*//'
endif

# there are systems that put out garbage if this is on a single echo (AIX)
NoteD $ALog "   Operating System: $HSY_OS_Name $HSY_OS_Release ($HSY_OS_Type)"

setenv HSY_OS_Type USE_$HSY_OS_Type

# export results
dbmset $ALog HSY_OS_Type     = $HSY_OS_Type^      \
             HSY_OS_Release  = $HSY_OS_Release^   \
             HSY_OS_Name     = $HSY_OS_Name

exit(0)

