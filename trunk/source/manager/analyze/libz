#!/bin/csh -f
#
# LIBZ - analyze platform libz
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/libz -----"
Note $ALog "Analyze: libz"
Note $ALog ""

dbmget $ALog HSY_RootLib^                 \
             Z_Inc     =\? ""^            \
             Z_Lib     =\? ""^            \
             Z_RPath   =\? $HSY_RootLib^  \
             Z_Path    =\? /usr^          \
             Cfe_CC_Exe^                  \
             Cfe_CC_Flags^                \
             Cfe_LD_Flags^                \
             Cfe_LD_Lib^                  \
             Cfe_LD_RPath^                \
             PACT_CC_VERSION^             \
             PSY_ASRoot^                  \
             LD_Lib^                      \
             LIB^                         \
             LibM_Lib^                    \
             RPATH

# if we have explicitly said we do not want libZ do NOT detect it
if ("$Z_Path" == none) then
   NoteD $ALog "      no libz (by request)"
   setenv HAVE_ZLIB ""

else

   set UTime = `timer -r`

# write a little Z test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <zlib.h>
int main(int c, char **v)
{int rv;
 gzFile fp;
 fp = gzopen("$Tmp.gz", "w");
 rv = gzclose(fp);
 return(rv);}
EOF

   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($Z_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/lib )
   end
   set lIncDirs = ( $lIncDirs $PSY_ASRoot/include /usr/local/include $HSY_RootInc )
   set lLibDirs = ( $lLibDirs $PSY_ASRoot/lib /usr/local/lib /usr/$LIB /$LIB )

   set Implicit = TRUE
   set lHdrs    = ( zlib.h )
   set ASList   = ( a so )
   set lLib     = z
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `var-list -l $RPATH` )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib $LibM_Lib )

   source $PSY_AnaDir/find-package

   setenv Z_Lib "-lz"
   if (("$PckLib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has libz ($PckLib)"

      if ("$PckPath" != "") then
         flog $ALog setenv RPATH    ${RPATH}:${PckPath}
      endif
      flog $ALog setenv HAVE_ZLIB   TRUE
   else
      NoteD $ALog "      no libz"
      flog $ALog setenv HAVE_ZLIB   ""
   endif

   setenv Z_Inc   "$PckInc"
   setenv Z_Lib   "$PckLib"
   setenv Z_RPath "$PckPath"

# NOTE: used Z_Path to find libs, incs - there are no executables
   setenv Z_Path ""

   set ETime = `timer -b $UTime`

# export the results
   dbmset $ALog Z_Inc   = $Z_Inc^    \
                Z_Lib   = $Z_Lib^    \
                Z_RPath = $Z_RPath^  \
                Z_Path  = $Z_Path^   \
                RPATH   = $RPATH

endif

dbexp HAVE_ZLIB

exit(0)

