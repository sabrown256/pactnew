#!/bin/csh -f
#
# LIBZ - analyze platform libz
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: libz"

# vanilla place to look if previously undefined
dbini MD_Inc    ""
dbini MD_Lib    ""
dbini MD_Pck    ""
dbini Z_Inc     ""
dbini Z_Lib     ""
dbini Z_RPath   $RootLib
dbini Z_Path    /usr
#SafeEnv MD_Inc    ""
#SafeEnv MD_Lib    ""
#SafeEnv MD_Pck    ""
#SafeEnv Z_Inc     ""
#SafeEnv Z_Lib     ""
#SafeEnv Z_RPath   $RootLib
#SafeEnv Z_Path    /usr

# if we have explicitly said we do not want libZ do NOT detect it
if ("$Z_Path" == none) then
   NoteD $ALog "      no libz (by request)"
   setenv HAVE_ZLIB ""

else

   set UTime = `timer -r`

# write a little Z test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <zlib.h>
int main(int c, char **v)
{int rv;
 gzFile fp;
 fp = gzopen("$Tmp.gz", "w");
 rv = gzclose(fp);
 return(rv);}
EOF

   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($Z_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/lib )
   end
   set lIncDirs = ( $lIncDirs $DAS_ROOT/include /usr/local/include $RootInc )
   set lLibDirs = ( $lLibDirs $DAS_ROOT/lib /usr/local/lib /usr/$LIB /$LIB )

   set Implicit = TRUE
   set lHdrs    = ( zlib.h )
   set ASList   = ( a so )
   set lLib     = z
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `echo $RPATH | sed 's|:| |g'` )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib $LibM_Lib )

   source $AnaDir/find-package

   setenv Z_Lib "-lz"
   if (("$PckLib" != "") && ("$Z_Lib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has libz ($PckLib)"

      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         flog $ALog setenv MD_Inc "$PckInc $MD_Inc"
      endif

      if ("$PckPath" != "") then
         flog $ALog setenv RPATH    ${RPATH}:${PckPath}
      endif
      flog $ALog setenv MD_Lib      "$MD_Lib $PckLib"
      flog $ALog setenv MD_Pck      "Z $MD_Pck"
      flog $ALog setenv HAVE_ZLIB   TRUE
   else
      NoteD $ALog "      no libz"
      flog $ALog setenv HAVE_ZLIB   ""
   endif

   setenv Z_Inc   "$PckInc"
   setenv Z_Lib   "$PckLib"
   setenv Z_RPath "$PckPath"

# NOTE: used Z_Path to find libs, incs - there are no executables
   setenv Z_Path ""

   set ETime = `timer -b $UTime`

# export the results
   dbexp MD_Inc
   dbexp MD_Lib
   dbexp MD_Pck
   dbexp Z_Inc
   dbexp Z_Lib
   dbexp Z_RPath
   dbexp Z_Path
#   SetParent MD_Inc
#   SetParent MD_Lib
#   SetParent MD_Pck
#   SetParent Z_Inc
#   SetParent Z_Lib
#   SetParent Z_RPath
#   SetParent Z_Path

   SetParent RPATH

endif

dbexp HAVE_ZLIB
#SetParent HAVE_ZLIB

exit(0)

