#!/bin/csh -f
#
# READLINE - analyze platform READLINE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: readline"

# vanilla place to look if previously undefined
dbini MD_Inc     ""
dbini MD_Lib     ""
dbini MD_Pck     ""
dbini RL_Inc    ""
dbini RL_Lib    ""
dbini RL_RPath  /usr/${LIB}
dbini RL_Path   /usr
dbini HAVE_READLINE   FALSE

SafeEnv HAVE_READLINE   FALSE

# if we have explicitly said we do not want READLINE
# do NOT detect it
if ("$RL_Path" == none) then
   NoteD $ALog "      no readline (by request)"

else

   set incl = ""
   set pthl = ""
   set libl = ""
   set spkl = ""

   set UTime = `timer -r`

# write a little readline test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdio.h>
#include <readline/readline.h>
#include <readline/history.h>
int main(int c, char **v)
{int rv;
 char *(*f)(const char *p);
 rl_already_prompted = 0;
 rl_save_prompt();
 f  = readline;
 rv = (f == NULL);
 rl_restore_prompt();
 rl_clear_message();
 printf("%s 0x%04x\n", rl_library_version, rl_readline_version);
 return(rv);}
EOF

   setenv HAVE_READLINE FALSE

   set lvers = none
   set llib  = ""
   set libl  = ( -lreadline -lcurses )
   set opt   = ""
   set opt   = ( $opt $CC_Flags $Cfe_LD_Flags $Cfe_LD_RPath $libl )
   flog $ALog $CC_Exe $Tmp.c -o $Tmp $opt
   if ($status == 0) then
      flog $ALog $CFE $Tmp
      if ($status == 0) then
         set inf   = ( `$CFE $Tmp` )
         set lvers = $inf[1]
         setenv HAVE_READLINE TRUE
         flog $ALog set llib = ( $libl )
         flog $ALog setenv MD_Pck        "RL $MD_Pck"
         flog $ALog $RM $Tmp.c $Tmp
      endif
   endif
   unset opt
   unset libl

   if ("$llib" != "") then
      flog $ALog setenv HAVE_READLINE   TRUE
      NoteD $ALog "      has readline $lvers ($llib)"
   else
      NoteD $ALog "      no readline"
   endif

#   setenv MD_Lib   "$llib $MD_Lib"
   setenv RL_Inc   ""
   setenv RL_Lib   "$llib"
   setenv RL_RPath ""

# NOTE: used RL_Path to find libs, incs - there are no executables
   setenv RL_Path  ""

# export the results
   dbexp MD_Lib
   dbexp MD_Pck
   dbexp RL_Inc
   dbexp RL_Lib
   dbexp RL_RPath
   dbexp RL_Path

   SetParent RPATH

endif

dbexp HAVE_READLINE

exit(0)

