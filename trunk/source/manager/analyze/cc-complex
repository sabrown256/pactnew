#!/bin/csh -f
#
# CC-COMPLEX - analyze the handling of C99 complex
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/cc-complex -----"
Note $ALog "Analyze: C90 complex"
Note $ALog ""

dbmget $ALog HAVE_ANSI_C9X_COMPLEX =\?  TRUE^  \
             HAVE_COMPLEX_VA_ARG   =\? FALSE^  \
             PACT_CC_VERSION

Note $ALog "HAVE_ANSI_C9X_COMPLEX = $HAVE_ANSI_C9X_COMPLEX"

if ($HAVE_ANSI_C9X_COMPLEX == TRUE) then

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# determine whether there is C99 complex support

   Separator $ALog

   cat << EOF >! $Tmp.c
#include <complex.h>
int main(int c, char **v)
   {
#ifdef complex
    return(0);
#else
    return(1);
#endif
    }
EOF

   flog $ALog $lCC -c $Tmp.c -o $Tmp.o
   set xstatus = $status
   if ($xstatus == 0) then
      NoteD $ALog "      has C99 complex support"
   else
      Note $ALog "Failed ($xstatus): $lCC -c $Tmp.c -o $Tmp.o"
      flog $ALog cat $Tmp.c

      flog $ALog setenv HAVE_ANSI_C9X_COMPLEX FALSE
      NoteD $ALog "      no C99 complex support"
   endif

   flog $ALog rm -f $Tmp $Tmp.c $Tmp.o

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# determine whether the compiler supports complex in va_arg

   Separator $ALog

   cat << EOF >! $Tmp.c
#include <math.h>
#include <complex.h>
#include <stdarg.h>
#undef complex
#define complex      double _Complex 
int f(int n, ...)
   {int rv;
    complex y, z;
    y = 1.0 + 2.0*I;
    va_list __a__;
    va_start(__a__, n);
    z = va_arg(__a__, complex);
    va_end(__a__);
    rv = (z == y);
    return(rv);}
int main(int c, char **v)
   {int rv;
    complex z;
    z = 1.0 + 2.0*I;
    rv = f(1, z);
    return(rv);}
EOF

   flog $ALog cat $Tmp.c

   set msg = "no complex in va_arg"

   flog $ALog $lCC -c $Tmp.c -o $Tmp
   set xstatus = $status
   if ($xstatus == 0) then
      flog $ALog $Tmp
      if ($status == 1) then
         flog $ALog setenv HAVE_COMPLEX_VA_ARG TRUE
         set msg = "has complex in va_arg"
      endif
   else
      Note $ALog "Failed ($xstatus): $lCC -c $Tmp.c -o $Tmp.o"
   endif

   NoteD $ALog "      $msg"

   flog $ALog rm -f $Tmp $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

else
   NoteD $ALog "      no C99 complex support (by request)"
endif

# export the result
dbmset $ALog HAVE_ANSI_C9X_COMPLEX = $HAVE_ANSI_C9X_COMPLEX^ \
             HAVE_COMPLEX_VA_ARG   = $HAVE_COMPLEX_VA_ARG

source $Me:h/post-common

exit(0)
