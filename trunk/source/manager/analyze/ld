#!/bin/csh -f
#
# LD - analyze the platform linker
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: ld"

NoteD $ALog -n "   Ld: "
Note $ALog ""

SafeEnv LD_Exe         ""
SafeEnv LD_Flags       ""
SafeEnv LD_Lib         ""
SafeEnv LD_Shared      ""
SafeEnv Cfg_LD_Flags   ""
SafeEnv Cfg_LD_RPath   ""
SafeEnv Cfe_LD_Flags   ""
SafeEnv Cfe_LD_Lib     ""
SafeEnv Cfe_LD_RPath   ""
SafeEnv MDG_Lib        ""
SafeEnv DP_Lib         ""

if (!(-x "$LD_Exe")) then
   if (-x `$Which ld`) then
      flog $ALog setenv LD_Exe `$Which ld`
   else
      flog $ALog setenv LD_Exe ""
   endif
endif

cat << EOF >! $Tmp.c
int main(int c, char **v)
{return(0);}
EOF

Note $ALog ""
Note $ALog "Checking for -rpath flag"
flog $ALog $Cfe_CC_Exe -c $Tmp.c -o $Tmp.o
if ($HostOS == AIX) then
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -bsvr4 -R $cwd
   set LStat = $status
else
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -rpath $cwd
   set LStat = $status
endif
if ($LStat == 0) then
   NoteD $ALog "$LD_Exe (has -rpath)"
   setenv HaveRPATH TRUE
else
   NoteD $ALog "$LD_Exe (no -rpath)"
   setenv HaveRPATH FALSE
endif

flog $ALog $RM $Tmp $Tmp.c $Tmp.o

SafeEnv Shared_LD_Exe "$LD_Exe"

# capture current notion of rpath
if ($?Cfg_RPATH == 1) then
   set lrpath = `../analyze/rpath -o rpath`
   Note $ALog "Set config rpath flags to: $lrpath"
   flog $ALog setenv LD_RPath      "$lrpath"
   flog $ALog setenv Cfg_LD_RPath  "$lrpath"
   Note $ALog ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($CROSS_COMPILE == "FALSE") then
   flog $ALog setenv Cfe_LD_Flags  "$Cfg_LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$Cfg_LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$Cfg_LD_RPath"
else
   flog $ALog setenv Cfe_LD_Flags  "$LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$LD_RPath"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# export the results
SetParent LD_Exe
SetParent LD_Flags
SetParent LD_Lib
SetParent LD_Shared
SetParent Cfg_LD_Flags
SetParent Cfg_LD_RPath
SetParent Cfe_LD_Flags
SetParent Cfe_LD_Lib
SetParent Cfe_LD_RPath
SetParent MDG_Lib
SetParent DP_Lib
SetParent Shared_LD_Exe
SetParent HaveRPATH

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

source $AnaDir/dl

exit(0)

