#!/bin/csh -f
#
# SIGNAL - analyze the handling of signals
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: signal"

dbini HAVE_POSIX_SIGNALS    FALSE

set vl = ""
set vl = ( $vl Cfe_CC_Exe Cfe_CC_Flags )
set vl = ( $vl Cfe_LD_Flags Cfe_LD_Lib Cfe_LD_RPath )
source $MngDir/write/import-db

set sig = ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# check for POSIX signal handling

cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <signal.h>
int main(int c, char **v)
{int rv;
 sigset_t os;
 rv = sigprocmask(SIG_BLOCK, NULL, &os);
 return(rv);}
EOF

set opt = ""
set opt = ( $opt $Cfe_CC_Flags )
set opt = ( $opt $Cfe_LD_Flags $Cfe_LD_RPath )

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $opt
if ($status == 0) then
   setenv HAVE_POSIX_SIGNALS TRUE
   set sig = ( $sig posix )
#   NoteD $ALog -n " posix"
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# check for SYSV signal handling

cat << EOF >! $Tmp.c
#include <signal.h>
int main(int c, char **v)
{int rv;
 rv = sighold(SIGINT);
 return(rv);}
EOF

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $opt
if ($status == 0) then
   set sig = ( $sig sysv )
#   NoteD $ALog -n " sysv"
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# check for BSD signal handling

cat << EOF >! $Tmp.c
#include <signal.h>
int main(int c, char **v)
{int rv;
 rv = sigblock(sigmask(SIGINT));
 return(rv);}
EOF

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $opt
if ($status == 0) then
   set sig = ( $sig bsd )
#   NoteD $ALog -n " bsd"
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ("$sig" == "") then
   NoteD $ALog "      no signal handling"
else
   NoteD $ALog "      has $sig signal handling"
endif

# export the results
dbexp HAVE_POSIX_SIGNALS

exit(0)

