#!/bin/csh -f
#
# SIZE - analyze the integer type sizes
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "      Find Integer Sizes"

dbini INT4  int
dbini INT8  long

set vl = ""
set vl = ( $vl CFE Cfe_CC_Exe )
source $MngDir/write/import-db

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

cat << EOF >! $Tmp.c
#include <stdio.h>
#include <stdlib.h>
int main(int c, char **v)
{long long i;
 i = strtoll("3", NULL, 10);
 return(0);}
EOF

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $ccopt
if ($status != 0) then
   setenv NO_LONG_LONG TRUE
else
   setenv NO_LONG_LONG FALSE
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# INT4

cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 4)
    printf("int\n");
 else if (sizeof(long) == 4)
    printf("long\n");
 else
    printf("unknown\n");
return(0);}
EOF

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $ccopt
if ($status == 0) then
   fexec $ALog $CFE $Tmp @o2 @e3 vw:INT4 @ fw:/dev/null
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# INT8

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($NO_LONG_LONG == TRUE) then
   goto VERSIONA
else
   goto VERSIONB
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

VERSIONA:

   cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 8)
    printf("int\n");
 else if (sizeof(long) == 8)
    printf("long\n");
 else
    printf("unknown\n");
return(0);}
EOF

   goto COMPILE

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

VERSIONB:

   cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 8)
    printf("int\n");
 else if (sizeof(long) == 8)
    printf("long\n");
 else if (sizeof(long long) == 8)
    printf("long long\n");
 else
    printf("unknown\n");
return(0);}
EOF

   goto COMPILE

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

COMPILE:

flog $ALog $Cfe_CC_Exe $Tmp.c -o $Tmp $ccopt
if ($status == 0) then
   fexec $ALog $CFE $Tmp @o2 @e3 vw:INT8 @ fw:/dev/null
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set vl = ( INT4 INT8 )

source $MngDir/write/import-db

Note $ALog ""
Note $ALog "Integer size info:"
Note $ALog "   INT4         = $INT4"
Note $ALog "   INT8         = $INT8"
Note $ALog "   NO_LONG_LONG = $NO_LONG_LONG"
Note $ALog ""

dbexp NO_LONG_LONG


