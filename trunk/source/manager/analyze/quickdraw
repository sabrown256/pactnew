#!/bin/csh -f
#
# QUICKDRAW - analyze OSX platform for Quickdraw
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

dbmget $ALog MDG_Inc          =\? ""^        \
             MDG_Lib          =\? ""^        \
             MDG_Pck          =\? ""^        \
             QD_Inc           =\? ""^        \
             QD_Lib           =\? ""^        \
             QD_RPath         =\? $RootLib^  \
             QD_Path          =\? /usr^      \
             GRAPHICS_Windows =\? ""^        \
             GRAPHICS_Devices =\? ""^        \
             GRAPHICS_Flags   =\? ""^        \
             Std_UseQD        =\? FALSE^     \
             Cfg_CC_Exe       =\? cc^        \
             CC_Exe^                         \
             LD_Flags^                       \
             OS_Name

if ($OS_Name == Darwin) then

   Separator $Log
   Note $Log "Analyze: Quickdraw"

   set UTime = `timer -r`

# write a little QD test
   cat << EOF >! $Tmp.c
#include <ApplicationServices/ApplicationServices.h>
#include <Carbon/Carbon.h>
int main(int c, char **v)
{FontInfo font_data;
 GrafPtr port;
 InitCursor();
 GetFontInfo(&font_data);
 TextSize(12);
 SetPort(port);
 MoveTo(10, 20);
 return(0);}
EOF

   set opt = ""
   set opt = ( $opt $LD_Flags $LD_RPath )
   if ($CC_Exe !~ pgc*) then
      set opt = ( $opt -framework Carbon )
   endif
   set opt = ( $opt $Cfg_LD_Lib )

   flog $ALog $Cfg_CC_Exe $Tmp.c -o $Tmp $opt
   set xstatus = $status
   if ($xstatus == 0) then
      if ($Std_UseQD == TRUE) then
         NoteD $ALog "      has Quickdraw (Carbon)"

         setenv GRAPHICS_Windows             "QD"
         setenv GRAPHICS_Devices  "$GRAPHICS_Devices QD"
         setenv GRAPHICS_Flags     "$GRAPHICS_Flags -framework Carbon"

         setenv MDG_Pck          "$MDG_Pck QD"

         setenv QD_Inc           "-framework Carbon"
         setenv QD_Lib           ""
         setenv QD_RPath         ""

      else
         NoteD $ALog "      no Quickdraw (by request)"
      endif
   else
      NoteD $ALog "      no Quickdraw"
      setenv Std_UseQD   FALSE
   endif

   set ETime = `timer -b $UTime`

# NOTE: used QD_Path to find libs, incs - there are no executables we need
   if ("$QD_Path" == /usr) then
      setenv QD_Path  ""
   endif

# export the results
   dbmset $ALog MDG_Inc          = $MDG_Inc^           \
                MDG_Lib          = $MDG_Lib^           \
                MDG_Pck          = $MDG_Pck^           \
                QD_Inc           = $QD_Inc^            \
                QD_Lib           = $QD_Lib^            \
                QD_RPath         = $QD_RPath^          \
                QD_Path          = $QD_Path^           \
                GRAPHICS_Windows = $GRAPHICS_Windows^  \
                GRAPHICS_Devices = $GRAPHICS_Devices^  \
                GRAPHICS_Flags   = $GRAPHICS_Flags

endif

dbexp Std_UseQD

exit(0)

