#!/bin/csh -f
#
# QUICKDRAW - analyze OSX platform for Quickdraw
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

dbini MDG_Inc           ""
dbini MDG_Lib           ""
dbini MDG_Pck           ""
dbini QD_Inc            ""
dbini QD_Lib            ""
dbini QD_RPath          $RootLib
dbini QD_Path           /usr
dbini GRAPHICS_Windows  ""
dbini GRAPHICS_Devices  ""
dbini GRAPHICS_Flags    ""
dbini Std_UseQD         FALSE
dbini Cfg_CC_Exe        "cc"

envini OS_Name           `uname -s`
envini AnaDir           analyze
envini ALog             $cwd/cfg-$System/log.analyze

if ($OS_Name == Darwin) then

   Separator $Log
   Note $Log "Analyze: Quickdraw"

   set UTime = `timer -r`

# write a little QD test
   cat << EOF >! $Tmp.c
#include <ApplicationServices/ApplicationServices.h>
#include <Carbon/Carbon.h>
int main(int c, char **v)
{FontInfo font_data;
 GrafPtr port;
 InitCursor();
 GetFontInfo(&font_data);
 TextSize(12);
 SetPort(port);
 MoveTo(10, 20);
 return(0);}
EOF

   set opt = ""
   set opt = ( $opt $LD_Flags $LD_RPath )
   if ($CC_Exe !~ pgc*) then
      set opt = ( $opt -framework Carbon )
   endif
   set opt = ( $opt $Cfg_LD_Lib )

   flog $ALog $Cfg_CC_Exe $Tmp.c -o $Tmp $opt
   set xstatus = $status
   if ($xstatus == 0) then
      if ($Std_UseQD == TRUE) then
         NoteD $ALog "      has Quickdraw (Carbon)"

         setenv GRAPHICS_Windows             "QD"
         setenv GRAPHICS_Devices  "$GRAPHICS_Devices QD"
         setenv GRAPHICS_Flags     "$GRAPHICS_Flags -framework Carbon"

         setenv MDG_Pck          "$MDG_Pck QD"

         setenv QD_Inc           "-framework Carbon"
         setenv QD_Lib           ""
         setenv QD_RPath         ""

      else
         NoteD $ALog "      no Quickdraw (by request)"
      endif
   else
      NoteD $ALog "      no Quickdraw"
      setenv Std_UseQD   FALSE
   endif

   set ETime = `timer -b $UTime`

# NOTE: used QD_Path to find libs, incs - there are no executables we need
   if ("$QD_Path" == /usr) then
      setenv QD_Path  ""
   endif

# export the results
   dbexp MDG_Inc
   dbexp MDG_Lib
   dbexp MDG_Pck
   dbexp QD_Inc
   dbexp QD_Lib
   dbexp QD_RPath
   dbexp QD_Path
   dbexp GRAPHICS_Windows
   dbexp GRAPHICS_Devices
   dbexp GRAPHICS_Flags

endif

dbexp Std_UseQD

exit(0)

