#!/bin/csh -f
#
# CC-GLIBC - analyze GNU LibC version
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/cc-glibc -----"
Note $ALog "Analyze: glibc version"
Note $ALog ""

dbmget $ALog HAVE_GNU_LIBC_6 =\? FALSE

setenv AF_VERSION_GLIBC ""

set GCC = `where gcc |& head -n 1`
if (-x "$GCC") then

   cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
   {char *s;
    extern char *gnu_get_libc_version(void);

    s = gnu_get_libc_version();
    printf("%s\n", s);

    return(0);}
EOF

   flog $ALog gcc $Tmp.c -o $Tmp -lc
   set xstatus = $status   
   set ok = FALSE
   if (-x $Tmp) then
      set lvers = `$Tmp`

      fexvars $ALog t echo "$lvers" @o sed 's/\./ /g'
      if ($#t > 1) then
         @ MajorV = $t[1]
         @ MinorV = $t[2]
   
         if (($MajorV >= 2) && ($MinorV >= 2)) then
            set ok = TRUE
         endif
      endif
      NoteD $ALog "      has GNU LibC $lvers"
      setenv AF_VERSION_GLIBC $lvers
      unset t
      unset lvers
   else
      Note $ALog ""
      Note $ALog "--Compilation failed--"
      Note $ALog "/* $lCC $Tmp.c -o $Tmp -lc */"
      cat $Tmp.c >>& $ALog

      NoteD $ALog "      no GNU LibC version"
   endif

   if ($ok == TRUE) then
      setenv HAVE_GNU_LIBC_6 TRUE
   endif

   rm -f $Tmp.c $Tmp

else
   NoteD $ALog "      no GCC found on this system"
endif

dbmset $ALog HAVE_GNU_LIBC_6  = $HAVE_GNU_LIBC_6^  \
             AF_VERSION_GLIBC = $AF_VERSION_GLIBC

source $Me:h/post-common

exit(0)


