#!/bin/csh -f
#
# SQL - analyze platform SQL
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: sql"

# vanilla place to look if previously undefined
SafeEnv MDI_Inc    ""
SafeEnv MDI_Lib    ""
SafeEnv MDI_Pck    ""
SafeEnv SQL_Inc    ""
SafeEnv SQL_Lib    ""
SafeEnv SQL_RPath  /usr/lib
SafeEnv SQL_Path   /usr
SafeEnv HAVE_SQL   FALSE

# if we have explicitly said we do not want SQL
# do NOT detect it
if ("$SQL_Path" == none) then
   NoteD $ALog "      no SQL (by request)"

else

   set incl = ""
   set pthl = ""
   set libl = ""
   set spkl = ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# MYSQL

   set UTime = `timer -r`

# write a little MYSQL test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <mysql.h>
int main(int c, char **v)
{MYSQL *conn;
 conn = mysql_init(NULL);
 if (mysql_real_connect(conn, v[1], v[2], NULL, v[3], 0, NULL, 0) != NULL)
    mysql_query(conn, "SET NAMES 'utf8'");
 return(0);}
EOF

   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($SQL_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/$LIB )
   end
   set lIncDirs = ( $lIncDirs /usr/local/include $RootInc/mysql $RootInc )
   set lLibDirs = ( $lLibDirs /usr/local/lib /usr/lib/mysql /usr/$LIB )

   set Implicit = FALSE
   set lHdrs    = ( mysql.h )
   set ASList   = ( a so )
   set lLib     = mysqlclient
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `echo $RPATH | sed 's|:| |g'` )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib $LibM_Lib -lz )

   source $AnaDir/find-package

   if (("$PckLib" != "") && ("$PckInc" != "")) then
      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         set incl = ( $incl $PckInc )
      endif
      if ("$PckPath" != "") then
         set pthl = ( $pthl $PckPath )
      endif
      set libl = ( $libl $PckLib )
      set spkl = ( $spkl mysql )

      setenv HAVE_MYSQL  TRUE
   else
      setenv HAVE_MYSQL  ""
   endif

   set ETime = `timer -b $UTime`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SQLITE - version 1

   set UTime = `timer -r`

# write a little SQLITE test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <sqlite3.h>
int main(int c, char **v)
{sqlite3 *conn;
 sqlite3_open(v[1], &conn);
 return(0);}
EOF

   if ($?SQL_Path == 1) then
      set lIncDirs = ( $SQL_Path/include /usr/local/include $RootInc )
      set lLibDirs = ( $SQL_Path/$LIB /usr/local/lib /usr/$LIB )
   else
      set lIncDirs = ( /usr/local/include $RootInc )
      set lLibDirs = ( /usr/local/lib /usr/$LIB )
   endif
   set Implicit = FALSE
   set lHdrs    = ( sqlite3.h )
   set ASList   = ( a so )
   set lLib     = sqlite3
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `echo $RPATH | sed 's|:| |g'` )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib )

   source $AnaDir/find-package

   if (("$PckLib" != "") && ("$PckInc" != "")) then
      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         set incl = ( $incl $PckInc )
      endif
      if ("$PckPath" != "") then
         set pthl = ( $pthl $PckPath )
      endif
      set libl = ( $libl $PckLib )
      set spkl = ( $spkl sqlite )

      setenv HAVE_SQLITE TRUE
   else
      setenv HAVE_SQLITE ""
   endif

   set ETime = `timer -b $UTime`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SQLITE - version 2

   set UTime = `timer -r`

# write a little SQLITE test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <sqlite3.h>
int main(int c, char **v)
{sqlite3 *conn;
 sqlite3_open_v2(v[1], &conn,
		 SQLITE_OPEN_READONLY | SQLITE_OPEN_NOMUTEX,
  	         NULL);
 return(0);}
EOF

   if ($?SQL_Path == 1) then
      set lIncDirs = ( $SQL_Path/include /usr/local/include $RootInc )
      set lLibDirs = ( $SQL_Path/lib /usr/local/lib /usr/$LIB )
   else
      set lIncDirs = ( /usr/local/include $RootInc )
      set lLibDirs = ( /usr/local/lib /usr/$LIB )
   endif
   set lHdrs    = ( sqlite3.h )
   set ASList   = ( a so )
   set lLib     = sqlite3
   set lFile    = $Tmp
   set lALibs   = ( $LD_Lib )
   set Implicit = FALSE

   source $AnaDir/find-package

   if (("$PckLib" != "") && ("$PckInc" != "")) then
      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         set incl = ( $incl $PckInc )
      endif
      if ("$PckPath" != "") then
         set pthl = ( $pthl $PckPath )
      endif
      set libl = ( $libl $PckLib )

      setenv HAVE_SQLITE_V2 TRUE
      setenv HAVE_SQLITE    TRUE
   else
      setenv HAVE_SQLITE_V2 ""
   endif

   set ETime = `timer -b $UTime`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   setenv SQL_Inc   "$incl"
   setenv SQL_Lib   "$libl"
   setenv SQL_RPath "$pthl"

# NOTE: used SQL_Path to find libs, incs - there are no executables
   if ("$SQL_Path" == /usr) then
      setenv SQL_Path  ""
   endif

   foreach p ($pthl)
      setenv RPATH   ${RPATH}:$p
   end

   if ("$SQL_Lib" != "") then
      flog $ALog setenv MDI_Inc    "$MDI_Inc $incl"
      flog $ALog setenv MDI_Lib    "$libl $MDI_Lib"
      flog $ALog setenv MDI_Pck    "SQL $MDI_Pck"
      flog $ALog setenv HAVE_SQL   TRUE
      NoteD $ALog "      has SQL ($spkl)"
   else
      NoteD $ALog "      no SQL"
   endif

# export the results
   SetParent RPATH
   SetParent MDI_Inc
   SetParent MDI_Lib
   SetParent MDI_Pck
   SetParent SQL_Inc
   SetParent SQL_Lib
   SetParent SQL_RPath
   SetParent SQL_Path
   SetParent HAVE_MYSQL
   SetParent HAVE_SQLITE
   SetParent HAVE_SQLITE_V2

endif

SetParent HAVE_SQL

exit(0)

