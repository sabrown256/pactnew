#!/bin/csh -f
#
# PNG - analyze platform PNG
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: png"

# vanilla place to look if previously undefined
dbini MDG_Inc      ""
dbini MDG_Lib      ""
dbini MDG_Pck      ""
dbini PNG_Inc      ""
dbini PNG_Lib      ""
dbini PNG_RPath    $RootLib
dbini PNG_Path     /usr
dbini Z_Lib        ""
dbini Z_Path       /usr
dbini HAVE_PNGLIB  FALSE

set vl = ""
set vl = ( $vl Cfe_CC_Exe Cfe_CC_Flags )
set vl = ( $vl Cfe_LD_Flags Cfe_LD_Lib Cfe_LD_RPath )
set vl = ( $vl LD_Lib LIB LibM_Lib RPATH )
source $MngDir/write/import-db

# if we have explicitly said we do not want PNG
# do NOT detect it
if ("$PNG_Path" == none) then
   NoteD $ALog "      no PNG (by request)"
   setenv HAVE_PNGLIB  FALSE

else if ($Z_Path == none) then
   NoteD $ALog "      has PNG (no libz)"
   setenv HAVE_PNGLIB  FALSE

else

   set UTime = `timer -r`

# write a little PNG test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <png.h>
int main(int c, char **v)
{png_structp pw;
 pw = png_create_write_struct(PNG_LIBPNG_VER_STRING,
		              (png_voidp) NULL, NULL, NULL);
 if (pw == NULL)
    return(1);
 else
    {png_destroy_write_struct(&pw, (png_infopp) NULL);
     return(0);};}
EOF


   set lIncDirs = ""
   set lLibDirs = ""
   set lrpath   = ( `var-list -l $RPATH` )
   foreach i ($PNG_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/lib )
   end
   set lLibDirs = ( $lLibDirs $lrpath )
   set lLibDirs = ( $lLibDirs $DAS_ROOT/lib /usr/local/lib /usr/$LIB /$LIB )
   set lIncDirs = ( $lIncDirs $DAS_ROOT/include /usr/local/include $RootInc )

   set Implicit = FALSE
   set lHdrs    = ( png.h )
   set ASList   = ( a so )
   set lLib     = png
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( $lrpath )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib $LibM_Lib )

   unset lrpath

   source $AnaDir/find-package

   if (("$PckLib" != "") && ("$Z_Lib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has PNG ($PckLib)"

      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         flog $ALog setenv MDG_Inc "$MDG_Inc $PckInc"
      endif

      if ("$PckPath" != "") then
         flog $ALog setenv RPATH     ${RPATH}:${PckPath}
      endif
      flog $ALog setenv MDG_Lib      "$MDG_Lib $PckLib"
      flog $ALog setenv MDG_Pck      "$MDG_Pck PNG"
      flog $ALog setenv HAVE_PNGLIB  TRUE
   else
      NoteD $ALog "      no PNG"
      flog $ALog setenv HAVE_PNGLIB FALSE
   endif

   set ETime = `timer -b $UTime`

   setenv PNG_Inc   "$PckInc"
   setenv PNG_Lib   "$PckLib"
   setenv PNG_RPath "$PckPath"

# NOTE: used PNG_Path to find libs, incs - there are no executables
   setenv PNG_Path  ""

# export the results
   dbexp MDG_Inc
   dbexp MDG_Lib
   dbexp MDG_Pck
   dbexp PNG_Inc
   dbexp PNG_Lib
   dbexp PNG_RPath
   dbexp PNG_Path
   dbexp RPATH

endif

dbexp HAVE_PNGLIB

exit(0)

