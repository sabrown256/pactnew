#!/bin/csh -f
#
# PNG - analyze platform PNG
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: png"

# vanilla place to look if previously undefined
SafeEnv MDG_Inc      ""
SafeEnv MDG_Lib      ""
SafeEnv MDG_Pck      ""
SafeEnv PNG_Inc      ""
SafeEnv PNG_Lib      ""
SafeEnv PNG_RPath    /usr/lib
SafeEnv PNG_Path     /usr
SafeEnv HAVE_PNGLIB  FALSE

# if we have explicitly said we do not want PNG
# do NOT detect it
if ("$PNG_Path" == none) then
   NoteD $ALog "      no PNG (by request)"

else if ($Z_Path == none) then
   NoteD $ALog "      has PNG (no libz)"

else

   set UTime = `timer -r`

# write a little PNG test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <png.h>
int main(int c, char **v)
{png_structp pw;
 pw = png_create_write_struct(PNG_LIBPNG_VER_STRING,
		              (png_voidp) NULL, NULL, NULL);
 if (pw == NULL)
    return(1);
 else
    {png_destroy_write_struct(&pw, (png_infopp) NULL);
     return(0);};}
EOF


   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($PNG_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/lib )
   end
   set lLibDirs = ( $lLibDirs `echo $RPATH | sed 's|:| |g'` )
   set lLibDirs = ( $lLibDirs $DAS_ROOT/lib /usr/local/lib /usr/$LIB /$LIB )
   set lIncDirs = ( $lIncDirs $DAS_ROOT/include /usr/local/include $RootInc )

   set Implicit = FALSE
   set lHdrs    = ( png.h )
   set ASList   = ( a so )
   set lLib     = png
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `echo $RPATH | sed 's|:| |g'` )
       set lALibs = ( $lALibs -L$i )
   end
   set lALibs = ( $lALibs $LD_Lib $LibM_Lib )

   source $AnaDir/find-package

   if (("$PckLib" != "") && ("$Z_Lib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has PNG ($PckLib)"

      if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
         setenv MDG_Inc "$MDG_Inc $PckInc"
      endif

      if ("$PckPath" != "") then
         setenv RPATH     ${RPATH}:${PckPath}
      endif
      setenv MDG_Lib      "$MDG_Lib $PckLib"
      setenv MDG_Pck      "$MDG_Pck PNG"
      setenv HAVE_PNGLIB  TRUE
   else
      NoteD $ALog "      no PNG"
      setenv HAVE_PNGLIB FALSE
   endif

   set ETime = `timer -b $UTime`

   setenv PNG_Inc   "$PckInc"
   setenv PNG_Lib   "$PckLib"
   setenv PNG_RPath "$PckPath"

# NOTE: used PNG_Path to find libs, incs - there are no executables
   setenv PNG_Path  ""

# export the results
   SetParent RPATH
   SetParent MDG_Inc
   SetParent MDG_Lib
   SetParent MDG_Pck
   SetParent PNG_Inc
   SetParent PNG_Lib
   SetParent PNG_RPath
   SetParent PNG_Path

endif

SetParent HAVE_PNGLIB

exit(0)

