#!/bin/csh -f
#
# PYTHON - analyze the platform PYTHON
#        - PY_Exe     the full path to the python executable
#        - PY_Vers    the version of the python executable
#        - PY_Path    addition to path in order to find python executable
#        -            this comes from config files only
#        - PY_DirSrc  pact/dev/$arch/lib (install this to PY_DirDst and PY_DirPy)
#        - PY_DirDst  pact    lib/pythond.d                (install into)
#        - PY_DirPy   python  lib/pythond.d/site-packages  (install into)
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: python"

dbmget $ALog HAVE_PYTHON     =\? ""^        \
             HAVE_PY_NUMERIC =\? ""^        \
             HAVE_PY_NUMPY   =\? ""^        \
             Python_Exe      =\? "python"^  \
             Python_Path     =\? ""^        \
             Python_CC_Exe   =\? "gcc"^     \
             PY_Exe          =\? ""^        \
             PY_Vers         =\? ""^        \
             PY_DirPy        =\? ""^        \
             PY_DirDst       =\? ""^        \
             PY_DirSrc       =\? ""^        \
             PY_CC           =\? ""^        \
             PY_Cfg          =\? ""^        \
             PY_Inc          =\? ""^        \
             PY_Path         =\? ""^        \
             CC_Exe^                        \
             OS_Name

Note $ALog ""
Note $ALog "Python_Exe    = $Python_Exe"
Note $ALog "Python_Path   = $Python_Path"
Note $ALog "Python_CC_Exe = $Python_CC_Exe"
Note $ALog ""

# load up PY variables from Group Python specifications
if ("$Python_Exe" =~ /*) then
   flog $ALog setenv PY_Path    $Python_Exe:h:h
   flog $ALog setenv Python_Exe $Python_Exe:t
endif
if ("$Python_Path" != "") then
   flog $ALog setenv PY_Path $Python_Path
endif

# if there is no python directory no need to look further
if (-d ../../python) then

   NoteD $ALog -n "   Python: "
   Note $ALog ""

   set lDirs = ( $path /usr/bin /bin )
   if (-d "$PY_Path") then
      set lDirs = ( $PY_Path/bin $lDirs )
      Note $ALog "Specified python path: $PY_Path"
   else
      setenv PY_Path ""
   endif

   Note $ALog "Search Dirs = |$lDirs|"
   flog $ALog set found = FALSE
   foreach i ($lDirs)
      if (-x $i/$Python_Exe) then
         Note $ALog ""
         Note $ALog "Checking $i/$Python_Exe"
         setenv PY_Exe $i/$Python_Exe

         setenv PY_Ver ""
         setenv PY_Inc ""
         setenv PY_Lib ""
         setenv PY_Cfg ""
         setenv PY_CC  ""

         @ ok = 0
         set inf = ( `$PY_Exe ../../python/python-info.py` )
         if ($inf[1] == "Version:") then
            flog $ALog setenv PY_Ver $inf[2]
            @ ok = $ok + 1
         endif
         if ($inf[3] == "IncDir:") then
            flog $ALog setenv PY_Inc $inf[4]
            @ ok = $ok + 1
         endif
         if ($inf[5] == "LibDir:") then
            flog $ALog setenv PY_Lib $inf[6]
            @ ok = $ok + 1
         endif
         if ($inf[7] == "CfgDir:") then
            flog $ALog setenv PY_Cfg $inf[8]
            @ ok = $ok + 1
         endif
         if ($inf[9] == "Compiler:") then
            flog $ALog setenv PY_CC  $inf[10]
            @ ok = $ok + 1

# get the compiler python was built with
            if ((($OS_Name == "Linux")    || \
                 ($OS_Name == "Darwin")   || \
                 ($OS_Name == "FreeBSD")) && \
                ($PY_CC == cc)) then
               flog $ALog setenv PY_CC Python_CC_Exe
            endif
         endif

# if we have all 5 pieces of info then we have a usable python
         if ($ok == 5) then
            flog $ALog setenv HAVE_PYTHON TRUE

            NoteD $ALog "Python summary:"
            NoteD $ALog "      executable $PY_Exe"
            NoteD $ALog "      compiled with $PY_CC"
            NoteD $ALog "      has include directory $PY_Inc"
            NoteD $ALog "      has lib directory $PY_Lib"

# check for Numeric
            if (-d $PY_Inc/Numeric) then
               NoteD $ALog "      has numeric extensions"
               flog $ALog setenv HAVE_PY_NUMERIC TRUE
            else
               flog $ALog setenv HAVE_PY_NUMERIC FALSE
            endif

# check for NumPy
            if ((-d $PY_Lib/NumPy) || \
                (-d $PY_Lib/numpy) || \
                (-d $PY_Lib/site-packages/NumPy) || \
                (-d $PY_Lib/site-packages/numpy) || \
                (-d $PY_Lib/dist-packages/NumPy) || \
                (-d $PY_Lib/dist-packages/numpy) || \
                (-d $PY_Inc/numpy)) then
               NoteD $ALog "      has numpy extensions"
               flog $ALog setenv HAVE_PY_NUMPY TRUE
            else
               flog $ALog setenv HAVE_PY_NUMPY FALSE
            endif

            if (($HAVE_PY_NUMERIC == FALSE) && ($HAVE_PY_NUMPY == FALSE)) then
               NoteD $ALog "      does not have numeric extensions"
# NOTE: if we can build sensibly without any numeric extensions delete these
               flog $ALog setenv PY_Exe "no-numeric"
               flog $ALog setenv HAVE_PYTHON FALSE
            endif

            if ($OS_Name == "AIX") then
               set CCVer = `$CC_Exe -qversion`
               set PCVer = `$PY_CC -qversion`
               Note $ALog "CCVer = |$CCVer|"
               Note $ALog "PCVer = |$PCVer|"
               if ("$CCVer" != "$PCVer") then
                  flog $ALog setenv PY_Exe "wrong-cc"
                  flog $ALog setenv HAVE_PYTHON FALSE
               endif      
            else if ($PY_CC != $CC_Exe) then
               Note $ALog "PY_CC   = |$PY_CC|"
               Note $ALog "CC_Exe = |$CC_Exe|"
               flog $ALog setenv PY_Exe "wrong-cc"
               flog $ALog setenv HAVE_PYTHON FALSE
            endif
            flog $ALog setenv PY_Path $i
            flog $ALog set found = TRUE
            break
         endif
      endif
   end

   if ($found == FALSE) then
      NoteD $ALog "none recognizable"
      flog $ALog setenv HAVE_PYTHON     FALSE
      flog $ALog setenv HAVE_PY_NUMERIC FALSE
      flog $ALog setenv HAVE_PY_NUMPY   FALSE
      flog $ALog setenv PY_Path ""
      flog $ALog setenv PY_Exe ""
      flog $ALog setenv PY_CC ""
      flog $ALog setenv PY_Cfg""
      flog $ALog setenv PY_DirSrc ""
      flog $ALog setenv PY_DirDst ""
      flog $ALog setenv PY_DirPy ""
      flog $ALog setenv PY_Inc ""
      flog $ALog setenv PY_Vers ""
   endif

   if ($MAKE_Shared_Libs == FALSE) then
      flog $ALog setenv HAVE_PYTHON FALSE
   endif

   if ($HAVE_PYTHON == TRUE) then
      set PyRoot = $SysDir
      set PyHead = python${PY_Vers}/pact
      if ("$PY_Path" != "") then
         set PyRoot = $PY_Exe
         set PyRoot = $PyRoot:h
         set PyRoot = $PyRoot:h
         set PyHead = python${PY_Vers}
      endif

# install destination in python tree
      setenv PY_DirPy  $PyRoot/lib/$PyHead/site-packages

# install destination in PACT tree
      setenv PY_DirDst $InstBase/lib/$PyHead

# install source (local build destination)
      setenv PY_DirSrc $SysDir/lib/python${PY_Vers}

      dbmset $ALog PY_DirSrc = $PY_DirSrc^       \
                   PY_DirDst = $PY_DirDst^       \
                   PY_DirPy  = $PY_DirPy
   endif

# export the results
dbmset $ALog HAVE_PYTHON     = $HAVE_PYTHON^       \
             HAVE_PY_NUMERIC = $HAVE_PY_NUMERIC^   \
             HAVE_PY_NUMPY   = $HAVE_PY_NUMPY^     \
             PY_Path         = $PY_Path^           \
             PY_Exe          = $PY_Exe^            \
             PY_CC           = $PY_CC^             \
             PY_Cfg          = $PY_Cfg^            \
             PY_Inc          = $PY_Inc^            \
             PY_Vers         = $PY_Vers
endif

exit(0)

