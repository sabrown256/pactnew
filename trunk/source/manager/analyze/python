#!/bin/csh -f
#
# PYTHON - analyze the platform PYTHON
#        - PY_Exe     the full path to the python executable
#        - PY_Vers    the version of the python executable
#        - PY_Path    addition to path in order to find python executable
#        -            this comes from config files only
#        - PY_DirSrc  pact/dev/$arch/lib (install this to PY_DirDst and PY_DirPy)
#        - PY_DirDst  pact    lib/pythond.d                (install into)
#        - PY_DirPy   python  lib/pythond.d/site-packages  (install into)
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: python"

SafeEnv HavePython        FALSE
SafeEnv HavePythonNumeric FALSE
SafeEnv HavePythonNumpy   FALSE
SafeEnv PY_Exe      ""
SafeEnv PY_Vers     ""
SafeEnv PY_DirPy    ""
SafeEnv PY_DirDst   ""
SafeEnv PY_DirSrc   ""

SafeEnv PY_CC       ""
SafeEnv PY_Cfg      ""
SafeEnv PY_Inc      ""
SafeEnv PY_Path     ""

# if there is no python directory no need to look further
if (-d ../../python) then

   NoteD $ALog -n "   Python: "
   Note $ALog ""

   set lDirs = ( $path /usr/bin /bin )
   if (-d "$PY_Path") then
      set lDirs = ( $PY_Path/bin $lDirs )
      Note $ALog "Specified python path: $PY_Path"
   else
      setenv PY_Path ""
   endif

   Note $ALog "Search Dirs = |$lDirs|"
   flog $ALog set found = FALSE
   foreach i ($lDirs)
      if (-x $i/python) then
         Note $ALog ""
         Note $ALog "Checking $i/python"
         setenv PY_Exe $i/python

# NOTE: get it into the log what is going on here
         flog $ALog $PY_Exe ../../python/python-cc.py
         set LStat1 = $status
         set lPY_Inc = ( `$PY_Exe ../../python/python-inc-dir.py |& cat` )
         if (($LStat1 != 0) || !(-f "$lPY_Inc/Python.h")) then
            flog $ALog setenv PY_Exe ""
         else
            set pyv = `$PY_Exe -V |& awk '{ print $2 }'`
            if ($#pyv > 2) continue

            flog $ALog setenv HavePython TRUE
            setenv PY_CC  `$PY_Exe ../../python/python-cc.py |& awk '{ print $1 }'`
            setenv PY_Inc `$PY_Exe ../../python/python-inc-dir.py`
            setenv PY_Lib `echo $PY_Inc | sed 's/include/lib/'`
            setenv PY_Cfg `$PY_Exe ../../python/python-cfg-dir.py`

            NoteD $ALog "$PY_Exe ($pyv)"
            NoteD $ALog "      compiled with $PY_CC"
            NoteD $ALog "      has include directory $PY_Inc"
            set lPY_Vers = `$PY_Exe ../../python/python-version.py`

            if (-d $PY_Inc/Numeric) then
               NoteD $ALog "      has numeric extensions"
               flog $ALog setenv HavePythonNumeric TRUE
            else
               flog $ALog setenv HavePythonNumeric FALSE
            endif
            if ((-d $PY_Lib/site-packages/NumPy) || (-d $PY_Lib/site-packages/numpy) || \
                (-d $PY_Lib/dist-packages/NumPy) || (-d $PY_Lib/dist-packages/numpy) || \
                (-d $PY_Inc/numpy)) then
               NoteD $ALog "      has numpy extensions"
               flog $ALog setenv HavePythonNumpy TRUE
            else
               flog $ALog setenv HavePythonNumpy FALSE
            endif

            if (($HavePythonNumeric == FALSE) && ($HavePythonNumpy == FALSE)) then
               NoteD $ALog "      does not have numeric extensions"
# NOTE: if we can build sensibly without any numeric extensions delete these
               flog $ALog setenv PY_Exe "no-numeric"
               flog $ALog setenv HavePython FALSE
            endif

            if ((($HostOS == "Linux") || ($HostOS == "Darwin") || ($HostOS == "FreeBSD")) && ($PY_CC == cc)) then
               flog $ALog setenv PY_CC gcc
            endif
            if ($HostOS == "AIX") then
               set CCVer = `$CC_Exe -qversion`
               set PCVer = `$PY_CC -qversion`
               Note $ALog "CCVer = |$CCVer|"
               Note $ALog "PCVer = |$PCVer|"
               if ("$CCVer" != "$PCVer") then
                  flog $ALog setenv PY_Exe "wrong-cc"
                  flog $ALog setenv HavePython FALSE
               endif      
            else if ($PY_CC != $CC_Exe) then
               Note $ALog "PY_CC   = |$PY_CC|"
               Note $ALog "CC_Exe = |$CC_Exe|"
               flog $ALog setenv PY_Exe "wrong-cc"
               flog $ALog setenv HavePython FALSE
            endif
            flog $ALog setenv PY_Path $i
            flog $ALog set found = TRUE
            break
         endif
      endif
   end

   if ($found == FALSE) then
      NoteD $ALog "none recognizable"
      flog $ALog setenv HavePython        FALSE
      flog $ALog setenv HavePythonNumeric FALSE
      flog $ALog setenv HavePythonNumpy   FALSE
      flog $ALog setenv PY_Path ""
      flog $ALog setenv PY_Exe ""
      flog $ALog setenv PY_CC ""
      flog $ALog setenv PY_Cfg ""
      flog $ALog setenv PY_DirSrc ""
      flog $ALog setenv PY_DirDst ""
      flog $ALog setenv PY_DirPy ""
      flog $ALog setenv PY_Inc ""
      flog $ALog setenv PY_Vers ""
   endif

   if ($BuildSharedLibs == FALSE) then
      flog $ALog setenv HavePython FALSE
   endif

   if ($HavePython == TRUE) then
      if (`expr "$lPY_Vers" : '.*\..*\..*'` > 0) then
         set lPY_Vers = $lPY_Vers:r
      endif
      flog $ALog setenv PY_Vers     $lPY_Vers
      set PyRoot = $SysDir
      set PyHead = python${PY_Vers}/pact
      if ("$PY_Path" != "") then
         set PyRoot = $PY_Exe
         set PyRoot = $PyRoot:h
         set PyRoot = $PyRoot:h
         set PyHead = python${PY_Vers}
      endif

# install destination in python tree
      setenv PY_DirPy  $PyRoot/lib/$PyHead/site-packages

# install destination in PACT tree
      setenv PY_DirDst $InstBase/lib/$PyHead

# install source (local build destination)
      set PY_DirSrc = $SysDir/lib/python${PY_Vers}
   endif
endif

# export the results
SetParent HavePython
SetParent HavePythonNumeric
SetParent HavePythonNumpy
SetParent PY_Path
SetParent PY_Exe
SetParent PY_CC
SetParent PY_Cfg
SetParent PY_DirSrc
SetParent PY_DirDst
SetParent PY_DirPy
SetParent PY_Inc
SetParent PY_Vers

exit(0)

