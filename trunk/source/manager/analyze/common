#
# COMMON - common setup for programs in analyze
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set Cmd    = $0
set Exe    = $Cmd:t
set ExeDir = $Cmd:h
if ($?PSY_AnaDir == 0) then
   setenv PSY_AnaDir $ExeDir
endif

if (-f ../scripts/functions-pact.csh) then
   set psy_mngdir  = $cwd
else if (-f ../../scripts/functions-pact.csh) then
   set psy_mngdir  = $cwd:h
endif
set ldir  = $psy_mngdir:h/scripts
set Which = $psy_mngdir/pwhich

if ($?FirstCommon == 0) then
   set lroot = $ldir:h
   set larch = `$ldir/cfgman use`
   set lbin  = $lroot/dev/$larch/bin
   set lalog = $lroot/dev/$larch/log/analyze.$Exe
   set path  = ( $ldir $lbin $path )

   unset lbin
   unset lroot
   unset larch

   setenv FirstCommon FALSE
endif

source $ldir/functions-pact.csh
source $ldir/csh-subroutines

set Tmp = $cwd/tmp-$Exe

if ($?ALog == 1) then
   set lalog = $ALog:h/analyze.$Exe
endif

if (-f $lalog) then
   set ALog = $lalog
else
   InitLog ALog $lalog
endif
unset lalog

Note $ALog ""
Separator $ALog
Note $ALog "Command: $Cmd"
Note $ALog "Current: $cwd"
Note $ALog "Date: `date`"
Note $ALog "Temporary file base: $Tmp"
Note $ALog "Manager directory: $psy_mngdir"
Note $ALog ""

dbmget $ALog AF_CList =\? ""^           \
             CROSS_FE =\? ""^           \
             DP_BE    =\? ""^           \
             DP_FE    =\? "do-run -m"

envini PSY_MngDir     $cwd

Note $ALog ""
Separator $ALog
