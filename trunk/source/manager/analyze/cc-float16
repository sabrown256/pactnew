#!/bin/csh -f
#
# CC-FLOAT16 - analyze long double support for C compiler
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/cc-float16 -----"
Note $ALog "Analyze: C90 long double support"
Note $ALog ""

dbmget $ALog CROSS_FE^        \
             PACT_CC_VERSION

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

cat << EOF >! $Tmp.c
#include <float.h>
#include <limits.h>
#include <math.h>
#include <stdio.h>
#include <string.h>
int main(int c, char **v)
   {int rv;
    double x, y;
    long double a, b;
    char s[100];
    a = 1.23e34l;
    b = sqrtl(2.0);
    x = 1.23e34;
    y = sqrt(2.0);
    printf("max = %50.42Le\n", LDBL_MAX);
    printf("a = %44.34Le\n", a/b);
    printf("x = %26.16e\n", x/y);
    printf("b = %44.34Le\n", b);
    printf("y = %26.16e\n", y);
    snprintf(s, 100, "%44.34Le", b);
    if (strstr(s, "1.4142135623730") != NULL)
       {rv = 0;
        fprintf(stderr, "TRUE\n");}
    else
       {rv = 1;
        fprintf(stderr, "FALSE\n");};
    return(rv);}
EOF

flog $ALog cat $Tmp.c

# no long double support at all
flog $ALog setenv AF_LONG_DOUBLE 0

flog $ALog $lCC $Tmp.c -o $Tmp -lm
if ($status == 0) then

# compiler thinks we have long double support (PGI gets here)
   flog $ALog setenv AF_LONG_DOUBLE 1
   fexec $ALog $CROSS_FE $Tmp @o2 @e3 fa:$ALog @ vw:HAVE_ANSI_FLOAT16
   if ($gstatus[1] == 0) then

# we have working long double support (PGI does not get here)
      flog $ALog setenv AF_LONG_DOUBLE 2
   endif
endif

rm -f $Tmp $Tmp.c $Tmp.o

if ($AF_LONG_DOUBLE == 2) then
   NoteD $ALog "      has long double support"
else if ($AF_LONG_DOUBLE == 1) then
   NoteD $ALog "      has broken long double support"
else
   NoteD $ALog "      no long double support"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

dbexp AF_LONG_DOUBLE
