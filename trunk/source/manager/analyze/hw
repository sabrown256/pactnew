#!/bin/csh -f
#
# HW - analyze the hardware
#    - Computes: CPU, FPU
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/hw -----"
Note $ALog "Analyze: platform hardware"
Note $ALog ""

if ($?SYS_Arch == 0) then
   setenv SYS_Arch `../scripts/system-id`
endif

dbmget $ALog CPU =\? "unknown"^  \
             FPU =\? "unknown"

set HostHW  = `uname -m`

if ($CPU == "unknown") then
   setenv CPU "$HostHW"
   if ("$HostHW" =~ i*86) then
      setenv CPU  ia32
   else if ("$SYS_Arch" =~ osx-*-x86) then
      setenv CPU  ia32
   else if ("$SYS_Arch" =~ osx-*-ppc) then
      setenv CPU  ppc
   endif
endif

if ($FPU == "unknown") then
   if ("$HostHW" == "alpha") then
      setenv FPU  alpha
   else if ("$HostHW" =~ sun*) then
      setenv FPU  sparc
   else if ("$HostHW" == x86_64) then
      setenv FPU  x87
   else if ("$HostHW" =~ i*86) then
      setenv FPU  x87
   else if ("$SYS_Arch" =~ osx-*-x86) then
      setenv FPU  x87
   else if ("$SYS_Arch" =~ osx-*-ppc) then
      setenv FPU  ppc
   endif
endif

set msg = "Hardware: $HostHW"
if ("$CPU" != "unknown") then
   set msg = "$msg $CPU"
endif
if ("$FPU" != "unknown") then
   set msg = "$msg $CPU ($FPU)"
endif
NoteD $ALog "   $msg"

dbmset $ALog CPU = $CPU^  \
             FPU = $FPU

exit(0)

