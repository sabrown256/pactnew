#!/bin/csh -f
#
# GRAPHICS - manage detected graphics
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "Analyze: system graphics"

NoteD $ALog "   System Graphics:"

gexec $AnaDir/quickdraw  @ \
      $AnaDir/opengl     @ \
      $AnaDir/x11        @ \
      $AnaDir/png        @ \
      $AnaDir/jpeg

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# gather the results into the MDG_ variables

   Separator $ALog

   dbmget $ALog CC_Exe^                   \
                MDG_Inc^                  \
                MDG_Lib^                  \
                MDG_Pck^                  \
                JPEG_Inc^                 \
                JPEG_Lib^                 \
                PNG_Inc^                  \
                PNG_Lib^                  \
                X11_Inc^                  \
                X11_Lib^                  \
                X11_RPath^                \
                OGL_Inc^                  \
                OGL_Lib^                  \
                OGL_RPath^                \
                GRAPHICS_Windows =\? ""^  \
                GRAPHICS_Devices =\? ""^  \
                GRAPHICS_Flags   =\? ""^  \
   	        HAVE_PNGLIB^              \
   	        HAVE_JPEGLIB^             \
                OS_Name^                  \
                RPATH^                    \
                Std_UseOGL^               \
                Std_UseQD^                \
                Std_UseX
   
   flog $ALog setenv MDG_Inc "$MDG_Inc $OGL_Inc $X11_Inc $PNG_Inc $JPEG_Inc"

   flog $ALog setenv MDG_Lib "$MDG_Lib $OGL_Lib $X11_Lib $PNG_Lib $JPEG_Lib"
   if (($OS_Name == Darwin) && ($CC_Exe !~ pgc*)) then
      Note $ALog "Finding GL on MAC OS X"
      flog $ALog setenv MDG_Lib "$MDG_Lib -framework GLUT -framework OpenGL"
   endif

   if ($Std_UseQD == TRUE) then
      flog $ALog setenv MDG_Pck            "$MDG_Pck QD"
      flog $ALog setenv GRAPHICS_Windows   "QD"
      flog $ALog setenv GRAPHICS_Devices   "$GRAPHICS_Devices QD"
      flog $ALog setenv GRAPHICS_Flags     "$GRAPHICS_Flags -framework Carbon"
   endif
   if ($Std_UseOGL == TRUE) then
      flog $ALog setenv MDG_Pck            "$MDG_Pck OGL"
      flog $ALog setenv GRAPHICS_Windows   "OGL"
      flog $ALog setenv GRAPHICS_Devices   "$GRAPHICS_Devices OGL"
      flog $ALog setenv GRAPHICS_Flags     "$GRAPHICS_Flags -DOGL"
   endif
   if ($Std_UseX == TRUE) then
      flog $ALog setenv MDG_Pck            "$MDG_Pck X11"
      flog $ALog setenv GRAPHICS_Windows   "X"
      flog $ALog setenv GRAPHICS_Devices   "$GRAPHICS_Devices X"
      flog $ALog setenv GRAPHICS_Flags     "$GRAPHICS_Flags -DX11"
      if ("$X11_RPath" != "") then
         flog $ALog setenv RPATH           ${RPATH}:${X11_RPath}
      endif
   endif
   if ($HAVE_PNGLIB == TRUE) then
      flog $ALog setenv MDG_Pck          "$MDG_Pck PNG"
   endif
   if ($HAVE_JPEGLIB == TRUE) then
      flog $ALog setenv MDG_Pck          "$MDG_Pck JPEG"
   endif

# eliminate redundancies in MDG_Inc
# NOTE: compilers look at /usr/include by default not
# necessarily $RootInc
   set lincs = ""
   foreach i ($MDG_Inc)
      if ("$i" != "-I/usr/include") then
         set ok = TRUE
         foreach j ( $lincs )
             if ("$i" == "$j") then
                set ok = FALSE
                break
             endif
         end
         if ($ok == TRUE) then
            set lincs = ( $lincs $i )
         endif
      endif
   end
   flog $ALog setenv MDG_Inc  "$lincs"
   unset lincs

# eliminate redundancies in MDG_Lib
   set llibs = ""
   foreach i ($MDG_Lib)
      set ok = TRUE
      foreach j ( $llibs )
          if ("$i" == "$j") then
             set ok = FALSE
             break
          endif
      end
      if ($ok == TRUE) then
         set llibs = ( $llibs $i )
      endif
   end
   flog $ALog setenv MDG_Lib  "$llibs"
   unset llibs

   Note $ALog "Graphics summary variables:"
   Note $ALog "MDG_Inc = |$MDG_Inc|"
   Note $ALog "MDG_Lib = |$MDG_Lib|"
   Note $ALog "MDG_Pck = |$MDG_Pck|"

   dbmset $ALog MDG_Inc          = $MDG_Inc^            \
                MDG_Lib          = $MDG_Lib^            \
                MDG_Pck          = $MDG_Pck^            \
                GRAPHICS_Windows = $GRAPHICS_Windows^   \
                GRAPHICS_Devices = $GRAPHICS_Devices^   \
                GRAPHICS_Flags   = $GRAPHICS_Flags^     \
                RPATH            = $RPATH

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $ALog

exit(0)

