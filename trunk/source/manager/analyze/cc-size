#!/bin/csh -f
#
# CC-SIZE - analyze the integer type sizes
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/cc-size -----"
Note $ALog "Analyze: Find Integer Sizes"
Note $ALog ""

dbmget $ALog INT4       =\? int^   \
             INT8       =\? long^  \
             CROSS_FE^             \
             PACT_CC_VERSION

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

cat << EOF >! $Tmp.c
#include <stdio.h>
#include <stdlib.h>
int main(int c, char **v)
{long long i;
 i = strtoll("3", NULL, 10);
 return(0);}
EOF

flog $ALog $lCC $Tmp.c -o $Tmp
if ($status != 0) then
   setenv NO_LONG_LONG TRUE
else
   setenv NO_LONG_LONG FALSE
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# INT4

cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 4)
    printf("int\n");
 else if (sizeof(long) == 4)
    printf("long\n");
 else
    printf("unknown\n");
return(0);}
EOF

flog $ALog $lCC $Tmp.c -o $Tmp
if ($status == 0) then
   fexec $ALog $CROSS_FE $Tmp @o2 @e3 vw:INT4 @ fw:/dev/null
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# INT8

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($NO_LONG_LONG == TRUE) then
   goto VERSIONA
else
   goto VERSIONB
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

VERSIONA:

   cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 8)
    printf("int\n");
 else if (sizeof(long) == 8)
    printf("long\n");
 else
    printf("unknown\n");
return(0);}
EOF

   goto COMPILE

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

VERSIONB:

   cat << EOF >! $Tmp.c
#include <stdio.h>
int main(int c, char **v)
{
 if (sizeof(int) == 8)
    printf("int\n");
 else if (sizeof(long) == 8)
    printf("long\n");
 else if (sizeof(long long) == 8)
    printf("long long\n");
 else
    printf("unknown\n");
return(0);}
EOF

   goto COMPILE

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

COMPILE:

flog $ALog $lCC $Tmp.c -o $Tmp
if ($status == 0) then
   fexec $ALog $CROSS_FE $Tmp @o2 @e3 vw:INT8 @ fw:/dev/null
endif

flog $ALog rm -f $Tmp.c $Tmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

dbmget $ALog INT4^   \
             INT8

Note $ALog ""
Note $ALog "Integer size info:"
Note $ALog "   INT4         = $INT4"
Note $ALog "   INT8         = $INT8"
Note $ALog "   NO_LONG_LONG = $NO_LONG_LONG"
Note $ALog ""

dbexp NO_LONG_LONG


