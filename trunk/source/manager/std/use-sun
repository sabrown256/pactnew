#!/bin/csh -f
#
# USE-SUN - decide on the correct Sun compiler environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

source ../scripts/env-csh

set CmpPath = ""
while ($#argv > 0)
   switch ($1)
      default:
           if ("$CmpPath" == "") then
              set CmpPath = $1
           endif
           breaksw
   endsw
   shift
end

# if blank find suncc on your path
if ("$CmpPath" == "") then
   set CmpPath = ( `which suncc` )

# if not a full path find it on your path
else if ("$CmpPath" !~ /*) then
   set CmpPath = ( `which $CmpPath` )
endif

if (-xf "$CmpPath") then
   set CCRoot = $CmpPath:h
   set CCRoot = $CCRoot:h
else if (-x "$CmpPath/bin/suncc") then
   set CCRoot = $CmpPath
endif

set SDir   = $CCRoot/bin
set Exe    = $SDir/suncc
set Info   = ( `$Exe -V |& grep "cc: Sun"` )
set lvers  = $Info[4]
set BinDir = $Exe:h
set Root   = $BinDir:h

setenv PATH                 $SDir':$PATH'
setenv PACT_CC_FAMILY       SUN
setenv PACT_CC_VERSION      $lvers
setenv PACT_CC_PATH         $Root
setenv PACT_CC_EXE          $Exe

SetParent PATH
SetParent PACT_CC_FAMILY
SetParent PACT_CC_VERSION
SetParent PACT_CC_PATH
SetParent PACT_CC_EXE

exit(0)

