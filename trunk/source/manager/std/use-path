#!/bin/csh -f
#
# USE-PATH - decide on the correct PathScale environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../scripts/env-csh

set Awk = `which awk`

set lbits = ""
set lvers = ""
set cpath = ""
while ($#argv > 0)
   switch ($1)
      case bits:
           shift
           set lbits = $1
           breaksw
      case vers:
           shift
           set lvers = $1
           breaksw
      default:
           if ("$cpath" == "") then
              set cpath = $1
           endif
           breaksw
   endsw
   shift
end

# figure out the site context
if (-f extensions/sys-site) then
   eval `extensions/sys-site`
else
   setenv DAI_ROOT /usr/share
   setenv DAS_ROOT /usr/local
endif

# find and source env-csh
if (-f ../scripts/env-csh) then
   set scrd = $cwd
else if (-f ../../scripts/env-csh) then
   set scrd = $cwd:h
endif
set ldir = $scrd:h/scripts
set path = ( $ldir $path )
source $ldir/env-csh
unset ldir
unset scrd

set cname = pathcc

# default compiler and query info
set cc  = ( `which $cname` )
set opt = ( -v )
set pat = ( target on )

# if blank find $cname on your path
if ("$cpath" == "") then
   if (-x $DAI_ROOT/pact/scripts/$cname) then
      set cc  = $DAI_ROOT/pact/scripts/$cname
      set pat = ( Compile line: )
      set opt = ( $opt -cwvrb )
      if ("$lvers" != "") then
         set opt = ( $opt -cwvers $lvers )
      endif
   endif

# if not a full path find it on your path
else if ("$cpath" !~ /*) then
   set cpath = ( `which $cpath` )

else if (-x "$cpath") then
   set cc = $cpath

endif

set sdir   = $cc:h
set bindir = $sdir

# query the compiler for information
set inf = ( `$cc $opt |& $Awk '$0 ~ /Compiler Suite: Version/ { print $NF } $0 ~ /Compile line:/ { print $3 }'` )
if ($#inf > 1) then
   set lvers  = ( `echo $inf[2] | sed 's/[-_]/./g'` )
   set cc     = $inf[1]
   set bindir = $cc:h
else if ($#inf > 0) then
   set lvers = ( `echo $inf[1] | sed 's/[-_]/./g'` )
endif
set root = $bindir:h

setenv LD_LIBRARY_PATH /lib
foreach v ($root/lib/*)
   setenv LD_LIBRARY_PATH ${v}:$LD_LIBRARY_PATH
end

setenv PATH                  $sdir':$PATH'
setenv PACT_CC_FAMILY        PATHSCALE
setenv PACT_CC_VERSION       $lvers
setenv PACT_CC_PATH          $root
setenv PACT_CC_EXE           $cc

SetParent PATH
SetParent PACT_CC_FAMILY
SetParent PACT_CC_VERSION
SetParent PACT_CC_PATH
SetParent PACT_CC_EXE

exit(0)
