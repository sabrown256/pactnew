#!/bin/csh -f
#
# USE-INTEL - decide on the correct Intel environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set Awk = `which awk`

set lbits = ""
set lvers = ""
set cpath = ""
while ($#argv > 0)
   switch ($1)
      case bits:
           shift
           set lbits = $1
           breaksw
      case vers:
           shift
           set lvers = $1
           breaksw
      default:
           if ("$cpath" == "") then
              set cpath = $1
           endif
           breaksw
   endsw
   shift
end

# figure out the site context
if (-f extensions/sys-site) then
   eval `extensions/sys-site`
else
   setenv DAI_ROOT /usr/share
   setenv DAS_ROOT /usr/local
endif

# find and source env-csh
if (-f ../scripts/env-csh) then
   set scrd = $cwd
else if (-f ../../scripts/env-csh) then
   set scrd = $cwd:h
endif
set ldir = $scrd:h/scripts
set path = ( $ldir $path )
source $ldir/env-csh
unset ldir
unset scrd

# default compiler and query info
set cc  = ( `which icc` )
set opt = ( -v )
set pat = ( Version )

# because Intel puts its compilers in different
# directories check both ifort and icc to get
# both on the PATH
# do icc last to get its environment as the final result
foreach c (ifort icc)

   set cc = ( `which $c` )

# if blank find compiler on your path
   if ("$cpath" == "") then
      if (-x $DAI_ROOT/pact/scripts/$c) then
         set cc  = $DAI_ROOT/pact/scripts/$c
         set pat = ( Compile line: )
         set opt = ( $opt -cwvrb )
         if ("$lvers" != "") then
            set opt = ( $opt -cwvers $lvers )
         endif
      endif

# if not a full path find it on your path
   else if ("$cpath" !~ /*) then
      set cc = ( `which $cpath` )

   else if (-x "$cpath") then
      set cc = $cpath

   endif

   set sdir   = $cc:h
   set bindir = $sdir

# query the compiler for information
   set inf = ( `$cc $opt |& $Awk '$1 ~ /Version/ { print $2 } $0 ~ /Compile line:/ { print $3 }'` )
   if ($#inf > 1) then
      set lvers  = $inf[2]
      set cc     = $inf[1]
      set bindir = $cc:h
   else if ($#inf > 0) then
      set lvers = $inf[1]
   endif
   set root = $bindir:h

   setenv PATH    $sdir':$PATH'
end

setenv PACT_CC_FAMILY   INTEL
setenv PACT_CC_VERSION  $lvers
setenv PACT_CC_PATH     $root
setenv PACT_CC_EXE      $cc

SetParent PATH
SetParent PACT_CC_FAMILY
SetParent PACT_CC_VERSION
SetParent PACT_CC_PATH
SetParent PACT_CC_EXE

exit(0)

