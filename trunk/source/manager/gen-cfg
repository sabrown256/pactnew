#!/bin/csh -f
#
# GEN-CFG - generate a config file from a name
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set Err  = 0
set cfg  = ""
set vrb  = FALSE
set flst = ""
set clst = ""
set blst = ""

while ($#argv > 0)
   switch ($1)
      case -cc:
           shift
           set clst = ( $clst $1 )
           breaksw
      case -f:
           shift
           set flst = ( $flst $1 )
           breaksw
      case -h:
           echo ""
           echo "Usage: gen-cfg [-cc [<cc>[:<cc>]*] [-f <feature>[,<feature>]*] [-h] [-v] <name>"
           echo "  cc  compilers for which to generate configs"
           echo "  h   this help message"
           echo "  v   verbose mode"
           echo ""
           exit(1)
      case -v:
           set vrb  = TRUE
           set flst = ( $flst -v )
           breaksw
      default:
           set cfg = $1
           breaksw
   endsw
   shift
end

if ("$clst" == "") then
   set clst = gnu
endif

set flst = ( `echo "$flst" | sed 's/,/ /g'` )

if ("$cfg" == "") then
   echo "No name specified - exiting"
   exit(1)
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set cfgt     = $cfg:t
set features = ( `gen-cfg-root $cfgt` )
set croot    = $features[1]
shift features

if ($vrb == TRUE) then
   echo "Config directories: $PSY_CONFIG_PATH"
   echo "Root config file:   $croot"
   echo "Feature list:       $features"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# check list of features and setup environment

if ($?bits == 0) then
   setenv bits ""
endif

set pthread = FALSE
set openmp  = FALSE
set shared  = FALSE
set mpi     = FALSE
set python  = FALSE
set pydir   = ""
set pyexe   = ""

foreach i ($features)
   switch ($i)
      case 32:
      case 64:
           setenv bits $i
           set blst = ( -bits $bits )
           breaksw
      case mpi:
           set mpi  = TRUE
           set flst = ( $flst -mpi )
           breaksw
      case py:
           set python = TRUE
           set flst   = ( $flst -py )
           breaksw
      case s:
           set openmp = TRUE
           set flst   = ( $flst -s )
           breaksw
      case sl:
           set shared = TRUE
           set flst   = ( $flst -sl )
           breaksw
      case t:
           set pthread = TRUE
           set flst    = ( $flst -t )
           breaksw
      default:
           set flst = ( $flst $i )
           breaksw
   endsw
end

if (($pthread == TRUE) && ($openmp == TRUE)) then
   echo "pthread and openmp are mutually exclusive options - pick one"
   exit(1)
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set psy_arch = `system-id`
set gdir     = gen/$psy_arch
if (!(-d $gdir)) then
   mkdir -p $gdir
endif

set ltmp = $gdir/$cfg

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# generate a config file from scratch

if ("$croot" == "") then

   if ($vrb == TRUE) then
      echo "Generate from scratch"
   endif

   set cfga = `write/cfg-base -gdir $gdir $blst $clst`
   mv $cfga $ltmp

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# use an existing config file

else if ("$features" == "") then

   if ($vrb == TRUE) then
      echo "Use existing config $croot"
   endif

   set ltmp = $croot

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# copy an existing config file which can be used as a base

else
   if ($vrb == TRUE) then
      echo "Start with existing config $croot"
   endif
   cp $croot $ltmp
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

unset features

echo "write/cfg-features -c |$ltmp| |$blst| |$flst|"
write/cfg-features -c $ltmp $blst $flst > /dev/null

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

echo "$ltmp"

exit($Err)

