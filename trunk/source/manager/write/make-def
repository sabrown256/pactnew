#!/bin/csh -f
#
# MAKE-DEF - write the MAKE-DEF file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# put these in shell variables since
# prune-env will remove them as environment variables
set Log    = $1
set ScrDir = $2

if ($?pruned == 0) then
   eval `$ScrDir/prune-env pact`
   set path   = ( $ScrDir $path )
   set pruned = TRUE
endif

source $ScrDir/env-csh

source $ScrDir/csh-subroutines
HAVE_SUBROUTINES

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $Log

NoteD $Log "   Site Dependent MAKE Configuration - make-def"
Note $Log ""

set vl = ""
set vl = ( $vl AR_IFlag AR_XFlag ArW32Objs )
set vl = ( $vl BaseDir BE Bits BuildStrategy )
set vl = ( $vl CDefDebug CDefOptimize CFE )
set vl = ( $vl DEFAULT_SHELL EtcDir )
#set vl = ( $vl FC_LD_Lib )
set vl = ( $vl FDefDebug FDefOptimize )
set vl = ( $vl GOOD_OPENMP_PRAGMAS GnuMakeOpt )
set vl = ( $vl GraphicsDevices GraphicsFlag GSYS )
set vl = ( $vl HAVE_PYTHON HostOS )
set vl = ( $vl IncDir InstBase INSTALL IPC_STD )
set vl = ( $vl Load MngDir )
set vl = ( $vl OStd OSType OW32Objs )
set vl = ( $vl ParCommSupport PubLib PubInc )
set vl = ( $vl PY_CC PY_Cfg PY_Exe PY_Inc PY_Vers )
set vl = ( $vl RPATH SUFFIXES Sys SysDir System )
set vl = ( $vl ThreadLibExt UPDATE URFile )

# compiler tools
foreach t (CC FC CXX)
   foreach f (Debug Exe Flags Inc Linker Optimize)
      set vl = ( $vl ${t}_${f} )
   end
end

# linker tool
foreach f (Exe Flags Lib RPath)
   set vl = ( $vl LD_${f} )
end

# other tools
foreach t (AR Lex Ranlib Yacc)
   foreach f (Exe Flags)
      set vl = ( $vl ${t}_${f} )
   end
end

# groups
foreach g (Cfg Cfe Ser Std Shared)

# compilers
   foreach t (CC FC CXX)
      foreach f (Exe Flags Inc Linker)
         set vl = ( $vl ${g}_${t}_${f} )
      end
   end

# linker
   foreach f (Exe Lib RPath)
      set vl = ( $vl ${g}_LD_${f} )
   end
end

# others
foreach t (MD MDE MDI MDG DP LibM)
   foreach f (Inc Lib)
      set vl = ( $vl ${t}_${f} )
   end
end

source $MngDir/write/import-db

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ("$OSType" == USE_MSW) then
   set OStd = WIN32
else
   set OStd = UNIX
endif

alias RESET_VAR  'setenv \!\!:1_\!\!:2 \!\!:3-$'
alias DEFD_VAR   'set ok = `env | grep \!\!:1`'
alias VAR_VAL_SP 'set value = `printenv \!\!:1_\!\!:2 | sed "s^ ^+sp+^g"`'
alias VAR_VALUE  'set value = `printenv \!\!:1_\!\!:2`'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# add DP_Inc to CC_Inc
    flog $Log setenv CC_Inc  "$CC_Inc $DP_Inc"

    set STDOUT = $EtcDir/make-def

    flog $Log $RM $STDOUT
    flog $Log touch $STDOUT

    Note $STDOUT "#"
    Note $STDOUT "# MAKE-DEF - installation dependent definitions for $Sys"
    Note $STDOUT "#"
    Note $STDOUT ""
    Note $STDOUT "System  = $System"
    Note $STDOUT 'archo   = z-${System}/obj'
    set QUOTE = \"
    set THE_DATE = `cat .pact-version`
    Note $STDOUT "Version = $THE_DATE"

    set mk = ( `$MngDir/pwhich gmake` )
    if (-x "$mk") then
       Note $STDOUT "UMake   = $mk"
    else
       Note $STDOUT "UMake   = `$MngDir/pwhich make`"
    endif

    Note $STDOUT "BaseDir = $BaseDir"
    Note $STDOUT "SysDir  = $SysDir"
    Note $STDOUT 'ScmDir  = ${SysDir}/scheme'
    Note $STDOUT 'BinDir  = ${SysDir}/bin'
    Note $STDOUT 'LibDir  = ${SysDir}/lib'
    Note $STDOUT 'IncDir  = ${SysDir}/include'
    Note $STDOUT 'EtcDir  = ${SysDir}/etc'
    Note $STDOUT 'Man1Dir = ${SysDir}/man/man1'
    Note $STDOUT 'Man3Dir = ${SysDir}/man/man3'
    Note $STDOUT ""

    if ("$HostOS" == "HP-UX") then
       Note $STDOUT 'TGTLib  = nolib'
    endif
    Note $STDOUT 'TGTBin  = nobin'
    Note $STDOUT 'TGTEtc  = noetc'
    Note $STDOUT 'TGTInc  = noinc'
    Note $STDOUT 'TGTScm  = noscm'
    Note $STDOUT 'TGTScr  = noscr'
    Note $STDOUT 'TGTScmX = noscmx'
    Note $STDOUT 'TGTScrX = noscrx'
    Note $STDOUT 'TGTMan3 = noman'
    Note $STDOUT 'LibName = noname'
    Note $STDOUT ""

# GOTCHA: when this goes remove reset_make_vars in pco.c
#    if (-f $MVFile) then
#       Note $STDOUT "#"
#       Note $STDOUT "# variables defined in config file"
#       Note $STDOUT "#"
#       sort $MVFile >>& $STDOUT
#       $RM $MVFile
#       Note $STDOUT ""
#    endif

    if ("$GnuMakeOpt" == "-DUSE_GNU_MAKE") then
       Note $STDOUT "HaveGMake = TRUE"
    else
       Note $STDOUT "HaveGMake = FALSE"
    endif
    Note $STDOUT ""

# Compiler info
    gexec ../scripts/gcc-info $CC_Exe @o vw:eGCCInfo
    dbget eGCCInfo
    set GCCInfo = ( $eGCCInfo )
#    set GCCInfo = ( `../scripts/gcc-info $CC_Exe |& cat` )
    if ($GCCInfo[2] == yes) then    
       Note $STDOUT "#"
       Note $STDOUT "# GCC Info"
       Note $STDOUT "#"
       Note $STDOUT "GCCLib = $GCCInfo[8]"
       Note $STDOUT ""
    endif

# OS support
    Note $STDOUT "#"
    Note $STDOUT "# Site Dependent OS Support"
    Note $STDOUT "#"
    Note $STDOUT -n "OSSupport ="
    if ($OStd == UNIX) then
       if ($BuildStrategy == "SpaceSaving") then
	  Note $STDOUT -n ' ${ArUnxObjs}'
       else
	  Note $STDOUT -n ' ${OUnxObjs'}'
       endif

# cannot ever get here but it shows how the abstraction works
    else if ($OStd == WIN32) then
       if ($BuildStrategy == "SpaceSaving") then
	  Note $STDOUT -n ' ${ArW32Objs}'
       else
	  Note $STDOUT -n ' ${OW32Objs'}'
       endif
    endif
    Note $STDOUT ""
    Note $STDOUT ""

# graphics devices
    Note $STDOUT "#"
    Note $STDOUT "# Site Dependent Graphics Information"
    Note $STDOUT "#"
    Note $STDOUT "GraphicsFlag    = $GraphicsFlag"
    Note $STDOUT "MDGInc          = $MDG_Inc"
    Note $STDOUT "MDGLib          = $MDG_Lib"

    Note $STDOUT -n "GraphicsDevices ="
    foreach dev ($GraphicsDevices)
       if ($BuildStrategy == "SpaceSaving") then
	  Note $STDOUT -n ' ${Ar'$dev'}'
       else
	  Note $STDOUT -n ' ${O'$dev'}'
       endif
    end
    Note $STDOUT ""

    Note $STDOUT ""
    Note $STDOUT "GSYS=$GSYS"
    Note $STDOUT ""

# Syntax Modes
    Note $STDOUT "#"
    Note $STDOUT "# Site Dependent Syntax Modes"
    Note $STDOUT "#"

    set SynSCM = ""
    Note $STDOUT -n "SyntaxModes  ="
    if ("$Yacc_Exe" != "") then
       set scsd = `scs-meta -n`
       pushd $BaseDir/scheme/syntax >& /dev/null
       foreach mod (*)
          if (-d $mod) then
             if ($mod != $scsd) then
                set SynSCM = ( $SynSCM applications/${mod}synt.scm )
                if ($BuildStrategy == "SpaceSaving") then
   	           Note $STDOUT -n ' ${Ar'$mod'}'
                else
	           Note $STDOUT -n ' ${O'$mod'}'
                endif
             endif
          endif
       end
       popd >& /dev/null
    endif
    Note $STDOUT ""
    Note $STDOUT "SyntaxScheme = $SynSCM"

# Parallel I/O
    if ($BuildStrategy == "SpaceSaving") then
       set MPDev  = '${Ar'$ParCommSupport'}'
       set IPCDev = '${Ar'$IPC_STD'}'
    else
       set MPDev  = '${O'$ParCommSupport'}'
       set IPCDev = '${O'$IPC_STD'}'
    endif
    Note $STDOUT "MPassDevices = $MPDev"
    Note $STDOUT "IPCDevices   = $IPCDev"
    Note $STDOUT ""

    Note $STDOUT "#"
    Note $STDOUT "# Compiler/Loader Options"
    Note $STDOUT "#"

# emit items from the defined groups
#    foreach g ($DefGroups)
    foreach g (Std Cfg Cfe Ser Shared)
       Note $STDOUT ""
       Note $STDOUT "# group ${g}"
       foreach v ($ConfigVars)
          DEFD_VAR ${g}_${v}
          if ("$ok" == "") then
             Note $STDOUT "${g}_${v} = `printenv ${v}`"
          else
             VAR_VALUE ${g} ${v}
             Note $STDOUT "${g}_${v} = $value"
          endif
       end
    end
    Note $STDOUT ""

    Note $STDOUT "# tool CC"
    foreach v (Exe Flags Debug Optimize Shared Inc)
       DEFD_VAR CC_${v}
       if ("$ok" == "") then
          Note $STDOUT "CC_${v} = `printenv ${v}`"
       else
          VAR_VALUE CC ${v}
          Note $STDOUT "CC_${v} = $value"
       endif
    end
    Note $STDOUT ""

    Note $STDOUT "# tool FC"
    foreach v (Exe Flags Debug Optimize Shared)
       DEFD_VAR FC_${v}
       if ("$ok" == "") then
          Note $STDOUT "FC_${v} = `printenv ${v}`"
       else
          VAR_VALUE FC ${v}
          Note $STDOUT "FC_${v} = $value"
       endif
    end
    Note $STDOUT ""

    Note $STDOUT "# tool LD"
    foreach v (Exe Flags Shared Lib)
       DEFD_VAR LD_${v}
       if ("$ok" == "") then
          Note $STDOUT "LD_${v} = `printenv ${v}`"
       else
          VAR_VALUE LD ${v}
          Note $STDOUT "LD_${v} = $value"
       endif
    end
    Note $STDOUT ""

    Note $STDOUT "# tool AR"
    foreach v (Exe Flags IFlag XFlag)
       DEFD_VAR Ar_${v}
       if ("$ok" == "") then
          Note $STDOUT "Ar_${v} = `printenv ${v}`"
       else
          VAR_VALUE Ar ${v}
          Note $STDOUT "Ar_${v} = $value"
       endif
    end
    Note $STDOUT ""

    Note $STDOUT "# tool LEX"
    foreach v (Exe Flags)
       DEFD_VAR Lex_${v}
       if ("$ok" == "") then
          Note $STDOUT "Lex_${v} = `printenv ${v}`"
       else
          VAR_VALUE Lex ${v}
          Note $STDOUT "Lex_${v} = $value"
       endif
    end
    Note $STDOUT ""

    Note $STDOUT "# tool RANLIB"
    foreach v (Exe)
       DEFD_VAR Ranlib_${v}
       if ("$ok" == "") then
          Note $STDOUT "Ranlib_${v} = `printenv ${v}`"
       else
          VAR_VALUE Ranlib ${v}
          Note $STDOUT "Ranlib_${v} = $value"
       endif
    end
    Note $STDOUT ""

# emit items from the Glb group
    Note $STDOUT "# Global variables"
    foreach v ($Globals)
       set value = `printenv $v`
       Note $STDOUT "$v = $value"
    end
    Note $STDOUT ""

    Note $STDOUT "YACC       = $Yacc_Exe $Yacc_Flags"
    Note $STDOUT "LEX        = $Lex_Exe"

    Note $STDOUT 'CCAnnounce = ${CC}'
    Note $STDOUT 'FCAnnounce = ${FC}'

    Note $STDOUT ""
    Note $STDOUT "CFE       = $CFE"
    Note $STDOUT "BE        = $BE"

    if ("$InstBase" == "none") then
       Note $STDOUT "PubLib    = "
       Note $STDOUT "PubInc    = "
    else
       Note $STDOUT "PubLib    = $PubLib"
       Note $STDOUT "PubInc    = $PubInc"
    endif

# emit the load names for the libraries - cover threaded/nonthreaded
    Note $STDOUT ""
    Note $STDOUT "ThreadLibExt=$ThreadLibExt"
    Note $STDOUT "LSCORE      = -lscore"
    Note $STDOUT "LMM         = -lpsmm"
    Note $STDOUT "LPML        = -lpml"
    Note $STDOUT "LPDB        = -lpdb"
    Note $STDOUT "LPPC        = -lppc"
    Note $STDOUT "LPGS        = -lpgs"
    Note $STDOUT "LPANACEA    = -lpanacea"
    Note $STDOUT "LSCHEME     = -lscheme"
    Note $STDOUT "LSX         = -lsx"
    Note $STDOUT "LULTRA      = -lultra"

    Note $STDOUT ""
    Note $STDOUT "#"
    Note $STDOUT "# System utilities"
    Note $STDOUT "#"
    Note $STDOUT "INSTALL    = $INSTALL"
    Note $STDOUT "RM         = $RM"

# emit RANLIB in case a pre-Make has to reference it!!!!
    Note $STDOUT "RANLIB     = $Ranlib_Exe"
    Note $STDOUT "UPDATE     = $UPDATE"

    if ($HAVE_PYTHON == TRUE) then
       set lPython = $PY_Exe
       Note $STDOUT "PY_Vers     = $PY_Vers"
       Note $STDOUT "PY_CC       = $PY_CC"
       Note $STDOUT "PY_CfgDir   = $PY_Cfg"
       Note $STDOUT "PY_IncDir   = $PY_Inc"
       Note $STDOUT "PythonPath = $lPython:h"
    endif

    Note $STDOUT ""
    Note $STDOUT "#"
    Note $STDOUT "# Space Saving Build"
    Note $STDOUT '#     If you want that option do the following:'
    Note $STDOUT '#                    LibDep = ${ArObjs}'
    Note $STDOUT '#                    .PRECIOUS: ${TGTLib}'
    Note $STDOUT '#     otherwise do:'
    Note $STDOUT '#                    LibDep = ${Objs}'
    Note $STDOUT "#"

# BuildStrategy handler

    if ($BuildStrategy == "SpaceSaving") then
       Note $STDOUT 'LibDep = ${ArObjs}'
       Note $STDOUT ""
       Note $STDOUT '.PRECIOUS: ${TGTLib}'
    else
       Note $STDOUT 'LibDep = ${Objs}'
    endif
    Note $STDOUT ""

    Note $STDOUT "SHELL   = $DEFAULT_SHELL"
    Note $STDOUT "Manager = $MngDir"
    Note $STDOUT 'DATE    = `./code-date`'
    Note $STDOUT ""

# match up what derives from the config process with what is used in the pre-Makes
    Note $STDOUT "CCompiler     = ${CC_Exe}"
    Note $STDOUT "CCmpLdr       = ${CC_Linker}"
    Note $STDOUT "CFLAGS        = ${CC_Flags}"
    Note $STDOUT "CDebug        = ${CC_Debug}"
    Note $STDOUT "COptimize     = ${CC_Optimize}"
    Note $STDOUT "CDefDebug     = ${CDefDebug}"
    Note $STDOUT "CDefOptimize  = ${CDefOptimize}"

    Note $STDOUT "CXCompiler    = ${CXX_Exe}"
    Note $STDOUT "CXCmpLdr      = ${CXX_Linker}"
    Note $STDOUT "CXFLAGS       = ${CXX_Flags}"
    Note $STDOUT "CXDebug       = ${CXX_Debug}"
    Note $STDOUT "CXOptimize    = ${CXX_Optimize}"
    Note $STDOUT "CXDefDebug    = ${CDefDebug}"
    Note $STDOUT "CXDefOptimize = ${CDefOptimize}"

    Note $STDOUT "FCompiler     = ${FC_Exe}"
    Note $STDOUT "FFLAGS        = ${FC_Flags}"
    Note $STDOUT "FDebug        = ${FC_Debug}"
    Note $STDOUT "FOptimize     = ${FC_Optimize}"
    Note $STDOUT "FDefDebug     = ${FDefDebug}"
    Note $STDOUT "FDefOptimize  = ${FDefOptimize}"

    Note $STDOUT "Linker       = ${LD_Exe}"
    Note $STDOUT ""

    Note $STDOUT 'DefaultG     = DefDebug'
    Note $STDOUT 'DefaultO     = DefOptimize'
    Note $STDOUT 'g            = Debug'
    Note $STDOUT 'o            = Optimize'
    Note $STDOUT 'OD           = DefaultG'
    Note $STDOUT 'ODC          = ${C${${OD}}}'
    Note $STDOUT 'ODF          = ${F${${OD}}}'
    Note $STDOUT ""

# get the entries for RPATH
    set npath = `analyze/rpath -o link`
    Note $Log "analyze/rpath -o link => |$npath|"
    Note $STDOUT "LDPath     = $npath"

    set lrpath  = `analyze/rpath -o rpath`
    Note $Log "analyze/rpath -o rpath => |$npath|"
    Note $STDOUT "LDRPath    = $lrpath"

    Note $STDOUT "LDFLAGS    = ${LD_Flags}"' ${LDRPath} ${LDPath}'
    Note $STDOUT "LXFLAGS    = ${Lex_Flags}"
    Note $STDOUT "MDInc      = ${MD_Inc} ${CC_Inc}"
    Note $STDOUT "MDLib      = ${MD_Lib} ${LD_Lib} ${LibM_Lib}"
    Note $STDOUT "DPInc      = ${DP_Inc}"
    Note $STDOUT "DPLib      = ${DP_Lib}"
    Note $STDOUT "MDI_Inc    = ${MDI_Inc}"
    Note $STDOUT "MDI_Lib    = ${MDI_Lib}"
    Note $STDOUT "MDE_Lib    = ${MDE_Lib}"
    if ($?FC_LD_Lib == 1) then
       Note $STDOUT "FLib       = ${FC_LD_Lib}"
    else
       Note $STDOUT "FLib       = "
    endif
    Note $STDOUT ""

    Note $STDOUT "AR         = ${AR_Exe}"
    Note $STDOUT "AROpt      = ${AR_Flags} ${AR_IFlag}"
    Note $STDOUT "ARXOpt     = ${AR_Flags} ${AR_XFlag}"
    Note $STDOUT ""

    Note $STDOUT 'CC  = ${CCompiler} ${CFLAGS} ${Shared_CC_Flags} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'CXX = ${CXCompiler} ${CXFLAGS} ${UniFlags} ${CxFlags}'
    Note $STDOUT 'FC  = ${FCompiler} ${FFLAGS} ${UniFlags} ${FcFlags}'
    Note $STDOUT 'LX  = ${CCompiler} ${LXFLAGS} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'YC  = ${CCompiler} ${LXFLAGS} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'LD  = ${Linker} ${UniFlags} ${LdFlags}'
    Note $STDOUT ""
 
    Note $STDOUT 'CLD            = ${CCmpLdr} ${CFLAGS} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'CCCfg          = ${Cfg_CC_Exe} ${Cfg_CC_Flags} ${Shared_CC_Flags} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'FCCfg          = ${Cfg_FC_Exe} ${Cfg_FC_Flags} ${UniFlags} ${FcFlags}'

    Note $STDOUT 'CCSer          = ${Ser_CC_Exe} ${Ser_CC_Flags} ${Shared_CC_Flags} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'FCSer          = ${Ser_FC_Exe} ${Ser_FC_Flags} ${UniFlags} ${FcFlags}'

    Note $STDOUT 'CCCfe          = ${Cfe_CC_Exe} ${Cfe_CC_Flags} ${Shared_CC_Flags} ${UniFlags} ${CcFlags}'
    Note $STDOUT 'FCCfe          = ${Cfe_FC_Exe} ${Cfe_FC_Flags} ${UniFlags} ${FcFlags}'
    Note $STDOUT 'CCCfe_LD_Flags = ${Cfe_LD_Flags}'
    Note $STDOUT 'CCCfe_LD_Lib   = ${Cfe_LD_Lib}'

    if ($Load == TRUE) then
       Note $STDOUT 'Load           = ${CCmpLdr}'
    else
       Note $STDOUT 'Load           = echo'
    endif
    Note $STDOUT "BinAction      = "
    Note $STDOUT "IncAction      = "
    Note $STDOUT "LibAction      = "
    Note $STDOUT "RemoveAction   = "
    Note $STDOUT "CleanAction    = "
    Note $STDOUT ""

    Note $STDOUT ""
    Note $STDOUT ".SUFFIXES: $SUFFIXES"
    Note $STDOUT ""

    if ("$GnuMakeOpt" == "-DUSE_GNU_MAKE") then
       Note $STDOUT '(%): %'
       Note $STDOUT ''
    endif

# write the default make rules
    call PrintRule(".c.i:", CCP)

    if ($GOOD_OPENMP_PRAGMAS == TRUE) then
       call PrintRule(".c.o:", CCObj)
       call PrintRule(".c.a:", CCArc)
       call PrintRule(".l.o:", LexObj)
       call PrintRule(".l.a:", LexArc)
    else
       call PrintRule(".c.o:", CCObj_BP)
       call PrintRule(".c.a:", CCArc_BP)
       call PrintRule(".l.o:", LexObj_BP)
       call PrintRule(".l.a:", LexArc_BP)
    endif
    call PrintRule(".l.c:", LexC)
    if ($GOOD_OPENMP_PRAGMAS == TRUE) then
       call PrintRule(".y.o:", YaccObj)
       call PrintRule(".y.a:", YaccArc)
    else
       call PrintRule(".y.o:", YaccObj_BP)
       call PrintRule(".y.a:", YaccArc_BP)
    endif
    call PrintRule(".y.c:", YaccC)
    call PrintRule(".f.o:", FCObj)
    call PrintRule(".f.a:", FCArc)

    Note $STDOUT ""

# add the user defined rules here
    if (-f $URFile) then
       set ln   = ( `wc -l $URFile` )
       @ nlines = $ln[1]
       if ($nlines > 0) then
          Note $STDOUT "#"
          Note $STDOUT "# Configuration defined rules"
          Note $STDOUT "#"
          Note $STDOUT ""
          cat $URFile >>& $STDOUT
          $RM $URFile
          Note $STDOUT ""
       endif
    endif

    exit(0)

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# PRINTRULE - do the required rule printing with \n and \t handled
#           - properly (some csh's print them literally instead of the
#           - C way)

PrintRule:

    set Rule = $args_[1]
    set Val  = $args_[2]

    echo "$Rule" >> $STDOUT

    echo '	@[ ! -f ${LibDir}/.lock ] || { echo "${LibDir} is locked" ; exit 1 ; }' >> $STDOUT

#    gexec vr:$Val @o grep "\\t" @o fa:/dev/null
#    gexec grep "\\t" @i+1 @o+2 vr:$Val @ fa:/dev/null
#    dbget gstatus
#    if ($gstatus[1] == 0) then

    printenv $Val |& grep "\\t" >>& /dev/null
    if ($status == 0) then

       set STDAUX = tmp-$System

       printenv $Val >! $STDAUX
       (echo '1,$s/[\]t/	/g' ; echo '1,$s/[\]n//g' ; echo '1,$p') |&  \
       ed - $STDAUX                                                        |&  \
       tr "\015" "\012"                                                    |&  \
       awk '($1 != "?") {print}' >>& $STDOUT

       $RM $STDAUX
    else
       printenv $Val | sed 's/\\n/\n/g' | sed 's/\\t/\t/g' >> $STDOUT
    endif

    return

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

