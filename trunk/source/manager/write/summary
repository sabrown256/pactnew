#!/bin/csh -f
#
# SUMMARY - summarize important PACT configuration information
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

dbmget $ALog Bits^             \
             CC_Exe^           \
             Cfe_CC_Exe^       \
             Cfe_FC_Exe^       \
             Cfe_FC_Flags^     \
             CROSS_COMPILE^    \
             FC_Exe^           \
             FC_Flags^         \
             HAVE_FORTRAN^     \
             HAVE_PYTHON^      \
             HAVE_PY_NUMERIC^  \
             HAVE_PY_NUMPY^    \
             MAKE_Exe^         \
             MAKE_Shared_Libs^ \
             PSY_Base^         \
             PSY_Root^         \
             PSY_ID^           \
             PSY_InstRoot^     \
             PY_Exe^           \
             PY_Ext^           \
             PY_DirPy^         \
             PY_DirSrc^        \
             PY_DirDst^        \
             RF_DEBUG^         \
             RF_OPTIMIZE^      \
             RF_PROFILE^       \
             USE_TmpDir^       \
             Yacc_Exe

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

    NoteD $ALog "Configuration Summary for $PSY_ID"

    if (($?Cfe_CC_Exe == 0) || ("$Cfe_CC_Exe" == "none") || ("$Cfe_CC_Exe" == "")) then
       NoteD $ALog "   ERROR: No C compiler available - check your system or your path"
       NoteD $ALog ""
       exit(1)
    else
       set UCC = `where $CC_Exe |& head -n 1`
       NoteD $ALog "   C Compiler used is $UCC"
    endif

    if ($HAVE_FORTRAN == FALSE) then
       if (($?Cfe_FC_Exe == 0) || ("$Cfe_FC_Exe" == "none")) then
          NoteD $ALog "   No Fortran compiler found"
       else
          NoteD $ALog "   Not building Fortran bindings"
       endif
    else if ("$Cfe_FC_Exe" == "") then
       NoteD $ALog "   ERROR: No Fortran compiler available - check your system or your path"
       NoteD $ALog ""
    else
       set UFC = `where $FC_Exe |& head -n 1`
       if (-x "$UFC") then
          NoteD $ALog "   Fortran Compiler used is $UFC"
       else
          NoteD $ALog "   Fortran Compiler requested is $Cfe_FC_Exe (not available)"
       endif
    endif

    if ("$Bits" != "") then
       NoteD $ALog "   Building for $Bits bit pointers"
    endif

    if ($CROSS_COMPILE != "FALSE") then
       NoteD $ALog "   Cross compiling for target $CROSS_COMPILE"
    endif

    if ($MAKE_Shared_Libs == TRUE) then
       NoteD $ALog "   Building shared libraries"
    else
       NoteD $ALog "   Building static libraries"
    endif

    Note $ALog "   Cfg C Compiler used is $Cfe_CC_Exe"
    Note $ALog "   Cfg Fortran Compiler used is $Cfe_FC_Exe"

    if ($RF_DEBUG == TRUE) then
       NoteD $ALog "   Building absolutely debuggable version"
    endif

    if ($RF_OPTIMIZE == TRUE) then
       NoteD $ALog "   Building absolutely optimized version"
    endif

    if ($RF_PROFILE == TRUE) then
       NoteD $ALog "   Building profiling version"
    endif
       
# PY_Exe     the full path to the python executable
# PY_Ext     the extension/wrapper set name (e.g. 2.numpy)
# PY_Vers    the version of the python executable
# PY_Path    addition to path in order to find python executable
#            this comes from config files only
# PY_DirSrc  pact/dev/$PSY_ID/lib/pythond.d       (install to PY_DirDst and PY_DirPy)
# PY_DirDst  pact    lib/pythond.d                (install into)
# PY_DirPy   python  lib/pythond.d/site-packages  (install into)

    if (("$HAVE_PYTHON" =~ *TRUE*) && ($MAKE_Shared_Libs == TRUE)) then
       NoteD $ALog "   Python bindings will be built in:"
       set lhave_python = ( $HAVE_PYTHON )
       set lpy_ext      = ( $PY_Ext )
       set lpy_dirpy    = ( $PY_DirPy )
       set lpy_dirsrc   = ( $PY_DirSrc )
       set lpy_dirdst   = ( $PY_DirDst )
  
       @ n = $#lhave_python
       @ i = 0
       while ($i < $n)
          @ i = $i + 1
          if ($lhave_python[$i] == TRUE) then
             set lex = $lpy_ext[$i]
             set ldp = $lpy_dirpy[$i]
             set ldd = $lpy_dirdst[$i]
             set lds = $lpy_dirsrc[$i]

             flog $ALog mkdir -p $ldp

             NoteD $ALog "      $lex -> $lds"
             if ("$PSY_InstRoot" != none) then
                NoteD $ALog "   Python bindings will be installed in:"
                NoteD $ALog "      $ldd"
                if (-d $ldp) then
                   NoteD $ALog "      $ldp"
                else
                   NoteD $ALog "      $ldp (no permissions)"
                endif
             endif
          endif
       end
       unset ldp
       unset ldd
       unset lds
       unset lpy_dirpy
       unset lpy_dirsrc
       unset lpy_dirdst
       unset lhave_python

    else if ($MAKE_Shared_Libs == FALSE) then
       NoteD $ALog "   Python bindings will not be built (need shared library option)"
    else if ("$PY_Exe" == "wrong-cc") then
       NoteD $ALog "   Python bindings will not be built (wrong compiler)"
    else if ("$PY_Exe" == "no-numeric") then
       NoteD $ALog "   Python bindings will not be built (no numeric extensions)"
    else if ("$PY_Exe" == "") then
       NoteD $ALog "   Python bindings will not be built (no python)"
    endif

    if ("$Yacc_Exe" == "") then
       NoteD $ALog "   Scheme syntax modes will not be built (no yacc or bison)"
    endif

    if ($USE_TmpDir == TRUE) then
       NoteD $ALog "   Doing fast builds using /tmp"
    endif

    NoteD $ALog "   Building into $cwd:h/dev/$PSY_ID"

    NoteD $ALog ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# PRE-INSTALL - do any installations that need to be done before
#             - writing generated files

# ensure that the PSH files are installed in $IncDir/shell
# before the write scripts are invoked
# some of them include these files

Note $ALog "Install shell includes from psh"

flog $ALog pushd $PSY_Base/psh
flog $ALog $MAKE_Exe IncDir=$PSY_Root/include -f pre-Make mkinstall
flog $ALog popd

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

source $Me:h/post-common

Separator $Log

exit(0)

