#
# CFG-BASE - common code to generate the main part of a config
#          - from OS, Compiler, and bits
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# NOTE: use node name for config file base
# if multiple hosts have the same SYS_TYPE there will be contention
# over the file - the node name is truly host specific
   set cfg = `uname -n | tr "[A-Z]" "[a-z]" | sed 's/\..*\..*$//'`
   set los = `uname -s | tr "[A-Z]" "[a-z]"`
   set lhw = `uname -m | tr "[A-Z]" "[a-z]"`

   rm -f $Tmp
   touch $Tmp

   set lcc = $CCompiler
   switch ($lcc)
      case gnu:
           set lcc = gcc
           breaksw
      case intel:
           set lcc = icc
           breaksw
      case path:
           set lcc = pathcc
           breaksw
      case pgi:
           set lcc = pgcc
           breaksw
      case sun:
           set lcc = suncc
           breaksw
   endsw

   echo "include spokes" >>& $Tmp
   switch ($los)
      case aix:
           set cfg = "$cfg-$Bits"
           echo "include ${Bits}-bit" >>& $Tmp
           echo "include compilers/xlc" >>& $Tmp
           breaksw
      case cygwin*:
           echo "include ${Bits}-bit" >>& $Tmp
           echo "include compilers/cygwin" >>& $Tmp
           breaksw
      case darwin:
           switch ($lcc)
              case *pgcc*:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include compilers/pgi-osx" >>& $Tmp
                   breaksw
              default:
              case *gcc*:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include compilers/gnu-osx" >>& $Tmp
                   breaksw
           endsw
           breaksw
      case freebsd:
           echo "define FREEBSD" >>& $Tmp
           echo "run std/use-compiler $lcc" >>& $Tmp
           echo "include ${Bits}-bit" >>& $Tmp
           echo "include compilers/gnu" >>& $Tmp
           breaksw
      case hp-ux*:
           echo "include compilers/hpux" >>& $Tmp
           breaksw
      case irix*:
           echo "include ${Bits}-bit" >>& $Tmp
           echo "include cpp-rule" >>& $Tmp
           echo "include compilers/irix" >>& $Tmp
           breaksw
      case osf1:
           echo "include compilers/osf" >>& $Tmp
           breaksw
      case sunos:
           set CCRoot = `$Which cc`
           set CCRoot = $CCRoot:h
           set CCRoot = $CCRoot:h
           echo "setenv PACT_CC_PATH $CCRoot" >>& $Tmp
           echo "include compilers/sol" >>& $Tmp
           breaksw
      default:
      case linux:
           switch ($lcc)
              case gnu-bgl:
                   echo "run std/use-compiler mpgcc" >>& $Tmp
                   echo "include auto-bgl-gnu" >>& $Tmp
                   breaksw
              case gnu-bgp:
                   echo "run std/use-compiler mpicc" >>& $Tmp
                   echo "include auto-bgp-gnu" >>& $Tmp
                   breaksw
              case gnu-cxe:
                   echo "include auto-cxe-gnu" >>& $Tmp
                   breaksw
              case *icc:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include ${Bits}-bit" >>& $Tmp
                   echo "include compilers/icc" >>& $Tmp
                   breaksw
              case *path:
              case *pathcc:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include ${Bits}-bit" >>& $Tmp
                   echo "include compilers/path" >>& $Tmp
                   breaksw
              case pgi-cxe:
                   echo "include auto-cxe-pgi" >>& $Tmp
                   breaksw
              case *pgi:
              case *pgcc:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include ${Bits}-bit" >>& $Tmp
                   echo "include compilers/pgi" >>& $Tmp
                   breaksw
	      case *sun:
	      case *suncc:
                   echo "run std/use-compiler $lcc" >>& $Tmp
		   echo "include compilers/sun" >>& $Tmp
                   breaksw
              case xlc-bgl:
                   echo "run std/use-compiler mpxlc" >>& $Tmp
                   echo "include auto-bgl-xlc" >>& $Tmp
                   breaksw
              case xlc-bgp:
                   echo "run std/use-compiler mpixlc_r" >>& $Tmp
                   echo "include auto-bgp-xlc" >>& $Tmp
                   breaksw
              default:
              case *gnu:
              case *gcc:
                   echo "run std/use-compiler $lcc" >>& $Tmp
                   echo "include ${Bits}-bit" >>& $Tmp
                   echo "include compilers/gnu" >>& $Tmp
                   unset lcc
                   breaksw
           endsw
           breaksw
   endsw
