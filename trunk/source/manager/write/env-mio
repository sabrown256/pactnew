#!/bin/csh -f
#
# ENV-MIO - write the ENV-MIO file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

if (-f ../scripts/env-csh) then
   set SrcDir = $cwd
else if (-f ../../scripts/env-csh) then
   set SrcDir = $cwd:h
endif
set ldir = $SrcDir:h/scripts
set path = ( $ldir $path )
source $ldir/env-csh
source $ScrDir/csh-subroutines
HAVE_SUBROUTINES

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

    Separator $Log
    NoteD $Log "   MIO specifications - env-mio"

    set FOUT = $IncDir/env-mio

    flog $Log $RM $FOUT
    flog $Log touch $FOUT

    Note $FOUT "#"
    Note $FOUT "# ENV-MIO - installation dependent specifications for MIO"
    Note $FOUT "#         - configuration defined by $Sys"
    Note $FOUT "#"
    Note $FOUT ""

    set lccexe = `pwhich $Cfe_CC_Exe`
    if (-x "$lccexe") then
       set CCBin = $lccexe:h
    else
       set CCBin = none
    endif

    set lfcexe = `pwhich $Cfe_FC_Exe`
    if (-x "$lfcexe") then
       set FCBin = $lfcexe:h
    else
       set FCBin = none
    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# COMPILER - generate the compiler info

    Note $FOUT '# COMPILER specifications'
    Note $FOUT ''

    set cmplib = ""
    set lnuma  = ""

    set lbits = $Bits
    if ($CPU == ia64) then
       set lbits = ""
    endif

    if ($PACT_CC_FAMILY == GNU) then
       set PACTSpec = /gnu
       set CmpBin   = $PACT_CC_PATH/bin
       set cmplib   = $PACT_CC_PATH/$LIB
       Note $FOUT 'VMake += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
       Note $FOUT 'VEnv  += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
       Note $FOUT ''
       Note $FOUT "PACT_CC_FAMILY  = $PACT_CC_FAMILY"
       Note $FOUT "PACT_CC_VERSION = $PACT_CC_VERSION"
       Note $FOUT "PACT_CC_PATH    = $PACT_CC_PATH"

    else if ($PACT_CC_FAMILY == PGI) then
       set PACTSpec = /pgi
       set cmplib   = $PACT_CC_PATH/lib
       set lnuma    = $NUMA
       Note $FOUT 'VMake += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH NUMA $@'
       Note $FOUT 'VEnv  += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
       Note $FOUT ''
       Note $FOUT "PACT_CC_FAMILY  = $PACT_CC_FAMILY"
       Note $FOUT "PACT_CC_VERSION = $PACT_CC_VERSION"
       Note $FOUT "PACT_CC_PATH    = $PACT_CC_PATH"
       Note $FOUT "NUMA            = $NUMA"

    else if ($PACT_CC_FAMILY == INTEL) then
       set PACTSpec = /intel
       set cmplib   = $PACT_CC_PATH/lib
       Note $FOUT 'VMake += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
       Note $FOUT 'VEnv  += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
       Note $FOUT ''
       Note $FOUT "PACT_CC_FAMILY  = $PACT_CC_FAMILY"
       Note $FOUT "PACT_CC_VERSION = $PACT_CC_VERSION"
       Note $FOUT "PACT_CC_PATH    = $PACT_CC_PATH"

# Solaris cannot handle long variable names so bail now
    else if ($HostOS == SunOS) then
       Note $FOUT ''

    else
       if ($PACT_CC_FAMILY == PATHSCALE) then
          set PACTSpec = /path
          if ("$Bits" == 32) then
             set cmplib = ${PACT_CC_PATH}:${PACT_CC_PATH}/32
          else
             set cmplib = $PACT_CC_PATH
          endif
          Note $FOUT 'VMake += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
          Note $FOUT 'VEnv  += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
          Note $FOUT ''
          Note $FOUT "PACT_CC_FAMILY  = $PACT_CC_FAMILY"
          Note $FOUT "PACT_CC_VERSION = $PACT_CC_VERSION"
          Note $FOUT "PACT_CC_PATH    = $PACT_CC_PATH"
       endif
    endif

    if (!(-d "$cmplib")) then
       set cmplib = ""
    endif
    if ($?CROSS_COMPILE == 1) then
       set cmplib = ""
    endif

    Note $FOUT "Path += $CCBin"
    Note $FOUT ''

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SYSTEM - generate the System group

    Note $FOUT '# PACT SYSTEM specifications'
    Note $FOUT ''

    if ($InstBase == none) then
       set proot = $RootDir
    else
       set proot = $InstBase
    endif
    set pvers = `cat .pact-version`

    set lrpath = `analyze/rpath -o list`

    Note $FOUT 'CFE           = '$CFE
    Note $FOUT 'PFE           = '$PFE
    Note $FOUT 'CROSS_COMPILE = '$CROSS_COMPILE
    Note $FOUT 'VMake        += CFE PFE CROSS_COMPILE'

# NOTE: PathScale/Intel build of Basis/MPPL requires this
    if ("$cmplib" != "") then
       Note $FOUT 'RPATH = '$cmplib' $@'
    endif
    Note $FOUT ''

    Note $FOUT 'System/pact {'
    Note $FOUT '  PACT          = '$proot
    Note $FOUT '  PACTRoot      = '$proot
    Note $FOUT '  PACTVersion   = '$pvers
    Note $FOUT '  PACTLibs      = -lpdb -lpml -lscore '${LD_Lib}
    Note $FOUT '  PACTMDGL      = '${MDG_Lib}
    Note $FOUT '  PACTMDL       = '${LD_Lib} ${LibM}
    Note $FOUT '  VMake         = PACT PACTRoot PACTVersion PACTMDGL PACTMDL PACTLibs'
    Note $FOUT '  VEnv          = PACT'
    Note $FOUT '}'
    Note $FOUT ''

    Note $FOUT 'Tool/config_pact {'
    Note $FOUT '  AddressSize = '$lbits
    Note $FOUT '}'
    Note $FOUT ''

    set llibs = ""
    set llibs = ( $llibs $cmplib )
    set llibs = ( $llibs `echo $RPATH | sed 's/:/ /g'` )

    if ($CCBin != none) then
       Note $FOUT 'Tool/cc_pact {'
       Note $FOUT '  ToolClass    = CGroup'
       Note $FOUT '  BinDir       = '$CCBin
       Note $FOUT '  LibDir       = '$llibs
       Note $FOUT '  Exe          = '$Cfe_CC_Exe

# NOTE: eventually remove this when Lee says so
       Note $FOUT '  MPI_Compiler = '$CC_Exe
       Note $FOUT '  MPI_Exe      = '$CC_Exe

       Note $FOUT '  Flags        = '$Cfe_CC_Flags
       Note $FOUT '  LXFlags      = '$Lex_Flags
       Note $FOUT '  AddressSize  = '$lbits
       Note $FOUT '  Numa         = '$lnuma
       Note $FOUT '}'
       Note $FOUT ''
    endif

    if ($FCBin != none) then
       Note $FOUT 'Tool/fc_pact {'
       Note $FOUT '  ToolClass    = FGroup'
       Note $FOUT '  BinDir       = '$FCBin
       Note $FOUT '  LibDir       = '$llibs
       Note $FOUT '  Exe          = '$Cfe_FC_Exe

# NOTE: eventually remove this when Lee says so
       Note $FOUT '  MPI_Compiler = '$FC_Exe
       Note $FOUT '  MPI_Exe      = '$FC_Exe

       Note $FOUT '  Flags        = '$Cfe_FC_Flags
       Note $FOUT '  AddressSize  = '$lbits
       Note $FOUT '  Numa         = '$lnuma
       Note $FOUT '}'
       Note $FOUT ''
    endif

    Note $FOUT 'Tool/lib_pact {'
    Note $FOUT '  ToolClass   = LibGroup'
    Note $FOUT '  LibFlags    = -r'
    Note $FOUT '  ARFlags     = '$AR_Flags $AR_IFlag
    Note $FOUT '  AddressSize = '$lbits
    Note $FOUT '}'
    Note $FOUT ''

    Note $FOUT 'Library/pact {'
    Note $FOUT '  Archives = pgs ppc pdb pml score'
    Note $FOUT '  IncPaths = '$proot/include

# temporary expedient
    Note $FOUT '  LibPaths = '$proot/lib $LibM_Path
    Note $FOUT '  LDFlags  = '$LD_Lib $MDG_Lib $MD_Lib $LibM
    Note $FOUT '  RPaths   = '$lrpath

    Note $FOUT '}'
    Note $FOUT ''

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# MATH - generate MATH library configuration info

    if ("$LibM" != "") then
       set libm = `echo $LibM | sed 's/-l//'`

       Note $FOUT '# MATH specifications'
       Note $FOUT ''
       Note $FOUT 'Library/lib_math {'
       Note $FOUT '  Archives = '$libm
       Note $FOUT '  LibPaths = '$LibM_Path
       Note $FOUT '  RPaths   = '$LibM_Path
       Note $FOUT '}'
       Note $FOUT ''
    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# MPI - generate MPI configuration info

    if ("$DP_Lib" != "") then
       Note $FOUT '# MPI specifications'
       Note $FOUT ''

       if ($?MPIBin == 1) then
          Note $FOUT 'Path = '${MPIBin}' $@'
       endif
       Note $FOUT ''

       set linc = ( `echo "$IncMPI" | sed 's/-I//g'` )

# more convenient access to include path for mio
       Note $FOUT "IncMPI = $linc"

       if ($CCBin != none) then
          Note $FOUT 'Tool/cc_pact_mpi {'
          Note $FOUT '  MPI_Flags = -I'$linc
          Note $FOUT '}'
          Note $FOUT ''
       endif

       if ($FCBin != none) then
          Note $FOUT 'Tool/fc_pact_mpi {'
          Note $FOUT '  MPI_Flags = -I'$linc
          Note $FOUT '}'
          Note $FOUT ''
       endif

       set lar = ""
       foreach i ($LibMPI)
          if ("$i" =~ \-L*) then
             continue
          else if ("$i" =~ \-l*) then
             set lar = ( $lar `echo "$i" | sed 's/-l//'` )
          endif
       end
       Note $FOUT 'Library/lib_pact_mpi {'
       Note $FOUT '  Archives = '$lar
       Note $FOUT '  LibPaths = '${MPILib}
       Note $FOUT '  RPaths   = '${MPILib}
       Note $FOUT '}'
       Note $FOUT ''

    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit(0)

