#!/bin/csh -f
#
# VERIFY-CC - verify that specified compiler can build hello world
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

@ err = 0

set cc = `which $1`
if (!(-x "$cc")) then

# attempt to map vendors to compilers as cfg-base does
   switch ($1)
      case gnu:
           set cc = `which gcc`
           breaksw
      case pgi:
           set cc = `which pgcc`
           breaksw
      case intel:
           set cc = `which icc`
           breaksw
      case path:
           set cc = `which pathcc`
           breaksw
      case sun:
           set cc = `which suncc`
           breaksw
   endsw
endif

set hst = `uname -n`
set ce  = hello.$hst.$$

if ($err == 0) then
   $cc ../psh/hello.c -o $ce >& /dev/null
   @ err = $err + $status
   if ($err != 0) then
      echo "$cc cannot compile simple executable - exiting"
   endif
endif

if ($err == 0) then
   $ce >& /dev/null
   @ err = $err + $status
   if ($err != 0) then
      echo "$cc built executable fails to run - exiting"
   endif
endif

rm -f $ce

exit($err)
