#!/bin/csh -f
#
# PACT-LIB - make unified libpact.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

@ err = 0

while ($#argv > 0)
   switch ($1)
      case -a:
           shift
           setenv SESSION_CONFIG $1
           breaksw
      case -h:
           echo ""
           echo "Usage: pact-lib [-a <cfg>] [-h]"
           echo ""
           echo ""
           exit(1)
      default:
           breaksw
   endsw
   shift
end

setenv OS_Name  `uname -s`

set MngDir = $cwd

eval `../scripts/cfgsys`

set BaseDir = $MngDir:h
set DevDir  = $BaseDir/dev
set IncDir  = $SYS_Dir/include
set BinDir  = $SYS_Dir/bin
set LibDir  = $SYS_Dir/lib
set EtcDir  = $SYS_Dir/etc
setenv ScrDir $BaseDir/scripts

source $EtcDir/env-pact.csh
source $ScrDir/functions-pact.csh

set AR     = `pact +l -info AR`
set AROpt  = `pact +l -info AROpt`
set ARXOpt = `pact +l -info ARXOpt`

echo ""
echo "Making integrated PACT library for $SYS_ID"

pushd $LibDir >& /dev/null

# make a temporary directory to work in
set tmp = tmp.$$
mkdir $tmp

# copy the .a files to the temporary directory
cp *.a $tmp
cd $tmp >& /dev/null

rm -f libpsmm.a libscc.a libultra.a libpy.a libpact.a

# extract all the .o files from all the .a files
echo "   Extracting:"
foreach i (*.a)
   if ($i !~ libpy*.a) then
      echo "      $i"
      $AR $ARXOpt $i
   endif
end

# make libpact.a with all the .o files
echo -n "   Making libpact.a .... "
$AR $AROpt libpact.a *.o >& /dev/null
mv libpact.a ..
echo "done"

# make libpact.so with all the .o files
if ($OS_Name != Darwin) then
   set bshld = `pact +l -info MAKE_Shared_Libs | tail -n 1`
   set sld   = `pact +l -info Shared_LD_Exe`
   set ldf   = `pact +l -info Shared_LD_Flags`
   if ("$bshld" == TRUE) then
      echo -n "   Making libpact.so ... "
      $sld[1] $ldf[1] -o libpact.so.1 *.o
      mv libpact.so.1 ..
      pushd .. >& /dev/null
      rm -f libpact.so*
      ln -s libpact.so.1 libpact.so
      popd >& /dev/null
      echo "done"
   endif
endif

# cleanup
cd .. >& /dev/null
$RMDir $tmp

# ls -l libpact.*

popd >& /dev/null

echo ""

exit($err)
