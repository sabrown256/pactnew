#!/bin/csh -f
#
# DO-SPLINT - map splint over source code in current directory
#

unalias *

@ err = 0

set lst = ""
#set opt = ( -warnposix -retvalint +boolint +matchanyintegral )
set opt = ""
set opt = ( $opt -warnposix -retval +boolint +matchanyintegral -nullret )
set opt = ( $opt -paramuse )
set opt = ( $opt +charint +charintliteral )
set opt = ( $opt -nullpass )
set opt = ( $opt -exportlocal )

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: do-splint [-h] [-u] <C-files>*"
           echo "   h   this help message"
           echo "   u   turn off unrecognized identifier warning"
           echo ""
           exit(0)
      case -u:
           set opt = ( $opt -unrecog )
           breaksw
      default:
           set lst = ( $lst $1 )
           breaksw
   endsw
   shift
end

if ("$lst" == "") then
   echo ""
   echo "No C source files specified - exiting"
   echo ""
   exit(1)
endif

set path = ( /usr/gapps/pact/scripts $path )
set arch = `../scripts/cfgman use`

source ../dev/$arch/etc/functions-pact.csh

InitLog log $cwd/log.splint

foreach i ($lst)
   if (($i =~ foo*) || ($i =~ bar*) || ($i =~ fu*) || ($i =~ baz*)) continue

   NoteD $log ""
   NoteD $log "------------------------------------------------------------"
   NoteD $log ""
   NoteD $log "File: $i"
   NoteD $log ""
#   ftee $log splint $opt -I../dev/$arch/include -I. $i
   gtee $log splint $opt -I../dev/$arch/include -I. $i
   @ err = $err + $gstatus[1]
end

exit($err)
