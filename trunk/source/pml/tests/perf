#!/bin/csh -f
#
# PERF - PML performance tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

#--------------------------------------------------------------------------

#                               PML PERF TEST

#--------------------------------------------------------------------------

# GOTCHA: temporarily disable this test for optimized OSX builds
# strange FPE crash or FPE handling
   set OD = ( `pact +l -info OD` )
   set OS = ( `uname -s` )
   if (("$OD" == DefaultO) && ("$OS" != Darwin) && ($TEST_ERROR_TRAPS == YES)) then
      NoteD $Log ""
      NoteD $Log "                    PML Performance Test ... "

      flog $Log pushd $SrcDir
      flog $Log $MAKE perf
      set TStatus = $status
      flog $Log popd

      set UTime = `$TIMER -r`

      if ($TStatus != 0) then
         NoteD $Log ""
         NoteD $Log "                         Can't build PML Performance Test Program"
         set xstatus = $TStatus
      else
         if ("$Valgrind" == "") then
            $CFE $BinDir/mlpfts
            set xstatus = $status
         else
            $Valgrind $BinDir/mlpfts
            set xstatus = $status
         endif

         if ("$Valgrind" != "") then
            flog $Log set xstatus = 0
         endif
      endif

      set ETime = `$TIMER -b $UTime`

      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $Log "                    PML Performance Test ... OMITTED ($xstatus/$ETime)"
      else if ($xstatus != 0) then
         NoteD $Log "                    PML Performance Test ... FAILED ($xstatus/$ETime)"
         @ Err = 1
      else
         NoteD $Log "                    PML Performance Test ... PASSED ($ETime)"
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($Err)
