#!/bin/csh -f
#
# INTERP - PML interpolation tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

#--------------------------------------------------------------------------

#                               PML INTERP TEST

#--------------------------------------------------------------------------

   if ($TEST_ERROR_TRAPS == YES) then
      NoteD $Log ""
      NoteD $Log  -n "                    PML Interp Test ........ "

      flog $Log pushd $SrcDir
      flog $Log $MAKE interp
      set TStatus = $status
      flog $Log popd

      set UTime = `$TIMER -r`

      if ($TStatus != 0) then
         NoteD $Log ""
         NoteD $Log "                         Can't build PML Interp Test Program"
         set xstatus = $TStatus
      else
         if ("$Valgrind" == "") then
            flog $LogF $CFE $BinDir/mlitst
            set xstatus = $status
         else
            flog $Log $Valgrind $BinDir/mlitst
            set xstatus = $status
         endif

         if ($xstatus == 0) then
            foreach i (1 2 3 4 5)
               set res = test_$i.out
               set ref = $RefDir/$res
               if (-f $ref) then
                  flog $Log diff $res $ref
                  if ($status != 0) then
                     flog $Log set xstatus = 1
                  endif
               else
                  flog $Log cp $res $ref
               endif
            end
         endif
      endif

      set ETime = `$TIMER -b $UTime`

      if ($xstatus != 0) then
         NoteD $Log  "FAILED ($xstatus/$ETime)"
         @ Err = 1
      else
         NoteD $Log "PASSED ($ETime)"
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($Err)
