#!/bin/csh -f
#
# IO-SCAN - check I/O timings in various situations
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set ldir = `dirname $0`

foreach iv (1 10 100 1000 10000)

   if ($iv == 1) then
      set nil = (1 10 100 1000 10000 100000 1000000 10000000)
   else if ($iv < 11) then
      set nil = (1 10 100 1000 10000 100000 1000000)
   else if ($iv < 101) then
      set nil = (1 10 100 1000 10000 100000)
   else if ($iv < 1001) then
      set nil = (1 10 100 1000 10000)
   else if ($iv < 10001) then
      set nil = (1 10 100 1000)
   else
      set nil = (1 10 100)
   endif

   set dat = io-$iv.dat
   rm -f $dat
   touch $dat

   echo "   $iv packs"

   set bt = `timer -r`
   echo -n "      "
   foreach i ($nil)
      echo $i | awk '{printf(" %7.1e", $1);}'
      do-run tpdd -q -n $i -nv $iv >>& $dat
      rm -f io-asc.dat io-bins.dat io-hdf5.dat io-pdb.dat
   end

   set et = `timer -b $bt`

   echo ""
   echo "       scan complete ($et)"

   set base = $dat:r

   sort $dat                                                      |& \
   sed '/^$/d'                                                    |  \
   awk 'BEGIN  {cmt = none;}                                         \
        $1 != cmt {cmt = $1;                                         \
                   printf("# %s %s\n", cmt, "'$base'");}             \
        $1 == cmt {printf(" %10.2e %10.2e %8.2f %10.2e %10.2e\n",    \
                          $2, $3, $4, $5, $6);                       \
                   cmt = $1;}' >&! $base.tab
   echo "       tables made"

end

# GOTCHA: using -s loses the annotations (should fix that)
do-run ultra -e -l $ldir/io-scan.scm

exit($status)
