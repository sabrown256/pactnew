#!/bin/csh -f
#
# PDTEST - test PDB
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = pdb
source ../scripts/test-env

setenv LogF  $Log

set Tests = ""
set TestL = ""
while ($#argv > 0)
   switch ($1)
      case -f:
           shift
           set Tests = ( $1 )
           breaksw
      case -h:
           echo ""
           echo "Usage: pdtest [-h] [-f #]"
           echo "  f  format version to use in tests"
           echo "  h  this help message"
           echo ""
           exit(1);
      case -*:
           breaksw
      default:
           set TestL = ( $TestL $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

# omit tests - usually in difficult CROSS_COMPILE situation
if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "PDB TESTS ... omitted"

# run the tests
else if (-e $RootDir/pdb/pdb.h) then
   set BeginTime = `$TIMER -r`

   echo ""
   echo "PDB TEST ..."

   rm -f $Log $Log
   touch $Log

   Note $Log "BinDir = $BinDir"
   Note $Log "path   = $path"  
   Note $Log "MAKE   = $MAKE"

   setenv Tmp $cwd/.tmp.$$
   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$RootDir/score/tests/vg.suppress"
      setenv Valgrind  "$Valgrind --suppressions=$RootDir/score/tests/vg.suppress-test"
   endif

   if ($?autotool == 0) then
      Note $Log "PACT     = $PACT"
      Note $Log "Parallel = |$Parallel|"
      Note $Log "MPI      = |$MPI|"
      flog $Log ../../../manager/pwhich pact
      flog $Log ../../../manager/pwhich cc
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   NoteD $Log -n "                    Compiling PDB tests ..... "
   Note $Log ""

   flog $Log pushd $SrcDir
   flog $Log $MAKE Cstd
   set TStatus = $status
   flog $Log popd

   if ($TStatus != 0) then
      NoteD $Log ""
      NoteD $Log "                         Can't build PDB Test Programs"
      set ERROR = "TRUE"
   else
      NoteD $Log "DONE"
      NoteD $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# MAIN

      if ("$TestL" == "") then
         set TestL = ""
         set TestL = ($TestL master c99 bufa bufb appdef gatsca checksum)
         set TestL = ($TestL usrtyp net fd hyper fixdnrm attr)
         set TestL = ($TestL cast typdef browse free)
         set TestL = ($TestL smp dp)
      endif
    
      if ("$Tests" == "") then

# GOTCHA: OSX takes too long on this test so just do one
         if ("$OS" == Darwin) then
            set Tests = ( 3 )
         else
            set Tests = ( 2 3 )
         endif
      endif

      foreach vrs ($Tests)

         if ($vrs == 2) then
            set Vers = "II"
         else
            set Vers = "III"
         endif
         NoteD $Log "      Version $Vers Series"
         set VTime = `$TIMER -r`

         foreach i ($TestL)
            $SrcDir/tests/$i $vrs
            if ($status == 1) then
               set ERROR = TRUE
            endif
         end

         set VTime = `$TIMER -b $VTime`
         NoteD $Log ""
         if ($ERROR == FALSE) then
            NoteD $Log "      Version $Vers ... OK ($VTime)"
         else
            NoteD $Log "      Version $Vers ... FAIL ($VTime)"
         endif
         NoteD $Log ""
      end
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP

   flog $Log $RM $Tmp

   set ETime = `$TIMER -b $BeginTime`

   if ($ERROR == "TRUE") then
      NoteD $Log ""
      NoteD $Log "PDB TEST ... FAILED ($ETime)"
      NoteD $Log ""
      NoteD $Log "See $Log for details"
   else
      NoteD $Log "PDB TEST ... PASSED ($ETime)"
   endif
endif

   NoteD $Log ""
   NoteD $Log " --------------------------------------------------------"

set xstatus = 0
if ($ERROR == "TRUE") then
   set xstatus = 1
endif

exit($xstatus)

