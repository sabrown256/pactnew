#!/bin/csh -f
#
# PCSH - test pcsh
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PCSH Test ...................... "
set rpf = none

set PCSH = pcsh

while ($#argv > 0)
   switch ($1)
      case -c:
           shift
           set PCSH = $1
           breaksw
      case -h:
           echo ""
           echo "Usage: pcsh [-c <code>] [-h]"
           echo "  c  test code (defaults to pcsh)"
           echo "  h  this help message"
           echo ""
           exit(1);
      default:
           breaksw
   endsw
   shift
end

InitLog Log $TestDir/log.pcsh.$LogSfx

NoteD $Log ""
NoteD $Log "                    $msg"

set TstDir = $SrcDir/tests
set RefDir = $TstDir/ref
set List = (psh1 psh2 psh3 psh4)
set nerr = 0

flog $Log setenv PATH $RootDir/scripts:$PATH

Note $Log "Using $PCSH"
Note $Log "Path = $PATH"

foreach t ($List)
   Separator $Log

   set f = ( `fill-string -n 28 $t` )
   NoteD $Log -n "                        $f "
   Note $Log ""

   flog $Log set Res = $t.res
   flog $Log set Ref = $RefDir/$t

   flog $Log $RM ${t}*

   set noglob
   $PCSH $TstDir/$t |& sed 's|/tmp/psh.*$|xxx|' | grep -v "open display" >&! $Res
   set xstatus = $status
   unset noglob

   if (-f $Ref) then
      flog $Log ( diff -w -b $Res $Ref >! $Res.diff )
      set xstatus = $status
   else
      flog $Log cp $Res $Ref
      set xstatus = $status
   endif

   if ($xstatus == 0) then
      NoteD $Log "ok"
      flog $Log rm -f $Res $Res.diff
   else
      flog $Log cat $Res.diff
      flog $Log set nerr = 1
      NoteD $Log "ng"
   endif
end

flog $Log touch run.foo
set rpf = ( $rpf run.* s* )

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

$TEST_CHECK part -a pcsh -e $nerr -rpf "$rpf" "$msg"

exit($status)
