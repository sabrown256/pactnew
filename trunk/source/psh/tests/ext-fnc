#!/bin/csh -f
#
# EXT-FNC - test gexec with functions
#

unalias *

set t = fnc

@ err = 0

source $TestScrDir/env-csh

set f = ( `fill-string -n 8 -c ' ' $t` )
NoteD $Log -n "                        $f "
Note $Log ""

set exe = $t
set opt = ""
set rpf = ""

set lt = `timer -r`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4 5 6)

   Note $Log ""
   Note $Log "---------------------------------------------------"
   Note $Log ""

   set id  = p$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   set pa = p1out

   switch ($i)

# simple stdout variants
      case 1:
           rm -rf $id
           set cmd = ( gexec /bin/echo "$id" @o pw:test ${pa} '>&!' $raw )
           set gst = "0 0"
           set srt = TRUE
           breaksw

# simple stdin variants
      case 2:
           set cmd = ( gexec cat @i pr:test ${pa} '>&!' $raw )
           set gst = "0 0"
           set srt = TRUE
           breaksw

# simple append variants
      case 3:
           set cmd = ( gexec /bin/echo "$id" @o2 pa:test p3out '>&!' $raw )
           set gst = "0 0"
           set srt = TRUE
           breaksw

# simple bonded variants
      case 4:
           set cmd = ( gexec /bin/echo "$id" @b2 pw:test p4bond '>&!' $raw )
           set gst = "0 0"
           set srt = TRUE
           breaksw

# non-simple stdout variants
      case 5:
           set cmd = ( gexec /bin/echo "$id" @o2 @e3 pw:test p5out @ pw:test p5err '>&!' $raw )
           set gst = "0 0 0"
           set srt = TRUE
           breaksw

      case 6:
           set cmd = ( gexec ls ext-[fv]* foo.h @o2 @e3 pw:test p6out @ pw:test p6err '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           breaksw
   endsw

   Note $Log "cmd = $cmd"
   Note $Log "srt = $srt"

   flog $Log eval $cmd

   flog $Log cat $raw

   if ($srt == TRUE) then
      flog $Log ( sort $raw | grep -v 'gen:'>&! $res )
      flog $Log rm -f $raw
   endif

   flog $Log cat $res

   if ($?PERDB_PATH == 0) then
      eval `grep "setenv gstatus" $res`
      grep -v "setenv gstatus" $res >&! $res.new
      flog $Log mv $res.new $res
   else
      dbget gstatus
   endif

   Note $Log "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      flog $Log @ err = $err + 1
      NoteD $Log -n "x"
      Note $Log "$id failed"
   else if (-f $ref) then
      flog $Log ( diff $res $ref >&! $dif )
      if ($status == 0) then
         NoteD $Log -n "."
         Note $Log "$id passed"
         flog $Log rm -f $res $dif
      else
         flog $Log @ err = $err + 1
         NoteD $Log -n "o"
         Note $Log "$id differed"
         flog $Log cat $dif
      endif
   else
      NoteD $Log -n "+"
      Note $Log "$id added"
      flog $Log cp $res $ref
      flog $Log rm -f $res
   endif

end

NoteD $Log -n " ................."

Note $Log "exiting with err = $err"
Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set xstatus = $err

set lt = `timer -b $lt`

set rpf = ( $rpf ext-$t )

$TEST_CHECK part -a gexec_${t} -e $xstatus -dt $lt -x $exe -l -rpf "$rpf" none

exit($status)
