#!/bin/csh -f
#
# EXT-SPEC - test special options @r, @x, @v, and @l
#

unalias *

set t = spec

@ err = 0

source $TestScrDir/env-csh

set f = ( `fill-string -n 8 -c ' ' $t` )
NoteD $Log -n "                        $f "
Note $Log ""

set exe = $t
set opt = ( $argv )
set rpf = ""

set lt = `timer -r`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4)

   Note $Log ""
   Note $Log "---------------------------------------------------"
   Note $Log ""

   set id  = e$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   switch ($i)
      case 1:
           set cmd = ( gexec $opt /bin/echo "$id" @x cat -n '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw
      case 2:
           set cmd = ( gexec $opt /bin/echo "$id" @r cat -n '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw
      case 3:
           set cmd = ( gexec $opt /bin/echo "foo=bar" @v printenv foo '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw
      case 4:
           set cmd = ( gexec $opt /bin/echo "core=13000" @l /bin/csh -cf limit @o grep coredumpsize '>&!' $res )
           set gst = "0 0 0"
           set srt = FALSE
           breaksw
   endsw

   Note $Log "cmd = $cmd"
   Note $Log "srt = $srt"

   flog $Log eval $cmd

   if ($srt == TRUE) then
      flog $Log ( sort $raw >&! $res )
      flog $Log rm -f $raw
   endif

   flog $Log cat $res
   if ($?PERDB_PATH == 0) then
      eval `grep "setenv gstatus" $res`
      grep -v "setenv gstatus" $res >&! $res.new
      flog $Log mv $res.new $res
   else
      dbget gstatus
   endif

   Note $Log "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      flog $Log @ err = $err + 1
      NoteD $Log -n "x"
      Note $Log "$id failed"
   else if (-f $ref) then
      flog $Log ( diff $res $ref >&! $dif )
      if ($status == 0) then
         NoteD $Log -n "."
         Note $Log "$id passed"
         flog $Log rm -f $res $dif
      else
         flog $Log @ err = $err + 1
         NoteD $Log -n "o"
         Note $Log "$id differed"
         flog $Log cat $dif
      endif
   else
      NoteD $Log -n "+"
      Note $Log "$id added"
      flog $Log cp $res $ref
      flog $Log rm -f $res
   endif

end

NoteD $Log -n " .................."

Note $Log "exiting with err = $err"
Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set xstatus = $err

set lt = `timer -b $lt`

set rpf = ( $rpf ext-$t )

$TEST_CHECK part -a gexec_${t} -e $xstatus -dt $lt -x $exe -l -rpf "$rpf" none

exit($status)
