#!/bin/csh -f
#
# PSTEST - test PSH
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package psh
setenv Test    ""
set vgdirs = ( score )
source ../scripts/test-env

set Tests = ""
set opt   = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: pstest [-h] [<test>*]"
           echo "  h  this help message"
           echo ""
#           echo "Tests: info ext do-net elide blang templ regx pcsh perdb dorun pco verify"
           echo "Tests: info ext do-net elide blang templ regx pcsh perdb dorun verify"
           echo ""
           exit(1);
      case -*:
           set opt = ( $opt $1 )
           breaksw
      default:
           set Tests = ( $1 )
           breaksw
   endsw
   shift
end

flog $Log pact install

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "PSH TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else
   NoteD $Log ""
   NoteD $Log "PSH TEST ..."

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ($Tests info ext)
      set Tests = ($Tests do-net elide blang templ)
      set Tests = ($Tests regx pcsh perdb dorun verify)
#      set Tests = ($Tests pco)
   endif

   @ nerr = 0
   Note $Log "Starting with $nerr errors"

   foreach i ($Tests)
      ftee $Log $SrcDir/tests/$i $opt
      if ($gstatus != 0) then
         Note $Log "Test $i exited with error"
         flog $Log @ nerr = $nerr + 1
      endif
   end
   
   $TEST_CHECK clean -n PSH -e $nerr

   set xstatus = $status
endif

$TEST_CHECK fin -e $xstatus

exit($status)

