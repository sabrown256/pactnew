#!/bin/csh -f
#
# PDB - Fortran test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PDB API Test ....................... "

set tst = $1

# check for Fortran compiler before doing Fortran test
set FC = `awk '$1 == "FCompiler" { print $3 }' $IncDir/make-def`
if ("$FC" != "") then
   Note $Log "Using fortran compiler $FC"
   flog $Log swhich -1 $FC
   flog $Log set xstatus = $status
else
   fog $Log set xstatus = 1
endif

#if (($xstatus == 0) && ("$MPI" == "")) then
if ($xstatus == 0) then
   NoteD $Log ""
   NoteD $Log "                    $msg"

   flog $Log pushd $SrcDir
   flog $Log $MAKE +na 2 pdb
   set xstatus = $status
   flog $Log popd

   if ($xstatus != 0) then
      NoteD $Log ""
      NoteD $Log "                         Cannot build PDB FORTRAN Test Program"
      flog $Log rm -f $BinDir/pdftst
      flog $Log rm -f $BinDir/pdfgts
      flog $Log rm -f $BinDir/pdfhyts
   else

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# test hand written wrappers
      NoteD $Log -n "                        manual ......................... "
      Note $Log ""

      set exe = $BinDir/pdftst
      set rpf = none

      if ("$Valgrind" == "") then
         flog $Log ( $CFE $exe -p |& cat )
         set xstatus = $status
      else
         Note $Log ""
         ftee $Log $Valgrind $exe            |& \
         grep "ERROR SUMMARY:"               |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a pdb_manual -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# test automatically generated wrappers
      NoteD $Log -n "                        auto ........................... "
      Note $Log ""

      set exe = $BinDir/pdfgts
      set rpf = none

      if ("$Valgrind" == "") then
         flog $Log ( $CFE $exe -p |& cat )
         set xstatus = $status
      else
         Note $Log ""
         ftee $Log $Valgrind $exe            |& \
         grep "ERROR SUMMARY:"               |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a pdb_auto -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# hyperstride test
      NoteD $Log -n "                        hyperstride .................... "
      Note $Log ""

      set exe = $BinDir/pdfhyts
      set rpf = none
      set opt = ""
      set io  = flog

      source ../../../scripts/test-run

      $TEST_CHECK part -a pdb_hyper -e $xstatus -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# distribute parallel test
      if ($MPI == HAVE_MPI) then
         flog $Log pushd $SrcDir
         flog $Log $MAKE +na 2 fdp
         set xstatus = $status
         flog $Log popd

         set CFE = $BinDir/do-run

         NoteD $Log -n "                        dp ............................. "
         Note $Log ""

         set exe = $BinDir/pdfdpt
         set rpf = none
         set opt = ( -p 4 $exe )
         set io  = flog

         source ../../../scripts/test-run

         $TEST_CHECK part -a pdb_dp -e $xstatus -x $exe -l -rpf "$rpf" none

         @ nerr = $nerr + $status
      endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK part -a pdb -e $nerr "$msg"

exit($status)
