#!/bin/csh -f
#
# SMP - PDB SMP parallel test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set tst = $1

if ("$Parallel" != "") then
   NoteD $LogF ""
   NoteD $LogF -n "                    PDB SMP Parallel Test .............. "
   Note $LogF ""

   set UTime = `$TIMER -r`

   flog $LogF pushd $SrcDir
   flog $LogF $MAKE smp
   set TStatus = $status
   flog $LogF popd

   if ($TStatus != 0) then
      NoteD $LogF ""
      NoteD $LogF "                         ERROR Building SMP Parallel Tests"
      NoteD $LogF "                    PDB SMP Parallel Test ......... FAILED (-1)"
      set Err = 1
   else
      set UTime = `$TIMER -r`
      if ("$Valgrind" == "") then
         flog $LogF $CFE $BinDir/pdsmp -v $tst -m 0
         set xstatus = $status
      else
         ftee $LogF $Valgrind $BinDir/pdsmp -v $tst -m 0  |& \
         grep "ERROR SUMMARY:"                |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         flog $LogF $RM $Tmp
      endif
      set ETime   = `$TIMER -b $UTime`

      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $LogF "OMITTED ($xstatus/$ETime)"
      else if ($xstatus != 0) then
         NoteD $LogF "FAILED ($xstatus/$ETime)"
         set Err = 1
      else
         NoteD $LogF "PASSED ($ETime)"
      endif
   endif
endif

exit($Err)
