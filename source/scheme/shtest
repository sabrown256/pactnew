#!/bin/csh -f
#
# SHTEST - test SCHEME
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = scheme
source ../scripts/test-env

setenv LogF  $Log

set Test = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: shtest [-h] [-vg] [<test>]*"
           echo "  h       this help message"
           echo "  vg      run tests under valgrind"
           echo "  <test>  one of: mem string numer"
           echo "                  proc shell"
           echo "                  csynt bsynt"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Test = ( $Test $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

# omit tests - usually in difficult CROSS_COMPILE situation
if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "SCHEME TESTS ... omitted"

# run the tests
else if (-e $BinDir/scheme) then
   set BeginTime = `$TIMER -r`

   NoteD $Log ""
   NoteD $Log "SCHEME TEST ..."

   rm -f $Log
   touch $Log

   Note $Log "cwd      = $cwd"
   Note $Log "path     = $path"  
   Note $Log "BuildDir = $BuildDir"
   Note $Log "RootDir  = $RootDir"
   Note $Log "BinDir   = $BinDir"
   Note $Log "TestDir  = $TestDir"
   Note $Log "FilesDir = $FilesDir"
   Note $Log "SCHEME   = $SCHEME"

   setenv Tmp $cwd/.tmp.$$
   if ("$Valgrind" != "") then
      setenv Valgrind  "$Valgrind --suppressions=$RootDir/score/tests/vg.suppress"
   endif

   if ( !($?autotool) ) then
      Note $Log "PACT = $PACT"
      flog $Log ../../../manager/pwhich pact
      flog $Log ../../../manager/pwhich cc
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   if ("$Test" == "") then
      set Test = ""
      set Test = ( $Test string numer proc shell csynt bsynt )
# GOTCHA: restore memory tests after hasharr conversion
#      set Test = ( $Test mem string numer proc shell csynt bsynt )
   endif

   foreach i ($Test)
      $SrcDir/tests/$i
      if ($status == 1) then
         set ERROR = TRUE
      endif
   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CLEAN UP 

   flog $Log $RM $Tmp

   set ETime = `$TIMER -b $BeginTime`

   NoteD $Log ""
   if ($ERROR == "TRUE") then
      NoteD $Log ""
      NoteD $Log "SCHEME TEST ... FAILED ($ETime)"
      NoteD $Log ""
      NoteD $Log "See $Log for details"
      NoteD $Log ""
   else
      NoteD $Log "SCHEME TEST ... PASSED ($ETime)"
   endif

else
   NoteD $Log ""
   NoteD $Log "$BinDir/scheme does not exist"
endif

NoteD $Log ""
NoteD $Log " --------------------------------------------------------"

set xstatus = 0
if ($ERROR == "TRUE") then
   set xstatus = 1
endif

exit($xstatus)

