#!/bin/csh -f
#
# LOOKUP - test hash tables and trees in SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

@ nerr = 0

flog $LogF pushd $SrcDir
flog $LogF $MAKE ha
set TStatus = $status
flog $LogF popd

if ($TStatus != 0) then
   NoteD $LogF "                         ERROR Building HASH Test Program"
   set Err = 1
else

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# HASH test

   set msg = "SCORE HASH Test ............... "
   set exe = $BinDir/scatst
   set rpf = none

   NoteD $LogF ""
   NoteD $LogF "                    $msg"
   Note $LogF ""

   if ("$Valgrind" == "") then
      ftee $LogF $CFE $exe
      set xstatus = $status
   else
      NoteD $LogF ""
      ftee $LogF $Valgrind $exe      |& \
      grep "ERROR SUMMARY:"          |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

   $TEST_CHECK part -a lookup_hash -e $xstatus -x $exe -rpf "$rpf" "$msg"

   @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# TREE test

   set msg = "SCORE TREE Test ............... "
   set exe = $BinDir/sctrts
   set rpf = none

   NoteD $LogF ""
   NoteD $LogF -n "                    $msg"
   Note $LogF ""

   if ("$Valgrind" == "") then
      flog $LogF $CFE $exe
      set xstatus = $status
   else
      ftee $LogF $Valgrind $exe       |& \
      grep "ERROR SUMMARY:"           |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

   $TEST_CHECK part -a lookup_tree -e $xstatus -x $exe -rpf "$rpf" none

   @ nerr = $nerr + $status

endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

$TEST_CHECK part -a lookup -e $nerr

exit($status)

