#!/bin/csh -f
#
# SMP - test SMP facilities in SCORE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "SCORE Parallel Test ..........."
set exe = $BinDir/tscthr

   NoteD $Log ""
   NoteD $Log "                    $msg"

   flog $Log pushd $SrcDir
   flog $Log $MAKE smp
   set xstatus = $status
   flog $Log popd

   if ($xstatus != 0) then
      NoteD $Log ""
      NoteD $Log "                         ERROR Building Parallel Tests"
      NoteD $Log "                    ${msg}FAILED"
      set lerr = 1
   else

      if ($?TEST_MAX_THREADS == 1) then
         @ nc = $TEST_MAX_THREADS
      else
         @ nc = `ncpu`
      endif

      set NThrds = ""
      @ ic = 1
      while (($ic <= $nc) && ($ic < 16))
         set NThrds = ( $NThrds $ic )
         @ ic = 2 * $ic
      end          

      set lerr = 0

      if ("$CFE" != "") then
         setenv CFE   "$CFE -v"
      endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# THREAD - test thread execution

      foreach arg (" " "-m 100")
         NoteD $Log ""

         switch ("$arg")
            case "-m*" :
              NoteD $Log "                       Parallel Memory Test"
              breaksw
            default :
              NoteD $Log "                       Simple Parallel Test"
              breaksw
         endsw

         NoteD $Log "                          Mode    # Threads ($NThrds)"

         foreach mode ("  " "-t" "-p")
            set LUTime = `$TIMER -r`

            switch ($mode)
               case "-p" :
                 set Name = "pool"
                 breaksw
               case "-t" :
                 set Name = "new "
                 breaksw
               default :
                 set Name = "low "
                 breaksw
            endsw

            NoteD $Log -n "                          $Name      "

            foreach n ($NThrds)
               flog $Log $CFE $exe $arg -n $n $mode -d 1 -s
               set xstatus = $status

               if (($xstatus == 254) || ($xstatus == 255)) then
                  NoteD $Log -n "o"
                  Note $Log ""
               else if ($xstatus == 123) then
                  NoteD $Log -n "*"
                  Note $Log ""
               else if ($xstatus != 0) then
                  set lerr = 1
                  NoteD $Log -n "x"
                  Note $Log ""
               else
                  NoteD $Log -n "."
                  Note $Log ""
               endif
            end

            set LETime = `$TIMER -b $LUTime`
            NoteD $Log "   ($LETime)"
         end
      end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# LOCK - test thread locking

      NoteD $Log ""
      NoteD $Log "                       Thread Lock Test"
      NoteD $Log "                          # Items   # Threads ($NThrds)"

      foreach mode (10 100 1000)
         set LUTime = `$TIMER -r`

         NoteD $Log -n "                          "
         echo $mode | awk '{printf("%-4s      ", $1);}'

         foreach n ($NThrds)
            flog $Log $CFE $BinDir/tsctl -nd $mode -nt $n
            set xstatus = $status

            if (($xstatus == 123) || ($xstatus == 255) || ($xstatus == 254)) then
               NoteD $Log -n "*"
               Note $Log ""
            else if ($xstatus != 0) then
               NoteD $Log -n "x"
               set lerr = 1
               Note $Log ""
            else
               NoteD $Log -n "."
               Note $Log ""
            endif
         end

         set LETime   = `$TIMER -b $LUTime`
         NoteD $Log "   ($LETime)"
      end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   endif

$TEST_CHECK part -a smp -e $lerr -rpf "$rpf" "$msg"

exit($status)

