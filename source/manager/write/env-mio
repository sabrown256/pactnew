#!/bin/csh -f
#
# ENV-MIO - write the ENV-MIO file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $Log "----- write/env-mio -----"
Note $Log "Write: env-mio"
Note $Log ""

HAVE_SUBROUTINES

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $Log

NoteD $Log "   MIO specifications - env-mio"
Note $Log ""

dbmget $Log MDG_Pck^            \
            MDI_Pck^            \
            MD_Pck^             \
            AR_Flags^           \
            AR_IFlag^           \
            Bits^               \
            CFE^                \
            Cfe_CC_Exe^         \
            Cfe_CC_Flags^       \
            Cfe_FC_Exe^         \
            Cfe_FC_Flags^       \
            CPU^                \
            CROSS_COMPILE^      \
            DAI_ROOT^           \
            DAS_ROOT^           \
            EtcDir^             \
            HAVE_READLINE^      \
            HOST_SERVER_DB^     \
            InstBase^           \
            LD_Lib^             \
            LIB^                \
            LibM_Lib^           \
            MDE_Lib^            \
            MDG_Lib^            \
            NUMA^               \
            OpenMP_SharedFnc^   \
            PACT_CC_FAMILY^     \
            PACT_CC_VERSION^    \
            PACT_CC_PATH^       \
            PACT_FC_FAMILY^     \
            PACT_FC_VERSION^    \
            PACT_FC_PATH^       \
            PFE^                \
            RPATH^              \
            RUN_SIGNATURE_DB^   \
            SITE_SIGNATURE_DB^  \
            Sys^                \
            SYS_Dir^            \
            SYS_SITE^           \
            SYS_TYPE^           \
            UPDATE_SYS_DB

set vl = ""
set plst = ( `echo $MDG_Pck $MDI_Pck $MD_Pck | sed 's|\"||g'` )
foreach p ( $plst )
   set plib = ${p}_Lib
   set pinc = ${p}_Inc
   set ppth = ${p}_Path
   set prpt = ${p}_RPath
   set pvrs = ${p}_VERSION
   set vl = ( $vl $plib $pinc $ppth $prpt $pvrs )
end
unset plib
unset pinc
unset ppth
unset prpt
unset plst

source $MngDir/write/import-db

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

    set FOUT = $EtcDir/env-mio

    flog $Log $RM $FOUT
    flog $Log touch $FOUT

    Note $FOUT "#"
    Note $FOUT "# ENV-MIO - installation dependent specifications for MIO"
    Note $FOUT "#         - configuration defined by $Sys"
    Note $FOUT "#"
    Note $FOUT ""

    set lccexe = `pwhich $Cfe_CC_Exe`
    if (-x "$lccexe") then
       set CCBin = $lccexe:h
    else
       set CCBin = none
    endif

    set lfcexe = `pwhich $Cfe_FC_Exe`
    if (-x "$lfcexe") then
       set FCBin = $lfcexe:h
    else
       set FCBin = none
    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SITE - generate the site definition info

    Note $FOUT '# SITE specifications'
    Note $FOUT ""

# site signature variables
    Note $FOUT "SYS_SITE = $SYS_SITE"
    Note $FOUT "SYS_TYPE = $SYS_TYPE"
    Note $FOUT "DAI_ROOT = $DAI_ROOT"
    Note $FOUT "DAS_ROOT = $DAS_ROOT"
    Note $FOUT ""

    Note $FOUT "VMake += DAI_ROOT DAS_ROOT"
    Note $FOUT "VEnv  += DAI_ROOT DAS_ROOT"
    Note $FOUT "VCDef += DAI_ROOT DAS_ROOT"
    Note $FOUT "VMDef += DAI_ROOT DAS_ROOT"
    Note $FOUT ""

# database variables
    if ("$HOST_SERVER_DB" != "") then
       Note $FOUT "HOST_SERVER_DB    = $HOST_SERVER_DB"
    endif
    if ("$RUN_SIGNATURE_DB" != "") then
       Note $FOUT "RUN_SIGNATURE_DB  = $RUN_SIGNATURE_DB"
    endif
    if ("$SITE_SIGNATURE_DB" != "") then
       Note $FOUT "SITE_SIGNATURE_DB = $SITE_SIGNATURE_DB"
    endif
    if ("$UPDATE_SYS_DB" != "") then
       Note $FOUT "UPDATE_SYS_DB     = $UPDATE_SYS_DB"
    endif
    Note $FOUT ""

    Note $FOUT "VMake += HOST_SERVER_DB RUN_SIGNATURE_DB SITE_SIGNATURE_DB UPDATE_SYS_DB"
    Note $FOUT "VEnv  += HOST_SERVER_DB RUN_SIGNATURE_DB SITE_SIGNATURE_DB UPDATE_SYS_DB"
    Note $FOUT ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# COMPILER - generate the compiler info

    Note $FOUT '# COMPILER specifications'
    Note $FOUT ''

    set cmplib  = ""
    set ccmplib = ""
    set fcmplib = ""
    set lnuma   = ""

    set lbits = $Bits
    if ($CPU == ia64) then
       set lbits = ""
    endif

    Note $FOUT 'VMake += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
    Note $FOUT 'VEnv  += PACT_CC_FAMILY PACT_CC_VERSION PACT_CC_PATH $@'
    Note $FOUT ""
    Note $FOUT "PACT_CC_FAMILY  = $PACT_CC_FAMILY"
    Note $FOUT "PACT_CC_VERSION = $PACT_CC_VERSION"
    Note $FOUT "PACT_CC_PATH    = $PACT_CC_PATH"
    Note $FOUT ""

    Note $FOUT 'VMake += PACT_FC_FAMILY PACT_FC_VERSION PACT_FC_PATH $@'
    Note $FOUT 'VEnv  += PACT_FC_FAMILY PACT_FC_VERSION PACT_FC_PATH $@'
    Note $FOUT ""
    Note $FOUT "PACT_FC_FAMILY  = $PACT_FC_FAMILY"
    Note $FOUT "PACT_FC_VERSION = $PACT_FC_VERSION"
    Note $FOUT "PACT_FC_PATH    = $PACT_FC_PATH"
    Note $FOUT ""

    if ($PACT_CC_FAMILY == GNU) then
       set CmpBin  = $PACT_CC_PATH/bin
       set cmplib  = $PACT_CC_PATH/$LIB
       set ccmplib = $PACT_CC_PATH/$LIB
       set fcmplib = $PACT_FC_PATH/$LIB

    else if ($PACT_CC_FAMILY == PGI) then
       set ccmplib = $PACT_CC_PATH/lib
       set fcmplib = $PACT_FC_PATH/lib
       set cmplib  = $PACT_CC_PATH/lib
       set lnuma   = $NUMA
       Note $FOUT ""
       Note $FOUT 'VMake += NUMA'

    else if ($PACT_CC_FAMILY == INTEL) then
       set cmplib  = $PACT_CC_PATH/lib
       set ccmplib = $PACT_CC_PATH/lib
       set fcmplib = $PACT_FC_PATH/lib

    else if ($PACT_CC_FAMILY == PATHSCALE) then
       if ("$Bits" == 32) then
          set cmplib  = ${PACT_CC_PATH}:${PACT_CC_PATH}/32
          set ccmplib = ${PACT_CC_PATH}:${PACT_CC_PATH}/32
          set fcmplib = ${PACT_FC_PATH}:${PACT_FC_PATH}/32
       else
          set cmplib  = $PACT_CC_PATH
          set ccmplib = $PACT_CC_PATH
          set fcmplib = $PACT_FC_PATH
       endif
    endif

    if (!(-d "$cmplib")) then
       set cmplib = ""
    endif
    if (!(-d "$ccmplib")) then
       set ccmplib = ""
    endif
    if (!(-d "$fcmplib")) then
       set fcmplib = ""
    endif
    if ($?CROSS_COMPILE == 1) then
       if ($CROSS_COMPILE != FALSE) then
          set cmplib  = ""
          set ccmplib = ""
          set fcmplib = ""
       endif
    endif

    Note $FOUT "Path += $CCBin"
    Note $FOUT ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SYSTEM - generate the System group

    Note $FOUT '# PACT SYSTEM specifications'
    Note $FOUT ""

    if ($InstBase == none) then
       set proot = $SYS_Dir
    else
       set proot = $InstBase
    endif
    set pvers = `cat .pact-version`

    if ($OpenMP_SharedFnc == yes) then
       Note $FOUT "define(NEED_OMP_SHARED_FUNCTION)"
       Note $FOUT ""
    endif

# get CFE and PFE right when being installed
    set lcfe = ( `echo "$CFE" | sed "s@$SYS_Dir@$proot@"` )
    set lpfe = ( `echo "$PFE" | sed "s@$SYS_Dir@$proot@"` )

    Note $FOUT "CFE           = $lcfe"
    Note $FOUT "PFE           = $lpfe"
    Note $FOUT "CROSS_COMPILE = $CROSS_COMPILE"
    Note $FOUT 'VMake        += CFE PFE CROSS_COMPILE'

# NOTE: PathScale/Intel build of Basis/MPPL requires this
    if ("$cmplib" != "") then
       Note $FOUT "RPATH = $cmplib" '$@'
    endif
    if ("$ccmplib" != "") then
       Note $FOUT "RPATH = $ccmplib" '$@'
    endif
    if ("$fcmplib" != "") then
       Note $FOUT "RPATH = $fcmplib" '$@'
    endif
    Note $FOUT ""

    Note $FOUT "System/pact {"
    Note $FOUT "  PACT          = $proot"
    Note $FOUT "  PACTRoot      = $proot"
    Note $FOUT "  PACTVersion   = $pvers"
    Note $FOUT "  PACTLibs      = -lpdb -lpml -lscore ${LD_Lib}"
    Note $FOUT "  PACTMDEL      = ${MDE_Lib}"
    Note $FOUT "  PACTMDGL      = ${MDG_Lib}"
    Note $FOUT "  PACTMDL       = ${LD_Lib} ${LibM_Lib}"
    Note $FOUT "  VMake         = PACT PACTRoot PACTVersion PACTMDGL PACTMDL PACTLibs"
    Note $FOUT "  VEnv          = PACT"
    Note $FOUT "}"
    Note $FOUT ""

    Note $FOUT "Tool/config_pact {"
    Note $FOUT "  AddressSize = $lbits"
    Note $FOUT "}"
    Note $FOUT ""

    if ($CCBin != none) then
       Note $FOUT "Tool/cc_pact {"
       Note $FOUT "  ToolClass    = CGroup"
       Note $FOUT "  BinDir       = $CCBin"
       Note $FOUT "  LibDir       = $ccmplib"
       Note $FOUT "  Exe          = $Cfe_CC_Exe"
       Note $FOUT "  Flags        = $Cfe_CC_Flags"
       Note $FOUT "  AddressSize  = $lbits"
       Note $FOUT "  Numa         = $lnuma"
       Note $FOUT "}"
       Note $FOUT ""
    endif

    if ($FCBin != none) then
       Note $FOUT "Tool/fc_pact {"
       Note $FOUT "  ToolClass    = FGroup"
       Note $FOUT "  BinDir       = $FCBin"
       Note $FOUT "  LibDir       = $fcmplib"
       Note $FOUT "  Exe          = $Cfe_FC_Exe"
       Note $FOUT "  Flags        = $Cfe_FC_Flags"
       Note $FOUT "  AddressSize  = $lbits"
       Note $FOUT "  Numa         = $lnuma"
       Note $FOUT "}"
       Note $FOUT ""
    endif

    Note $FOUT "Tool/lib_pact {"
    Note $FOUT "  ToolClass   = LibGroup"
    Note $FOUT "  LibFlags    = -r"
    Note $FOUT "  ARFlags     = $AR_Flags $AR_IFlag"
    Note $FOUT "  AddressSize = $lbits"
    Note $FOUT "}"
    Note $FOUT ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# emit the PACT Library
    set lrpath = ""
    if ($?PACT_CC_PATH == 1) then
       if (-d ${PACT_CC_PATH}/lib$Bits) then
          set lrpath = ${PACT_CC_PATH}/lib$Bits
       else if (-d ${PACT_CC_PATH}/lib) then
          set lrpath = ${PACT_CC_PATH}/lib
       endif
    endif

# FPACT Library
    Note $FOUT "Library/fpact {"
    Note $FOUT "  Archives  = fpact"
    Note $FOUT "  Libraries = pact"
    Note $FOUT "}"
    Note $FOUT ""

# PACT Library
    Note $FOUT "Library/pact {"
    Note $FOUT "  Libraries = pgs pdb pml score"
    Note $FOUT "}"
    Note $FOUT ""

# SCORE Library
    set plibs = ""
    foreach p ($MD_Pck)
       if ($p != DP) then
          set lp = `echo $p | tr "[A-Z]" "[a-z]"`
          set plibs = ( $plibs $lp )
       endif
    end

    Note $FOUT "Library/score {"
    Note $FOUT "  Archives  = score"
    Note $FOUT "  IncPaths  = $proot/include"
    Note $FOUT "  LibPaths  = $proot/lib"
    Note $FOUT "  RPaths    = $lrpath"
    Note $FOUT "  LDFlags   = $LD_Lib"
    Note $FOUT "  Libraries = $plibs"
    Note $FOUT "}"
    Note $FOUT ""

    unset plibs

# PML Library
    Note $FOUT "Library/pml {"
    Note $FOUT "  Archives  = pml"
    Note $FOUT "  Libraries = score"
    Note $FOUT "}"
    Note $FOUT ""

# PDB Library
    set plibs = ""
    foreach p ($MDI_Pck)
       set lp = `echo $p | tr "[A-Z]" "[a-z]"`
       set plibs = ( $plibs $lp )
    end
    set plibs = ( $plibs dp )

    Note $FOUT "Library/pdb {"
    Note $FOUT "  Archives  = pdb"
    Note $FOUT "  Libraries = pml $plibs"
    Note $FOUT "}"
    Note $FOUT ""

    unset plibs

# PGS Library
    set plibs = ""
    foreach p ($MDG_Pck)
       set lp = `echo $p | tr "[A-Z]" "[a-z]"`
       set plibs = ( $plibs $lp )
    end

    set llibs = ""
    set llibs = ( $llibs `echo $RPATH | sed 's/:/ /g'` )
    set llibs = ( `../scripts/simplify-list $llibs` )

    Note $FOUT "Library/pgs {"
    Note $FOUT "  Archives  = pgs ppc"
    Note $FOUT "  Libraries = pml $plibs"
    Note $FOUT "  LibPaths  = $llibs"
    Note $FOUT "}"
    Note $FOUT ""

    unset llibs
    unset plibs

    if ($HAVE_READLINE == TRUE) then
       Note $FOUT 'PkgRL = RL*'
       Note $FOUT ""
    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# PACKAGES - generate configuration info packages

    foreach p ($MDG_Pck $MDI_Pck $MD_Pck)
       Note $FOUT "# $p package"
       Note $FOUT ""

       set lp    = `echo $p | tr "[A-Z]" "[a-z]"`
       set lar   = ( `printenv ${p}_Lib | sed 's|-l||g'` )
       set linc  = ( `printenv ${p}_Inc | sed 's|-I||g'` )
       set lrpth = `printenv ${p}_RPath`

# filter out /usr/include from include list
       set ninc = ""
       foreach i ($linc)
          if ($i != /usr/include) then
             set ninc = ( $ninc $i )
          endif
       end
       set linc = ( $ninc )
       unset ninc

# emit the package only if at least one element is non-empty
       if (("$lar" != "") || ("$linc" != "") || ("$lrpth" != "")) then
          Note $FOUT "Library/$lp {"
          Note $FOUT "  Archives = $lar"
          Note $FOUT "  IncPaths = $linc"
          Note $FOUT "  LibPaths = $lrpth"
          Note $FOUT "  RPaths   = $lrpth"
          Note $FOUT "}"
          Note $FOUT ""
       endif
    end

# see if any package has additions to the PATH
    set lpth = ""
    foreach p ($MDG_Pck $MDI_Pck $MD_Pck)
       set lval = ( `printenv ${p}_Path` )
       if ("$lval" != "") then
          set lpth = ( $lpth $lval )
       endif
    end
    if ("$lpth" != "") then
       Note $FOUT "Path = $lpth" '$@'
       Note $FOUT ""
    endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit(0)

