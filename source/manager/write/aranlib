#!/bin/csh -f
#
# ARANLIB - abstract ranlib wrapper
#         - no error for empty or non-existent archive
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $WLog "----- write/aranlib -----"
Note $WLog "Write: aranlib"
Note $WLog ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $WLog

NoteD $WLog "   Abstract ranlib wrapper - aranlib"
Note $WLog ""

dbmget $WLog Ranlib_Exe^     \
             PSY_Root
            
setenv BinDir  $PSY_Root/bin

flog $WLog set STDOUT = $BinDir/aranlib
flog $WLog $RM $STDOUT
flog $WLog touch $STDOUT

Note $STDOUT '#\!/bin/csh -f'
Note $STDOUT '#'
Note $STDOUT '# ARANLIB - execute ranlib type command with proper behavior'
Note $STDOUT '#'
Note $STDOUT '# include "cpyright.h"'
Note $STDOUT '#'
Note $STDOUT ''
Note $STDOUT 'unalias *'
Note $STDOUT ''
Note $STDOUT 'if ($?SC_EXEC_N_ATTEMPTS) then'
Note $STDOUT '   @ na = $SC_EXEC_N_ATTEMPTS'
Note $STDOUT 'else'
Note $STDOUT '   @ na = 3'
Note $STDOUT 'endif'
Note $STDOUT ''
Note $STDOUT '@ nerr = 0'
Note $STDOUT ''
Note $STDOUT 'set libs = ""'
Note $STDOUT ''
Note $STDOUT 'while ($#argv)'
Note $STDOUT '   switch ($1)'
Note $STDOUT '      case -h:'
Note $STDOUT '           echo ""'
Note $STDOUT '           echo "Usage: aranlib [-h] [-na #] <lib>*"'
Note $STDOUT '           echo ""'
Note $STDOUT '           echo "   h      this help message"'
Note $STDOUT '           echo "   na     number of attempts to make (default 3)"'
Note $STDOUT '           echo "   <lib>  archive to be fixed"'
Note $STDOUT '           echo ""'
Note $STDOUT '           exit(1)'
Note $STDOUT '      case -na:'
Note $STDOUT '           shift'
Note $STDOUT '           @ na = $1'
Note $STDOUT '	   breaksw'
Note $STDOUT '      default:'
Note $STDOUT '           set libs = ( $libs $1 )'
Note $STDOUT '           breaksw'
Note $STDOUT '   endsw'
Note $STDOUT '   shift'
Note $STDOUT 'end'
Note $STDOUT ''
Note $STDOUT 'if ("$libs" == "") then'
Note $STDOUT '   echo "No file specifications - exiting"'
Note $STDOUT '   exit(1)'
Note $STDOUT 'else'
Note $STDOUT '   foreach l ( $libs )'
Note $STDOUT '      if (-f $l) then'
Note $STDOUT '         gexec -nv -st 1 '$Ranlib_Exe' $l @b grep -v "has no symbols"'
Note $STDOUT '         @ nerr = $nerr + $status'
Note $STDOUT '      endif'
Note $STDOUT '   end'
Note $STDOUT 'endif'
Note $STDOUT ''
Note $STDOUT 'exit($nerr)'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

flog $WLog chmod a+x $STDOUT
flog $WLog ls -l $STDOUT
flog $WLog cat $STDOUT

source $Me:h/post-common

exit(0)

