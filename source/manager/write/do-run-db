#!/bin/csh -f
#
# DO-RUN-DB - write the DO-RUN database file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# put these in shell variables since
# prune-env will remove them as environment variables
set Log    = $1
set ScrDir = $2

eval `$ScrDir/prune-env -e ConfigDir pact`
set path = ( $ScrDir $path )
source $ScrDir/env-csh
source $ScrDir/csh-subroutines
HAVE_SUBROUTINES

dbget DO_RUN_DBG
dbget DO_RUN_MPI
dbget DO_RUN_CROSS
dbget EtcDir

# convert these to shell variables so they
# do not get whacked by subroutine calls
set ldbg = $DO_RUN_DBG
set lmpi = $DO_RUN_MPI
set lcrs = $DO_RUN_CROSS

Separator $Log
Note $Log "   ConfigDir     = $ConfigDir"
Note $Log "   DO_RUN_DBG    = $ldbg"
Note $Log "   DO_RUN_MPI    = $lmpi"
Note $Log "   DO_RUN_CROSS  = $lcrs"
Note $Log "   EtcDir        = $EtcDir"

@ err = 0

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

NoteD $Log "   Do-run signature database - do-run-db"

flog $Log set db = $EtcDir/do-run-db
cat >&! $db << EOF
#
# DO-RUN-DB - execution signatures for do-run
#

EOF

if ($ldbg != none) then
   set ldbg = $ldbg
   echo "default DBG $ldbg:r" >>& $db
endif
if ($lmpi != none) then
   set lmpi = $lmpi
   echo "default MPI $lmpi:r" >>& $db
endif
if ($lcrs != none) then
   set lcross = $lcrs
   echo "default CROSS $lcross:r" >>& $db
endif

foreach d ($ConfigDir)
   if (-f $d/do-run-prolog) then
      call ADD($d/do-run-prolog)
      break
   endif
end

flog $Log ls -l $db

set cnd = ""
if ($ldbg != none) then
   set cnd = ( $cnd $ldbg )
else
   set cnd = ( gdb tv vg vgd zf )
endif

if ($lmpi != none) then
   set cnd = ( $cnd $lmpi )
else
   set cnd = ( $cnd poe srun dmpirun mpirun )
endif

if ($lcrs != none) then
   set cnd = ( $cnd $lcrs )
else
   set cnd = ( $cnd submit )
endif

Note $Log "PATH = $PATH"
foreach i ( $cnd )
   Note $Log "Checking $i"

   set file = ""
   if (-f $i) then
      set file = $i
   else
      foreach d ($ConfigDir)
         if (-f $d/do-run-$i) then
            set file = $d/do-run-$i
            break
         endif
      end
   endif
   if ("$file" == "") continue

   Note $Log "   config file = |$file|"

   set exe = `where $i:r | head -n 1`
   Note $Log "   candidate for $i name = |$exe|"
   if (-x "$exe") then
      call ADD($file)
   else
      set inf = ( `grep Exe $file` )
      if ($#inf > 2) then
         set exe = $inf[3]
         if ("$exe" !~ /*) then
            set exe = `where $exe | head -n 1`
         endif
         Note $Log "   candidate for $i file = |$exe|"
         if (-x "$exe") then
            call ADD($file)
         endif
      endif
   endif
end

echo "" >>& $db

flog $Log ls -l $db

exit($err)

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# ADD - add an entry to the database

ADD:

   set lf = $args_[1]

   cat $lf | grep -v '#' >>& $db

   echo "" >>& $db
   echo "#-------------------------------------------------------------------------" >>& $db

   Note $Log "Using $lf"

   return

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

