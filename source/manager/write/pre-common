#
# PRE-COMMON - common setup for programs in write
#
# include "cpyright.h"
#

unalias *

set Cmd    = $0
set Exe    = $Cmd:t
set ExeDir = $Cmd:h

set llog       = $1
set psy_scrdir = $2
set psy_mngdir = $psy_scrdir:h/manager

source $psy_scrdir/functions-pact.csh
source $psy_scrdir/csh-subroutines

if ($?FirstCommon == 0) then
   set lroot = $psy_scrdir:h
   set larch = `$psy_scrdir/cfgman use`
   set lbin  = $lroot/dev/$larch/bin
   set path  = ( $psy_scrdir $lbin $path )

   InitLog WLog $llog:h/write.$0:t
   unset llog

   Note $WLog ""
   Separator $WLog
   Note $WLog "Command: $Cmd"
   Note $WLog "Current: $cwd"
   Note $WLog "Date: `date`"
   Note $WLog "Manager directory: $psy_mngdir"
   Note $WLog "Session: $larch"
   if ($?SESSION_CONFIG == 1) then
      Note $ALog "SESSION_CONFIG: $SESSION_CONFIG"
   else
      Note $ALog "SESSION_CONFIG: undefined"
   endif
   Note $WLog ""

   unset lbin
   unset lroot
   unset larch

   eval `$psy_scrdir/prune-env -e psy_mngdir pact`

   setenv FirstCommon FALSE

   dbmget $WLog PATH
   flog $WLog set path = ( $psy_scrdir $path )

   Note $Log "write> doing $Exe"

endif


