#!/bin/csh -f
#
# PACKAGE - write the PACKAGE file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $WLog "----- write/package -----"
Note $WLog "Write: package"
Note $WLog ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $WLog
NoteD $WLog "   Package List - package"
Note $WLog ""

dbmget $WLog HAVE_FORTRAN^     \
             HAVE_PYTHON^      \
             HAVE_PY_NUMPY^    \
             HAVE_PY_NUMERIC^  \
             PSY_Root

    setenv EtcDir  $PSY_Root/etc

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

    set STDOUT = $EtcDir/package

    flog $WLog $RM $STDOUT
    flog $WLog touch $STDOUT

    set Pkgs = ( `awk '$1 == "pkg" {for (i = 2; i <= NF; i++) {printf("%s ", $i);};}' pact.manifest` )

# must have one package per line because dmake needs it that way
    foreach i ($Pkgs)
       if ($i == python) then
          if ("$HAVE_PYTHON" =~ *TRUE*) then
             echo "$i" >>& $STDOUT
          endif
       else if ($i == fortran) then
          if ($HAVE_FORTRAN == TRUE) then
             echo "$i" >>& $STDOUT
          endif
       else
          echo "$i" >>& $STDOUT
       endif
    end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

source $Me:h/post-common

exit(0)

