#!/bin/csh -f
#
# PACKAGE - write the PACKAGE file
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

# put these in shell variables since
# prune-env will remove them as environment variables
set Log    = $1
set ScrDir = $2

eval `$ScrDir/prune-env pact`
set path = ( $ScrDir $path )
source $ScrDir/env-csh

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $Log
NoteD $Log "   Package List - package"
Note $Log ""

set vl = ""
set vl = ( $vl EtcDir HAVE_FORTRAN )
set vl = ( $vl HavePython HavePythonNumpy HavePythonNumeric )

source $MngDir/write/import-db

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

    set STDOUT = $EtcDir/package

    flog $Log $RM $STDOUT
    flog $Log touch $STDOUT

    set Pkgs = ( `awk '$1 == "pkg" {for (i = 2; i <= NF; i++) {printf("%s ", $i);};}' pact.manifest` )

# must have one package per line because dmake needs it that way
    foreach i ($Pkgs)
       if ($i == python) then
          if (($HavePython == TRUE) && ($HavePythonNumpy == TRUE)) then
             echo "$i" >>& $STDOUT
          endif
       else if ($i == pya) then
          if (($HavePython == TRUE) && ($HavePythonNumeric == TRUE)) then
             echo "$i" >>& $STDOUT
          endif
       else if ($i == fortran) then
          if ($HAVE_FORTRAN == TRUE) then
             echo "$i" >>& $STDOUT
          endif
       else
          echo "$i" >>& $STDOUT
       endif
    end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit(0)

