#!/bin/csh -f
#
# CFG-BASE - common code to generate the main part of a config
#          - from OS, Compiler, and bits
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# NOTE: use node name for config file base
# if multiple hosts have the same SYS_TYPE there will be contention
# over the file - the node name is truly host specific
   set cfg = `uname -n | tr "[A-Z]" "[a-z]" | sed 's/\..*\..*$//'`
   set los = `uname -s | tr "[A-Z]" "[a-z]"`
   set lhw = `uname -m | tr "[A-Z]" "[a-z]"`

   rm -f $Tmp
   touch $Tmp

   echo "include spokes" >>& $Tmp
   switch ($los)
      case aix:
           set cfg = "$cfg-$bits"
           echo "include ${bits}-bit" >>& $Tmp
           echo "include compilers/xlc" >>& $Tmp
           breaksw
      case cygwin*:
           echo "include ${bits}-bit" >>& $Tmp
           echo "include compilers/cygwin" >>& $Tmp
           breaksw
      case darwin:
           echo "include compilers/gnu-osx" >>& $Tmp
           breaksw
      case freebsd:
           echo "define FREEBSD" >>& $Tmp
           echo "include ${bits}-bit" >>& $Tmp
           echo "include compilers/gnu" >>& $Tmp
           breaksw
      case hp-ux*:
           echo "include compilers/hpux" >>& $Tmp
           breaksw
      case irix*:
           echo "include ${bits}-bit" >>& $Tmp
           echo "include cpp-rule" >>& $Tmp
           echo "include compilers/irix" >>& $Tmp
           breaksw
      case osf1:
           echo "include compilers/osf" >>& $Tmp
           breaksw
      case sunos:
           set CCRoot = `$Which cc`
           set CCRoot = $CCRoot:h
           set CCRoot = $CCRoot:h
           echo "setenv SOLRoot $CCRoot" >>& $Tmp
           echo "include compilers/sol" >>& $Tmp
           breaksw
      default:
      case linux:
           switch ($CCompiler)
              case gnu-bgl:
                   echo "include auto-bgl-gnu" >>& $Tmp
                   breaksw
              case gnu-bgp:
                   echo "include auto-bgp-gnu" >>& $Tmp
                   breaksw
              case *icc:
                   echo "run std/use-intel $CCompiler" >>& $Tmp
                   echo "include ${bits}-bit" >>& $Tmp
                   echo "include compilers/icc" >>& $Tmp
                   breaksw
              case *path:
              case *pathcc:
                   echo "run std/use-path $CCompiler" >>& $Tmp
                   echo "include ${bits}-bit" >>& $Tmp
                   echo "include compilers/path" >>& $Tmp
                   breaksw
              case *pgi:
              case *pgcc:
                   echo "run std/use-pgi $CCompiler" >>& $Tmp
                   echo "include ${bits}-bit" >>& $Tmp
                   echo "include compilers/pgi" >>& $Tmp
                   breaksw
	      case sun:
		   set CCRoot = '/usr/apps/gnu/sun/sunstudio12/bin'
		   echo "setenv SOLRoot $CCRoot" >>& $Tmp
		   echo "include compilers/sun" >>& $Tmp
                   breaksw
              case xlc-bgl:
                   echo "include auto-bgl-xlc" >>& $Tmp
                   breaksw
              case xlc-bgp:
                   echo "include auto-bgp-xlc" >>& $Tmp
                   breaksw
              default:
              case *gnu:
              case *gcc:
                   if ("$CCompiler" == gnu) then
                      set lcc = gcc
                   else
                      set lcc = $CCompiler
                   endif
                   echo "run std/use-gnu $lcc" >>& $Tmp
                   echo "include ${bits}-bit" >>& $Tmp
                   echo "include compilers/gnu" >>& $Tmp
                   unset lcc
                   breaksw
           endsw
           breaksw
   endsw
