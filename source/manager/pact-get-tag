#!/bin/csh -f
#>
#> PACT-GET-TAG - Get the tag of the source repository header
#>
#> Usage:   pact-get-tag [-h] [-d]
#>
#> Options:
#>    -h         Help package 
#>    -d         Get the "date" only portion of the Code tag.
#>
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Base        = $cwd
set HelpCode    = $Base/$0

set help     = "FALSE"
set DateOnly = "FALSE"

while ($#argv > 0)
   switch ($argv[1])
      case -h:
               set help = "TRUE"
               breaksw
      case -d:
               set DateOnly = "TRUE"
               breaksw
      default:
               set help = "TRUE"
   endsw
   shift
end

if ($help == "TRUE") then
   goto Help
endif

set yr = `date +%Y`
set dt = "$yr-01-01"

set inf = ( `cvs history -a -x UP -D $dt |& grep -v cvs | sort -r | head -n 1` )
if ($DateOnly == "TRUE") then
   set ts  = ( `echo $inf[2] | sed 's/-/_/g' | sed 's/^20//'` )
else
   set tod = ( `echo $inf[3] | sed 's/:/ /g'` )
   @ hr    = $tod[1]
   @ mn    = `echo $tod[2] | sed 's/^0//'`

# add one minute or else CVS won't take this latest version
# it is evidently doing time < tag rather than time <= tag
   @ mn = $mn + 1
   if ($mn > 59) then
      @ hr = $hr + 1
      @ mn = $mn - 60
   endif
   set ts  = ( `echo "$inf[2] $hr $mn" | awk '{printf("%s %02d:%02d", $1, $2, $3);}'` )
endif

echo $ts

#if ($DateOnly == "TRUE") then
#   expr "`cvs history -T -a | tail -n 1`"  : '.*\[.\(.*_.*_.*\)_.*_.*_.*'
#else
#   expr "`cvs history -T -a | tail -n 1`"  : '.*\[\(.*_.*_.*_.*_.*_.*\):.*'
#endif

exit($status)

Help:
   awk '($1 == "#>") {print}' $HelpCode      #Print Usage:
   exit(1)
