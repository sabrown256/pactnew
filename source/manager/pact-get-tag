#!/bin/csh -f
#
# PACT-GET-TAG - Get the tag of the source repository header
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set Base     = $cwd
set DateOnly = "FALSE"

while ($#argv > 0)
   switch ($argv[1])
      case -h:
           echo "Usage: pact-get-tag [-h] [-d]"
           echo "   h   this help message"
           echo "   d   get the 'date' only portion of the Code tag"
           echo ""
           exit(1)
           breaksw
      case -d:
           set DateOnly = "TRUE"
      default:
           breaksw
   endsw
   shift
end

set yr = `date +%Y`
set dt = "$yr-01-01"

if (-d CVS) then
   set inf = ( `cvs history -a -x UP -D $dt |& grep -v cvs | sort -r | head -n 1` )
   set inf = ( $inf[2] $inf[3] )
else if (-d .svn) then
   set rev = ( `svn update |& grep "At revision"` )
   set rev = ( `echo $rev[3] | sed 's/\.$//'` )
   set inf = ( `svn info -r $rev |& grep "Last Changed Date"` )
   set inf = ( $inf[4] $inf[5] )

#   set inf = ( `svn info .pact-version |& grep "Text Last Updated" | head -n 1` )
#   set inf = ( $inf[4] $inf[5] )
else if (-d ../.git) then
   set inf = ( `git log --reverse -1 |& grep Date: | sed 's/^Date: *//'` )
   set inf[$#inf] = ""
   set inf = ( `date -d "$inf" '+%y %m %d %H %M %S'` )
#   set inf = ( `git log --reverse -1 |& tail -n 1 | sed 's/^.*| D//' | sed 's/_/ /g'` )
   set inf = ( $inf[1]-$inf[2]-$inf[3] ${inf[4]}:${inf[5]} )
else
   echo "none"
   exit(1)
endif

if ($DateOnly == "TRUE") then
   set ts  = ( `echo $inf[1] | sed 's/-/_/g' | sed 's/^20//'` )
else
   set tod = ( `echo $inf[2] | sed 's/:/ /g' | sed 's/^0//'` )
   @ hr    = $tod[1]
   @ mn    = `echo $tod[2] | sed 's/^0//'`

# add one minute or else CVS won't take this latest version
# it is evidently doing time < tag rather than time <= tag
   @ mn = $mn + 1
   if ($mn > 59) then
      @ hr = $hr + 1
      @ mn = $mn - 60
   endif
   set ts  = ( `echo "$inf[1] $hr $mn" | awk '{printf("%s %02d:%02d", $1, $2, $3);}'` )
endif

echo $ts

exit($status)

