#!/bin/csh -f
#
# WHAT-PYTHON - analyze the platform PYTHON
#        - PY_Exe       the full path to the python executable
#        - PY_Vers      the version of the python executable
#        - PY_Path      addition to path in order to find python executable
#        -              this comes from config files only
#        - PY_DirSrc    pact/dev/$PSY_ID/lib/pythond.d       (install to PY_DirDst and PY_DirPy)
#        - PY_DirDst    pact    lib/pythond.d                (install into)
#        - PY_DirPy     python  lib/pythond.d/site-packages  (install into)
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv OS_Name          `uname -s`
setenv HAVE_PYTHON      FALSE
setenv HAVE_PY_NUMERIC  FALSE
setenv HAVE_PY_NUMPY    FALSE
setenv PY_Exe           ""
setenv PY_Vers          ""
setenv PY_CC            ""
setenv PY_Cfg           ""
setenv PY_Inc           ""
setenv PY_Path          ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: what-python [-h] <python>"
           echo "   h   this help message"
           echo ""
           exit(1)
      default:
           setenv PY_Exe  $1
           breaksw
   endsw
   shift
end

   setenv found  FALSE
   setenv PY_Path  $PY_Exe:h

      if (-x "$PY_Exe") then
         setenv PY_Ver ""
         setenv PY_Inc ""
         setenv PY_Lib ""
         setenv PY_Cfg ""
         setenv PY_CC  ""

         @ ok = 0
         set inf = ( `$PY_Exe ../python/python-info.py` )
         if ($inf[1] == "Version:") then
            setenv PY_Vers $inf[2]
            @ ok = $ok + 1
         endif
         if ($inf[3] == "IncDir:") then
            setenv PY_Inc $inf[4]
            @ ok = $ok + 1
         endif
         if ($inf[5] == "LibDir:") then
            setenv PY_Lib $inf[6]
            @ ok = $ok + 1
         endif
         if ($inf[7] == "CfgDir:") then
            setenv PY_Cfg $inf[8]
            @ ok = $ok + 1
         endif
         if ($inf[9] == "Compiler:") then
            setenv PY_CC  $inf[10]
            @ ok = $ok + 1

# get the compiler python was built with
            if ((($OS_Name == "Linux")    || \
                 ($OS_Name == "Darwin")   || \
                 ($OS_Name == "FreeBSD")) && \
                ($PY_CC == cc)) then
               setenv PY_CC Python_CC_Exe
            endif
         endif

# if we have all 5 pieces of info then we have a usable python
         if ($ok == 5) then
            setenv HAVE_PYTHON TRUE

            echo "Python summary:"
            echo "      executable $PY_Exe"
            echo "      compiled with $PY_CC"
            echo "      has include directory $PY_Inc"
            echo "      has lib directory $PY_Lib"

# check for Numeric
            if (-d $PY_Inc/Numeric) then
               echo "      has numeric extensions"
               setenv HAVE_PY_NUMERIC TRUE
            else
               setenv HAVE_PY_NUMERIC FALSE
            endif

# check for NumPy
            if ((-d $PY_Lib/NumPy) || \
                (-d $PY_Lib/numpy) || \
                (-d $PY_Lib/site-packages/NumPy) || \
                (-d $PY_Lib/site-packages/numpy) || \
                (-d $PY_Lib/dist-packages/NumPy) || \
                (-d $PY_Lib/dist-packages/numpy) || \
                (-d $PY_Inc/numpy)) then
               echo "      has numpy extensions"
               setenv HAVE_PY_NUMPY TRUE
            else
               setenv HAVE_PY_NUMPY FALSE
            endif

            if (($HAVE_PY_NUMERIC == FALSE) && ($HAVE_PY_NUMPY == FALSE)) then
               echo "      does not have numeric extensions"
# NOTE: if we can build sensibly without any numeric extensions delete these
               setenv PY_Exe "no-numeric"
               setenv HAVE_PYTHON FALSE
            endif

            set found = TRUE
         endif
      endif

   if ($found == FALSE) then
      echo "none recognizable"
      setenv HAVE_PYTHON      FALSE
      setenv HAVE_PY_NUMERIC  FALSE
      setenv HAVE_PY_NUMPY    FALSE
      setenv PY_Path          ""
      setenv PY_Exe           ""
      setenv PY_CC            ""
      setenv PY_Cfg           ""
      setenv PY_Inc           ""
      setenv PY_Vers          ""
   endif

   set lbase = /usr/local/apps/pact

   if ($HAVE_PYTHON == TRUE) then
      setenv PY_Root  $lbase
      setenv PY_Head  python${PY_Vers}/pact
      if ("$PY_Path" != "") then
         set lRoot = $PY_Exe
         set lRoot = $lRoot:h
         setenv PY_Root  $lRoot:h
         setenv PY_Head  python${PY_Vers}/site-packages/pact
      endif
   endif

# export the results
echo "HAVE_PYTHON     = $HAVE_PYTHON"
echo "HAVE_PY_NUMERIC = $HAVE_PY_NUMERIC"
echo "HAVE_PY_NUMPY   = $HAVE_PY_NUMPY"
echo "PY_Path         = $PY_Path"
echo "PY_Exe          = $PY_Exe"
echo "PY_CC           = $PY_CC"
echo "PY_Cfg          = $PY_Cfg"
echo "PY_Inc          = $PY_Inc"
echo "PY_Vers         = $PY_Vers"

exit(0)

