# !/bin/csh -f
#
# PACT-SCS - define the Source Control System being used
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set SCSVersion = "3.0"
set SCSRelease = "LLNL-CODE-422942"

# be sure to define the SCSOpt if undefined by this time
if ($?SCSOpt == 0) then
   set SCSOpt = ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CVS - setup for CVS

if (-d CVS) then
   set SCSExe = cvs
   set SCSDir = CVS

   set RemoteHost = ""

   if ($?CVSROOT == 1) then
      set RemoteUser = `expr "$CVSROOT" : '\(.*\)@.*:.*'`
      set RemoteRepo = `expr "$CVSROOT" : '.*:\(.*\)'`
      if ("$RemoteUser" != "") then
         set RemoteHost = `expr "$CVSROOT" : '.*@\(.*\):.*'`
      else
         set RemoteHost = `expr "$CVSROOT" : '\(.*\):.*'`
      endif
   endif

   if ("$RemoteHost" == "") then

# handle the LOCAL repository case

      if ($?CVSROOT) then
         if (`expr "$CVSROOT" : '.*\(:\).*'` == "") then
            set Lock = $CVSROOT/commit.lock
         else
            set Lock = commit.lock
         endif
      else
         set Lock = commit.lock
      endif

      alias Unlock     "rm -f $Lock"
      alias Makelock   "touch $Lock ; chmod 660 $Lock"
      alias CatLock    "cat $Lock"
      alias KillZombie ""

      if (-e $Lock) then
         set ExistLock = TRUE
      else
         set ExistLock = FALSE
      endif

   else

# handle the REMOTE repository case

      if ($?CVS_RSH == 0) then
         setenv CVS_RSH  ssh
      endif
      if ($?RCP == 0) then
         setenv RCP      scp
      endif

      set Host    = `uname -n`
      set WhoIAm  = $USER

# for commits
      if ($?TagOnly == 1) then
         if ($TagOnly == TRUE) then
            set Date = `/bin/date '+T%y_%m_%d_%H_%M_%S'`
         else
            set Date = `/bin/date '+D%y_%m_%d_%H_%M_%S'`
         endif

# for other repository interactions
      else
         set Date = `/bin/date '+%y_%m_%d_%H_%M_%S'`
      endif

      if ($?TagAnn == 1) then
         set Message = (\'<$WhoIAm $TagAnn $Date|Remote $Host> $argv\')
      else
         set Message = (\'<$WhoIAm|$Date|Remote $Host> $argv\')
      endif

      set Stage   = "$MngDir/stage"
      set Lock    = "$Stage/commit.lock"
      set RemLock = "$RemoteRepo/commit.lock"

      if (!(-d $Stage)) then
         mkdir $Stage
         chmod 770 $Stage
      endif

      alias Unlock     "$CVS_RSH $RemoteHost rm -f $RemLock ; rm -f $Lock"
      alias Makelock   'touch' $Lock '; chmod 660' $Lock '; echo' $Message '>>' $Lock ';' $RCP $Lock $RemoteHost':'$RemoteRepo' >& /dev/null'
      alias CatLock    "$CVS_RSH $RemoteHost cat $RemLock"

# current CVS doesn't leave zombies
# but hedge against the future
      alias KillZombie ""

      if ($?DoingConfig == 0) then
         set LockTest  = `$CVS_RSH $RemoteHost ls $RemoteRepo`
         set ExistLock = FALSE
         foreach i ($LockTest)
            set entry = $i:h
            if ("$entry" == "commit.lock") then
               set ExistLock = TRUE
            endif
         end
      endif

      if (!(-d $Stage)) then
         mkdir $Stage
      endif

   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SVN - setup for SVN

else if (-d .svn) then
   set SCSExe = svn
   set SCSDir = .svn

   set Host       = `uname -n`
   set WhoIAm     = $USER
   set Date       = `/bin/date '+%y_%m_%d_%H_%M_%S'`
   set Message    = (\'<$WhoIAm|$Date|Remote $Host> $argv\')
   set Stage      = "$MngDir/stage"
   set Lock       = "$Stage/commit.lock"
   set RemoteHost = ""

   if (!(-d $Stage)) then
      mkdir $Stage
      chmod 770 $Stage
   endif

   alias Unlock     "rm -f $Lock"
   alias Makelock   'touch $Lock ; chmod 660 $Lock ; echo $Message >> $Lock'
   alias CatLock    "cat $Lock"
   alias KillZombie ""

   if ($?DoingConfig == 0) then
      if (-f $Lock) then
         set ExistLock = TRUE
      else
         set ExistLock = FALSE
      endif
   endif

endif

