#!/bin/csh -f
#
# VERIFY-CC - verify that specified compiler can build hello world
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

@ err = 0

# give the special purpose IBM BG/X and CRAY XE compilers a pass
if (("$1" =~ *-bg?) || ("$1" =~ *-cxe)) then
   exit(0)
endif

set clst = ( `echo $1 | sed 's|:| |g'` )
foreach i ($clst)
   set cc = `which $i`
   if (!(-x "$cc")) then

# attempt to map vendors to compilers as cfg-base does
      switch ($1)
         case clang:
              set cc = `which clang`
              breaksw
         case gnu:
              set cc = `which gcc`
              breaksw
         case intel:
              set cc = `which icc`
              breaksw
         case mingw*:
              exit($err)
              breaksw
         case open64:
              set cc = `which opencc`
              breaksw
         case path:
              set cc = `which pathcc`
              breaksw
         case pgi:
              set cc = `which pgcc`
              breaksw
         case sun:
              set cc = `which suncc`
              breaksw
      endsw
   endif
   
   set hst = `uname -n`
   set ce  = hello.$hst.$$

   if ($err == 0) then
      $cc ../psh/hello.c -o $ce >& /dev/null
      @ err = $err + $status
      if ($err != 0) then
         echo "$cc cannot compile simple executable - exiting"
      endif
   endif

   if ($err == 0) then
      $ce >& /dev/null
      @ err = $err + $status
      if ($err != 0) then
         echo "$cc built executable fails to run - exiting"
      endif
   endif
   
   rm -f $ce
end

exit($err)
