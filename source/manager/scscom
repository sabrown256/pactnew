#!/bin/csh -f
#
# SCCOM - commit sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set MngDir = `dirname $0`

set args = ""
set msg  = ""

while ($#argv > 0)
   switch ($1)
      case -m:
           set msg = ( $argv )
           break
           breaksw
      default:
           set args = ( $args $1 )
           breaksw
   endsw
   shift
end


if (-d $MngDir/.git) then

# find changed files
   set inf = ( `git status -s |& egrep -v '^\?'` )
   echo "Committing:"
   set files = ""
   while ($#inf > 0)
      set tag  = $inf[1]
      set file = $inf[2]
      shift inf
      shift inf
      set files = ( $files $file )
      echo " $tag  $file"
   end

# do the local commit
   git commit $args -q -m "$msg" $files

# push it up to the master repository
   set inf  = ( `cat .git/config | grep url` )
   set root = $inf[3]
   git push $root

else if (-d $MngDir/CVS) then

   cvs commit $args -m "$msg"

else if (-d $MngDir/.svn) then

   svn commit $args -m "$msg"

endif

exit($status)
