#!/bin/csh -f
#>
#> PACT-ANALYZE - analyze the current UNIX environment
#>
#> Generally used by the script dsys via pact-config
#>
#> Usage: pact-analyze [-c] [-h] [-t]
#>
#> Options:
#>   -c     Check C Declarations
#>   -h     Help Package
#>   -t     Analyzing Programming Environment Characteristics for
#>          $HSY_Name
#> WARNING: This analysis is partly based on your PATH and MANPATH.
#>          The analysis may not reflect the complete state of this system.
#>
#
#-----------------------------------------------------------------------
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

if ($?PSY_ScrDir == 0) then
   if (-d ../scripts) then
      setenv PSY_ScrDir ../scripts
   else if (-d ../../scripts) then
      setenv PSY_ScrDir ../../scripts
   endif
endif
set psy_base = $PSY_ScrDir:h
set PSY_ScrDir  = $psy_base/scripts
source $PSY_ScrDir/functions-pact.csh
source $PSY_ScrDir/csh-subroutines
HAVE_SUBROUTINES

set help     = FALSE
set HelpCode = ./$0                      

setenv HSY_Name        `uname -n`
setenv HSY_OS_Name     `uname -s`
setenv HSY_OS_Release  `uname -r`
setenv MajorVersion  `expr $HSY_OS_Release : "\(.\)"`

envini PSY_ID    `$PSY_ScrDir/cfgman use`
envini PSY_Root  $psy_base/dev/$PSY_ID

InitLog ALog $PSY_Root/log/analyze

Note $ALog "HSY_Name         = $HSY_Name"
Note $ALog "HSY_OS_Name      = $HSY_OS_Name"
Note $ALog "HSY_OS_Release   = $HSY_OS_Release"
Note $ALog "MajorVersion     = $MajorVersion"
Note $ALog "Source Directory = $PSY_ScrDir"

if ($?USER == 0) then
   if ($?LOGNAME == 0) then
      flog $ALog set USER = "anonymous"
   else
      flog $ALog set USER = $ALogNAME
   endif
endif

if ("$HSY_OS_Name" == "Windows_NT") then
   flog $ALog setenv AF_CDecls TRUE
else
   flog $ALog setenv AF_CDecls FALSE
endif
flog $ALog setenv AF_ANSI    ANSI

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

setenv ToTTY  FALSE

while ($#argv > 0)
   switch ($1)
      case -c:
           flog $ALog setenv AF_CDecls  TRUE
           shift
           flog $ALog setenv AF_ANSI    $1
           breaksw

      case -h:
           flog $ALog setenv help  "TRUE"

      case -t:
           setenv ToTTY  TRUE
           breaksw
   endsw
   shift
end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# initialization of non-graphics flags

set lTmp = ( "tmp-"`echo $HSY_OS_Name-$$ | tr "[A-Z]" "[a-z]"` )
setenv Tmp "$lTmp"

flog $ALog setenv DORUN  $BinDir/do-run
flog $ALog setenv TIMER  $PSY_ScrDir/timer

set lNM = ( `where nm | head -n 1` -g )
envini NM_Exe   "$lNM"
envini NM_Cmd   "-g"

envini HSY_CPU  "unknown"
envini HSY_FPU  "unknown"

if ($HSY_OS_Name == Linux) then
   set i = `uname -m`
   if ($i == "alpha") then
      flog $ALog setenv HSY_FPU "alpha"
   else if (`expr $i : 'i.86'` != 0) then
      flog $ALog setenv HSY_FPU "x87"
   else if ($HSY_CPU == "x86") then
      flog $ALog setenv HSY_FPU "x87"
   endif
endif

envini CROSS_FE   ""
envini DP_FE      "$DORUN -m"
envini DP_BE      ""
envini CC_Inc     ""
envini CC_Flags   ""
envini LD_Lib     ""
envini LD_Flags   ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# initialization of graphics flags

Separator $ALog

envini GRAPHICS_Windows       "X"
envini Std_UseX   TRUE
envini Std_UseOGL FALSE
envini Std_UseQD  FALSE

envini MDG_Inc         ""
envini MDG_Lib         ""
envini GRAPHICS_Devices "PS CGM MPG PNG JPG"
envini GRAPHICS_Flags    ""

Note $ALog ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# initialize Cfg group flags

Separator $ALog

envini Cfg_CC_Flags  "$CC_Flags"
envini Cfg_CC_Inc    "$CC_Inc"
envini Cfg_LD_Flags  "$LD_Flags"
envini Cfg_LD_Lib    "$LD_Lib"

Note $ALog "Cfg_CC_Inc   = |$Cfg_CC_Inc|"
Note $ALog "Cfg_LD_Flags = |$Cfg_LD_Flags|"

Note $ALog ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

Separator $ALog

if ($help == "TRUE") then
   awk '($1 == "#>") {print}' $HelpCode  #Print Usage:
   endinclude
endif

if ($ToTTY == TRUE) then

   NoteD $ALog ""
   NoteD $ALog "Analyzing Programming Environment Characteristics for $HSY_Name"
   NoteD $ALog ""
   NoteD $ALog "WARNING: This analysis is partly based on your PATH and MANPATH."
   NoteD $ALog "         The analysis may not reflect the complete state of this system."
   NoteD $ALog ""

endif

if (-d analyze) then
   setenv PSY_AnaDir  analyze
else
   setenv PSY_AnaDir  ../analyze
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run programs to analyze specific features

set Features = ""
set Features = ( $Features fix-rpath site hw os )
set Features = ( $Features csh sh cc fc ld yacc lex )
set Features = ( $Features make python util tracker )
set Features = ( $Features features graphics )

#set Features = ""
#set Features = ( $Features hw os csh sh cc fc ld yacc lex make python util )
#set Features = ( $Features termio ipc signal poll pty socket mmap )
#set Features = ( $Features openmp mpi )
#set Features = ( $Features math fpe )
#set Features = ( $Features opengl x11 )
#set Features = ( $Features libz png jpeg hdf5 tracker )
#
set Functions = ""
#set Functions = ( $Functions vsnprintf strerror_r )

set Post = ""
set Post = ( $Post finish )

set Fncs = FALSE
foreach f ($Features "-fnc-" $Functions "-pst-" $Post)
   if ("$f" == "-fnc-") then
      set Fncs = TRUE
      NoteD $ALog "   Library Functions:"
   else if ("$f" == "-pst-") then
      set Fncs = FALSE
   else
      Separator $ALog

# run the program f
      set Text = (`$PSY_AnaDir/$f | awk '{ printf("%s ; \n", $0); }'`)

# look through the output for setenvs and other text to echo
      set Line = ""
      set First = TRUE
      foreach i ($Text)
         if ("$i" == ";") then

# catch exports to parent environment
            if ("$Line[1]" == parent) then
               set lst = ( `echo $Line | sed 's/(/ /' | sed 's/)/ /'` )
               set Cmd = ( 'setenv '$lst[2]' "'$lst[3-]'"' )
               eval "$Cmd"
               Note $ALog "Command: $Cmd"
               Note $Log "Command: $Cmd"

# indent the first printed line with 3 spaces 
            else if (($First == TRUE) && ($Fncs == FALSE)) then
               set First = FALSE
               echo "   $Line"

# indent the all other printed line with 6 spaces 
            else
               echo "      $Line"
            endif
            set Line = ""

         else
            set Line = ( $Line $i )
         endif
      end
   endif
end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

NoteD $ALog ""

endinclude

