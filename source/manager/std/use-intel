#!/bin/csh -f
#
# USE-INTEL - decide on the correct Intel environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

source ../scripts/env-csh

set Which   = "$cwd/pwhich"
set CmpPath = ""
while ($#argv > 0)
   switch ($1)
      default:
           if ("$CmpPath" == "") then
              set CmpPath = $1
           endif
           breaksw
   endsw
   shift
end

# because Intel puts its compilers in different
# directories check both ifort and icc to get
# both on the PATH
# do icc last to get its environment as the final result
foreach c (ifort icc)

# if blank find compiler on your path
   if ("$CmpPath" == "") then
      set Exe = ( `$Which $c` )

# if not a full path find it on your path
   else if ("$CmpPath" !~ /*) then
      set Exe = ( `$Which $CmpPath` )
   endif

   if (-xf "$Exe") then
      set Root = $Exe:h
      set Root = $Root:h
   else if (-x "$Exe/bin/$c") then
      set Root = $Exe
   endif

   setenv PATH    $Root/bin':$PATH'

   set Info  = ( `$PACT_CC_EXE -v |& cat` )
   set lvers = $Info[2]
   set lroot = $Root
   set lexe  = $Root/bin/$c
end

setenv PACT_CC_FAMILY   INTEL
setenv PACT_CC_VERSION  $lvers
setenv PACT_CC_PATH     $lroot
setenv PACT_CC_EXE      $lexe

SetParent PATH
SetParent PACT_CC_FAMILY
SetParent PACT_CC_VERSION
SetParent PACT_CC_PATH
SetParent PACT_CC_EXE

exit(0)

