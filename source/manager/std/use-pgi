#!/bin/csh -f
#
# USE-PGI - decide on the correct PGI environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set lbits = ""
set lvers = ""
set cpath = ""
while ($#argv > 0)
   switch ($1)
      case bits:
           shift
           set lbits = $1
           breaksw
      case vers:
           shift
           set lvers = $1
           breaksw
      default:
           if ("$cpath" == "") then
              set cpath = $1
           endif
           breaksw
   endsw
   shift
end

set Awk = `which awk`

# figure out the site context
if (-f extensions/sys-site) then
   eval `extensions/sys-site`
else
   setenv DAI_ROOT /usr/share
   setenv DAS_ROOT /usr/local
endif

# find and source env-csh
if (-f ../scripts/env-csh) then
   set scrd = $cwd
else if (-f ../../scripts/env-csh) then
   set scrd = $cwd:h
endif
set ldir = $scrd:h/scripts
set path = ( $ldir $path )
source $ldir/env-csh
unset ldir
unset scrd

set cname = pgcc

# default compiler and query info
set cc  = ( `which $cname` )
set opt = ( -V )
set pat = ( target on )

# if blank find $cname on your path
if ("$cpath" == "") then
   if (-x $DAI_ROOT/pact/scripts/$cname) then
      set cc  = $DAI_ROOT/pact/scripts/$cname
      set pat = ( Compile line: )
      set opt = ( $opt -cwvrb )
      if ("$lvers" != "") then
         set opt = ( $opt -cwvers $lvers )
      endif
   endif

# if not a full path find it on your path
else if ("$cpath" !~ /*) then
   set inf = ( `which $cpath` )
   if (-x "$inf") then
      set cc = $inf
   endif

else if (-x "$cpath") then
   set cc = $cpath

endif

set sdir   = $cc:h
set bindir = $sdir

# query the compiler for information
set inf = ( `$cc $opt |& $Awk '$0 ~ /'"$pat"'/ { print $2 } $0 ~ /Compile line:/ { print $3 }'` )
if ($#inf > 1) then
   set lvers  = ( `echo $inf[2] | sed 's/[-_]/./g'` )
   set cc     = $inf[1]
   set bindir = $cc:h
else if ($#inf > 0) then
   set lvers = ( `echo $inf[1] | sed 's/[-_]/./g'` )
endif
set root = $bindir:h

# check the bits for the compiler library
if ("$bits" == 32) then
   set root = `echo $root | sed 's/-64//'`
endif

# check the compiler for NUMA flags
expr "$lvers" '>' "7.0" >& /dev/null
if ($status == 0) then
   setenv NUMA  "-Mconcur=numa"
else
   expr "$lvers" '>' "6.2" >& /dev/null
   if ($status == 0) then
      setenv NUMA  "-Mconcur=nonuma"
   else
      setenv NUMA  ""
   endif
endif

setenv PATH             $sdir':$PATH'
setenv PACT_CC_FAMILY   PGI
setenv PACT_CC_VERSION  $lvers
setenv PACT_CC_PATH     $root
setenv PACT_CC_EXE      $cc

SetParent PATH
SetParent PACT_CC_FAMILY
SetParent PACT_CC_VERSION
SetParent PACT_CC_PATH
SetParent PACT_CC_EXE
SetParent NUMA

exit(0)

