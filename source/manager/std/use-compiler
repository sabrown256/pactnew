#!/bin/csh -f
#
# USE-COMPILER - decide on the correct compiler environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set lbits = ""
set lvers = ""
set cpath = ""
set vrb   = FALSE

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: use-compiler [-h] [-v] [bits #] [vers #] <cc>"
           echo "   h     this help message"
           echo "   v     verbose mode"
           echo "   <cc>  compiler name or full path"
           echo ""
           echo ""
           exit(1)
      case -v:
           set vrb = TRUE
           breaksw
      case bits:
           shift
           set lbits = $1
           breaksw
      case vers:
           shift
           set lvers = $1
           breaksw
      default:
           if ("$cpath" == "") then
              set cpath = $1
           endif
           breaksw
   endsw
   shift
end

if (($?PSY_ID == 1) && ($?PSY_Base == 1)) then
   setenv ALog $PSY_Base/dev/$PSY_ID/log/analyze.use-compiler
else
   setenv ALog /dev/null
endif

# find and source functions-pact.csh
if (-f ../scripts/functions-pact.csh) then
   set scrd = $cwd
else if (-f ../../scripts/functions-pact.csh) then
   set scrd = $cwd:h
endif
set ldir = $scrd:h/scripts
set path = ( $ldir $path )
source $ldir/functions-pact.csh
unset ldir
unset scrd

# figure out the site context
if (-f extensions/sys-site) then
   eval `extensions/sys-site`
else
   eval `sys-site -dc`
endif

if ("$cpath" == "") then
   NoteD $ALog "No compiler specified - exiting"
   exit(1)
endif
set cname = $cpath:t
if ($cpath == $cname) then
   set cpath = ""
endif

set Sed = `which sed`
set Awk = `which awk`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SETUP - get default compiler and query basic info

set cc = ( `which $cname` )

switch ($cname)

# ABSOFT
   case af90*:
        set pact_cc_family = GNU
        set pact_fc_family = ABSOFT
        set cname = gcc
        set fname = af90
        set opt   = ( --version )
        set pat   = ( Absoft )
        set prog  = ( '$0 ~ /'$pat'/ { print $5 } $0 ~ /Compile line:/ { print $3 }' )
        breaksw

# CLANG
   case clang*:
        set pact_cc_family = CLANG
        set pact_fc_family = GNU
        set opt   = ( -v )
        set pat   = ( clang version )
        set prog  = ( '$0 ~ /'$pat'/ { print $3 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = gfortran
        breaksw

# INTEL
   case intel*:
        set cname = icc
   case icc*:
        set fname = ifort
        set pact_cc_family = INTEL
        set pact_fc_family = INTEL
        set opt   = ( -v )
        set pat   = ( Version )
        set prog  = ( '$0 ~ /'$pat'/ { print $2 } $0 ~ /Compile line:/ { print $3 }' )
        breaksw

# OPEN64
   case open64*:
        set cname = opencc
        set fname = openf90
   case opencc*:
        set pact_cc_family = OPEN64
        set pact_fc_family = OPEN64
        set opt   = ( -v )
        set pat   = ( Open64 Compiler Suite: Version )
        set prog  = ( '$0 ~ /'$pat'/ { print $NF } $0 ~ /Compile line:/ { print $3 }' )
        set fname = openf90
        breaksw

# PATHSCALE
   case pathscale*:
        set cname = pathcc
   case pathcc*:
        set fname = pathf90
        set pact_cc_family = PATHSCALE
        set pact_fc_family = PATHSCALE
        set opt   = ( -v )
        set pat   = ( Compiler Suite: Version )
        set prog  = ( '$0 ~ /'$pat'/ { print $NF } $0 ~ /Compile line:/ { print $3 }' )
        breaksw

# PCC
   case pcc*:
        set pact_cc_family = PCC
        set pact_fc_family = GNU
        set opt   = ( -v )
        set pat   = ( pcc )
        set prog  = ( '$0 ~ /'$pat'/ { print $2 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = gfortran
        breaksw

# PGI
   cae pgi*
        set cname = pgcc
        set fname = pgf90
   case pgcc*:
        set pact_cc_family = PGI
        set pact_fc_family = PGI
        set opt   = ( -V )
        set pat   = ( target on )
        set prog  = ( '$0 ~ /'$pat'/ { print $2 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = pgf90
        breaksw

# SUN
   case suncc*:
        set pact_cc_family = SUN
        set pact_fc_family = SUN
        set opt   = ( -V )
        set pat   = ( cc: Sun C )
        set prog  = ( '$0 ~ /'$pat'/ { print $4 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = sunf90
        breaksw

# Tau
   case tau_cc*:
        set pact_cc_family = TAU
        set pact_fc_family = TAU
        set opt   = ( -v )
        set pat   = ( tau_cc version )
        set prog  = ( '$0 ~ /'$pat'/ { print $4 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = tau_f90
        breaksw

# TinyCC
   case tcc*:
        set pact_cc_family = TCC
        set pact_fc_family = GNU
        set opt   = ( -v )
        set pat   = ( tcc version )
        set prog  = ( '$0 ~ /'$pat'/ { print $4 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = gfortran
        breaksw

# XLC
   case *xlc*:
        set pact_cc_family = XLC
        set pact_fc_family = XLF
        set opt   = ( -qversion )
        set pat   = ( IBM XL C )
        set prog  = ( '$0 ~ /'$pat'/ { print $NF } $0 ~ /Compile line:/ { print $3 }' )
        set fname = ( `echo $cname | sed 's|xlc|xlf90|'` )
        breaksw

# GNU is default
   default:
        set pact_cc_family = GNU
        set pact_fc_family = GNU
        set opt   = ( -v )
        set pat   = ( gcc version )
        set prog  = ( '$0 ~ /'$pat'/ { print $3 } $0 ~ /Compile line:/ { print $3 }' )
        set fname = gfortran
        breaksw
endsw

Note $ALog "Info derived from ${cname}:"
Note $ALog "   pact_cc_family  = $pact_cc_family"
Note $ALog "   pact_fc_family  = $pact_fc_family"
Note $ALog "   cname           = $cname"
Note $ALog "   fname           = $fname"
Note $ALog "   Version option  = $opt"
Note $ALog "   Version pattern = $pat"
Note $ALog "   Version awk     = $prog"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# COMPILER - get detailed compiler info

Separator $ALog

set cc     = none
set fc     = none
set croot  = none
set froot  = none
set lcvers = none
set lfvers = none
set lrpath = ""

# in case compilers were put in different directories (ala Intel)
# check both Fortran and C compilers to get both on the PATH
# do C last to get its environment as the final result

set dirl = ""
foreach c ($fname $cname)

   set cmp = `which $c`

# if blank try to find compiler on your path
   if ("$cpath" == "") then
      if (-x $PSY_AIRoot/pact/scripts/$c) then
         set cmp = $PSY_AIRoot/pact/scripts/$c
         set opt = ( $opt -cwvrb )
         if ("$lvers" != "") then
            set opt = ( $opt -cwvers $lvers )
         endif
      endif

# if not a full path find it on your path
   else if ("$cpath" !~ /*) then
      set inf = ( `which $cpath` )
      if (-x "$inf") then
         set cmp = $inf
      endif

   else if (-xf "$cpath") then
      set cmp = $cpath

# try adding bin/$c
   else if (-x "$cpath/bin/$c") then
      set cmp = $cpath/bin/$c

   endif

# check for valid executable or else bail now
   if (-x "$cmp") then
      set sdir = $cmp:h
      set dirl = ( $dirl $sdir )
   else
      continue
   endif

# only take lfvers and froot from Fortran compiler
   if ($c == $fname) then
      set bindir = $sdir

# query the compiler for information
      set inf = ( `$cmp $opt |& $Awk "$prog"` )
      if ($#inf > 1) then
         set lfvers = $inf[2]
         set fc     = $inf[1]
         set bindir = $fc:h

      else if ($#inf > 0) then

# NOTE: some compilers such as ifort lie about their versions
# ifort 12.1.xxx all say they are 12.1.0
# so take the version from the installed path in such a case.
# this depends on some coordination with the ccomp script
# and the fact that installations of multiple versions must
# be more nearly correct in their identifications
         echo "$inf" |& grep $lvers >& /dev/null
	 if ($status == 0) then
            set lfvers = $lvers
            set fc     = $inf
            set bindir = $inf:h
         else
            set lfvers = $inf[1]
            set fc     = $cmp
         endif
      endif
      if (!(-f "$lfvers")) then
         set lfvers = ( `echo $lfvers | $Sed 's/[-_]/./g'` )
      endif
      set froot = $bindir:h
      unset bindir

# only take lcvers and croot from C compiler
   else if ($c == $cname) then
      set bindir = $sdir

# query the compiler for information
      set inf = ( `$cmp $opt |& $Awk "$prog"` )
      if ($#inf > 1) then
         set lcvers = $inf[2]
         set cc     = $inf[1]
         set bindir = $cc:h

      else if ($#inf > 0) then

# NOTE: some compilers such as icc lie about their versions
# icc 12.1.xxx all say they are 12.1.0
# so take the version from the installed path in such a case.
# this depends on some coordination with the ccomp script
# and the fact that installations of multiple versions must
# be more nearly correct in their identifications
         echo "$inf" |& grep $lvers >& /dev/null
	 if ($status == 0) then
            set lcvers = $lvers
            set cc     = $inf
            set bindir = $inf:h
         else
            set lcvers = $inf[1]
            set cc     = $cmp
         endif
      endif
      if (!(-f "$lcvers")) then
         set lcvers = ( `echo $lcvers | $Sed 's/[-_]/./g'` )
      endif
      set croot = $bindir:h
      unset bindir
   endif

# gather up LD_LIBRARY_PATH settings from compiler database
   set inf = ( `$cmp -cwenv |& grep LD_LIBRARY_PATH` )
   if ("$inf" =~ LD_LIBRARY_PATH*) then
      set lp = $inf[2]
      set ok = TRUE
      foreach p ( $lrpath )
         if ($p == $lp) then
            set ok = FALSE
            break
         endif
      end
      if ($ok == TRUE) then
         set lrpath = ( $lrpath $lp )
      endif
   endif

end

# add the LD_LIBRARY_PATH settings to the configuration database
if ("$lrpath" != "") then
   analyze/rpath -a $lrpath
endif

# check the bits for the compiler library
if ("$lbits" == 32) then
   set croot = `echo $croot | $Sed 's/-64//'`
   set froot = `echo $froot | $Sed 's/-64//'`
endif

set pact_cc_version = $lcvers
set pact_cc_path    = $croot
set pact_cc_exe     = $cc

set pact_fc_version = $lfvers
set pact_fc_path    = $froot
set pact_fc_exe     = $fc

if ($pact_cc_family == PATHSCALE) then
   set pact_cc_path = $pact_cc_path/lib/$lcvers
   set pact_fc_path = $pact_fc_path/lib/$lfvers
endif

dbmset $ALog PACT_CC_FAMILY   = $pact_cc_family^   \
             PACT_CC_VERSION  = $pact_cc_version^  \
             PACT_CC_PATH     = $pact_cc_path^     \
             PACT_CC_EXE      = $pact_cc_exe^      \
             PACT_FC_FAMILY   = $pact_fc_family^   \
             PACT_FC_VERSION  = $pact_fc_version^  \
             PACT_FC_PATH     = $pact_fc_path^     \
             PACT_FC_EXE      = $pact_fc_exe

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# NUMA - get numa info

Separator $ALog

source analyze/numa

# adjust the PATH to find the compiler
if ($#dirl == 1) then
   setenv PATH  $dirl[1]':$PATH'
else
   if ($dirl[1] == $dirl[2]) then
      setenv PATH  $dirl[1]':$PATH'
   else
      setenv PATH  ${dirl[1]}:${dirl[2]}':$PATH'
   endif
endif

dbexp PATH

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($vrb == TRUE) then
   NoteD $ALog "   PACT_CC_FAMILY   = $pact_cc_family"
   NoteD $ALog "   PACT_CC_VERSION  = $pact_cc_version"
   NoteD $ALog "   PACT_CC_PATH     = $pact_cc_path"
   NoteD $ALog "   PACT_CC_EXE      = $pact_cc_exe"
   NoteD $ALog "   PACT_FC_FAMILY   = $pact_fc_family"
   NoteD $ALog "   PACT_FC_VERSION  = $pact_fc_version"
   NoteD $ALog "   PACT_FC_PATH     = $pact_fc_path"
   NoteD $ALog "   PACT_FC_EXE      = $pact_fc_exe"
   NoteD $ALog "   PATH             = $PATH"
endif

exit(0)

