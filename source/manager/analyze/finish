#!/bin/csh -f
#
# FINISH - final environment adjustments
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: finish"

dbmget $ALog MDG_Inc =\? ""^    \
             GRAPHICS_Windows^  \
             Std_UseX^          \
             Std_UseOGL^        \
             Std_UseQD^         \
             AbsoluteDeb^       \
             CC_Exe^            \
             CC_Flags^          \
             CC_Optimize^       \
             CC_Debug^          \
             FC_Optimize^       \
             FC_Debug^          \
             Save_CC^           \
             Save_CFLAGS^       \
             Cfe_CC_Inc

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($Std_UseX == FALSE) then

# have OpenGL
   if ($Std_UseOGL == "TRUE") then
      flog $ALog setenv GRAPHICS_Windows OGL

# have Quickdraw
   else if ($Std_UseQD == "TRUE") then
      flog $ALog setenv GRAPHICS_Windows QD

# no screen graphics
   else
      flog $ALog setenv GRAPHICS_Windows       ""
      flog $ALog setenv Std_UseX   FALSE
      flog $ALog setenv Std_UseOGL FALSE
      flog $ALog setenv Std_UseQD  FALSE

      dbmset $ALog Std_UseX   = $Std_UseX^    \
                   Std_UseOGL = $Std_UseOGL^  \
                   Std_UseQD  = $Std_UseQD
   endif

   dbexp GRAPHICS_Windows

endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ("$Cfe_CC_Inc" == "$RootInc") then
   flog $ALog setenv Cfe_CC_Inc ""
endif

# NOTE: compilers look at /usr/include by default not
# necessarily $RootInc
if ("$MDG_Inc" == "-I/usr/include") then
   flog $ALog setenv MDG_Inc ""
endif

if ($?Save_CC == 1) then
   flog $ALog setenv CC_Exe "$Save_CC"
endif

set tmp = ($Save_CFLAGS)
flog $ALog setenv CC_Flags "$tmp"

flog $Log setenv CC_Optimize_Default "$CC_Optimize"
flog $Log setenv CC_Debug_Default    "$CC_Debug"
flog $Log setenv FC_Optimize_Default "$FC_Optimize"
flog $Log setenv FC_Debug_Default    "$FC_Debug"

if ($AbsoluteDeb == TRUE) then
   flog $Log setenv CC_Optimize_Default "$CC_Debug"
   flog $Log setenv FC_Optimize_Default "$FC_Debug"
else if ($AbsoluteOpt == TRUE) then
   flog $Log setenv CC_Debug_Default "$CC_Optimize"
   flog $Log setenv FC_Debug_Default "$FC_Optimize"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# export the results
dbmset $ALog CC_Exe              = $CC_Exe^               \
             CC_Flags            = $CC_Flags^             \
             CC_Debug_Default    = $CC_Debug_Default^     \
             CC_Optimize_Default = $CC_Optimize_Default^  \
             FC_Debug_Default    = $FC_Debug_Default^     \
             FC_Optimize_Default = $FC_Optimize_Default^  \
             Cfe_CC_Inc          = $Cfe_CC_Inc^           \
             MDG_Inc             = $MDG_Inc

exit(0)

