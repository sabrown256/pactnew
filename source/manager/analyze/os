#!/bin/csh -f
#
# OS - analyze the platform OS
#    - Computes: OSType MACRev
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

if ($?Log == 0) then
   setenv Log $ALog
endif

Separator $Log
Note $Log "Analyze: os"

NoteD $ALog -n "   Operating System: "
Note $ALog ""

SafeEnv HostOS        `uname -s`
SafeEnv HostOSRel     `uname -r`
SafeEnv MACRev        ""

if ($?Arch == 0) then
   setenv Arch `../scripts/system-id`
endif

# remove parens - they confuse shells later on
set losr = ( `echo $HostOSRel | sed 's/(/,/g' | sed 's/)/,/g'` )
setenv HostOSRel $losr
unset losr

Note $ALog "RootInc   = $RootInc"
if ($CROSS_COMPILE == MINGW32) then
   setenv OSType     MSW
   setenv HostOS     WIN32
   setenv HostOSRel  mingw32

else if ($CROSS_COMPILE == MINGW64) then
   setenv OSType     MSW
   setenv HostOS     WIN64
   setenv HostOSRel  mingw64

else if (("$HostOS" == "Windows_NT") || \
    (("$HostOS" == "Linux") && (-e $RootInc/termio.h)) || \
    ($?IBM_BGP == 1) || ($?IBM_BGQ == 1) || \
    (-e $RootInc/sys/termio.h)) then
   setenv OSType "SYSV"

else
   setenv OSType "BSD"
endif
Note $ALog "HostOS    = $HostOS"
Note $ALog "HostOSRel = $HostOSRel"
Note $ALog "OSType    = $OSType"

if ($HostOS == Darwin) then
   setenv MACRev `echo $Arch | sed 's/osx-10\.//' | sed 's/-.*//'`
endif

# there are systems that put out garbage if this is on a single echo (AIX)
NoteD $ALog -n "$HostOS $HostOSRel"
NoteD $ALog " ($OSType)"

setenv OSType USE_$OSType

dbexp OSType
#SetParent OSType

SetParent HostOS
SetParent MACRev

exit(0)

