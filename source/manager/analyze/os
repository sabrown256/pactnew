#!/bin/csh -f
#
# OS - analyze the platform OS
#    - Computes: OS_Type, OS_Name, OSX_Version
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

if ($?Log == 0) then
   setenv Log $ALog
endif

Note $ALog "Analyze: os"

NoteD $ALog -n "   Operating System: "
Note $ALog ""

set los = ( `uname -s` )

dbmget $ALog OSX_Version  =\? ""^  \
             OS_Name      =\? "$los"

unset los

if ($?Arch == 0) then
   setenv Arch `../scripts/system-id`
endif

# remove parens - they confuse shells later on
fexvar $ALog OS_Release uname -r @b sed 's/(/,/g' @o sed 's/)/,/g'

Note $ALog "RootInc   = $RootInc"
if ($CROSS_COMPILE == MINGW32) then
   setenv OS_Type     MSW
   setenv OS_Name     WIN32
   setenv OS_Release  mingw32

else if ($CROSS_COMPILE == MINGW64) then
   setenv OS_Type     MSW
   setenv OS_Name     WIN64
   setenv OS_Release  mingw64

else if (("$OS_Name" == "Windows_NT") || \
    (("$OS_Name" == "Linux") && (-e $RootInc/termio.h)) || \
    ($?IBM_BGP == 1) || ($?IBM_BGQ == 1) || \
    (-e $RootInc/sys/termio.h)) then
   setenv OS_Type "SYSV"

else
   setenv OS_Type "BSD"
endif
Note $ALog "OS_Name    = $OS_Name"
Note $ALog "OS_Release = $OS_Release"
Note $ALog "OS_Type    = $OS_Type"

if ($OS_Name == Darwin) then
   fexvar $ALog OSX_Version echo $Arch @o sed 's/osx-10\.//' @o sed 's/-.*//'
endif

# there are systems that put out garbage if this is on a single echo (AIX)
NoteD $ALog -n "$OS_Name $OS_Release"
NoteD $ALog " ($OS_Type)"

setenv OS_Type USE_$OS_Type

# export results
dbmset $ALog OS_Type     = $OS_Type^      \
             OS_Release  = $OS_Release^   \
             OS_Name     = $OS_Name

exit(0)

