#!/bin/csh -f
#
# LIBM - analyze options for libm.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: math"

SafeEnv RPATH        ""
SafeEnv Cfg_RPATH    ""
SafeEnv LD_RPath     ""
SafeEnv LibM         "-lm"
SafeEnv LibM_Path    ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Intel Math library

SafeEnv IMF_Path ""

if (-d "$IMF_Path") then
   set nbits = `nbit -d`
   if ($nbits == 64) then
      set inf = ( `ls -rt $IMF_Path/i*/lib/*64/libimf.a |& tail -n 1` )
   else
      set inf = ( `ls -rt $IMF_Path/i*/lib/libimf.a |& tail -n 1` )
   endif
else
   set inf = ""
endif

if (-f "$inf") then
   set root = $inf:h
   flog $Log setenv IMF_Path $root
else
   flog $Log setenv IMF_Path ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# figure out the math library situation

if ("$IMF_Path" != "") then
   flog $ALog setenv RPATH         ${RPATH}:${IMF_Path}
   flog $ALog setenv LD_RPath      ${LD_RPath}:${IMF_Path}
   flog $ALog setenv Cfg_RPATH     ${Cfg_RPATH}:${IMF_Path}
   flog $ALog setenv LibM          "-limf"
   flog $ALog setenv LibM_Path     ${IMF_Path}

else
   flog $ALog setenv LibM      "-lm"

endif
NoteD $ALog "   Math library: $LibM"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

SetParent IMF_Path
SetParent RPATH
SetParent LD_RPath
SetParent Cfg_RPATH
SetParent LibM
SetParent LibM_Path

exit(0)

