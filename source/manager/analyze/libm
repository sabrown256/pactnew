#!/bin/csh -f
#
# LIBM - analyze options for libm.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: math"

dbini IMF_Lib      "-limf"
dbini IMF_Path     ""
dbini MD_Pck       ""

envini RPATH        ""
envini Cfg_RPATH    ""
envini LibM_Lib     "-lm"
envini LibM_RPath   ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Intel Math library

if (-d "$IMF_Path") then
   set nbits = `nbit -d`

   set inf = ( `ls -rt $IMF_Path/ic*/lib/libimf.a |& grep -v beta | tail -n 1` )

else
   set inf = ""
endif
Note $ALog "IMF_Path = |$IMF_Path|"
Note $ALog "inf      = |$inf|"

if (-f "$inf") then
   set root = $inf:h
   flog $ALog setenv IMF_Path $root
else
   flog $ALog setenv IMF_Path ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# figure out the math library situation

if ("$IMF_Path" != "") then
   flog $ALog setenv RPATH         ${RPATH}:${IMF_Path}
   flog $ALog setenv Cfg_RPATH     ${Cfg_RPATH}:${IMF_Path}
   flog $ALog setenv MD_Pck        "$MD_Pck LibM"
   flog $ALog setenv LibM_Lib      "${IMF_Lib}"
   flog $ALog setenv LibM_RPath    ${IMF_Path}

else
   flog $ALog setenv LibM_Lib      "-lm"

endif
NoteD $ALog "   Math library: $LibM_Lib"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

dbexp MD_Pck
dbexp IMF_Path

envexp RPATH
envexp Cfg_RPATH
envexp LibM_Lib
envexp LibM_RPath

exit(0)

