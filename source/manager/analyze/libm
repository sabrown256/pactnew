#!/bin/csh -f
#
# LIBM - analyze options for libm.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: math"

SafeEnv RPATH        ""
SafeEnv Cfg_RPATH    ""
SafeEnv MD_Pck       ""
SafeEnv LibM_Lib     "-lm"
SafeEnv LibM_RPath   ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Intel Math library

SafeEnv IMF_Path ""

if (-d "$IMF_Path") then
   set nbits = `nbit -d`
   if ($nbits == 64) then
      set inf = ( `ls -rt $IMF_Path/i*/lib/*64/libimf.a |& grep -v beta | tail -n 1` )
   else
      set inf = ( `ls -rt $IMF_Path/i*/lib/libimf.a |& grep -v beta | tail -n 1` )
   endif
else
   set inf = ""
endif
Note $ALog "IMF_Path = |$IMF_Path|"
Note $ALog "inf       = |$inf|"

if (-f "$inf") then
   set root = $inf:h
   flog $ALog setenv IMF_Path $root
else
   flog $ALog setenv IMF_Path ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# figure out the math library situation

if ("$IMF_Path" != "") then
   flog $ALog setenv RPATH         ${RPATH}:${IMF_Path}
   flog $ALog setenv Cfg_RPATH     ${Cfg_RPATH}:${IMF_Path}
   flog $ALog setenv MD_Pck        "$MD_Pck LibM"
   flog $ALog setenv LibM_Lib      "-limf"
   flog $ALog setenv LibM_RPath    ${IMF_Path}

else
   flog $ALog setenv LibM_Lib      "-lm"

endif
NoteD $ALog "   Math library: $LibM_Lib"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

SetParent IMF_Path
SetParent RPATH
SetParent Cfg_RPATH
SetParent MD_Pck
SetParent LibM_Lib
SetParent LibM_RPath

exit(0)

