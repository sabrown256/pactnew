#!/bin/csh -f
#
# LIBM - analyze options for libm.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "Analyze: libm"

dbmget $ALog IMF_Lib     =\? -limf^  \
             IMF_Path    =\? ""^     \
             USE_IMF     =\? FALSE^  \
             LibM_Lib    =\? "-lm"^  \
             LibM_RPath  =\? ""^     \
             Cfg_RPATH   =\? ""^     \
             PACT_CC_FAMILY^         \
             PACT_CC_PATH^           \
             RPATH

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Intel Math library
# use Intel Math library in preference to GNU math library
# because of very poor performance of functions such as sqrt near 1.0

if (-d "$IMF_Path") then

# for INTEL compiler use libimf that comes with compiler
   if (($PACT_CC_FAMILY == INTEL) && ("$PACT_CC_PATH" =~ */icc*)) then
      flog $ALog set t = $PACT_CC_PATH/lib/libimf.a

# for other compiler find "best" libimf
   else
      fexvars $ALog t ls -t $IMF_Path/{icc,ic*}/lib/libimf.a @b grep -v beta @o head -n 3 @o tail -n 1
   endif

else
   set t = ""
endif

Note $ALog "IMF_Path = |$IMF_Path|"
Note $ALog "t        = |$t|"

if (-f "$t") then
   set root = $t:h
   flog $ALog setenv IMF_Path $root
   flog $ALog setenv USE_IMF  TRUE
else
   flog $ALog setenv USE_IMF  FALSE
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# figure out the math library situation

if ($USE_IMF == TRUE) then
   flog $ALog setenv RPATH         ${RPATH}:${IMF_Path}
   flog $ALog setenv Cfg_RPATH     ${Cfg_RPATH}:${IMF_Path}
   flog $ALog setenv LibM_Lib      "${IMF_Lib}"
   flog $ALog setenv LibM_RPath    ${IMF_Path}
else
   flog $ALog setenv IMF_Path      ""
   flog $ALog setenv LibM_Lib      "-lm"
endif

NoteD $ALog "   Math library: $LibM_Lib"

# we do not have any executables from here
flog $ALog setenv IMF_Path ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

dbmset $ALog IMF_Path   = $IMF_Path^   \
             USE_IMF    = $USE_IMF^    \
             LibM_Lib   = $LibM_Lib^   \
             LibM_RPath = $LibM_RPath^ \
             Cfg_RPATH  = $Cfg_RPATH^  \
             RPATH      = $RPATH

source $Me:h/post-common

exit(0)

