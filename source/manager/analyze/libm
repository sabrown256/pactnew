#!/bin/csh -f
#
# LIBM - analyze options for libm.a
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "Analyze: libm"

dbmget $ALog IMF_Lib     =\? -limf^  \
             IMF_Path    =\? ""^     \
             LibM_Lib    =\? "-lm"^  \
             LibM_RPath  =\? ""^     \
             MD_Pck      =\? FALSE^  \
             Cfg_RPATH   =\? ""^     \
             RPATH

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# Intel Math library

if (-d "$IMF_Path") then
   fexvars $ALog t ls -t $IMF_Path/{icc,ic*}/lib/libimf.a @b grep -v beta @o head -n 3 @o tail -n 1
else
   set t = ""
endif

Note $ALog "IMF_Path = |$IMF_Path|"
Note $ALog "t        = |$t|"

if (-f "$t") then
   set root = $t:h
   flog $ALog setenv IMF_Path $root
else
   flog $ALog setenv IMF_Path ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# figure out the math library situation

if ("$IMF_Path" != "") then
   flog $ALog setenv RPATH         ${RPATH}:${IMF_Path}
   flog $ALog setenv Cfg_RPATH     ${Cfg_RPATH}:${IMF_Path}
   flog $ALog setenv MD_Pck        "$MD_Pck LibM"
   flog $ALog setenv LibM_Lib      "${IMF_Lib}"
   flog $ALog setenv LibM_RPath    ${IMF_Path}

else
   flog $ALog setenv LibM_Lib      "-lm"

endif
NoteD $ALog "   Math library: $LibM_Lib"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

dbmset $ALog MD_Pck     = $MD_Pck^     \
             IMF_Path   = $IMF_Path^   \
             LibM_Lib   = $LibM_Lib^   \
             LibM_RPath = $LibM_RPath^ \
             Cfg_RPATH  = $Cfg_RPATH^  \
             RPATH      = $RPATH

exit(0)

