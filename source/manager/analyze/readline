#!/bin/csh -f
#
# READLINE - analyze platform READLINE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/readline -----"
Note $ALog "Analyze: readline"
Note $ALog ""

dbget LIB

dbmget $ALog MD_Inc        =\? ""^           \
             MD_Lib        =\? ""^           \
             MD_Pck        =\? ""^           \
             RL_Inc        =\? ""^           \
             RL_Lib        =\? ""^           \
             RL_RPath      =\? /usr/${LIB}^  \
             RL_Path       =\? /usr^         \
             HAVE_READLINE =\? FALSE^        \
             CC_Flags^                       \
             CFE^                            \
             Cfe_LD_Flags^                   \
             Cfe_LD_Lib^                     \
             Cfe_LD_RPath

# if we have explicitly said we do not want READLINE
# do NOT detect it
if ("$RL_Path" == none) then
   NoteD $ALog "      no readline (by request)"

else

   set incl = ""
   set pthl = ""
   set libl = ""
   set spkl = ""

   set UTime = `timer -r`

# write a little readline test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdio.h>
#include <readline/readline.h>
#include <readline/history.h>
int main(int c, char **v)
{int rv;
 char *(*f)(const char *p);
 rl_already_prompted = 0;
 rl_save_prompt();
 f  = readline;
 rv = (f == NULL);
 rl_restore_prompt();
 rl_clear_message();
/* printf("%s 0x%04x\n", rl_library_version, rl_readline_version); */
 fprintf(stdout, "%s\n", rl_library_version);
 if (rv == 0)
    fprintf(stderr, "TRUE\n");
 else
    fprintf(stderr, "FALSE\n");
 return(rv);}
EOF

   set ok    = FALSE
   set lvers = none
   set llib  = ""
   set libl  = ( -lreadline -lcurses )
   set opt   = ""
   set opt   = ( $opt $CC_Flags $Cfe_LD_Flags $Cfe_LD_RPath $libl )
   flog $ALog $CC_Exe $Tmp.c -o $Tmp $opt
   if ($status == 0) then
      fexec $ALog $CFE $Tmp @o2 @e3 vw:READLINE_Version @ vw:HAVE_READLINE
      if ($gstatus[1] == 0) then
         set ok = TRUE
      endif
   endif
   unset opt

   if ($ok == TRUE) then
      flog $ALog set llib = ( $libl )
      flog $ALog setenv MD_Pck        "RL $MD_Pck"
      dbget READLINE_Version
      NoteD $ALog "      has readline $READLINE_Version ($llib)"
   else
      NoteD $ALog "      no readline"
   endif

   unset libl
   unset ok

   flog $ALog $RM $Tmp.c $Tmp

#   setenv MD_Lib   "$llib $MD_Lib"
   setenv RL_Inc   ""
   setenv RL_Lib   "$llib"
   setenv RL_RPath ""

# NOTE: used RL_Path to find libs, incs - there are no executables
   setenv RL_Path  ""

# export the results
   dbmset $ALog MD_Inc   = $MD_Inc^    \
                MD_Lib   = $MD_Lib^    \
                MD_Pck   = $MD_Pck^    \
                RL_Inc   = $RL_Inc^    \
                RL_Lib   = $RL_Lib^    \
                RL_RPath = $RL_RPath^  \
                RL_Path  = $RL_Path
endif

exit(0)

