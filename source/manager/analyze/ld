#!/bin/csh -f
#
# LD - analyze the platform linker
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: ld"

NoteD $ALog -n "   Ld: "
Note $ALog ""

dbini LD_Exe         ""
dbini LD_Flags       ""
dbini LD_Lib         ""
dbini Cfe_LD_Flags   ""
dbini Cfe_LD_Lib     ""
dbini Cfe_LD_RPath   ""
dbini Cfg_LD_Flags   ""
dbini Cfg_LD_RPath   ""
dbini DP_Lib         ""

set vl = ""
set vl = ( $vl Cfe_CC_Exe Cfg_RPATH OS_Name )
source $MngDir/write/import-db

if (!(-x "$LD_Exe")) then
   if (-x `$Which ld`) then
      flog $ALog setenv LD_Exe `$Which ld`
   else
      flog $ALog setenv LD_Exe ""
   endif
endif

cat << EOF >! $Tmp.c
int main(int c, char **v)
{return(0);}
EOF

Note $ALog ""
Note $ALog "Checking for -rpath flag"
flog $ALog $Cfe_CC_Exe -c $Tmp.c -o $Tmp.o
if ($OS_Name == AIX) then
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -bsvr4 -R $cwd
   set LStat = $status
else
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -rpath $cwd
   set LStat = $status
endif
if ($LStat == 0) then
   NoteD $ALog "$LD_Exe (has -rpath)"
   setenv HAVE_RPATH TRUE
else
   NoteD $ALog "$LD_Exe (no -rpath)"
   setenv HAVE_RPATH FALSE
endif

flog $ALog $RM $Tmp $Tmp.c $Tmp.o

dbini Shared_LD_Exe "$LD_Exe"

# capture current notion of rpath
if ($?Cfg_RPATH == 1) then
   set lrpath = `../analyze/rpath -o rpath`
   Note $ALog "Set config rpath flags to: $lrpath"
   flog $ALog setenv LD_RPath      "$lrpath"
   flog $ALog setenv Cfg_LD_RPath  "$lrpath"
   Note $ALog ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($CROSS_COMPILE == "FALSE") then
   flog $ALog setenv Cfe_LD_Flags  "$Cfg_LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$Cfg_LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$Cfg_LD_RPath"
else
   flog $ALog setenv Cfe_LD_Flags  "$LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$LD_RPath"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# export the results
dbexp HAVE_RPATH
dbexp Shared_LD_Exe
dbexp DP_Lib

dbexp LD_Exe
dbexp LD_Flags
dbexp LD_Lib
dbexp Cfg_LD_Flags
dbexp Cfg_LD_RPath
dbexp Cfe_LD_Flags
dbexp Cfe_LD_Lib
dbexp Cfe_LD_RPath

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

source $AnaDir/dl
source $AnaDir/bfd

exit(0)

