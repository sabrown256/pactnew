#!/bin/csh -f
#
# LD - analyze the platform linker
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/ld -----"
Note $ALog "Analyze: linker"
Note $ALog ""

dbmget $ALog LD_Exe        =\? ""^   \
             LD_Flags      =\? ""^   \
             LD_Lib        =\? ""^   \
             Cfe_LD_Flags  =\? ""^   \
             Cfe_LD_Lib    =\? ""^   \
             Cfe_LD_RPath  =\? ""^   \
             Cfg_LD_Flags  =\? ""^   \
             Cfg_LD_RPath  =\? ""^   \
             DP_Lib        =\? ""^   \
             Cfe_CC_Exe^             \
             Cfg_RPATH^              \
             PACT_CC_VERSION^        \
             HSY_OS_Name

if (!(-x "$LD_Exe")) then
   if (-x `where ld |& head -n 1`) then
      flog $ALog setenv LD_Exe `where ld |& head -n 1`
   else
      flog $ALog setenv LD_Exe ""
   endif
endif

cat << EOF >! $Tmp.c
int main(int c, char **v)
{return(0);}
EOF

Note $ALog ""
Note $ALog "Checking for -rpath flag"
flog $ALog $Cfe_CC_Exe -c $Tmp.c -o $Tmp.o
if ($HSY_OS_Name == AIX) then
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -bsvr4 -R $cwd
   set LStat = $status
else
   flog $ALog $LD_Exe $Tmp.o -o $Tmp -rpath $cwd
   set LStat = $status
endif
if ($LStat == 0) then
   NoteD $ALog "   Ld: $LD_Exe (has -rpath)"
   setenv HAVE_RPATH TRUE
else
   NoteD $ALog "   Ld: $LD_Exe (no -rpath)"
   setenv HAVE_RPATH FALSE
endif

flog $ALog $RM $Tmp $Tmp.c $Tmp.o

dbmget $ALog Shared_LD_Exe =\? $LD_Exe

# capture current notion of rpath
if ($?Cfg_RPATH == 1) then
   set lrpath = `../analyze/rpath -o rpath`
   Note $ALog "Set config rpath flags to: $lrpath"
   flog $ALog setenv LD_RPath      "$lrpath"
   flog $ALog setenv Cfg_LD_RPath  "$lrpath"
   Note $ALog ""
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

if ($CROSS_COMPILE == "FALSE") then
   flog $ALog setenv Cfe_LD_Flags  "$Cfg_LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$Cfg_LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$Cfg_LD_RPath"
else
   flog $ALog setenv Cfe_LD_Flags  "$LD_Flags"
   flog $ALog setenv Cfe_LD_Lib    "$LD_Lib"
   flog $ALog setenv Cfe_LD_RPath  "$LD_RPath"
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# export the results
dbmset $ALog LD_Exe        = $LD_Exe^              \
             LD_Flags      = $LD_Flags^            \
             LD_Lib        = $LD_Lib^              \
             Cfg_LD_Flags  = $Cfg_LD_Flags^        \
             Cfg_LD_RPath  = $Cfg_LD_RPath^        \
             Cfe_LD_Flags  = $Cfe_LD_Flags^        \
             Cfe_LD_Lib    = $Cfe_LD_Lib^          \
             Cfe_LD_RPath  = $Cfe_LD_RPath^        \
             HAVE_RPATH    = $HAVE_RPATH^          \
             Shared_LD_Exe = $Shared_LD_Exe^       \
             DP_Lib        = $DP_Lib

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

ftee $ALog gexec $PSY_AnaDir/dl     @ \
                 $PSY_AnaDir/bfd    @ \
                 $PSY_AnaDir/libz

source $Me:h/post-common

exit(0)

