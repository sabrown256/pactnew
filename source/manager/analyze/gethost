#!/bin/csh -f
#
# GETHOST - analyze platform gethostbyname
#         - Computes: HAVE_GETHOSTBYNAME
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: gethostbyname"

set vl = ""
set vl = ( $vl Cfe_LD_Flags Cfe_LD_RPath )
source $MngDir/write/import-db

cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <stdio.h>
#include <netdb.h>
int main(int c, char **v)
   {int rv;
    struct hostent *hp;
    hp = gethostbyname(v[1]);
    if (hp != NULL)
       rv = 0;
    else
       rv = 1;
    if (rv == 0)
       printf("TRUE\n");
    else
       printf("FALSE\n");
    return(rv);}
EOF

setenv HAVE_GETHOSTBYNAME FALSE

set ok = FALSE

set opt = ""
set opt = ( $opt $CC_Flags $Cfe_LD_Flags $Cfe_LD_RPath )
flog $ALog $CC_Exe $Tmp.c -o $Tmp $opt
if ($status == 0) then
   fexec $ALog $CFE $Tmp `uname -n` @o2 @e3 vw:HAVE_GETHOSTBYNAME @ fw:/dev/null
   if ($gstatus[1] == 0) then
      set ok = TRUE
   endif
endif

flog $ALog rm -f $Tmp.c $Tmp

if ($ok == TRUE) then
   NoteD $ALog "      has gethostbyname"
else
   NoteD $ALog "      no gethostbyname"
endif

unset ok

exit(0)

