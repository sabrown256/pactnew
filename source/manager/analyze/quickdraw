#!/bin/csh -f
#
# QUICKDRAW - analyze OSX platform for Quickdraw
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

SafeEnv HostOS           `uname -s`
SafeEnv GraphicsDevices  ""
SafeEnv GraphicsFlag     ""
SafeEnv Cfg_CC_Exe       "cc"
SafeEnv AnaDir           analyze
SafeEnv ALog             $cwd/cfg-$System/log.analyze

SafeEnv MDG_Inc          ""
SafeEnv MDG_Lib          ""
SafeEnv MDG_Pck          ""
SafeEnv QD_Inc           ""
SafeEnv QD_Lib           ""
SafeEnv QD_RPath         /usr/lib
SafeEnv QD_Path          /usr
SafeEnv Std_UseQD        FALSE

if ($HostOS == Darwin) then

   Separator $Log
   Note $Log "Analyze: Quickdraw"

   set UTime = `timer -r`

# write a little QD test
   cat << EOF >! $Tmp.c
#include <ApplicationServices/ApplicationServices.h>
#include <Carbon/Carbon.h>
int main(int c, char **v)
{FontInfo font_data;
 GrafPtr port;
 InitCursor();
 GetFontInfo(&font_data);
 TextSize(12);
 SetPort(port);
 MoveTo(10, 20);
 return(0);}
EOF

   set opt = ""
   set opt = ( $opt $LD_Flags $LD_RPath )
   if ($CC_Exe !~ pgc*) then
      set opt = ( $opt -framework Carbon )
   endif
   set opt = ( $opt $Cfg_LD_Lib )

   flog $ALog $Cfg_CC_Exe $Tmp.c -o $Tmp $opt
   set xstatus = $status
   if ($xstatus == 0) then
      if ($Std_UseQD == TRUE) then
         NoteD $ALog "      has Quickdraw (Carbon)"

         setenv GSYS             "QD"
         setenv GraphicsDevices  "$GraphicsDevices QD"
         setenv GraphicsFlag     "$GraphicsFlag -framework Carbon"

         setenv MDG_Pck          "$MDG_Pck QD"

         setenv QD_Inc           "-framework Carbon"
         setenv QD_Lib           ""
         setenv QD_RPath         ""

      else
         NoteD $ALog "      no Quickdraw (by request)"
      endif
   else
      NoteD $ALog "      no Quickdraw"
      setenv Std_UseQD   FALSE
   endif

   set ETime = `timer -b $UTime`

# NOTE: used QD_Path to find libs, incs - there are no executables we need
   if ("$QD_Path" == /usr) then
      setenv QD_Path  ""
   endif

# export the results
   SetParent MDG_Inc
   SetParent MDG_Lib
   SetParent MDG_Pck
   SetParent QD_Inc
   SetParent QD_Lib
   SetParent QD_RPath
   SetParent QD_Path
   SetParent GraphicsDevices

endif

SetParent Std_UseQD

exit(0)

