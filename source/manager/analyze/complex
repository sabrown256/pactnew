#!/bin/csh -f
#
# COMPLEX - analyze the handling of C99 complex
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/complex -----"
Note $ALog "Analyze: C90 complex"
Note $ALog ""

dbmget $ALog HAVE_ANSI_C9X_COMPLEX =\?  TRUE^  \
             Cfe_CC_Exe^                       \
             PACT_CC_VERSION

Note $ALog "HAVE_ANSI_C9X_COMPLEX = $HAVE_ANSI_C9X_COMPLEX"

if ($HAVE_ANSI_C9X_COMPLEX == TRUE) then

   cat << EOF >! $Tmp.c
#include <complex.h>
int main(int c, char **v)
   {
#ifdef complex
    return(0);
#else
    return(1);
#endif
    }
EOF

   flog $ALog $Cfe_CC_Exe -c $Tmp.c -o $Tmp.o $ccopt
   if ($status == 0) then
      NoteD $ALog "      has C99 complex support"
   else
      setenv HAVE_ANSI_C9X_COMPLEX FALSE
      NoteD $ALog "      no C99 complex support"
   endif

   rm -f $Tmp $Tmp.c $Tmp.o
else
   NoteD $ALog "      no C99 complex support (by request)"
endif

# export the result
dbexp HAVE_ANSI_C9X_COMPLEX

