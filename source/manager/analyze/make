#!/bin/csh -f
#
# MAKE - analyze the platform MAKE
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: make"

SafeEnv GMAKE_Flag ""
if ("$GMAKE_Flag" == TRUE) then
   setenv GMAKE_Flag "-DUSE_GNU_MAKE"
endif

NoteD $ALog -n "   Make: "
Note $ALog ""

rm -f $Tmp.c $Tmp.a

rm -f $Tmp.make
touch $Tmp.make

Note $Tmp.make '.c.a :'
Note $Tmp.make '	ar r '$Tmp'.a $<'
Note $Tmp.make ""
Note $Tmp.make $Tmp'.a : '$Tmp'.a('$Tmp'.o)'
Note $Tmp.make ""

Note $ALog "Path = $PATH"
flog $ALog gmake --version
if ($status == 0) then
   NoteD $ALog -n "`$Which gmake`"
   NoteD $ALog -n " (GNU) "
   setenv GMAKE_Flag "-DUSE_GNU_MAKE"
   setenv BuildStrategy "SpaceSaving"
   NoteD $ALog " library aware"

else
   NoteD $ALog -n "`$Which make`"
   touch $Tmp.c
   flog $ALog make -f $Tmp.make $Tmp.a
   if (($status != 0) || !(-e $Tmp.a)) then
      setenv BuildStrategy "none"
      NoteD $ALog " minimal"
   else
      setenv BuildStrategy "SpaceSaving"
      NoteD $ALog " library aware"
   endif
   flog $ALog rm -f $Tmp.c $Tmp.a
endif

flog $ALog rm -f $Tmp.make

# export the results
SetParent BuildStrategy
SetParent GMAKE_Flag

exit(0)

