#!/bin/csh -f
#
# BFD - analyze platform exe format access support
#     - Computes: HAVE_BFD, MDE_Lib
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $ALog
Note $ALog "Analyze: exe access support"

SafeEnv MDE_Lib              ""
SafeEnv HAVE_BFD FALSE

cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <bfd.h>
int main(int c, char **v)
   {int rv;
    bfd *et;
    et = bfd_openr(v[0], NULL);
    rv = (et == NULL);
    bfd_close(et);
    return(rv);}
EOF

flog $ALog cat $Tmp.c

# try building without -lbfd
set ok = FALSE
flog $ALog $CC_Exe $Tmp.c -o $Tmp $CC_Flags
if ($status == 0) then
   flog $ALog $Tmp
   if ($status == 0) then
      set ok = TRUE
   endif
endif

# try building with -lbfd
if ($ok == FALSE) then
   flog $ALog $CC_Exe $Tmp.c -o $Tmp $CC_Flags -lbfd
   if ($status == 0) then
      flog $ALog $Tmp
      if ($status == 0) then
         set ok = NEED
      endif
   endif
endif

flog $ALog rm -f $Tmp.c $Tmp

if ($ok == FALSE) then
   NoteD $ALog "      no bfd"
else
   NoteD $ALog "      has bfd"
   flog $ALog setenv MDE_Lib "$MDE_Lib -lbfd"
   flog $ALog setenv HAVE_BFD TRUE
endif

SetParent MDE_Lib
SetParent HAVE_BFD

Separator $ALog

exit(0)

