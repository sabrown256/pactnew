#!/bin/csh -f
#
# OPENGL - analyze platform Open GL options
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/opengl -----"
Note $ALog "Analyze: opengl"
Note $ALog ""

dbmget $ALog HSY_RootInc^                        \
             OGL_Lib          =\? ""^            \
             OGL_RPath        =\? $HSY_RootLib^  \
             OGL_Path         =\? /usr^          \
             Std_UseOGL       =\? FALSE^         \
             Cfe_CC_Exe^                         \
             Cfe_CC_Flags^                       \
             Cfe_LD_Flags^                       \
             Cfe_LD_Lib^                         \
             Cfe_LD_RPath^                       \
             PACT_CC_VERSION^                    \
             OS_Name^                            \
             RPATH

# if we have explicitly said we do not want OGL
# do NOT detect it
if ("$OGL_Path" == none) then
   NoteD $ALog "      no OpenGL (by request)"
   setenv Std_UseOGL FALSE

else

   set UTime = `timer -r`

   cat << EOF >! $Tmp.c
#include <GL/gl.h>
#include <GL/glx.h>
int main(int c, char **v)
{void *x;
 x = (void *) glXQueryExtension;
 return(0);}
EOF

   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($OGL_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/$LIB )
   end
   set lIncDirs = ( $lIncDirs $OGL_Inc )
   set lLibDirs = ( $lLibDirs $OGL_RPath )

   set Implicit = TRUE
   set lIncDirs = ( $HSY_RootInc )
   set lLibDirs = ( $lLibDirs /usr/lib64 $HSY_RootLib /usr/openwin/lib )
   set lHdrs    = ( GL/gl.h GL/glx.h )
   set ASList   = ( a so )
   set lLib     = GL
   set lFile    = $Tmp

   set lALibs = ""
   foreach i ( `var-list -l $RPATH` )
       set lALibs = ( $lALibs -L$i )
   end

   source $PSY_AnaDir/find-package

   if (("$PckLib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has OpenGL ($PckLib)"
      Note $ALog "Include = |$PckInc|"

      flog $ALog setenv Std_UseOGL TRUE
   else
      NoteD $ALog "      no OpenGL"
      flog $ALog setenv Std_UseOGL FALSE
   endif

   setenv OGL_Inc   "$PckInc"
   setenv OGL_Lib   "$PckLib"
   setenv OGL_RPath "$PckPath"

# NOTE: used OGL_Path to find libs, incs - there are no executables we need
   if ("$OGL_Path" == /usr) then
      setenv OGL_Path  ""
   endif

   set ETime = `timer -b $UTime`

   if ($Std_UseOGL == TRUE) then

# export the results
      dbmset $ALog OGL_Inc          = $OGL_Inc^            \
                   OGL_Lib          = $OGL_Lib^            \
                   OGL_RPath        = $OGL_RPath^          \
                   OGL_Path         = $OGL_Path
   endif

endif

dbexp Std_UseOGL

exit(0)

