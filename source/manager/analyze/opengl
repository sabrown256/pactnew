#!/bin/csh -f
#
# OPENGL - analyze platform Open GL options
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: opengl"

SafeEnv HostOS           `uname -s`
SafeEnv GraphicsDevices  ""
SafeEnv GraphicsFlag     ""

SafeEnv MDG_Inc          ""
SafeEnv MDG_Lib          ""
SafeEnv MDG_Pck          ""
SafeEnv OGL_Path         /usr
SafeEnv OGL_RPath        /usr/lib
SafeEnv OGL_Inc          ""
SafeEnv OGL_Lib          ""
SafeEnv Std_UseOGL       FALSE

set UTime = `timer -r`

if ($HostOS == Darwin) then
   Note $ALog "Finding GL on MAC OS X"
   setenv MDG_Lib "$MDG_Lib -framework GLUT -framework OpenGL"
   setenv Cfe_LD_Flags  "$MDG_Lib"
endif

set lLibDirs = ""
foreach i ($MDG_Lib)
   set j = $i:h
   if (-d $j) then
      set lLibDirs = ( $lLibDirs $j )
   endif
end

Note $ALog "MDG_Lib  = |$MDG_Lib|"
Note $ALog "lLibDirs = |$lLibDirs|"

      cat << EOF >! $Tmp.c
#include <GL/gl.h>
#include <GL/glx.h>
int main(int c, char **v)
{void *x;
 x = (void *) glXQueryExtension;
 return(0);}
EOF

set Implicit = TRUE
set lIncDirs = ( $RootInc /usr/X11R6/include )
set lLibDirs = ( $lLibDirs /usr/lib64 /usr/lib /usr/openwin/lib /usr/X11R6/lib )
set lHdrs    = ( GL/gl.h GL/glx.h )
set ASList   = ( a so )
set lLib     = GL
set lFile    = $Tmp

set lALibs = ""
foreach i ( `echo $RPATH | sed 's|:| |g'` )
    set lALibs = ( $lALibs -L$i )
end

source $AnaDir/find-package

if (("$PckLib" != "") && ("$PckInc" != "")) then
   NoteD $ALog "      has OpenGL ($PckLib)"
   Note $ALog "Include = |$PckInc|"

   setenv GSYS             "OGL"
   setenv GraphicsDevices  "$GraphicsDevices OGL"
   setenv GraphicsFlag     "$GraphicsFlag -DOGL"

   if (("$PckInc" != "none") && ("$PckInc" != "-I$RootInc")) then
      setenv MDG_Inc "$MDG_Inc $PckInc"
   endif
   Note $ALog "MDG Include = |$MDG_Inc|"

   setenv MDG_Lib    "$MDG_Lib $PckLib"
   setenv MDG_Pck    "$MDG_Pck OGL"
   setenv Std_UseOGL TRUE
else
   NoteD $ALog "      no OpenGL"
   setenv Std_UseOGL FALSE
endif

setenv OGL_RPath "$PckPath"
setenv OGL_Lib   "$PckLib"
setenv OGL_Inc   "$PckInc"

set ETime = `timer -b $UTime`

if ($Std_UseOGL == TRUE) then

# export the results
   SetParent MDG_Inc
   SetParent MDG_Lib
   SetParent MDG_Pck
   SetParent OGL_Path
   SetParent OGL_RPath
   SetParent OGL_Lib
   SetParent OGL_Inc
   SetParent GraphicsDevices

endif

SetParent Std_UseOGL

exit(0)

