#
# COMMON-LIB64 - analyze 64-bit archive directories depending on $Bits
#              - input is shell variable lroot
#              - output is shell variable LibT
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# initial guess is that we will have $lroot/lib as the answer
set LibT  = lib

set lbits = ""
set ldir  = ""
foreach dir ($lroot/lib64 $lroot/lib)
   ls $dir/*.o >& /dev/null
   if ($status == 0) then
      set objs = ( `ls $dir/*.o` )
      foreach o ($objs)
         if (-l $o) then
            set ob = ( `ls -l $o | sed 's|^.*->||'` )
         else
            set ob = $o
         endif
         if (-f $ob) then
            set lbits = `file $ob`
            set ldir  = $dir:t
            break
         endif
      end
      unset objs
   endif
   if ("$ldir" != "") then
      break
   endif
end

# find out whether we have 64 bit objects or not
gexec echo "$lbits" @o awk '{for (i = 1; i <= NF; i++) {if ($i ~ /bit/) {print $i};};}' @o sed 's/-bit//' @o vw:t
dbgets t
if ($#t > 1) then
   set t = $t[1]
endif

if ($t == 64) then

# determine whether we want to use 64 bit objects or not (default is yes)
   set luse_64 = TRUE
   if ($?Bits == 1) then
      if ($Bits == 32) then
         set luse_64 = FALSE
      endif
   endif

# if everything agrees use <path>/lib64
   if ($luse_64 == TRUE) then
      set LibT = $ldir
   endif
endif

unset t
unset ldir
unset lbits
unset luse_64

