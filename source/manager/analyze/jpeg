#!/bin/csh -f
#
# JPEG - analyze platform JPEG
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Note $ALog "----- analyze/jpeg -----"
Note $ALog "Analyze: jpeg"
Note $ALog ""

dbmget $ALog JPEG_Inc     =\? ""^        \
             JPEG_Lib     =\? ""^        \
             JPEG_RPath   =\? $RootLib^  \
             JPEG_Path    =\? /usr^      \
             HAVE_JPEGLIB =\? FALSE^     \
             Cfe_CC_Exe^                 \
             Cfe_CC_Flags^               \
             Cfe_LD_Flags^               \
             Cfe_LD_Lib^                 \
             Cfe_LD_RPath^               \
             PACT_CC_VERSION^            \
             PSY_ASRoot^                 \
             LIB^                        \
             RPATH

# if we have explicitly said we do not want JPEG
# do NOT detect it
if ("$JPEG_Path" == none) then
   NoteD $ALog "      no JPEG (by request)"
   setenv HAVE_JPEGLIB  FALSE

else

   set lvjp = ""

   set UTime = `timer -r`

# write a little JPEG test to do better detection than mere
# existence - mere existence doesn't work with 32/64 bit machines
   cat << EOF >! $Tmp.c
#include <stdlib.h>
#include <stddef.h>
#include <stdio.h>
#include <jpeglib.h>
int main(int c, char **v)
{struct jpeg_error_mgr jerr;
 struct jpeg_compress_struct cinfo;
 cinfo.err = jpeg_std_error(&jerr);
 jpeg_create_compress(&cinfo);
 printf("%d\n", JPEG_LIB_VERSION);
 return(0);}
EOF

   set lIncDirs = ""
   set lLibDirs = ""
   foreach i ($JPEG_Path)
      set lIncDirs = ( $lIncDirs $i/include )
      set lLibDirs = ( $lLibDirs $i/lib )
   end
   set lIncDirs = ( $lIncDirs $PSY_ASRoot/include /usr/local/include $RootInc )
   set lLibDirs = ( $lLibDirs $PSY_ASRoot/lib /usr/local/lib /usr/$LIB /$LIB )

   set Implicit = FALSE
   set lHdrs    = ( jpeglib.h jmorecfg.h jerror.h )
   set ASList   = ( a so )
   set lLib     = jpeg
   set lFile    = $Tmp
   set lKeep    = TRUE

   set lALibs = ""
   foreach i ( `var-list -l $RPATH` )
       set lALibs = ( $lALibs -L$i )
   end

   source $PSY_AnaDir/find-package

   if (("$PckLib" != "") && ("$PckInc" != "")) then
      NoteD $ALog "      has JPEG ($PckLib)"

      if ("$PckPath" != "") then
         flog $ALog setenv RPATH       ${RPATH}:${PckPath}
      endif
      flog $ALog setenv HAVE_JPEGLIB   TRUE

      flog $ALog $Tmp
      set lvjp = ( `$Tmp` )

   else
      NoteD $ALog "      no JPEG"
      flog $ALog setenv HAVE_JPEGLIB FALSE
   endif

   flog $ALog $RM $Tmp $Tmp.c

   set ETime = `timer -b $UTime`

   setenv JPEG_Inc   "$PckInc"
   setenv JPEG_Lib   "$PckLib"
   setenv JPEG_RPath "$PckPath"

# NOTE: used JPEG_Path to find libs, incs - there are no executables
   setenv JPEG_Path ""

# export the results
   dbmset $ALog JPEG_Inc     = $JPEG_Inc^    \
                JPEG_Lib     = $JPEG_Lib^    \
                JPEG_RPath   = $JPEG_RPath^  \
                JPEG_Path    = $JPEG_Path^   \
                JPEG_VERSION = $lvjp^        \
                RPATH        = $RPATH

endif

dbexp HAVE_JPEGLIB

exit(0)

