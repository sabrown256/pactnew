#!/bin/csh -f
#
# HW - analyze the hardware
#    - Computes: HSY_CPU, HSY_FPU
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/hw -----"
Note $ALog "Analyze: platform hardware"
Note $ALog ""

if ($?PSY_Arch == 0) then
   setenv PSY_Arch `../scripts/system-id`
endif

dbmget $ALog HSY_CPU =\? "unknown"^  \
             HSY_FPU =\? "unknown"

set HostHW  = `uname -m`

if ($HSY_CPU == "unknown") then
   setenv HSY_CPU "$HostHW"
   if ("$HostHW" =~ i*86) then
      setenv HSY_CPU  ia32
   else if ("$PSY_Arch" =~ osx-*-x86) then
      setenv HSY_CPU  ia32
   else if ("$PSY_Arch" =~ osx-*-ppc) then
      setenv HSY_CPU  ppc
   endif
endif

if ($HSY_FPU == "unknown") then
   if ("$HostHW" == "alpha") then
      setenv HSY_FPU  alpha
   else if ("$HostHW" =~ sun*) then
      setenv HSY_FPU  sparc
   else if ("$HostHW" == x86_64) then
      setenv HSY_FPU  x87
   else if ("$HostHW" =~ i*86) then
      setenv HSY_FPU  x87
   else if ("$PSY_Arch" =~ osx-*-x86) then
      setenv HSY_FPU  x87
   else if ("$PSY_Arch" =~ osx-*-ppc) then
      setenv HSY_FPU  ppc
   endif
endif

set msg = "Hardware: $HostHW"
if ("$HSY_CPU" != "unknown") then
   set msg = "$msg $HSY_CPU"
endif
if ("$HSY_FPU" != "unknown") then
   set msg = "$msg $HSY_CPU ($HSY_FPU)"
endif
NoteD $ALog "   $msg"

dbmset $ALog HSY_CPU = $HSY_CPU^  \
             HSY_FPU = $HSY_FPU

source $Me:h/post-common

exit(0)

