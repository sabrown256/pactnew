#!/bin/csh -f
#
# PYTHON - analyze the platform PYTHON
#        - PY_Exe     the full path to the python executable
#        - PY_Vers    the version of the python executable
#        - PY_Path    addition to path in order to find python executable
#        -            this comes from config files only
#        - PY_DirSrc  pact/dev/$arch/lib (install this to PY_DirDst and PY_DirPy)
#        - PY_DirDst  pact    lib/pythond.d                (install into)
#        - PY_DirPy   python  lib/pythond.d/site-packages  (install into)
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/common

Separator $Log
Note $Log "Analyze: python"

dbini HAVE_PYTHON     FALSE
dbini HAVE_PY_NUMERIC FALSE
dbini HAVE_PY_NUMPY   FALSE
dbini PY_Exe          ""
dbini PY_Vers         ""
dbini PY_DirPy        ""
dbini PY_DirDst       ""
dbini PY_DirSrc       ""
dbini PY_CC           ""
dbini PY_Cfg          ""
dbini PY_Inc          ""
dbini PY_Path         ""

set vl = ""
set vl = ( $vl CC_Exe OS_Name )
source $MngDir/write/import-db

# if there is no python directory no need to look further
if (-d ../../python) then

   NoteD $ALog -n "   Python: "
   Note $ALog ""

   set lDirs = ( $path /usr/bin /bin )
   if (-d "$PY_Path") then
      set lDirs = ( $PY_Path/bin $lDirs )
      Note $ALog "Specified python path: $PY_Path"
   else
      setenv PY_Path ""
   endif

   Note $ALog "Search Dirs = |$lDirs|"
   flog $ALog set found = FALSE
   foreach i ($lDirs)
      if (-x $i/python) then
         Note $ALog ""
         Note $ALog "Checking $i/python"
         setenv PY_Exe $i/python

# NOTE: get it into the log what is going on here
         flog $ALog $PY_Exe ../../python/python-cc.py
         set xstatus = $status

# find the python include directory
         fexvar $ALog PY_Inc $PY_Exe ../../python/python-inc-dir.py
         if (($gstatus[1] != 0) || !(-f "$PY_Inc/Python.h")) then
            flog $ALog setenv PY_Exe ""
         else

# get the python version
            fexvars $ALog t $PY_Exe -V
            if ($#t > 2) continue

            flog $ALog setenv HAVE_PYTHON TRUE

# get the python lib directory
            fexvar $ALog PY_Lib echo $PY_Inc @b sed 's/include/lib/'

# get the compiler python was built with
            fexvar $ALog PY_CC $PY_Exe ../../python/python-cc.py @b sed 's| .*||'
            if ((($OS_Name == "Linux")    || \
                 ($OS_Name == "Darwin")   || \
                 ($OS_Name == "FreeBSD")) && \
                ($PY_CC == cc)) then
               flog $ALog setenv PY_CC gcc
            endif

# get the python configuration
            fexvar $ALog PY_Cfg $PY_Exe ../../python/python-cfg-dir.py

            NoteD $ALog "Python summary:"
            NoteD $ALog "      executable $PY_Exe"
            NoteD $ALog "      compiled with $PY_CC"
            NoteD $ALog "      has include directory $PY_Inc"
            NoteD $ALog "      has lib directory $PY_Lib"

# check for Numeric
            if (-d $PY_Inc/Numeric) then
               NoteD $ALog "      has numeric extensions"
               flog $ALog setenv HAVE_PY_NUMERIC TRUE
            else
               flog $ALog setenv HAVE_PY_NUMERIC FALSE
            endif

# check for NumPy
            if ((-d $PY_Lib/site-packages/NumPy) || \
                (-d $PY_Lib/site-packages/numpy) || \
                (-d $PY_Lib/dist-packages/NumPy) || \
                (-d $PY_Lib/dist-packages/numpy) || \
                (-d $PY_Inc/numpy)) then
               NoteD $ALog "      has numpy extensions"
               flog $ALog setenv HAVE_PY_NUMPY TRUE
            else
               flog $ALog setenv HAVE_PY_NUMPY FALSE
            endif

            if (($HAVE_PY_NUMERIC == FALSE) && ($HAVE_PY_NUMPY == FALSE)) then
               NoteD $ALog "      does not have numeric extensions"
# NOTE: if we can build sensibly without any numeric extensions delete these
               flog $ALog setenv PY_Exe "no-numeric"
               flog $ALog setenv HAVE_PYTHON FALSE
            endif

            if ($OS_Name == "AIX") then
               set CCVer = `$CC_Exe -qversion`
               set PCVer = `$PY_CC -qversion`
               Note $ALog "CCVer = |$CCVer|"
               Note $ALog "PCVer = |$PCVer|"
               if ("$CCVer" != "$PCVer") then
                  flog $ALog setenv PY_Exe "wrong-cc"
                  flog $ALog setenv HAVE_PYTHON FALSE
               endif      
            else if ($PY_CC != $CC_Exe) then
               Note $ALog "PY_CC   = |$PY_CC|"
               Note $ALog "CC_Exe = |$CC_Exe|"
               flog $ALog setenv PY_Exe "wrong-cc"
               flog $ALog setenv HAVE_PYTHON FALSE
            endif
            flog $ALog setenv PY_Path $i
            flog $ALog set found = TRUE
            break
         endif
      endif
   end

   if ($found == FALSE) then
      NoteD $ALog "none recognizable"
      flog $ALog setenv HAVE_PYTHON     FALSE
      flog $ALog setenv HAVE_PY_NUMERIC FALSE
      flog $ALog setenv HAVE_PY_NUMPY   FALSE
      flog $ALog setenv PY_Path ""
      flog $ALog setenv PY_Exe ""
      flog $ALog setenv PY_CC ""
      flog $ALog setenv PY_Cfg""
      flog $ALog setenv PY_DirSrc ""
      flog $ALog setenv PY_DirDst ""
      flog $ALog setenv PY_DirPy ""
      flog $ALog setenv PY_Inc ""
      flog $ALog setenv PY_Vers ""
   endif

   if ($MAKE_Shared_Libs == FALSE) then
      flog $ALog setenv HAVE_PYTHON FALSE
   endif

   if ($HAVE_PYTHON == TRUE) then
      set lPY_Vers = `$PY_Exe ../../python/python-version.py`
      if (`expr "$lPY_Vers" : '.*\..*\..*'` > 0) then
         set lPY_Vers = $lPY_Vers:r
      endif
      flog $ALog setenv PY_Vers     $lPY_Vers

      set PyRoot = $SysDir
      set PyHead = python${PY_Vers}/pact
      if ("$PY_Path" != "") then
         set PyRoot = $PY_Exe
         set PyRoot = $PyRoot:h
         set PyRoot = $PyRoot:h
         set PyHead = python${PY_Vers}
      endif

# install destination in python tree
      setenv PY_DirPy  $PyRoot/lib/$PyHead/site-packages

# install destination in PACT tree
      setenv PY_DirDst $InstBase/lib/$PyHead

# install source (local build destination)
      setenv PY_DirSrc $SysDir/lib/python${PY_Vers}

      dbexp PY_DirSrc
      dbexp PY_DirDst
      dbexp PY_DirPy
   endif

# export the results
   dbexp HAVE_PYTHON
   dbexp HAVE_PY_NUMERIC
   dbexp HAVE_PY_NUMPY
   dbexp PY_Path
   dbexp PY_Exe
   dbexp PY_CC
   dbexp PY_Cfg
   dbexp PY_Inc
   dbexp PY_Vers

endif

exit(0)

