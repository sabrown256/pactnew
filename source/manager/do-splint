#!/bin/csh -f
#
# DO-SPLINT - map splint over source code in current directory
#

unalias *

@ err = 0

set lst = ""
while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: do-splint [-h] <C-files>*"
           echo "   h   this help message"
           echo ""
           exit(0)
      default:
           set lst = ( $lst $1 )
           breaksw
   endsw
   shift
end

if ("$lst" == "") then
   echo ""
   echo "No C source files specified - exiting"
   echo ""
   exit(1)
endif

set path = ( /usr/gapps/pact/scripts $path )
set arch = `../scripts/cfgman use`

source ../dev/$arch/etc/functions-pact.csh

InitLog log $cwd/log.splint

set opt = ( -warnposix -retvalint +boolint +matchanyintegral )

foreach i ($lst)
   if (($i =~ foo*) || ($i =~ bar*) || ($i =~ fu*) || ($i =~ baz*)) continue

   NoteD $log ""
   NoteD $log "------------------------------------------------------------"
   NoteD $log ""
   NoteD $log "File: $i"
   NoteD $log ""
   ftee $log splint $opt -I../dev/$arch/include $i
   set xstatus = $status
   @ err = $err + $xstatus
end

exit($err)
