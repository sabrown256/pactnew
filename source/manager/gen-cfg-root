#!/bin/csh -f
#
# GEN-CFG-ROOT - find the root config file
#              - which is the existing file matching the
#              - longest substring of $cfg
#              - this must be called under 'dsys config'
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set cfg = $1

set features = ( `echo $cfg | sed 's/-/ /g'` )

if ($?ConfigDir == 0) then
   setenv ConfigDir "local std"
endif

set croot = "none"
set ok    = FALSE
@ i = $#features
while (($i > 0) && ($ok == FALSE))
   set lname = `echo $features[1-$i] | sed 's/ /-/g'`
   foreach d ($ConfigDir)
      if (-f $d/$lname) then
         set croot = $d/$lname
         set ok    = TRUE
         break
      endif
   end
   @ i = $i - 1
end

# complete the features list
@ i = $i + 2
if ($i > $#features) then
   set features = ""
else
   set features = ( $features[$i-] )
endif

unset i
unset ok
unset lname

# work over the features list to accomodate platform targets
# platform 0 is the "main" config which gets the default SYS_ID
# all other platforms are specified by the 'platform' command in
# the config file and are assigned an index 1, ...
# every platform should get non-targeted features
# specific platforms should get targeted features
# targeted feature syntax is:
#   <spec> := <n> . <f>+
#   <n>    := integer platform index (0 for base config)
#   <f>    := feature specification (e.g. py or s)

set nfeat = ""

# add all non-targeted features
foreach i ($features)
   if ($i !~ [0-9]*.*) then
      set nfeat = ( $nfeat $i )
   endif
end

# expand and add features targeted at platform 0
foreach i ($features)
   if ($i =~ 0.*) then
      set inf   = ( `echo $i | sed 's/\./ /g'` )
      set nfeat = ( $nfeat $inf[2-] )
   endif
end

# last add all targeted features
foreach i ($features)
   if ($i =~ [1-9]*.*) then
      set nfeat = ( $nfeat $i )
   endif
end

set features = ( $nfeat )

unset nfeat
unset inf

echo "$croot $features"

exit(0)

