#!/usr/bin/env pcsh
#OPT -f
#
# PDBLS - list the contents of a PDB file
#
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

# determine the bin directory path
set BinDir = $PACT/bin

# determine if sx is available
if (!(-x $BinDir/sx)) then
   echo ""
   echo "No SX executable found in '$BinDir' derived from '$0'"
   echo "Your path is: $PATH"
   echo ""
   exit(1)
endif

set digits = 4
set files  =
set full   = "#f"

while ($#argv > 0)
   switch ($1)
      case -f:
         set full = "#t"
         breaksw
      case -h:
	 echo ""
	 echo "Usage: pdbls [-f] [-h] [-p n] <file>*"
	 echo ""
	 echo "   -f   display variable values with descriptive info"
	 echo "   -h   display this usage information"
	 echo "   -p   display floating point numbers with n digits"
	 echo ""
         exit(0)
      case -p:
         shift
         set digits = $1
         breaksw
      default:
         set files = ( $files $1 )
         breaksw
   endsw
   shift
end

$CROSS_FE $BinDir/sx -s -l sxview.scm \(binls $files $digits $full\)
set RetVal = $status;

exit($RetVal)

