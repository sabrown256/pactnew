#!/usr/bin/env pcsh
#OPT -f
#
# PDBVIEW - the editor
#         - NOTE: the SCHEME variable setting is determined by
#         - the user's SCHEME variable and the way env-pact.csh
#         - is generated
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set VERSION = "2.2"

# determine the bin directory path
set BinDir = $PACT/bin

# determine if sx is available
if (!(-x $BinDir/sx)) then
   echo ""
   echo "No SX executable found in '$BinDir' derived from '$0'"
   echo "Your path is: $PATH"
   echo ""
   exit(1)
endif

# setup default values
set track  = 1
set mode   = 0
set digits = 4

# handle args
while ($#argv > 0)
   switch ("$1")
      case -d:
         set mode = 1
         breaksw
      case -h:
	 echo ""
	 echo "Usage: pdbview [-d] [-h] [-l <command-file>] [-p n] [-r] [<data-file>]"
	 echo ""
	 echo "Options:   d - dump the formatted contents in ASCII to stdout"
	 echo "           h - display this usage information"
	 echo "           l - execute commands in <command-file>"
	 echo "               (not available with -d option)"
	 echo "           p - display floating point numbers with n digits"
	 echo "               (only available with -d option)"
	 echo "           r - don't use the .pdbviewrc file"
	 echo ""
         exit(0)
      case -p:
         shift
         set digits = "$1"
         breaksw
      case -x:
         set track = 0
         breaksw
      default:
         break
         breaksw
   endsw
   shift
end

# track this run
if ((-x "$TRACKER_Exe") && ($track == 1)) then
  $TRACKER_Exe -b -n pdbview -v $VERSION -P 1 -T 1 -W 1.0
endif

# do the browsing of the PDB file
if ($mode == 1) then
   $CFE $BinDir/sx -s -l sxview.scm \(binls $argv $digits\)
   set RetVal = $status;
else
   $CFE $BinDir/sx -p $argv
   set RetVal = $status;
endif

exit($RetVal)

