#!/bin/csh -f
#
# DIFF - SX PDBDiff test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PDBDiff Test ................."
set rpf = ""

NoteD $Log ""
NoteD $Log "                    $msg"
Note $Log ""

set ulopt = "-e -r -x -q -s $ZeroSpace"
set dfopt = "-e -x -q $ZeroSpace"

if (($CROSS_COMPILE == FALSE) && ($HaveIPC == YES)) then

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# MAKE_DATA

   NoteD $Log -n "                       Make data ................."
   Note $Log ""

   flog $Log rm -f pdbd*.dat pdbd.res pdbd.res.diff

   flog $Log touch pdbd.res

   set exe = $BinDir/ultra
   set opt = ( $ulopt -l $FilesDir/pdbdtu.scm )
   set io  = flog

   source ../../../scripts/test-run

   $TEST_CHECK part -a diff_make -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
   flog $Log @ nerr = $nerr + $status

   if ($nerr == 0) then
      Note $Log "SCHEME  = |$SCHEME|"
      if ($?PACT == 1) then
         Note $Log "PACT  = |$PACT|"
      else
         Note $Log "PACT  = undefined"
      endif
      if ($?DO_RUN_SUBMIT_ENV == 1) then
         Note $Log "DO_RUN_SUBMIT_ENV = |$DO_RUN_SUBMIT_ENV|"
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# TEST_1 - all mappings

   if ($nerr == 0) then
      NoteD $Log -n "                       All mappings .............."
      Note $Log ""

      set exe = $BinDir/pdbdiff
      set opt = ( $dfopt -v -w pdbda.dat pdbdb.dat )
      set io  = pdbd.res

      source ../../../scripts/test-run

      $TEST_CHECK part -a diff_all -e $xstatus -dt $dt -x $exe -l -r -rpf "$rpf" none
      flog $Log set xstatus = $status
      flog $Log @ nerr = $nerr + $xstatus

      if ($xstatus == 1) then
         NoteD $Log "erroneous agreement ($dt)"
         @ nerr = $nerr + 1
      else if ((-f pdbd.res) && (-f pdbda.dat.chk)) then
         Note $Log "Checking for 1 different mapping"
         flog $Log grep "1 mapping out of 4 differs between pdbda.dat and pdbdb.dat:" pdbda.dat.chk
         if ($status != 0) then
            @ nerr = $nerr + 1
         endif
         set rpf = ( $rpf pdbd*.dat pdbd*.dat.chk )
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# TEST_2 - selected mappings

   if ($nerr == 0) then
      NoteD $Log -n "                       Selected mappings ........."
      Note $Log ""

      set exe = $BinDir/pdbdiff
      set opt = ( $dfopt -v -w pdbda.dat pdbdb.dat -M 2 4 )
      set io  = pdbd.res

      source ../../../scripts/test-run

      $TEST_CHECK part -a diff_select -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none
      if ($status != 0) then
         flog $Log @ nerr = $nerr + 1
         if (-f pdbd.res) then
            NoteD $Log ""
            ftee $Log cat pdbd.res
            NoteD $Log ""
         endif
      endif
      touch pdbd-foo.dat pdbd-foo.dat.chk
      set rpf = ( $rpf pdbd*.dat pdbd*.dat.chk )
      rm pdbd-foo.dat pdbd-foo.dat.chk
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   set rpf = ( $rpf pdbd.res )

endif

$TEST_CHECK part -a diff -e $nerr -rpf "$rpf" "$msg"

exit($status)
