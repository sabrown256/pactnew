#!/bin/csh -f
#
# MESH - SX PDBView mesh test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PDBView Mesh Test ............ "
set exe = $BinDir/sx
set rpf = 0
@ nerr  = 0

   NoteD $Log ""
   NoteD $Log -n "                    $msg"
   Note $Log ""

   flog $Log rm -f pdbvmesh*.diff pdbvmesh*.ps

   set opt = ( -l $RootDir/sx/tests/pdbvmesh.scm )
   if ("$Valgrind" == "") then
      flog $Log ($PDBView $opt >& pdbvmesh.res)
      set xstatus = $status
      if ($xstatus == 0) then
         foreach i (pdbvmesh.ps)
            if (-f $RootDir/sx/tests/ref/$i) then
               flog $Log ($CFE $PSDIFF $i $RootDir/sx/tests/ref/$i >&! $i.diff)
               @ st = $status
               @ xstatus = $xstatus + $st
               Note $Log "Test $i:r status $st"
            endif
         end
      endif
   else
      ftee $Log $Valgrind $exe -e -p -q $opt    |& \
      grep "ERROR SUMMARY:"                     |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

   set rpf = ( $rpf pdbvmesh.res.diff pdbvmesh.res pdbvmesh.ps pdbvmesh*.diff )

$TEST_CHECK part -a mesh -e $xstatus -rpf "$rpf" none

exit($status)
