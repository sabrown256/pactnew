#!/bin/csh -f
#
# EXA - SX PDBView example test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PDBView Example Test ......... "
set exe = $BinDir/sx

   NoteD $Log ""
   NoteD $Log -n "                    $msg"
   Note $Log ""

   touch Afoo.diff Afoo.ps demo.dx

   set files = ""
   set files = ( $files pdbvtest.log pdbvtest.pdb pdbvtest.asc )
   set files = ( $files pdbvtest.err pdbvtest.tmp )
   set files = ( $files pdbvtest.pdb.chk pdbvtest.ps.diff )
   set files = ( $files table1.tmp )
   set files = ( $files [ABC]*.diff [ABC]*.ps demo.d? )

   flog $Log rm -f $files

   set opt = ( -l $RootDir/sx/applications/pdbvbatch.scm )
   if ("$Valgrind" == "") then
      flog $Log ($PDBView $opt >& pdbvbatch.res)
      set xstatus = $status
      if ($xstatus == 0) then
         flog $Log @ xstatus = 0
         foreach i (A.ps B.ps C.ps)
            if (-f $RootDir/sx/tests/ref/$i) then
               flog $Log ($CFE $PSDIFF $i $RootDir/sx/tests/ref/$i >&! $i.diff)
               @ st = $status
               @ xstatus = $xstatus + $st
               Note $Log "Test $i:r status $st"
            endif
         end
         flog $Log (diff pdbvbatch.res $RootDir/sx/tests/ref/pdbvbatch > pdbvbatch.res.diff)
         @ st = $status
         @ xstatus = $xstatus + $st
         Note $Log "Test pdbvbatch status $st"

         set rpf = ( $rpf pdbvbatch.res.diff pdbvbatch.res )
         set rpf = ( $rpf A.ps B.ps C.ps plots.ps )
      endif

   else
      ftee $Log $Valgrind $exe -e -p -q $opt   |& \
      grep "ERROR SUMMARY:"                    |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      set rpf     = ( $rpf $Tmp )
   endif

$TEST_CHECK part -a exa -e $xstatus -rpf "$rpf" none

exit($status)
