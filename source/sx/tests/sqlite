#!/bin/csh -f
#
# SQLITE - SX SQLite DB test
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

if (($HAVE_SQLITE == yes) && ($?IBM_BGP == 0)) then

#--------------------------------------------------------------------------

#                                  SQLITE TEST

#--------------------------------------------------------------------------

   NoteD $Log ""
   NoteD $Log -n "                    SX PDB SQLite Test ........... "
   Note $Log ""

   flog $Log rm -f sxsqlite.log sxsqlite.res foo.sqlite

   set UTime = `$TIMER -r`
   if ("$Valgrind" == "") then
      flog $Log ($PDBView -l $RootDir/sx/tests/sxsqlite.scm >& sxsqlite.res)
      set xstatus = $status
   else
      ftee $Log $Valgrind $BinDir/sx -e -p -q -l $RootDir/sx/tests/sxsqlite.scm |& \
      grep "ERROR SUMMARY:"                                                     |& \
      awk '{ print $4 }'   >&! $Tmp
      set xstatus = `cat $Tmp`
      flog $Log $RM $Tmp
   endif

   set Ref   = $RootDir/sx/tests/ref/sxsqlite
   set ETime = `$TIMER -b $UTime`

   if ("$Valgrind" != "") then
      if ($xstatus != 0) then
         NoteD $Log "FAILED ($xstatus/$ETime)"
         set xstatus = 2
      else
         NoteD $Log "PASSED ($ETime)"
      endif
   else if (($xstatus == 254) || ($xstatus == 255)) then
      NoteD $Log "OMITTED ($xstatus/$ETime)"
      set xstatus = 0
   else if ($xstatus != 0) then
      NoteD $Log "CRASHED ($xstatus/$ETime)"
      set xstatus = 2
   else if (-f $Ref) then
      flog $Log diff sxsqlite.res $Ref
      set xstatus = $status
   else
      flog $Log cp sxsqlite.res $Ref
   endif

   if ($xstatus == 2) then
      set Err = 1
   else if ($xstatus != 0) then
      NoteD $Log "FAILED ($ETime)"
      set Err = 1
   else
      NoteD $Log "PASSED ($ETime)"
      flog $Log touch foo.sxsqlite sxsqlite.foo
      flog $Log rm -f sxsqlite.* *.sxsqlite
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

exit($Err)
