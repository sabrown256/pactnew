#!/bin/csh -f
#
# DRIVER - SCC test driver script
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set tst = $1

source ../../tests/common

if ($CROSS_COMPILE == FALSE) then
   set refdir = $SrcDir/tests/ref
   set fill   = `fill-string -n 25 $tst`
   set err    = ok
#   set opt    = ( -check -mem )
   set opt    = ( -check )

   Separator $Log
   Note $LogF ""
   NoteD $LogF -n "                    $fill "
   Note $LogF ""

   @ err = 0

   set UTime = `$TIMER -r`
   flog $LogF cp $SrcDir/tests/$tst.c .

   scc $opt -c $tst.c >&! $tst.res
   set xstatus = $status
   if ($xstatus != 0) then
      @ err = 253
   endif

# for tests with fiducial results
   if (-f $refdir/$tst) then
      flog $Log diff $tst.res $refdir/$tst
      set xstatus = $status
      if ($xstatus != 0) then
         flog $Log @ err = 252
         flog $Log cat $tst.res
      endif

# for tests without fiducial results
   else
      flog $Log grep "Memory Map" $tst.res
      set xstatus = $status
      if ($xstatus == 0) then
         @ err = 250
      endif
   endif

   set ETime = `$TIMER -b $UTime`

   $TEST_CHECK part -a scc_$tst -e $err -l -dt $ETime none

endif

exit($Err)

