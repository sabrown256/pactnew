#!/bin/csh -f
#
# EXT-JOB - test gexec with processes only
#

unalias *

@ err = 0

source $TestScrDir/env-csh

alias pe  'unsetenv gstatus ; echo "" ; echo "Command: \!*" ; gexec \!* ; dbget gstatus'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4 5 6 7 8 9)

   echo ""
   echo "---------------------------------------------------"
   echo ""

   set id  = j$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   switch ($i)

# simple stdout variants
      case 1:
           set cmd = ( pe /bin/echo "$id" @o cat -n '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw
      case 2:
           set cmd = ( pe /bin/echo "$id" @o+1 cat -n '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw
      case 3:
           set cmd = ( pe /bin/echo "$id" @o2 cat -n '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# simple stdout and stderr variants
      case 4:
           set cmd = ( pe ls ext-* foo.h @o+1 @e+2 cat -n @ cat -E '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           breaksw
      case 5:
           set cmd = ( pe ls ext-* foo.h @o2 @e3 cat -n @ cat -E '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           breaksw

# simple bonded stdout/stderr variants
      case 6:
           set cmd = ( pe ls ext-* foo.h @b cat -n '>&!' $raw )
           set gst = "2 0"
           set srt = TRUE
           breaksw
      case 7:
           set cmd = ( pe ls ext-* foo.h @b+1 cat -n '>&!' $raw )
           set gst = "2 0"
           set srt = TRUE
           breaksw
      case 8:
           set cmd = ( pe ls ext-* foo.h @b2 cat -n '>&!' $raw )
           set gst = "2 0"
           set srt = TRUE
           breaksw
      case 9:
           set cmd = ( pe ls ext-* foo.h @o2 @e2 cat -n '>&!' $raw )
           set gst = "2 0"
           breaksw
           set srt = TRUE
   endsw

   eval $cmd

   if ($srt == TRUE) then
      sort $raw >&! $res
      rm -f $raw
   endif

   cat $res

   echo "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      @ err = $err + 1
      echo "$id failed"
   else if (-f $ref) then
      diff $res $ref >&! $dif
      if ($status == 0) then
         echo "$id passed"
         rm -f $res $dif
      else
         echo "$id differed"
         cat $dif
      endif
   else
      cp $res $ref
      echo "$id added"
      rm -f $res
   endif

end

echo ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($err)
