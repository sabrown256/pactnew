#!/bin/csh -f
#
# PCSH - test pcsh
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = psh
if ($?Log == 0) then
   if (-f test-env) then
      source test-env
      pushd $TestDir >& /dev/null
   else
      mv $Log $Log.tmp
      source ../../../scripts/test-env
      mv $Log.tmp $Log
   endif
else
   source ../../../scripts/env-csh
endif

set PCSH = pcsh

while ($#argv > 0)
   switch ($1)
      case -c:
           shift
           set PCSH = $1
           breaksw
      case -h:
           echo ""
           echo "Usage: pcsh [-c <code>] [-h]"
           echo "  c  test code (defaults to pcsh)"
           echo "  h  this help message"
           echo ""
           exit(1);
      default:
           breaksw
   endsw
   shift
end

InitLog Log $TestDir/log.pcsh.$LogSfx

@ Err = 0

set BTime = `$TIMER -r`

NoteD $Log ""
NoteD $Log "                    PCSH Test ......................"

set TstDir = $SrcDir/tests
set RefDir = $TstDir/ref
set List = (psh1 psh2 psh3 psh4)
set lErr = 0

flog $Log setenv PATH $RootDir/scripts:$PATH

Note $Log "Using $PCSH"
Note $Log "Path = $PATH"

foreach t ($List)
   Separator $Log

   set f = ( `fill-string -n 28 $t` )
   NoteD $Log -n "                        $f "
   Note $Log ""

   flog $Log set Res = $t.res
   flog $Log set Ref = $RefDir/$t

   flog $Log $RM ${t}*

   set noglob
   $PCSH $TstDir/$t |& sed 's|/tmp/psh.*$|xxx|' | grep -v "open display" >&! $Res
   set xstatus = $status
   unset noglob

   if (-f $Ref) then
      flog $Log ( diff -w -b $Res $Ref >! $Res.diff )
      set xstatus = $status
   else
      flog $Log cp $Res $Ref
      set xstatus = $status
   endif

   if ($xstatus == 0) then
      NoteD $Log "ok"
      flog $Log rm -f $Res $Res.diff
   else
      flog $Log cat $Res.diff
      flog $Log set lErr = 1
      NoteD $Log "ng"
   endif
end

set ETime = `$TIMER -b $BTime`

if ($lErr != 0) then
   NoteD $Log "                    PCSH Test ...................... FAILED ($lErr/$ETime)"
   @ Err = $Err + 1
else
   NoteD $Log "                    PCSH Test ...................... PASSED ($ETime)"
   flog $Log rm -f run.* s*
endif
NoteD $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

$TEST_CHECK -a pcsh -e $Err part

exit($status)
