#!/bin/csh -f
#
# BLANG - blang testing
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Package = psh
if (-f test-env) then
   source test-env
   pushd $TestDir >& /dev/null
else if ($?Log == 1) then
   if (-f $Log) then
      mv $Log $Log.tmp
      source ../../../scripts/test-env
      mv $Log.tmp $Log
   else
      source ../../../scripts/test-env
   endif
else
   source ../../../scripts/test-env
endif

source $IncDir/env-pact.csh

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: blang [-h]"
           echo "  h  this help message"
           echo ""
           exit(1);
      default:
           breaksw
   endsw
   shift
end

InitLog Log $TestDir/log.blang.$LogSfx

@ Err  = 0
@ lerr = 0

flog $Log cd $TestDir

NoteD $Log ""

# omit tests - usually in difficult CROSS_COMPILE situation
if ($RUN_TESTS == FALSE) then
   NoteD $Log "                    BLANG Test ..................... omitted"
   NoteD $Log ""

else

   set BTime = `$TIMER -r`

   NoteD $Log "                    BLANG Test ..................... "
   Note $Log ""

   set TstDir = $SrcDir/tests
   set RefDir = $TstDir/ref

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   flog $Log set cc = ( ${CC_Exe} ${CDefDebug} ${CC_Flags} -I${IncDir} ${DP_Inc} )
   flog $Log set fc = ( ${FC_Exe} ${CDefDebug} ${FC_Flags} -I${IncDir} )

   @ nerr = 0

   foreach i (bl1 bl2 bl3 bl4 bl5)

      @ lerr = 0

      Separator $Log

      set f = ( `fill-string -n 23 $i` )
      NoteD $Log -n "                        $f"
      Note $Log ""

      flog $Log cp $TstDir/$i.* .

      flog $Log set hdr = ${i}_int.h
      flog $Log $RM $hdr
      flog $Log touch $hdr
      Note $hdr "#include <sx_int.h>"
      Note $hdr "#define G_STR  256"
      Note $hdr "struct s_str {int a; double b;};"
      Note $hdr "typedef struct s_str str;"
      flog $Log cat $hdr

      flog $Log blang $i.bind $i.proto

      cat $i.proto | \
      grep -v '#' | \
      awk '$0 != "" { printf("extern %s;\n", $0); }' >>& $hdr

# compile Fortran module
      if ($FC_Exe == none) then
         NoteD $Log -n "."
      else
         Note $Log ""
         Note $Log "Compile fortran module"
         flog $Log set file = gm-$i.f
         flog $Log ${fc} -c $file
         @ xstatus = $status
         if (-f $RefDir/$file) then
            flog $Log ( grep -v 'isizea =' $file >&! $file.cmp )
            flog $Log ( grep -v 'isizea =' $RefDir/$file >&! $file.ref.cmp )
            flog $Log diff $file.cmp $file.ref.cmp
            if ($status != 0) then
               @ xstatus = $xstatus + 1
               flog $Log rm -f $file.cmp $file.ref.cmp
            endif
         else
            flog $Log cp $file $RefDir/$file
         endif
         if ($xstatus == 0) then
            NoteD $Log -n "."
         else
            NoteD $Log -n "x"
            @ lerr = $lerr + 1
         endif
         unset file
      endif

# compile Fortran wrapper
      Note $Log ""
      Note $Log "Compile fortran wrapper"
      flog $Log set file = gf-$i.c
      flog $Log ${cc} -c $file
      @ xstatus = $status
      if (-f $RefDir/$file) then
         flog $Log diff $file $RefDir/$file
         if ($status != 0) then
            @ xstatus = $xstatus + 1
         endif
      else
         flog $Log cp $file $RefDir/$file
      endif
      if ($xstatus == 0) then
         NoteD $Log -n "."
      else
         NoteD $Log -n "x"
         @ lerr = $lerr + 1
      endif
      unset file

# compile Scheme wrapper
      Note $Log ""
      Note $Log "Compile scheme wrapper"
      flog $Log set file = gs-$i.c
      flog $Log ${cc} -c $file
      @ xstatus = $status
      if (-f $RefDir/$file) then
         flog $Log diff $file $RefDir/$file
         if ($status != 0) then
            @ xstatus = $xstatus + 1
         endif
      else
         flog $Log cp $file $RefDir/$file
      endif
      if ($xstatus == 0) then
         NoteD $Log -n "."
      else
         NoteD $Log -n "x"
         @ lerr = $lerr + 1
      endif
      unset file

# compile Python wrapper
      Note $Log ""
      Note $Log "Compile python wrapper"
      flog $Log set file = gp-$i.c
      flog $Log ${cc} -c $file
      @ xstatus = $status
      if (-f $RefDir/$file) then
         flog $Log diff $file $RefDir/$file
         if ($status != 0) then
            @ xstatus = $xstatus + 1
         endif
      else
         flog $Log cp $file $RefDir/$file
      endif
      if ($xstatus == 0) then
         NoteD $Log -n "."
      else
         NoteD $Log -n "x"
         @ lerr = $lerr + 1
      endif
      unset file

# compare HTML
      Note $Log ""
      flog $Log set file = gh-$i.html
      @ xstatus = 0
      if (-f $RefDir/$file) then
         flog $Log diff $file $RefDir/$file
         if ($status != 0) then
            @ xstatus = $xstatus + 1
         endif
      else
         flog $Log cp $file $RefDir/$file
      endif
      if ($xstatus == 0) then
         NoteD $Log -n "."
      else
         NoteD $Log -n "x"
         @ lerr = $lerr + 1
      endif
      unset file

# report status
      Note $Log ""
      if ($lerr == 0) then
         NoteD $Log " ok"
         $RM $i*.* $hdr
      else
         NoteD $Log " ng"
         @ nerr = $nerr + 1
      endif

   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   Separator $Log

   set ETime = `$TIMER -b $BTime`

   if ($nerr != 0) then
      NoteD $Log "                    BLANG Test ..................... FAILED ($lerr/$ETime)"
      @ Err = 1
   else
      NoteD $Log "                    BLANG Test ..................... PASSED ($ETime)"
      flog $Log rm -f *.o *.mod
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

exit($Err)

