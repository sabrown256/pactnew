#!/bin/csh -f
#
# EXT-FILE - test aexec with files
#

unalias *

@ err = 0

source $TestScrDir/env-csh

alias pe  'unsetenv astatus ; echo "" ; echo "Command: \!*" ; aexec \!* ; dbget astatus'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4 5 6)

   echo ""
   echo "---------------------------------------------------"
   echo ""

   set id  = f$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   set fa = f1.dat

   switch ($i)

# simple stdout variants
      case 1:
           rm -rf $id
           set cmd = ( pe /bin/echo "$id" @o fw:${fa} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set cfl = ( $fa )
           breaksw

# simple stdin variants
      case 2:
           set cmd = ( pe cat @i fr:${fa} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set cfl = ""
           breaksw

# simple append variants
      case 3:
           set cmd = ( pe /bin/echo "$id" @o2 fa:${fa} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set cfl = ( $fa )
           breaksw

# simple bonded variants
      case 4:
           set cmd = ( pe /bin/echo "$id" @b2 fw:f4bond '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set cfl = ( f4bond )
           breaksw

# non-simple stdout variants
      case 5:
           set cmd = ( pe /bin/echo "$id" @o2 @e3 fw:f5out @ fw:f5err '>&!' $res )
           set gst = "0 0 0"
           set srt = FALSE
           set cfl = ( f5out f5err )
           breaksw

      case 6:
           set cmd = ( pe ls 'ext-*' foo.h @o2 @e3 fw:f6out @ fw:f6err '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           set cfl = ( f6out f6err )
           breaksw
   endsw

   if ("$cfl" != NULL) then
      rm -rf $cfl
   endif

   eval $cmd

   if ($srt == TRUE) then
      sort $raw >&! $res
      rm -f $raw
   endif

   if ("$cfl" != NULL) then
      foreach c ($cfl)
         echo "> $c = `cat $c`"
      end
   endif

   cat $res

   echo "> astatus = $astatus"
   if ("$astatus" != "$gst") then
      @ err = $err + 1
      echo "$id failed"
   else if (-f $ref) then
      diff $res $ref >&! $dif
      if ($status == 0) then
         echo "$id passed"
         rm -f $res $dif
      else
         echo "$id differed"
         cat $dif
      endif
   else
      cp $res $ref
      echo "$id added"
      rm -f $res
   endif

end

echo ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($err)
