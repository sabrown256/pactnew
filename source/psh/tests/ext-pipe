#!/bin/csh -f
#
# EXT-PIPE - test gexec with old shell pipe syntax
#

unalias *

@ err = 0

source $TestScrDir/env-csh

alias pe  'unsetenv gstatus ; echo "" ; echo "Command: \!*" ; gexec \!* ; dbget gstatus'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3)

   echo ""
   echo "---------------------------------------------------"
   echo ""

   set id  = s$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   switch ($i)

# simple stdout variants
      case 1:
           set cmd = ( pe /bin/echo "$id" "'|'" cat -n >&! $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# simple bonded stdout/stderr variants
      case 2:
           set cmd = ( pe ls 'ext-*' foo.h "'|'" cat -n >&! $raw )
           set gst = "2 0"
           set srt = TRUE
           breaksw
      case 3:
           set cmd = ( pe ls 'ext-*' foo.h "'|&'" cat -n >&! $raw )
           set gst = "2 0"
           set srt = TRUE
           breaksw
   endsw

   Note $Log "cmd = $cmd"
   Note $Log "srt = $srt"

   eval $cmd

   if ($srt == TRUE) then
      flog $Log ( sort $raw >&! $res )
      flog $Log rm -f $raw
   endif

   flog $Log cat $res

   if ($?PERDB_PATH == 0) then
      eval `grep "setenv gstatus" $res`
      grep -v "setenv gstatus" $res >&! $res.new
      flog $Log mv $res.new $res
   endif

   Note $Log "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      flog $Log @ err = $err + 1
      echo "$id failed"
   else if (-f $ref) then
      flog $Log ( diff $res $ref >&! $dif )
      if ($status == 0) then
         echo "$id passed"
         flog $Log rm -f $res $dif
      else
         flog $Log @ err = $err + 1
         echo "$id differed"
         flog $Log cat $dif
      endif
   else
      echo "$id added"
      flog $Log cp $res $ref
      flog $Log rm -f $res
   endif

end

Note $Log "exiting with err = $err"
Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($err)

