#!/bin/csh -f
#
# EXT-FNC - test gexec with functions
#

unalias *

@ err = 0

source $TestScrDir/env-csh

alias pe  'unsetenv gstatus ; echo "" ; echo "Command: \!*" ; gexec \!* ; dbget gstatus'

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4 5 6)

   echo ""
   echo "---------------------------------------------------"
   echo ""

   set id  = p$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   set pa = p1out

   switch ($i)

# simple stdout variants
      case 1:
           rm -rf $id
           set cmd = ( pe /bin/echo "$id" @o pw:test ${pa} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# simple stdin variants
      case 2:
           set cmd = ( pe cat @i pr:test ${pa} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# simple append variants
      case 3:
           set cmd = ( pe /bin/echo "$id" @o2 pa:test p3out '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# simple bonded variants
      case 4:
           set cmd = ( pe /bin/echo "$id" @b2 pw:test p4bond '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           breaksw

# non-simple stdout variants
      case 5:
           set cmd = ( pe /bin/echo "$id" @o2 @e3 pw:test p5out @ pw:test p5err '>&!' $res )
           set gst = "0 0 0"
           set srt = FALSE
           breaksw

      case 6:
           set cmd = ( pe ls ext-* foo.h @o2 @e3 pw:test p6out @ pw:test p6err '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           breaksw
   endsw

   Note $Log "cmd = $cmd"
   Note $Log "srt = $srt"

   eval $cmd

   if ($srt == TRUE) then
      flog $Log ( sort $raw >&! $res )
      flog $Log rm -f $raw
   endif

   flog $Log cat $res

   if ($?PERDB_PATH == 0) then
      eval `grep "setenv gstatus" $res`
      grep -v "setenv gstatus" $res >&! $res.new
      flog $Log mv $res.new $res
   endif

   Note $Log "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      flog $Log @ err = $err + 1
      echo "$id failed"
   else if (-f $ref) then
      flog $Log ( diff $res $ref >&! $dif )
      if ($status == 0) then
         echo "$id passed"
         flog $Log rm -f $res $dif
      else
         flog $Log @ err = $err + 1
         echo "$id differed"
         flog $Log cat $dif
      endif
   else
      echo "$id added"
      flog $Log cp $res $ref
      flog $Log rm -f $res
   endif

end

Note $Log "exiting with err = $err"
Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($err)
