#!/bin/csh -f
#
# EXT-VAR - test gexec with variables
#

unalias *

set t = var

@ err = 0

source $TestScrDir/env-csh

alias pe  'unsetenv gstatus ; gexec \!* ; dbget gstatus'

set f = ( `fill-string -n 8 -c ' ' $t` )
NoteD $Log -n "                        $f "
Note $Log ""

set exe = $t
set opt = ""
set rpf = ""

set lt = `timer -r`

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

foreach i (1 2 3 4 5 6)

   Note $Log ""
   Note $Log "---------------------------------------------------"
   Note $Log ""

   set id  = v$i
   set res = ext-$id.res
   set raw = $res.raw
   set dif = $res.dif
   set ref = $RefDir/ext-$id

   set va = v1out

   switch ($i)

# simple stdout variants
      case 1:
           rm -rf $id
           set cmd = ( pe /bin/echo "$id" @o vw:${va} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set pvl = ( $va )
           breaksw

# simple stdin variants
      case 2:
           set cmd = ( pe cat @i vr:${va} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set pvl = ""
           breaksw

# simple append variants
      case 3:
           set cmd = ( pe /bin/echo "$id" @o2 va:${va} '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set pvl = ( $va )
           breaksw

# simple bonded variants
      case 4:
           set cmd = ( pe /bin/echo "$id" @b2 vw:v4bond '>&!' $res )
           set gst = "0 0"
           set srt = FALSE
           set pvl = ( v4bond )
           breaksw

# non-simple stdout variants
      case 5:
           set cmd = ( pe /bin/echo "$id" @o2 @e3 vw:v5out @ vw:v5err '>&!' $res )
           set gst = "0 0 0"
           set srt = FALSE
           set pvl = ( v5out v5err )
           breaksw

      case 6:
           set cmd = ( pe ls 'ext-*' foo.h @o2 @e3 vw:v6out @ vw:v6err '>&!' $raw )
           set gst = "2 0 0"
           set srt = TRUE
           set pvl = ( v6out v6err )
           breaksw
   endsw

#   if ("$pvl" != NULL) then
#      foreach v ($pvl)
#         unsetenv $v
#      end
#   endif

   Note $Log "cmd = $cmd"
   Note $Log "srt = $srt"

   flog $Log eval $cmd

   if ($srt == TRUE) then
      flog $Log ( sort $raw >&! $res )
      flog $Log rm -f $raw
   endif

   if ("$pvl" != NULL) then
      foreach v ($pvl)
         dbget $v
         Note $Log "> $v = `printenv $v`"
      end
   endif

   flog $Log cat $res

   if ($?PERDB_PATH == 0) then
      eval `grep "setenv gstatus" $res`
      grep -v "setenv gstatus" $res | sed 's/setenv .* ; //' >&! $res.new
      flog $Log mv $res.new $res
   endif

   Note $Log "> gstatus = $gstatus"
   if ("$gstatus" != "$gst") then
      flog $Log @ err = $err + 1
      NoteD $Log -n "x"
      Note $Log "$id failed"
   else if (-f $ref) then
      flog $Log ( diff $res $ref >&! $dif )
      if ($status == 0) then
         NoteD $Log -n "."
         Note $Log "$id passed"
         flog $Log rm -f $res $dif
      else
         NoteD $Log -n "o"
         flog $Log @ err = $err + 1
         Note $Log "$id differed"
         flog $Log cat $dif
      endif
   else
      NoteD $Log -n "+"
      Note $Log "$id added"
      flog $Log cp $res $ref
      flog $Log rm -f $res
   endif

end

NoteD $Log -n " ................."

Note $Log "exiting with err = $err"
Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

set xstatus = $err

set lt = `timer -b $lt`

set rpf = ( $rpf ext-$t )

$TEST_CHECK part -a gexec_${t} -e $xstatus -dt $lt -x $exe -l -rpf "$rpf" none

exit($status)

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

echo "------------------------------------------------------------------------"
echo ""
echo "# simple stdout variants"

   unsetenv v1
   pe /bin/echo v1-text @o vw:v1 >&! ext-v1.res
   dbget v1
   cat ext-v1.res
   echo "> v1      = `printenv v1`"
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "0 0") then
      @ err = $err + 1
      echo "v1 failed"
   else
      rm -f ext-v1.res
      echo "v1 passed"
   endif

echo "------------------------------------------------------------------------"
echo ""
echo "# simple stdin variants"

   pe cat @i vr:v1 >&! ext-v2.res
   cat ext-v2.res
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "0 0") then
      @ err = $err + 1
      echo "v2 failed"
   else
      rm -f ext-v2.res
      echo "v2 passed"
   endif

echo "------------------------------------------------------------------------"
echo ""
echo "# simple append variants"

   pe /bin/echo v3-text @o2 va:v1 >&! ext-v3.res
   dbget v1
   cat ext-v3.res
   echo "> v1      = `printenv v1`"
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "0 0") then
      @ err = $err + 1
      echo "v3 failed"
   else
      rm -f ext-v3.res
      echo "v3 passed"
   endif

echo "------------------------------------------------------------------------"
echo ""
echo "# simple bonded variants"

   pe /bin/echo v4-text @b2 vw:v4 >&! ext-v4.res
   dbget v4
   cat ext-v4.res
   echo "> v4      = `printenv v4`"
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "0 0") then
      @ err = $err + 1
      echo "v4 failed"
   else
      rm -f ext-v4.res
      echo "v4 passed"
   endif

echo "------------------------------------------------------------------------"
echo ""
echo "# non-simple stdout variants"

if (0) then
   unsetenv v5out
   unsetenv v5err
   pe /bin/echo v5-text @o2 @e3 vw:v5out @ vw:v5err >&! ext-v5.res
   dbget v5out
   dbget v5err
   cat ext-v5.res
   echo "> v5out   = `printenv v5out`"
   echo "> v5err   = `printenv v5err`"
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "0 0 0") then
      @ err = $err + 1
      echo "v5 failed"
   else
      rm -f ext-v5.res
      echo "v5 passed"
   endif

   unsetenv v5out
   unsetenv v5err
   pe ls ext-* foo.h @o2 @e3 vw:v6out @ vw:v6err >&! ext-v6.res.raw
   sort ext-v6.res.raw >&! ext-v6.res
   rm -f ext-v6.res.raw
   dbget v6out
   dbget v6err
   cat ext-v6.res
   echo "> v6out   = `printenv v6out`"
   echo "> v6err   = `printenv v6err`"
   echo "> gstatus = $gstatus"
   if ("$gstatus" != "2 0 0") then
      @ err = $err + 1
      echo "v6 failed"
   else
      rm -f ext-v6.res
      echo "v6 passed"
   endif
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------
