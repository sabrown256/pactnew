#!/bin/csh -f
#
# PSYS - Python build script
#
# include "cpyright.h"
#

unalias *

@ err = 0

set exe = `which pact`
set bindir = $exe:h
set sysdir = $bindir:h
set etcdir = $sysdir/etc
source $etcdir/env-pact.csh
unset etcdir
unset bindir
unset exe

source ../scripts/functions-pact.csh

@ n = $#argv
set tgt = $argv[$n]
@ n = $n - 1
set arg = ( $argv[1-$n] )
unset n

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: psys [-h] [<arg>*] <tgt>"
           echo "   h     this help message"
           echo "   <arg> argument to pact executable"
           echo "   <tgt> make target"
           echo ""
           exit(1)
      default:
           breaksw
   endsw
   shift
end

if (("$HAVE_PYTHON" !~ *FALSE*) && ($tgt != generate)) then

   InitLog log $cwd/$SYS_TmpDir/log.psys.$tgt

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   set lhave_py    = ( $HAVE_PYTHON )
   set lpy_vers    = ( $PY_Vers )
   set lpy_ext     = ( $PY_Ext )
   set lpy_inc     = ( $PY_Inc )
   set lpy_numeric = ( $HAVE_PY_NUMERIC )
   set lpy_numpy   = ( $HAVE_PY_NUMPY )

   setenv SESSION_CONFIG $SYS_ID

   @ n = $#lhave_py
   @ i = 0
   while ( $i < $n)
      @ i = $i + 1

      set v   = $lpy_ext[$i]
      set dev = ( lpycfg=$i )
      set dev = ( $dev lpyver=$lpy_vers[$i] lpyinc=$lpy_inc[$i] lpyext=$v )
      if ("$v" =~ *numeric) then
         set dev = ( $dev lpynum=-DHAVE_NUMERIC )
      else if ("$v" =~ *numpy) then
         set dev = ( $dev lpynum=-DHAVE_NUMPY )
      endif

      NoteD $log "----------------------------------------------------------"
      NoteD $log ""
      NoteD $log "   in $v pact $arg $tgt"
      NoteD $log ""

      flog $log mkdir -p $v/$SYS_TmpDir/obj

      if (($tgt == devlib) || ("$tgt" =~ *.i) || ("$tgt" =~ *.i)) then
         pact -f pre-Make.bind sysdir=$sysdir $dev $arg $tgt
         @ err = $err + $status
         if ($tgt != devlib) then
            mv $tgt $v
         endif
      else
         flog $log pushd $v

         pact -f ../pre-Make.bind sysdir=$sysdir $dev $arg $tgt
         @ err = $err + $status

         flog $log popd
      endif   

   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

exit($err)
