#!/bin/csh -f
#
# FPE - PML FPE tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

#--------------------------------------------------------------------------

#                               PML FPE TEST

#--------------------------------------------------------------------------

   if ($TEST_ERROR_TRAPS == YES) then
      NoteD $Log ""
      NoteD $Log  -n "                    PML FPE Test ........... "

      flog $Log pushd $SrcDir
      flog $Log $MAKE fpe
      set TStatus = $status
      flog $Log popd

      set UTime = `$TIMER -r`

      if ($TStatus != 0) then
         NoteD $Log ""
         NoteD $Log "                         Can't build PML FPE Test Program"
         set xstatus = $TStatus
      else
         if ("$Valgrind" == "") then
            flog $LogF $CFE $BinDir/mlfpets
            set xstatus = $status
         else
            flog $Log $Valgrind $BinDir/mlfpets
            set xstatus = $status
         endif

         if ("$Valgrind" != "") then
            flog $Log set xstatus = 0
         endif
      endif

      set ETime = `$TIMER -b $UTime`

      if (($xstatus == 254) || ($xstatus == 255)) then
         NoteD $Log "OMITTED ($xstatus/$ETime)"
      else if ($xstatus != 0) then
         NoteD $Log  "FAILED ($xstatus/$ETime)"
         @ Err = 1
      else
         NoteD $Log "PASSED ($ETime)"
      endif
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

exit($Err)
