#!/bin/csh -f
#
# MPEG - PGS MPEG tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source ../../tests/common

set msg = "PGS MPEG Test .......... "

@ err = 0

   NoteD $Log ""
   NoteD $Log "                    $msg"

   flog $Log pushd $SrcDir
   flog $Log $MAKE mpeg
   set TStatus = $status
   flog $Log popd

   if ($TStatus != 0) then
      NoteD $Log ""
      NoteD $Log "                         Can't build PGS MPEG Test Program"
      NoteD $Log "                    PGS MPEG Test .......... FAILED (-1)"
      set Err = 1
   else
      flog $Log rm -f gsmpg1.mpg gsmpg2.mpg

      set rpf = "none"
      set exe = $BinDir/gsmpgt1

      NoteD $Log -n "                        curve .............. "
      set BTime = `$TIMER -r`
      if ("$Valgrind" == "") then
         flog $Log $CFE $exe
         set xstatus = $status
      else
         ftee $Log $Valgrind $exe                |& \
         grep "ERROR SUMMARY:"                   |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a mpeg1 -e $xstatus -l -x $exe -rpf "$rpf" none

      @ err = $err + $status

      set rpf = "none"
      set exe = $BinDir/gsmpgt2

      NoteD $Log -n "                        fill ............... "
      set BTime = `$TIMER -r`
      if ("$Valgrind" == "") then
         flog $Log $CFE $exe
         set xstatus = $status
      else
         ftee $Log $Valgrind $exe                |& \
         grep "ERROR SUMMARY:"                   |& \
         awk '{ print $4 }'   >&! $Tmp
         set xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      $TEST_CHECK part -a mpeg2 -e $xstatus -l -x $exe -rpf "$rpf" none

      @ err = $err + $status

      set rpf = ( $rpf gsmpg1.mpg gsmpg2.mpg )
   endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

$TEST_CHECK part -a mpeg -e $err -rpf "$rpf" "$msg"

exit($status)
