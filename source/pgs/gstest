#!/bin/csh -f
#
# GSTEST - test PGS
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package pgs
setenv Test    ""
set vgdirs = ( . score )
source ../scripts/test-env

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: gstest [-h] [-vg] [<test>]*"
           echo "  h       this help message"
           echo "  vg      run tests under valgrind"
           echo "  <test>  one of: rendlr rendac"
           echo "                  grot axis mpeg smp"
           echo ""
           exit(1)
      case -*:
           if ("$Tests" != "") then
              break
           endif
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

set RefDir = $RootDir/pgs/ref

cd $TestDir

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "PGS TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if (-e $RootDir/pgs/pgs.h) then

   NoteD $Log ""
   NoteD $Log "PGS TEST ..."

   Note $Log "BinDir = $BinDir"
   Note $Log "path   = $path"  
   Note $Log "MAKE   = $MAKE"

   setenv Tmp $cwd/.tmp.$$

   Note $Log "Parallel = |$Parallel|"
   flog $Log ../../../manager/pwhich pact
   flog $Log ../../../manager/pwhich cc

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ( $Tests trans axis rendlr rendac grot mpeg smp )
   endif

   foreach i ($Tests)
      $SrcDir/tests/$i $argv
      if ($status == 1) then
         set ERROR = TRUE
      endif
   end

   $TEST_CHECK clean -n PGS -e $ERROR -raf "$Tmp"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endif

$TEST_CHECK fin -e $ERROR

exit($status)

