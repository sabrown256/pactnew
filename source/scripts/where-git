#!/bin/csh -f
#
# WHERE-GIT - report the nearest GIT repo
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *
set edir = `dirname $0`
set path = ( /usr/bin /bin $edir )

set cur = $cwd
pushd $cur >& /dev/null

set ok   = 1
set kind = unknown
set rdir = "+none+"
set part = "."

@ n = 5
@ i = 0

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CVS/SVN

if ((-d CVS) || (-d .svn)) then
   if (-d CVS) then
      set t = CVS
   else
      set t = .svn
   endif

# temporary until scs scripts are refitted for this change to where-git
@ i = 10
   while ($i < $n)
      @ i = $i + 1

# GOTCHA: need check that it is the same repo going down directories
#         and it cannot use scs-meta
      if (!(-d $t)) then
         set ok = 0
         if ($t == CVS) then
            set kind = cvs
         else
            set kind = svn
         endif

         if ("$rdir" == "$cur") then
            set part = "."
         else
            set part = ( `echo $cur | sed 's|'$rdir'/||'` )
         endif

         break
      else
         set rdir = $cwd
         cd .. >& /dev/null
      endif
   end

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# GIT

else

   while ($i < $n)
      @ i = $i + 1

      if (-d .git) then
         set ok   = 0
         set rdir = $cwd
         set kind = git

         if ("$rdir" == "$cur") then
            set part = "."
         else
            set part = ( `echo $cur | sed 's|'$rdir'/||'` )
         endif

         break
      else
         cd .. >& /dev/null
      endif
   end

endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

echo $rdir $part $kind

popd >& /dev/null

exit($ok)
