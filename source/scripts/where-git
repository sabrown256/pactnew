#!/bin/csh -f
#
# WHERE-GIT - report the nearest GIT repo
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *
set path = ( /usr/bin /bin )

set cur = $cwd
pushd $cur >& /dev/null

set ok   = 1
set gitd = "+none+"
set part = "."

@ n = 5
@ i = 0
while ($i < $n)
   @ i = $i + 1

# if CVS or SVN metadata is encountered first we are not GIT
   if ((-d CVS) || (-d .svn)) then
      break
   else if (-d .git) then
      set ok   = 0
      set gitd = $cwd

      if ("$gitd" == "$cur") then
         set part = "."
      else
         set part = ( `echo $cur | sed 's|'$gitd'/||'` )
      endif

      break
   else
      cd .. >& /dev/null
   endif
end

echo $gitd $part

popd >& /dev/null

exit($ok)
