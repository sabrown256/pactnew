#
# TEST-ENV - standard environment for PACT tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *
umask 007

setenv TestScrDir $cwd
foreach f (../scripts ../../scripts ../../../scripts)
   if (-f $f/env-csh) then
      pushd $f >>& /dev/null
      setenv TestScrDir $cwd
      popd >>& /dev/null
      break
   endif
end
source $TestScrDir/env-csh
setenv TEST_CHECK $TestScrDir/test-check

# settle log file base
# at package level Test should be ""
# at test level Test should be name of test script
set TLog = test
if ($?Test == 1) then
   if ("$Test" != "") then
      set TLog = $Test
   endif
endif

set VG   = `where valgrind | head -n 1`
if (-x "$VG") then
   set largs = ( $argv )
   while ($#largs > 0)
      switch ($largs[1])
         case -vg:
              set TLog = test-vg
              setenv Valgrind  "valgrind --tool=memcheck --track-origins=yes --gen-suppressions=all"
              breaksw
         case -vgd:
              set TLog = test-vg
              setenv Valgrind  "valgrind --tool=memcheck --track-origins=yes --db-attach=yes"
              breaksw
         case -vgs:
              set TLog = test-vgs
              setenv Valgrind  "valgrind --tool=memcheck --track-origins=yes"
              breaksw
      endsw
      shift largs
   end
endif

if ($?Valgrind == 0) then
   setenv Valgrind ""
endif

set DbgOpt = `pact -info DbgOpt`
switch ($DbgOpt)
   case debug:
        setenv LogSfx  g
        breaksw
   case optmize:
        setenv LogSfx  o
        breaksw
   case mixed:
   default:
        setenv LogSfx  O
        breaksw
endsw

# tests run concurrently with the DISPLAY environment variable
# set, may result in X11 connection failures on some systems
unsetenv DISPLAY

setenv OS  `uname`

setenv ZeroSpace  "-z 2"

   pushd .. >& /dev/null
   while (!(-d dev) && !(-d manager))
      cd .. >& /dev/null
   end
   setenv RootDir  $cwd
   popd >& /dev/null

   setenv System    `$RootDir/scripts/cfgman use`
   setenv BuildDir  $RootDir/dev/$System
   setenv SrcDir    $RootDir/$Package

   setenv IncDir    $BuildDir/include
   setenv LibDir    $BuildDir/lib
   setenv BinDir    $BuildDir/bin

   setenv TestDir   $SrcDir/z-$System/test
   setenv FilesDir  $SrcDir/tests
   setenv RefDir    $FilesDir/ref

   setenv TIMER     $BinDir/timer
   setenv MAKE      $BinDir/pact
   setenv DORUN     $BinDir/do-run
   setenv PSDIFF    $BinDir/ps-diff
   setenv PX        $RootDir/scripts/px

   source $IncDir/env-pact.csh

   if ($?IBM_BGL == 1) then
      setenv RUN_TESTS FALSE
   else
      setenv RUN_TESTS TRUE
   endif

   if ($?CROSS_COMPILE == 0) then
      setenv CROSS_COMPILE FALSE
   endif

# control when to use do-run
   setenv PFE   $DORUN
   setenv CFE   ""
   if ($CROSS_COMPILE != FALSE) then
      setenv CFE $DORUN

# make up the list of environment variable to pass through submit
      set lenv = ""
      foreach i (SCHEME)
         printenv $i >& /dev/null
         if ($status == 0) then
            set lenv = ( $lenv -exp_env $i )
         endif
      end

      setenv DO_RUN_SUBMIT_ENV "$lenv"
      unset lenv
   endif

   setenv PACT_Exe  "$CFE $BinDir/pact"

   setenv Parallel     `$PACT_Exe -info THREADING`
   setenv MPI          `$PACT_Exe +l -info HAVE_MPI`
   setenv HaveDevZero  `$PACT_Exe +l -info HAVE_DEV_ZERO`
   setenv LARGE_FILES  `$PACT_Exe -info _LARGE_FILES`

   setenv PYVER      `$PACT_Exe -info PyVers`
   setenv PYTHONBIN  `$PACT_Exe -info PythonPath`
   setenv PYTHON     $PYTHONBIN/python
   setenv LPYTHON    $LibDir/python$PYVER

   if (-e $IncDir/noipc) then
      setenv HaveIPC  NO
   else
      setenv HaveIPC  YES
   endif

set path = ( . $BinDir $path )

setenv SCHEME $RootDir/scheme/applications
setenv SCHEME $RootDir/scheme/applications/extensions:${SCHEME}
setenv SCHEME $RootDir/scheme/tests:${SCHEME}
setenv SCHEME $RootDir/sx/applications:${SCHEME}
setenv SCHEME $RootDir/sx/applications/extensions:${SCHEME}
setenv SCHEME $RootDir/sx/tests:${SCHEME}
setenv SCHEME $RootDir/ultra:${SCHEME}
setenv SCHEME $RootDir/ultra/applications:${SCHEME}
setenv SCHEME $RootDir/ultra/applications/extensions:${SCHEME}
setenv SCHEME $RootDir/panacea/applications:${SCHEME}

setenv SCHEME $RootDir/dev/$System/scheme:${SCHEME}

if ($?PYTHONPATH == 1) then
   setenv PYTHONPATH ${LPYTHON}:$PYTHONPATH
else
   setenv PYTHONPATH ${LPYTHON}
endif
 
# test inside a subdirectory
if (!(-d $TestDir)) then
  mkdir -p $TestDir
endif

InitLog Log $TestDir/log.$TLog.$LogSfx
Note $Log "Package: $Package"
Note $Log "Test:    $Test"
Note $Log "Log:     $Log"
Note $Log ""

Separator $Log

if ("$Valgrind" != "") then
   Note $Log "Using valgrind: $VG"
endif

if ($?TEST_RM_EXE == 1) then
   Note $Log "Removing test executables tests that pass"
   setenv rm_cache $TestDir/rm-cache
   if ($?rm_cache == 1) then
      flog $Log $RM $rm_cache
   endif
endif

if ("$Test" == "") then
   if ("$Valgrind" != "") then
      foreach i ($vgdirs)
         if ("$i" == ".") then
            set base = $FilesDir/vg.suppress
         else
            set base = $RootDir/$i/tests/vg.suppress
         endif
         if (-f $base) then
            setenv Valgrind  "$Valgrind --suppressions=$base"
         endif
         if (-f ${base}-test) then
            setenv Valgrind  "$Valgrind --suppressions=${base}-test"
         endif
      end
   endif
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# state for test-check

set xstatus = 0
set rpf     = none
@ nerr      = 0

setenv t0 `$TIMER -r`
setenv dt -1

