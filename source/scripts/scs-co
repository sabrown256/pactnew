#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
set repo = ( `$ldir/scs-meta -r` )

set opt = ""
set hst = ""
set dir = ""
set pkg = ""
set knd = ""

set srcdate = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           set hst  = `echo $repo | sed 's|:/.*$||'`
           set dir  = `echo $repo | sed 's|^.*:||'`
           breaksw
      case -k:
           shift
           set knd = $1
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

#echo "repo = $repo"
#echo "host = $hst"
#echo "root = $dir"
#echo "kind = $knd"

if ($knd == cvs) then

   set opt = ( $opt -d $repo )
   if ("$srcdate" != "") then
      cvs -d $repo co -D "$srcdate" $pkg
   else
      cvs -d $repo co $pkg
   endif

else if ($knd == svn) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   if ("$srcdate" != "") then
      set dt = "$srcdate[1]"
      svn co $repo -r "{${dt}}" $base
   else
      svn co $repo $base
   endif

else if ($knd == git) then

   if ($#pkg > 0) then
      set base = $pkg[1]
      shift pkg
   else
      set base = work-copy
   endif

   if ("$srcdate" != "") then
      set dt = "$srcdate[1]"
      git clone $repo $base
   else
      git clone $repo $base
   endif

endif

unset refdir

exit($status)
