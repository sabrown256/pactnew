#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set opt = ""
set hst = ""
set dir = ""
set pkg = ""

set repo    = ""
set srcdate = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           set hst  = `echo $repo | sed 's|:/.*$||'`
           set dir  = `echo $repo | sed 's|^.*:||'`
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

if (-d $repo/CVSROOT) then

   set opt = ( $opt -d ${hst}:$dir )
   if ("$srcdate" != "") then
      cvs $opt co -D "$srcdate" $pkg
   else
      cvs $opt co $pkg
   endif

else if (-f $repo/conf/svnserve.conf) then

   set opt  = ( $opt http://${hst}$dir )
   set root = `grep manager $refdir/.svn/entries | sed 's|/manager$||'`

   foreach p ( $pkg )
      if ("$srcdate" != "") then
         set dt = "$srcdate[1]"
         svn co $opt -r "{${dt}}" ${root}/$p
      else
         svn co $opt ${root}/$p
      endif
   end

else

   foreach p ( $repo/$pkg )
      if (-d $p/.git) then
         if ("$srcdate" != "") then
            set dt = "$srcdate[1]"
            git clone $p
         else
            git clone $p
         endif
      endif
   end

endif

unset refdir

exit($status)
