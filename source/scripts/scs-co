#!/bin/csh -f
#
# SCS-CO - check out sources whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

if ($?lmngdir == 0) then
   set exe = `dirname $0`
   if ("$exe" == ".") then
      set lmngdir = $cwd
   else
      set lmngdir = $exe
   endif
endif

set opt = ""
set hst = ""
set dir = ""
set pkg = ""

pushd $lmngdir >& /dev/null
set srcdate = ( `./pact-get-tag` )
popd >& /dev/null

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           set hst  = `echo $repo | sed 's|:/.*$||'`
           set dir  = `echo $repo | sed 's|^.*:||'`
           breaksw
      case -D:
           shift
           set srcdate = ( $1 )
           breaksw
      default:
           set pkg = ( $pkg $1 )
           breaksw
   endsw
   shift
end

if (-d $lmngdir/../.git) then

   pushd $lmngdir/.. >& /dev/null

   set inf  = ( `cat .git/config | grep url` )
   set root = $inf[3]

# GOTCHA: find correct repo path
   git clone $root/../$pkg

   popd >& /dev/null

else if (-d $lmngdir/CVS) then

   set opt = ( $opt -d ${hst}:$dir )
   cvs $opt co -D "$srcdate" $pkg

else if (-d $lmngdir/.svn) then

   set opt  = ( $opt http://${hst}$dir )
   set root = `grep manager $lmngdir/.svn/entries | sed 's|/manager$||'`

   set dt = "$srcdate[1]"
   foreach p ( $pkg )
      svn co $opt -r "{${dt}}" ${root}/$p
   end

endif

unset lmngdir

exit($status)
