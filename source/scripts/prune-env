#!/bin/csh -f
#
# PRUNE-ENV - trim down the environment
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

alias get_line  'set \!\!:1 = `awk "NR == \!\!:3 { print ; exit }" \!\!:2`'

set lbin = `dirname $0`
set path = ( $lbin $path )
unset lbin

set noglob

@ err = 0
set tgt = ""
set pat = ""
set db  = ""
set exc = ( MANPATH PATH PERDB GEXEC PERDB_PATH PSY_TYPE USER )

while ($#argv > 0)
   switch ($1)
      case -e:
           shift
           set exc = ( $exc $1 )
           breaksw
      case -h:
           echo ""
           echo "Usage: prune-env [-e <var>] [-h] rules | pact | <pattern>"
           echo "       prune-env [-e <var>] [-h] -db <config-database>"
           echo "   e    do not remove the specified variable"
           echo "        this can be used many times"
           echo "   h    this help message"
           echo ""
           exit(1)
      case -db:
           shift
           set db  = $1
           set tgt = db
           breaksw
      default:
           if ("$1" == rules) then
              set tgt = rules
           else if ("$1" == pact) then
              set tgt = pact
           else
              set tgt = pattern
              set pat = ( "$1" )
           endif
           breaksw
   endsw
   shift
end

switch ($tgt)

# remove variables matching PAT
   case pattern:
        foreach v (`env`)
           set j = ( `echo $v | sed 's~=.*$~~'` )
           if ("$j" =~ $pat) then
              echo "unsetenv $j ; "
           endif
        end
        breaksw

# remove implicit make rule variables
   case rules:
        set rules = ""
        set rules = ( $rules CCP CCObj CCArc CCObj_BP CCArc_BP )
        set rules = ( $rules FCObj FCArc )
        set rules = ( $rules LexC LexObj LexObj_BP LexArc LexArc_BP )
        set rules = ( $rules YaccC YaccObj YaccObj_BP YaccArc YaccArc_BP )
        set rules = ( $rules TemplH )
        foreach i ($rules)
           echo "unsetenv $i ; "
        end
        breaksw

# remove PACT config variables
   case pact:
        set sys = `cfgman use`
        set inf = "../dev/$sys/etc/env-pact.csh"

        if (-f $inf) then
           @ nl = `cat $inf | wc -l`
           @ il = 0
           while ($il < $nl)
              @ il = $il + 1
              get_line line $inf $il
              if ($#line > 2) then
                 if ("$line[1]" == setenv) then
                    set var = $line[2]
                    set ok = TRUE
                    foreach v ($exc)
                       if ("$v" == "$var") then
                          set ok = FALSE
                          break
                       endif
                    end
                    if ($ok == TRUE) then
                       echo "unsetenv $var ;"
                    endif
                 endif
              endif
           end
        endif
        breaksw

# remove config DB variables
   case db:
        if (!(-f "$db")) then
           set sys = `cfgman use`
           set db  = "../dev/$sys/etc/cfg.db"
        endif

        if (-f $db) then
           set noglob
           @ nl = `cat $db | wc -l`
           @ il = 0
           while ($il < $nl)
              @ il = $il + 1
              get_line var $db $il
              set var = ( `echo $var | sed 's|=.*$||'` )
              if ($#var == 1) then
                 set ok = TRUE
                 foreach v ($exc)
                    if ("$v" == "$var") then
                       set ok = FALSE
                       break
                    endif
                 end
                 if ($ok == TRUE) then
                    printenv $var >& /dev/null
                    if ($status == 0) then
                       echo "unsetenv $var ;"
                    endif
                 endif
              endif
           end
        endif
        breaksw
endsw

exit($err)



