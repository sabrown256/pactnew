#!/bin/csh -f
#
# SCS-LOCK - lock the repository whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set scsi = ( `$ldir/which-scs` )
set repo = ( `$ldir/scs-meta -r` )
unset ldir

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: scs-lock lock | unlock | status | cat | dir"
           echo ""
           exit(1)
      case lock:
           set oper = lock
           breaksw
      case unlock:
           set oper = unlock
           breaksw
      case status:
           set oper = status
           breaksw
      case dir:
           set oper = dir
           breaksw
      case cat:
           set oper = cat
           breaksw
   endsw
   shift
end

# make a lock dir next to the base SCS dir
pushd $scsi[2] >& /dev/null
set lstage = $cwd/.repo-lockd
if (!(-d $lstage)) then
   mkdir $lstage
   chmod 770 $lstage
endif
popd >& /dev/null

set lock = "$lstage/commit.lock"
set lmsg = ""

# initial lock message
if ($oper == lock) then
   set lhst = `uname -n`
   set lwho = $USER

# for commits
   if ($?TagOnly == 1) then
      if ($TagOnly == TRUE) then
         set ldate = `/bin/date '+T%y_%m_%d_%H_%M_%S'`
      else
         set ldate = `/bin/date '+D%y_%m_%d_%H_%M_%S'`
      endif

# for other repository interactions
   else
      set ldate = `/bin/date '+%y_%m_%d_%H_%M_%S'`
   endif

   if ($?TagAnn == 1) then
      set lmsg = (\'<$lwho $TagAnn $ldate|$lhst> $argv\')
   else
      set lmsg = (\'<$lwho|$ldate|$lhst> $argv\')
   endif

   unset lhst
   unset lwho
   unset ldate
endif

switch ($scsi[1])

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# GIT

   case git:
        setenv SSH  ssh
        setenv SCP  scp

        if ("$repo" =~ *:/*) then
           set repohost = `echo $repo | sed 's|:/.*$||'`
           set repodir  = `echo $repo | sed 's|^.*:||'`
        else
           set repohost = ""
           set repodir  = $repo
        endif

# handle the LOCAL repository case
        if ("$repohost" == "") then
           set lock = $repodir/commit.lock
           switch ($oper)
              case lock:
                   touch $lock
                   chmod 660 $lock
                   echo $lmsg >> $lock
                   breaksw
              case unlock:
                   rm -f $lock
                   breaksw
              case status:
                   if (-e $lock) then
                      echo "locked"
                   else
                      echo "unlocked"
                   endif
                   breaksw
              case dir:
                   echo "$lstage"
                   breaksw
              case cat:
                   if (-f $lock) then
                      cat $lock
                   endif
                   breaksw
           endsw

# handle the REMOTE repository case
        else
           set rlock = "$repodir/commit.lock"

           switch ($oper)
              case lock:
                   touch $lock
                   chmod 660 $lock
                   echo $lmsg >> $lock
                   $SCP $lock ${repohost}:${repodir} >& /dev/null
                   breaksw
              case unlock:
                   rm -f $lock
                   $SSH $repohost rm -f $rlock
                   breaksw
              case status:
                   if ($?DoingConfig == 0) then
                      set inf   = `$SSH $repohost ls $repodir |& cat`
                      set elock = FALSE
                      foreach i ($inf)
                         set entry = $i:h
                         if ("$entry" == "commit.lock") then
                            set elock = TRUE
                         endif
                      end
                      unset inf
                      if ($elock == TRUE) then
                         echo "locked"
                      else
                         echo "unlocked"
                      endif
                   endif
                   breaksw
              case dir:
                   echo "$lstage"
                   breaksw
              case cat:
                   $SSH $repohost cat $rlock
                   breaksw
           endsw

           unset rlock

        endif
        breaksw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# CVS

   case cvs:
        setenv CVS_RSH  ssh

        if ($?repo == 1) then
           if ("$repo" =~ *:*) then
              set repohost = `echo $repo | sed 's|:/.*$||' | sed 's|^.*@||'`
              set repodir  = `echo $repo | sed 's|^.*:||'`
           else
              set repohost = `hostname`
              set repodir  = $repo
           endif
        else
           set repohost = ""
        endif

# handle the LOCAL repository case
        if ("$repohost" == "") then
           set lock = commit.lock
           if ($?repo) then
              if (`expr "$repo" : '.*\(:\).*'` == "") then
                 set lock = $repo/commit.lock
              endif
           endif

           switch ($oper)
              case lock:
                   touch $lock
                   chmod 660 $lock
                   echo $lmsg >> $lock
                   breaksw
              case unlock:
                   rm -f $lock
                   breaksw
              case status:
                   if (-e $lock) then
                      echo "locked"
                   else
                      echo "unlocked"
                   endif
                   breaksw
              case dir:
                   echo "$lstage"
                   breaksw
              case cat:
                   if (-f $lock) then
                      cat $lock
                   endif
                   breaksw
           endsw

# handle the REMOTE repository case
        else
           set rlock = "$repodir/commit.lock"

           switch ($oper)
              case lock:
                   touch $lock
                   chmod 660 $lock
                   echo $lmsg >> $lock
                   scp $lock ${repohost}:${repodir} >& /dev/null
                   breaksw
              case unlock:
                   rm -f $lock
                   $CVS_RSH $repohost rm -f $rlock
                   breaksw
              case status:
                   if ($?DoingConfig == 0) then
                      set inf   = `$CVS_RSH $repohost ls $repodir |& cat`
                      set elock = FALSE
                      foreach i ($inf)
                         set entry = $i:h
                         if ("$entry" == "commit.lock") then
                            set elock = TRUE
                         endif
                      end
                      unset inf
                      if ($elock == TRUE) then
                         echo "locked"
                      else
                         echo "unlocked"
                      endif
                   endif
                   breaksw
              case dir:
                   echo "$lstage"
                   breaksw
              case cat:
                   $CVS_RSH $repohost cat $rlock
                   breaksw
           endsw

           unset rlock

        endif
        breaksw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# SVN

   case svn:
        switch ($oper)
           case lock:
                touch $lock
                chmod 660 $lock
                echo $lmsg >> $lock
                svn lock $scsi[2]/* >>& /dev/null
                breaksw
           case unlock:
                rm -f $lock
                svn unlock $scsi[2]/* >>& /dev/null
                breaksw
           case status:
                if (-e $lock) then
                   echo "locked"
                else
                   echo "unlocked"
                endif
                breaksw
           case dir:
                echo "$lstage"
                breaksw
           case cat:
                if (-f $lock) then
                   cat $lock
                endif
                breaksw
        endsw
        breaksw

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

endsw

unset lmsg
unset lock
unset lstage

exit($status)
