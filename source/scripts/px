#!/bin/csh -f
#
# PX - execute the given command upto NA times
#    - until it succeeds
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

if ($?SC_EXEC_N_ATTEMPTS) then
   @ na = $SC_EXEC_N_ATTEMPTS
else
   @ na = 3
endif

set cmd = ""
set crt = ""
set ncr = ""

while ($#argv)
   switch ("$1")
      case -h:
           echo ""
           echo "Usage: px [-c <criterion>] [-h] [-na #] <command>"
           echo "Run <command> and until it succeeds"
           echo "   c   run <criterion> to determine success"
           echo "       this should be a quoted string"
           echo "       success <criterion> has exit status of 0"
           echo "       defaults to exit status == 0"
           echo "   n   run <criterion> to determine success"
           echo "       this should be a quoted string"
           echo "       success <criterion> has exit status other than 0"
           echo "   na  number of attempts to make (default 3)"
           echo "   h   this help message"
           echo ""
           exit(1)
      case -c:
           shift
           set crt = ( $1 )
           breaksw
      case -n:
           shift
           set ncr = ( $1 )
           breaksw
      case -na:
           shift
           @ na = $1
           breaksw
      default:
           while ($#argv > 0)
              set cmd = ( $cmd $1 )
              shift
           end
           break
           breaksw
   endsw
   shift
end

@ err   = 0
@ count = 1
@ dt    = 0
set out = ""

set noglob

while ($count <= $na)
   if ($count > 1) then
      echo "***> failed ($err) [$dts] - attempt $count in $dt seconds"
      echo "***>       $cmd"
      sleep $dt
      echo "***> retry '"$cmd"'"
   endif

   set dts = `date +"%Y/%m/%d %H:%M:%S"`

# run the command
   set out = `eval "$cmd"`

# default criterion is exit status
   @ err = $status

# check for positive criterion
   if ("$crt" != "") then
      eval "$crt"
      if ($status == 0) then
         @ err = 0
      else
         @ err = 1
      endif

# check for negative criterion
   else if ("$ncr" != "") then
      eval "$ncr"
      if ($status != 0) then
         @ err = 0
      else
         @ err = 1
      endif
   endif

   if ($err == 0) then
      if ($count > 1) then
         echo "***> succeeded [$dts] - on attempt $count"
         echo "***>       $cmd"
      endif
      break
   else
      @ count = $count + 1
      if ($count < 3) then
         @ dt = $dt + 1
      else
         @ dt = $dt + 30
      endif
   endif
end

if (($count > $na) && ($err != 0)) then
   @ count = $count - 1
   echo "***> failed ($err) - quitting after $count attempts"
endif

if ("$out" != "") then
   echo "$out"
endif

exit($err)
