#!/bin/csh -f
#
# CC-DEBUG - help debug compiler crashes by narrowing to
#          - function causing the compiler to crash
#
# include cpyright.h
#

unalias *

@ err = 0

set file = ""
set cmd  = "gcc"

while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: cc-debug [-h] -c <file> <cmd>"
           echo "   h   this help message"
           echo ""
           echo ""
           exit(1)
      case -c:
           shift
           set file = $1
           breaksw
      default:
           set cmd = ( $argv )
           break
           breaksw
   endsw
   shift
end

if ("$file" == "") then
   echo "No file specified - exiting"
   exit(1)
endif

$cmd -c $file >&! $file.res
set xstatus = $status

set ok = TRUE

@ n = 20
@ i = 0
while ($i < $n)
   @ i = $i + 1
#   echo "------------------------------------------------------------"
#   echo "Pass #$i"
   set nf = $file:r-n.c
   elide -ns 1 -ne $i -s ';' '{' '}' $file >! $nf
   grep '{' $nf >& /dev/null
   if ($status == 0) then
      $cmd -c $nf >&! $nf.res
      if ($status != $xstatus) then
         echo "Status changed at expression #$i"
         set ok = FALSE
      else
         diff $nf.res $file.res >& /dev/null
         if ($status != 0) then
            echo "Results changed at expression #$i"
            set ok = FALSE
         endif
      endif
      if ($ok == FALSE) then
         @ i = $i - 1
         elide -ns 1 -ne $i -s ';' '{' '}' $file >! $nf
         break
      endif
   else
      break
   endif
end

rm $file.res $nf.res

exit($status)
