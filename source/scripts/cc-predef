#!/bin/csh -f
#
# CC-PREDEF - find the predefined macros for the specified compiler
#

unalias *

set cc = ""
while ($#argv > 0)
   switch ($1)
      case -h:
           echo ""
           echo "Usage: cc-predef [-h] [<cc>]"
           echo "    h   this help message"
           echo ""
           exit(1)
      default:
           set cc = $1
	   breaksw
   endsw
   shift
end

if ("$cc" == "") then
   set cc = ( `pwhere -n 1 cc` )
endif

if ("$cc" !~ /*) then
   set cc = ( `pwhere -n 1 $cc` )
endif

if (!(-x "$cc")) then
   echo "No cc found or specified - exiting"
   exit(1)
endif

set tmp = $cwd/.tmp-$$.h

switch ($cc)
   case *pcc*:
        breaksw

   case *suncc*:
   case *sunCC*:
        $cc -xdumpmacros -E /dev/null |& sort
        breaksw

   case *xlc*:
   case *xlC*:
        touch $tmp
        $cc -qshowmacros -E /dev/null $tmp |& sort
        rm $tmp
        breaksw

   case *pgcc*:
   case *mingw*:
        touch $tmp
        $cc -E -dM $tmp |& sort
        rm $tmp
        breaksw

# for C compilers
   case *icc*:
   case *gcc*:
   case *gnu*:
   case *clang*:
   case *opencc*:
   case *pathcc*:
   default:
        $cc -E -dM -x c /dev/null |& sort
        breaksw

# for C++ compilers
   case *icpc*:
   case *g++*:
   case *clang++*:
   case *openCC*:
   case *pathCC*:
        $cc -E -dM -x c++ /dev/null |& sort
        breaksw
endsw

exit($status)
