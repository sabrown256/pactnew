#!/bin/csh -f
#
# SCS-INIT - initialize a new repository whether GIT, CVS or SVN
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

set ldir = `dirname $0`
if ("$ldir" == ".") then
   set ldir = $cwd
endif
set repo = ""
set knd  = ""

set opt = ""

while ($#argv > 0)
   switch ($1)
      case -d:
           shift
           set repo = $1
           breaksw
      case -h:
           echo ""
           echo "Usage: scs-init [-d <repo>] [-h] [-k <knd>] [<arg>]*"
           echo "   d      location of source repository"
           echo "   D      checkout sources as of <date> stamp"
           echo "   h      this help message"
           echo "   k      source control system: cvs, git, or svn"
           echo "   <arg>  options to be passed on"
           echo ""
           exit(1)
      case -k:
           shift
           set knd = $1
           breaksw
      case -*:
           set opt = ( $opt $1 )
           breaksw
      default:
           breaksw
   endsw
   shift
end

if ("$repo" == "") then
   echo "No repository path specified - exiting"
   exit(1)
endif

if ("$knd" == "") then
   echo "No repository kind specified - exiting"
   exit(1)
endif

if ($knd == cvs) then

   setenv CVS_RSH  ssh
   cvs init

else if ($knd == svn) then

   svnadmin create $repo

else if ($knd == git) then

   pushd $repo >& /dev/null
   git init
   git config --bool core.bare true
   popd >& /dev/null

endif

exit($status)
