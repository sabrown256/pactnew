#!/bin/csh -f
#
# CFG-FEATURES - common code to generate the feature list of a config
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

unalias *

@ err = 0

set flst    = ""
set cfg     = ""
set mpi     = FALSE
set pydir   = ""
set pyexe   = "python"
set python  = FALSE
set openmp  = FALSE
set shared  = FALSE
set pthread = FALSE
set vrb     = FALSE

while ($#argv > 0)
   switch ($1)
      case -bits:
           shift
           set bits = $1
           breaksw
      case -c:
           shift
           set cfg = $1
           breaksw
      case -h:
           echo ""
           echo "Usage: cfg-features [-c <cfg>] [-h] <features>*"
           echo "   c   append specifictions to <cfg>"
           echo "   h   this help message"
           echo ""
           exit(1)
           breaksw
      case -epy:
           shift
           set pyexe  = $1
           set python = TRUE
           breaksw
      case -mpi:
           set mpi = TRUE
           breaksw
      case -py:
           set python = TRUE
           breaksw
      case -s:
           set openmp = TRUE
           breaksw
      case -sl:
           set shared = TRUE
           breaksw
      case -t:
           set pthread = TRUE
           breaksw
      case -v:
           set vrb = TRUE
           breaksw
      default:
           set flst = ( $flst $1 )
           breaksw
   endsw
   shift
end

if ("$cfg" == "") then
   set outf = /dev/stdout
else
   set outf = $cfg
endif

# expand delimiters to spaces
set flst = ( `echo $flst | sed 's/,/ /g'` )

foreach f ($flst)
   if (-f features/$f) then
      echo "include features/$f" >> $outf
   else if ($vrb == TRUE) then
      echo "unknown feature $f - ignored"
   endif
end

if ($mpi == TRUE) then
   echo "include mpi" >> $outf
   set cfg = "$cfg-mpi"
   if ($vrb == TRUE) then
      echo "   add mpi"
   endif
endif

if ($python == TRUE) then
   if (("$pyexe" != "") || ("$pydir" != "")) then
      echo "Group Python {" >> $outf
      if ("$pyexe" != "") then
         echo "   Exe  ?= $pyexe" >> $outf
      endif
      if ("$pydir" != "") then
         echo "   Path ?= $pydir" >> $outf
      endif
      echo "}" >> $outf
   endif
   echo "include python" >> $outf
   set cfg = "$cfg-py"
   if ($vrb == TRUE) then
      echo "   add python"
   endif
endif

if (($shared == TRUE) && ($python == FALSE)) then
   echo "include shared" >> $outf
   set cfg = "$cfg-sl"
   if ($vrb == TRUE) then
      echo "   add shared library support"
   endif
endif

if (($pthread == TRUE) && ($openmp == FALSE)) then
   echo "include pthread" >> $outf
   set cfg = "$cfg-t"
   if ($vrb == TRUE) then
      echo "   add pthread"
   endif
endif

if (($openmp == TRUE) && ($pthread == FALSE)) then
   echo "include openmp" >> $outf
   set cfg = "$cfg-s"
   if ($vrb == TRUE) then
      echo "   add openmp"
   endif
endif

echo "$cfg"

exit($err)
