#!/bin/csh -f
#
# DL - analyze platform dynamic linker
#    - Computes: HAVE_DYNAMIC_LINKER
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/dl -----"
Note $ALog "Analyze: dynamic linker"
Note $ALog ""

dbmget $ALog CC_Exe^                          \
             CC_Flags^                        \
             CROSS_FE^                        \
             HAVE_DYNAMIC_LINKER =\? FALSE^   \
             DL_Lib              =\? ""^      \
             PACT_SO_CACHE       =\? ""^      \
             PACT_CC_VERSION

Note $ALog "HAVE_DYNAMIC_LINKER = |$HAVE_DYNAMIC_LINKER|"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# check for DLSYM

Separator $ALog

cat << EOF >! $Tmp.c
#include <stdlib.h>
#define __USE_GNU
#include <dlfcn.h>
int main(int c, char **v)
   {int rv;
    void *(*mal)(size_t nb) = dlsym(RTLD_NEXT, "malloc");
    rv = (mal == NULL);
    return(rv);}
EOF

flog $ALog cat $Tmp.c

# try building without -ldl
set ok = FALSE
flog $ALog $CC_Exe $Tmp.c -o $Tmp $CC_Flags
if ($status == 0) then
   flog $ALog $CROSS_FE $Tmp
   if ($status == 0) then
      set ok = TRUE
   endif
endif

# try building with -ldl
if ($ok == FALSE) then
   flog $ALog $CC_Exe $Tmp.c -o $Tmp $CC_Flags -ldl
   if ($status == 0) then
      flog $ALog $CROSS_FE $Tmp
      if ($status == 0) then
         set ok = NEED
      endif
   endif
endif

flog $ALog rm -f $Tmp.c $Tmp

if ($ok == FALSE) then
   NoteD $ALog "      no dl"
else
   if ($ok == NEED) then
      NoteD $ALog "      need explicit dl"
      setenv DL_Lib  "-ldl"
   else
      NoteD $ALog "      has implicit dl"
   endif
   flog $ALog setenv HAVE_DYNAMIC_LINKER TRUE
endif

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# make PACT_SO_CACHE specific to the current configuration
setenv PACT_SO_CACHE  $PACT_SO_CACHE/$PSY_ID

dbmset $ALog PACT_SO_CACHE       = $PACT_SO_CACHE^         \
             DL_Lib              = $DL_Lib^                \
             HAVE_DYNAMIC_LINKER = $HAVE_DYNAMIC_LINKER

Separator $ALog

source $Me:h/post-common

exit(0)

