#!/bin/csh -f
#
# LEX - analyze the platform LEX
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

set Me = $0
source $Me:h/pre-common

Note $ALog "----- analyze/lex -----"
Note $ALog "Analyze: lex utility"
Note $ALog ""

dbmget $ALog Lex_Exe           =\? ""^    \
             Lex_Flags         =\? ""^    \
             Cfg_Lex_Flags     =\? ""^    \
             Cfe_Lex_Flags     =\? ""^    \
             HAVE_FLEX_SCANNER =\? FALSE

cat << EOF >! $Tmp.l
%{
%}
%%
.              {return(yytext[0]);}
%%
EOF

foreach i ($path /usr/bin /bin)
   set lLex = $i/lex
   set fLex = $i/flex
   if (-x $lLex) then
      $lLex $Tmp.l >& /dev/null
      if ($status == 0) then
         setenv Lex_Exe $lLex
         grep FLEX lex.yy.c >& /dev/null
         if ($status == 0) then
            NoteD $ALog "   Lex: $lLex (FLEX)"
            setenv HAVE_FLEX_SCANNER TRUE
         else
            NoteD $ALog "   Lex: $lLex"
         endif
         break
      endif
   else if (-x $fLex) then
      $fLex $Tmp.l >& /dev/null
      if ($status == 0) then
         setenv Lex_Exe $fLex
         setenv HAVE_FLEX_SCANNER TRUE
         NoteD $ALog "   Lex: $fLex (FLEX)"
      else
         NoteD $ALog "   Lex: $fLex"
      endif
      break
   endif
end

rm -f $Tmp.l

# export the results
dbmset $ALog Lex_Exe           = $Lex_Exe^           \
             Lex_Flags         = $Lex_Flags^         \
             Cfg_Lex_Flags     = $Cfg_Lex_Flags^     \
             Cfe_Lex_Flags     = $Cfe_Lex_Flags^     \
             HAVE_FLEX_SCANNER = $HAVE_FLEX_SCANNER

source $Me:h/post-common

exit(0)

