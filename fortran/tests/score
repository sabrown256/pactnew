#!/bin/csh -f
#
# SCORE - SCORE Fortran API tests
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

source $PSY_Base/fortran/tests/common

set msg = "SCORE API Test ....................."

@ err = 0

# check for Fortran compiler before doing Fortran test
set FC = `awk '$1 == "FCompiler" { print $3 }' $EtcDir/make-def`
if ("$FC" != "") then
   Note $Log "Using fortran compiler $FC"
   flog $Log ( where $FC | head -n 1 )
   flog $Log @ xstatus = $xstatus + $status
else
   flog $Log @ xstatus = $xstatus + 1
endif

# check for compiled modules
set inf = ( `ls -1 $IncDir/*.mod` )
if ($#inf >= 16) then
   Note $Log "Expected number of modules present: $#inf"
   Note $Log " $inf"
   flog $Log @ xstatus = $xstatus + 0
else
   Note $Log "Missing a number of modules: $#inf"
   flog $Log @ xstatus = $xstatus + 1
endif

#if (($xstatus == 0) && ("$MPI" == "")) then
if ($xstatus == 0) then

   NoteD $Log ""
   NoteD $Log "                    $msg"
   Note $Log ""

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   NoteD $Log -n "                        lexical scan ................... "
   Note $Log ""

   flog $Log pushd $SrcDir
   flog $Log $MAKE scan
   @ xstatus = $status
   flog $Log popd

   set exe = $BinDir/tscls
   set rpf = none
   set opt = ( $FilesDir/tscls.src )
   set io  = tscls.res

   source $PSY_Base/scripts/test-run

   if (("$Valgrind" == "") && ($xstatus == 0)) then
      diff tscls.res $RefDir/tscls > tscls.res.diff
      set rpf = ( $rpf tscls.res.diff )
   endif
   set rpf = ( $rpf tscls.res )

   $TEST_CHECK part -a score_lex -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none

   @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   NoteD $Log -n "                        hash ........................... "
   Note $Log ""

   flog $Log pushd $SrcDir
   flog $Log $MAKE -na 2 fha
   @ xstatus = $status
   flog $Log popd

   set exe = $BinDir/tscfh
   set rpf = none
   set opt = ""
   set io  = flog

   source $PSY_Base/scripts/test-run

   $TEST_CHECK part -a score_hash -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none

   @ nerr = $nerr + $status

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

   if ($HAVE_OPENMP == TRUE) then
      flog $Log pushd $SrcDir
      flog $Log $MAKE -na 2 omp
      @ xstatus = $status
      flog $Log popd

      NoteD $Log -n "                        omp ............................ "
      Note $Log ""

      set exe = $BinDir/tscfomp
      set rpf = none

      set t0 = `$TIMER -r`

      if ("$Valgrind" == "") then
         flog $Log ( echo 2 | $CROSS_FE $exe )
         @ xstatus = $status
      else
         ftee $Log echo 2 @o $Valgrind $exe              |& \
         grep "ERROR SUMMARY:"                           |& \
         awk '{ print $4 }'   >&! $Tmp
         @ xstatus = `cat $Tmp`
         set rpf     = ( $rpf $Tmp )
      endif

      set dt = `$TIMER -b $t0`
      unset t0

      $TEST_CHECK part -a score_omp -e $xstatus -dt $dt -x $exe -l -rpf "$rpf" none

      @ nerr = $nerr + $status

   endif

endif

$TEST_CHECK part -a score -e $nerr "$msg"

exit($status)

