#!/bin/csh -f
#
# FATEST - test FORTRAN APIs
#
# Source Version: 3.0
# Software Release #: LLNL-CODE-422942
#
# include "cpyright.h"
#

setenv Package fortran
setenv Test    ""
set vgdirs = ( . )

set ldir   = `dirname $0`
set ldir   = `cd $ldir && pwd`       # ensure absolute path
setenv PSY_Base `dirname $ldir`
source $PSY_Base/scripts/test-env
unset ldir

set Tests = ""

while ($#argv > 0)
   switch ($1)
      case -h:
           echo "Usage: fatest [-h] [<test>]*"
           echo "  h       this help message"
           echo "  <test>  one of: score pdb pgs panacea"
           echo ""
           exit(1)
      case -*:
           breaksw
      default:
           set Tests = ( $Tests $1 )
           breaksw
   endsw
   shift
end

cd $TestDir

unsetenv DO_RUN_SUBMIT_ENV

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# omit tests - usually in difficult CROSS_COMPILE situation

if ($RUN_TESTS == FALSE) then
   NoteD $Log ""
   NoteD $Log "FORTRAN TESTS ... omitted"

#--------------------------------------------------------------------------
#--------------------------------------------------------------------------

# run the tests

else if ($FC_Exe != none) then

   NoteD $Log ""
   NoteD $Log "FORTRAN TEST ..."

   if ("$Tests" == "") then
      set Tests = ""
      set Tests = ($Tests score pdb pgs panacea)
   endif

   foreach i ($Tests)
      $SrcDir/tests/$i
      @ nerr = $nerr + $status
   end

   $TEST_CHECK clean -n FORTRAN -e $nerr -raf "$Tmp"

   set xstatus = $status

endif

$TEST_CHECK fin -e $xstatus

exit($status)

